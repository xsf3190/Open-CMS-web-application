CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_SEC" AS 
    --
    PROCEDURE createAuthRequest(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE verifyAuthRequest(pWebsiteId IN website.id%type, pRequestType IN VARCHAR2, pUserId IN NUMBER, pVerifyData IN VARCHAR2, pStatus OUT NUMBER);
    --
    PROCEDURE checkAuthStatus(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE refreshAuthToken(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE refreshOrdsEndpoints;
    --
    PROCEDURE post_auth_facebook;
    --
    PROCEDURE post_auth_google;
    --
    -- Implementing Base64URL docede - URL safe Base64
    FUNCTION BASE64URL_DECODE(p_input IN varchar2) RETURN RAW DETERMINISTIC PARALLEL_ENABLE;
    -- Convert RSA modulus (n) and exponent (e) that JWKS returns into Base64 encoded PKCS1 public key format that DBMS_CRYPTO expects
    FUNCTION MAKE_PKCS1_RSA_KEY(v_key_modulus_binary IN RAW /* raw(256) 2048 bits*/, v_public_key_exponent_binary IN RAW /* raw(3) 24 bits*/) RETURN RAW DETERMINISTIC PARALLEL_ENABLE;
    -- Go to ISS and fetch the public keys
    PROCEDURE UPDATE_JWKS_KEY_CACHE(p_iss IN VARCHAR2);
    -- Returns FALSE if there are problems with validation or signature, TRUE if JWT is valid (not expired, correct ISS) and signature is valid
    FUNCTION DECODE_AND_VALIDATE(p_jwt IN VARCHAR2, p_iss IN VARCHAR2, p_aud IN VARCHAR2, p_payload OUT VARCHAR2) RETURN boolean;
    --
    PROCEDURE subscribe;
    --
END PCK_SEC;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_SEC" AS

    REFRESH_TOKEN_EXP CONSTANT PLS_INTEGER:=(24*60*60)*90; --90 DAYS
    ACCESS_TOKEN_EXP CONSTANT PLS_INTEGER:=60*5; -- 5 MINUTES

    FUNCTION emailTemplate(pAuthMethod IN VARCHAR2, pWebsite IN VARCHAR2, pFavicon IN VARCHAR2, pAuthData IN VARCHAR2) RETURN VARCHAR2 IS
        l_html LONG;
    BEGIN
    l_html:=
'<!doctype html>
<html lang="en">
  <head>
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
    <title>Simple Transactional Email</title>
    <style media="all" type="text/css">
@media all {
  .btn-primary table td:hover {
    background-color: #ec0867 !important;
  }

  .btn-primary a:hover {
    background-color: #ec0867 !important;
    border-color: #ec0867 !important;
  }
}
@media only screen and (max-width: 640px) {
  .main p,
.main td,
.main span {
    font-size: 16px !important;
  }

  .wrapper {
    padding: 8px !important;
  }

  .content {
    padding: 0 !important;
  }

  .container {
    padding: 0 !important;
    padding-top: 8px !important;
    width: 100% !important;
  }

  .main {
    border-left-width: 0 !important;
    border-radius: 0 !important;
    border-right-width: 0 !important;
  }

  .btn table {
    max-width: 100% !important;
    width: 100% !important;
  }

  .btn a {
    font-size: 16px !important;
    max-width: 100% !important;
    width: 100% !important;
  }
}
@media all {
  .ExternalClass {
    width: 100%;
  }

  .ExternalClass,
.ExternalClass p,
.ExternalClass span,
.ExternalClass font,
.ExternalClass td,
.ExternalClass div {
    line-height: 100%;
  }

  .apple-link a {
    color: inherit !important;
    font-family: inherit !important;
    font-size: inherit !important;
    font-weight: inherit !important;
    line-height: inherit !important;
    text-decoration: none !important;
  }

  #MessageViewBody a {
    color: inherit;
    text-decoration: none;
    font-size: inherit;
    font-family: inherit;
    font-weight: inherit;
    line-height: inherit;
  }
}
</style>
  </head>
  <body style="font-family: Helvetica, sans-serif; -webkit-font-smoothing: antialiased; font-size: 16px; line-height: 1.3; -ms-text-size-adjust: 100%; -webkit-text-size-adjust: 100%; background-color: #f4f5f6; margin: 0; padding: 0;">
    <table role="presentation" border="0" cellpadding="0" cellspacing="0" class="body" style="border-collapse: separate; mso-table-lspace: 0pt; mso-table-rspace: 0pt; background-color: #f4f5f6; width: 100%;" width="100%" bgcolor="#f4f5f6">
      <tr>
        <td style="font-family: Helvetica, sans-serif; font-size: 16px; vertical-align: top;" valign="top">&nbsp;</td>
        <td class="container" style="font-family: Helvetica, sans-serif; font-size: 16px; vertical-align: top; max-width: 600px; padding: 0; padding-top: 24px; width: 600px; margin: 0 auto;" width="600" valign="top">
          <div class="content" style="box-sizing: border-box; display: block; margin: 0 auto; max-width: 600px; padding: 0;">

            <!-- START CENTERED WHITE CONTAINER -->
            <span class="preheader" style="color: transparent; display: none; height: 0; max-height: 0; max-width: 0; opacity: 0; overflow: hidden; mso-hide: all; visibility: hidden; width: 0;">Your Login request to ' || pWebsite || '</span>
            <table role="presentation" border="0" cellpadding="0" cellspacing="0" class="main" style="border-collapse: separate; mso-table-lspace: 0pt; mso-table-rspace: 0pt; background: #ffffff; border: 1px solid #eaebed; border-radius: 16px; width: 100%;" width="100%">

              <!-- START MAIN CONTENT AREA -->
              <tr>
                <td class="wrapper" style="font-family: Helvetica, sans-serif; font-size: 16px; vertical-align: top; box-sizing: border-box; padding: 24px;" valign="top">
                  <img src="' || pFavicon || '" alt="Website favicon" width="96" height="96" border="0" style="border:0; outline:none; text-decoration:none; display:block;">
                  <p style="font-family: Helvetica, sans-serif; font-size: 16px; font-weight: bold; margin: 0; margin-bottom: 16px;">Your Login request to ' || pWebsite || '</p>
                  <p style="font-family: Helvetica, sans-serif; font-size: 16px; font-weight: normal; margin: 0; margin-bottom: 16px;">';
                  CASE pAuthMethod 
                    WHEN 'magic' THEN l_html:=l_html || 'Click the button below to log in. Your link expires in 5 minutes.';
                    WHEN 'passcode' THEN l_html:=l_html || 'Enter this passcode to log in. Expires in 5 minutes.';
                  END CASE;
                  l_html:=l_html ||
                  '</p>
                  <table role="presentation" border="0" cellpadding="0" cellspacing="0" class="btn btn-primary" style="border-collapse: separate; mso-table-lspace: 0pt; mso-table-rspace: 0pt; box-sizing: border-box; width: 100%; min-width: 100%;" width="100%">
                    <tbody>
                      <tr>
                        <td align="left" style="font-family: Helvetica, sans-serif; font-size: 16px; vertical-align: top; padding-bottom: 16px;" valign="top">
                          <table role="presentation" border="0" cellpadding="0" cellspacing="0" style="border-collapse: separate; mso-table-lspace: 0pt; mso-table-rspace: 0pt; width: auto;">
                            <tbody>
                              <tr>
                                <td style="font-family: Helvetica, sans-serif; font-size: 16px; vertical-align: top; border-radius: 4px; text-align: center; background-color: #0867ec;" valign="top" align="center" bgcolor="#0867ec">';
                                CASE pAuthMethod 
                                    WHEN 'magic' THEN l_html:=l_html || 
                                        '<a href="' || pAuthData || '" target="_blank" style="border: solid 2px #0867ec; border-radius: 4px; box-sizing: border-box; cursor: pointer; display: inline-block; font-size: 16px; font-weight: bold; margin: 0; padding: 12px 24px; text-decoration: none; text-transform: capitalize; background-color: #0867ec; border-color: #0867ec; color: #ffffff;">Login</a>';
                                    WHEN 'passcode' THEN l_html:=l_html || 
                                        '<span style="border: solid 2px #0867ec; border-radius: 4px; box-sizing: border-box; display: inline-block; font-size: 24px; letter-spacing: 8px; font-weight: bold; margin: 0; padding: 12px 24px; text-transform: capitalize; background-color: #0867ec; border-color: #0867ec; color: #ffffff;">' || pAuthData || '</span>';
                                END CASE;
                                l_html:=l_html ||
                                '</td>
                              </tr>
                            </tbody>
                          </table>
                        </td>
                      </tr>
                    </tbody>
                  </table>
                  <p style="font-family: Helvetica, sans-serif; font-size: 16px; font-weight: normal; margin: 0; margin-bottom: 16px;">Thank you.</p>
                </td>
              </tr>

              <!-- END MAIN CONTENT AREA -->
              </table>

            <!-- START FOOTER -->
            <div class="footer" style="clear: both; padding-top: 24px; text-align: center; width: 100%;">
              <table role="presentation" border="0" cellpadding="0" cellspacing="0" style="border-collapse: separate; mso-table-lspace: 0pt; mso-table-rspace: 0pt; width: 100%;" width="100%">
                <tr>
                  <td class="content-block" style="font-family: Helvetica, sans-serif; vertical-align: top; color: #9a9ea6; font-size: 16px; text-align: center;" valign="top" align="center">
                    <span class="apple-link" style="color: #9a9ea6; font-size: 16px; text-align: center;">Email sent through adfreesites.com</span>
                  </td>
                </tr>
                <tr>
                  <td class="content-block powered-by" style="font-family: Helvetica, sans-serif; vertical-align: top; color: #9a9ea6; font-size: 16px; text-align: center;" valign="top" align="center">
                    Powered by <a href="https://docs.aws.amazon.com/ses/latest/dg/Welcome.html" style="color: #9a9ea6; font-size: 16px; text-align: center; text-decoration: none;">Amazon Simple Email Service(SES)</a>
                  </td>
                </tr>
              </table>
            </div>

            <!-- END FOOTER -->
            
<!-- END CENTERED WHITE CONTAINER --></div>
        </td>
        <td style="font-family: Helvetica, sans-serif; font-size: 16px; vertical-align: top;" valign="top">&nbsp;</td>
      </tr>
    </table>
  </body>
</html>';
        RETURN (l_html);
    END;

    PROCEDURE sendMessage(pSuccess IN BOOLEAN, pMessage IN VARCHAR2) IS
    BEGIN
        APEX_JSON.open_object;
        APEX_JSON.write ('success', pSuccess);
        APEX_JSON.write ('message', pMessage);
        APEX_JSON.close_object;
    END;

    PROCEDURE refreshOrdsEndpoints IS 
    BEGIN
        MERGE INTO ords_endpoints t
        USING
        (
            SELECT t.id template_id, t.uri_template, h.method, h.comments as label
            FROM user_ords_modules m, user_ords_templates t, user_ords_handlers h, user_ords_parameters p
            WHERE m.name='public'
            AND m.id=t.module_id
            AND t.id=h.template_id
            AND h.id=p.handler_id
            AND h.comments IS NOT NULL
            AND p.name='X-ORDS-STATUS-CODE'
        ) s
        ON (t.template_id=s.template_id)
        WHEN MATCHED THEN
            UPDATE SET t.uri_template=s.uri_template, t.method=s.method, t.label=s.label
        WHEN NOT MATCHED THEN 
            INSERT (t.template_id, t.uri_template, t.method, t.label) VALUES (s.template_id, s.uri_template, s.method, s.label);

        /* temporary solution */
        delete website_role;
        delete api_endpoint;
        
        insert into website_role(website_id, role_id)
        select w.id, r.id
        from website w, role r
        where r.name in ('admin','owner');

        insert into api_endpoint(website_id, role_id, template_id)
        select w.id, r.id, o.template_id 
        from website w, role r, ords_endpoints o
        where r.name in ('admin','owner');
    END;

    /*
    ** USER SUBMITS EMAIL ADDRESS TO BE AUTHENTICATED
    ** - CREATE RANDOM PASSCODE OR MAGIC LINK AND SEND TO EMAIL ADDRESS
    */

    PROCEDURE createAuthRequest(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_email users.email%type;
        l_url varchar2(100);
        l_request_type VARCHAR2(10);
        l_user_id users.id%type;
        l_ords_user_id user_ords_clients.id%type;
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_token VARCHAR2(4000);
        l_digits   CONSTANT VARCHAR2(10) := '0123456789';
        l_passcode VARCHAR2(6);
        l_passcode_hash raw(32);
        l_token_hash raw(32);
        l_json JSON_OBJECT_T;
        l_email_payload CLOB;
        l_sendmail_subject VARCHAR2(200);
        l_sendmail_body LONG;
        l_sendmail_link VARCHAR2(500);
        l_clob CLOB;
    BEGIN
        FOR C IN (SELECT o.client_secret, j.email, j.url, j.request_type, NVL(u.id,0) user_id, NVL(o.id,0) ords_user_id
                    FROM user_ords_clients o, users u, JSON_TABLE(pBodyText, '$' COLUMNS email, url, request_type) j
                   WHERE o.name(+)=j.email
                     AND u.email(+)=j.email)
        LOOP
            l_email:=C.email;
            l_url:=C.url;
            l_request_type:=C.request_type;
            l_user_id:=C.user_id;
            l_ords_user_id:=C.ords_user_id;
            l_jwt_enc_key:=C.client_secret;
        END LOOP;

        IF (l_ords_user_id=0) THEN
            OAUTH.create_client(
                p_name            => l_email,
                p_grant_type      => 'client_credentials',
                p_owner           => 'mark.russellbrown@gmail.com',
                p_support_email   => 'mark.russellbrown@gmail.com',
                p_description     => 'Provide secure secret in order to encrypt JWT tokens',
                p_privilege_names => 'api_priv'
            );
            SELECT client_secret INTO l_jwt_enc_key FROM user_ords_clients WHERE name=l_email;
        END IF;
        

        CASE l_request_type
            WHEN 'magic' THEN
                l_token:=apex_jwt.ENCODE (
                    p_iss       => 'ORDS',
                    p_sub       => l_email,
                    p_aud       => 'xxx',
                    p_iat_ts    => current_timestamp,
                    p_exp_sec   => ACCESS_TOKEN_EXP,
                    p_signature_key => UTL_RAW.cast_to_raw (l_jwt_enc_key)
                );
                l_token_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_token,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

                IF (l_user_id=0) THEN
                    INSERT INTO users (email, login_status, token) VALUES (l_email, 'AUTH WAITING', l_token_hash);
                ELSE
                    UPDATE users SET login_status='AUTH WAITING', token=l_token_hash, updated_date=current_timestamp WHERE id=l_user_id;
                END IF;

                
                l_sendmail_link:=pck_core.getRestUrl || 'authenticate/' || pWebsiteId || '?request=magic&user='||l_user_id||'&verify=' ||l_token;
                l_sendmail_body:=emailTemplate(l_request_type, l_url, 'https://adfreesites.com/favicon.' || pWebsiteId || '.png', l_sendmail_link);

            WHEN 'passcode' THEN
                FOR i IN 1..6 LOOP
                    l_passcode:=l_passcode || SUBSTR(l_digits,FLOOR(DBMS_RANDOM.VALUE(1, LENGTH(l_digits) + 1)),1);
                END LOOP;

                /* Store passcode as hash for later comparison with user-entered value */
                l_passcode_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_passcode,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

                IF (l_user_id=0) THEN
                    INSERT INTO users (email, login_status, passcode) VALUES (l_email, 'AUTH WAITING', l_passcode_hash);
                ELSE
                    UPDATE users SET login_status='AUTH WAITING', passcode=l_passcode_hash WHERE id=l_user_id;
                END IF;

                l_sendmail_body:=emailTemplate(l_request_type, l_url, 'https://adfreesites.com/favicon.' || pWebsiteId || '.png', l_passcode);
        END CASE;

        l_json:=new JSON_OBJECT_T;
        l_json.put('contactEmail',l_email);
        l_json.put('subject','Login to ' || l_url);
        l_json.put('body', l_sendmail_body);
        l_json.put('sourceEmail','login@adfreesites.com');
        l_email_payload:=l_json.stringify;
        pck_api.callAWSemailAPI(pMethod=>'POST', pBody=>l_email_payload, pData=>l_clob);

        CASE l_request_type
            WHEN 'magic' THEN
                sendMessage(TRUE,'Magic link sent to your inbox');
            WHEN 'passcode' THEN
                APEX_JSON.open_object;
                APEX_JSON.write ('success', TRUE);
                APEX_JSON.write ('message', 'Passcode sent to your inbox');
                APEX_JSON.write ('userid', l_user_id);
                APEX_JSON.close_object;
        END CASE;

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    PROCEDURE getKeyAndScope(pWebsiteId IN website.id%type, pEmail IN VARCHAR2, pJwtEncKey IN OUT user_ords_clients.client_secret%type, pUserId IN OUT users.id%type, pScope IN OUT VARCHAR2) 
    IS
        l_scope VARCHAR2(7);
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_user_id users.id%type;
        l_contact_email website.contact_email%type;
        TYPE t_role IS TABLE OF role%ROWTYPE INDEX BY BINARY_INTEGER;
        l_role t_role;
        
    BEGIN
        SELECT w.contact_email, o.client_secret
          INTO l_contact_email, l_jwt_enc_key
          FROM website w, user_ords_clients o
         WHERE w.id=pWebsiteId
           AND o.name=pEmail;

        pJwtEncKey:=l_jwt_enc_key;
        
        IF (l_contact_email=pEmail) THEN 
            l_scope:='owner';
        ELSE 
            FOR C IN (SELECT u.id FROM website w, users u WHERE w.id=pWebsiteId AND u.id=w.user_id AND u.email=pEmail) LOOP
                l_scope:='admin';
                l_user_id:=C.id;
            END LOOP;
        END IF;
        
        IF (l_user_id IS NULL) THEN
            SELECT id INTO pUserId FROM users WHERE email=pEmail;
        ELSE
            pUserId:=l_user_id;
        END IF;
        pScope:=NVL(l_scope,'visitor');

    END;

    /*
    ** ISSUE NEW PAIR OF TOKENS AND RETURN TO CLIENT
    */
    PROCEDURE issueJWTTokens(pWebsiteId IN website.id%type, pEmail IN users.email%type, pMenulist IN BOOLEAN, pUserId IN OUT users.id%type, pTokenIssued IN OUT refresh_token.token_issued%type) IS 
        l_new_token VARCHAR2 (4000);
        l_new_refresh VARCHAR2 (4000);
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_user_id users.id%type;
        l_scope VARCHAR2(7);
        l_menulist LONG;
        l_reports LONG;
        n PLS_INTEGER;
    BEGIN
        getKeyAndScope(pWebsiteId, pEmail, l_jwt_enc_key, l_user_id, l_scope);
        pUserId:=l_user_id;

        l_new_token:=apex_jwt.ENCODE (
            p_iss       => 'ORDS',
            p_sub       => pEmail,
            p_aud       => l_scope,
            p_jti       => l_user_id,
            p_iat_ts    => current_timestamp,
            p_exp_sec   => ACCESS_TOKEN_EXP,
            p_signature_key => UTL_RAW.cast_to_raw (l_jwt_enc_key)
        );

        INSERT INTO access_token(user_id, token) VALUES (l_user_id, dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_new_token,'AL32UTF8'), typ=>dbms_crypto.hash_sh256));

        l_new_refresh:=apex_jwt.ENCODE (
            p_iss       => 'ORDS',
            p_sub       => pEmail,
            p_aud       => l_scope,
            p_jti       => l_user_id,
            p_iat_ts    => current_timestamp,
            p_exp_sec   => REFRESH_TOKEN_EXP,
            p_signature_key => UTL_RAW.cast_to_raw (l_jwt_enc_key)
        );

        pTokenIssued:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_new_refresh,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

        APEX_JSON.open_object;
        APEX_JSON.write ('success', TRUE);
        APEX_JSON.write ('token', l_new_token);
        APEX_JSON.write ('refresh', l_new_refresh);
        IF (pMenulist) THEN
            FOR C IN (
                SELECT o.label, o.uri_template, o.method
                  FROM api_endpoint a, role r, ords_endpoints o
                 WHERE a.website_id=pWebsiteId 
                   AND a.role_id=r.id 
                   AND r.name=l_scope
                   AND a.template_id=o.template_id)
            LOOP
                l_reports:=NULL;
                IF (INSTR(C.uri_template,'visits/')>0) THEN
                    l_reports:='website|Visits;location|Locations';
                    SELECT COUNT(*) INTO n FROM website_article WHERE website_id=pWebsiteId;
                    IF (n>1) THEN
                        l_reports:=l_reports || ';page|Pages';
                        SELECT COUNT(*) INTO n FROM website_article WHERE website_id=pWebsiteId AND collection_type='BLOG';
                        IF (n>0) THEN
                            l_reports:=l_reports || ';blog|Blogs';
                        END IF;
                    END IF;
                END IF;
                l_menulist:=l_menulist || '<li><button type="button" data-endpoint="' || C.uri_template || '" data-method="' || C.method || '" data-reports="' || l_reports || '" class="button">' || C.label || '</button></li>';
            END LOOP;
            APEX_JSON.write ('menulist', l_menulist);
        END IF;
        APEX_JSON.close_object;
    END;

    /*
    ** VERIFY PASSCODE OR TOKEN ENTERED BY USER MATCHES DATA ORIGINALLY SENT BY EMAIL
    ** RETURN JWT ACCESS AND REFRESH TOKENS IF VERIFIED
    */
    PROCEDURE verifyAuthRequest(pWebsiteId IN website.id%type, pRequestType IN VARCHAR2, pUserId IN NUMBER, pVerifyData IN VARCHAR2, pStatus OUT NUMBER) IS
        l_email users.email%type;
        l_user_id users.id%type;
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_verify_data_hash RAW(32);
        l_token APEX_JWT.T_TOKEN;
        l_token_issued refresh_token.token_issued%type;
        l_scope VARCHAR2 (5);
    BEGIN
        /* 
        ** Check issued and received tokens are the same 
        */
        FOR C IN (SELECT email, DECODE(pRequestType,'magic',token,'passcode',passcode) original_data_hash FROM users WHERE id=pUserId) 
        LOOP
            l_email:=C.email;
            l_verify_data_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(pVerifyData,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);
            IF (l_verify_data_hash<>C.original_data_hash) THEN
                IF (pRequestType='passcode') THEN
                    sendMessage(FALSE,'Passcode does not match.');
                END IF;
                IF (pRequestType='magic') THEN
                    htp.p('<h1>Token expired. Login again.</h1>');
                END IF;
                RETURN;
            END IF;
        END LOOP;

        /* 
        ** Decode and Validate token has not expired etc.
        */
        CASE pRequestType
            WHEN 'magic' THEN
                getKeyAndScope(pWebsiteId, l_email, l_jwt_enc_key, l_user_id, l_scope);

                l_token:=apex_jwt.DECODE (
                    p_value   => pVerifyData,
                    p_signature_key   => UTL_RAW.cast_to_raw (l_jwt_enc_key)
                );

                BEGIN
                    apex_jwt.VALIDATE (p_token => l_token, p_iss => 'ORDS');
                    EXCEPTION 
                        WHEN VALUE_ERROR THEN
                            pck_core.log('Was VALUE_ERROR');
                            htp.p('<h1>Token expired. Login again</h1>');
                            --sendMessage(FALSE,pRequestType || ' token expired. Login again.');
                            RETURN;
                        WHEN OTHERS THEN 
                            pck_core.log(sqlerrm);
                            htp.p('<h1>' || sqlerrm || '</h1>');
                            RETURN;
                END;

                htp.p('<h1>Authenticated Successfully</h1>');
            
            WHEN 'passcode' THEN
                issueJWTTokens(pWebsiteId, l_email, TRUE, l_user_id, l_token_issued);
        END CASE;

        UPDATE users SET login_status='AUTH OK' WHERE id=pUserId;

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /* 
    ** CLIENT HAS SUBMITTED EMAIL ADDRESS AND WAITING TO BE LOGGED IN 
    ** IF LOGIN_STATUS FOR USER IS "AUTH OK" ISSUE NEW PAIR OF TOKENS
    */
    PROCEDURE checkAuthStatus(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_email users.email%type;
        l_user_id users.id%type;
        l_time_since_sent number;
        l_token_issued refresh_token.token_issued%type;
    BEGIN
        SELECT u.email, extract (minute from (current_timestamp - coalesce(u.updated_date, u.created_date)))
          INTO l_email, l_time_since_sent
          FROM users u, JSON_TABLE(pBodyText, '$' COLUMNS email) j
         WHERE u.email=j.email
           AND u.login_status='AUTH OK';

        issueJWTTokens(pWebsiteId, l_email, TRUE, l_user_id, l_token_issued);

        pStatus:=200;

        EXCEPTION 
            WHEN NO_DATA_FOUND THEN
                APEX_JSON.open_object;
                APEX_JSON.write ('success', TRUE);
                IF (l_time_since_sent * 60 >= ACCESS_TOKEN_EXP) THEN
                    APEX_JSON.write ('expired', TRUE);
                END IF;
                APEX_JSON.close_object;

            WHEN OTHERS THEN
                pck_core.log_error(pStatus);
    END;

    /*
    ** CREATE AND RETURN NEW ACCESS / REFRESH TOKEN PAIR
    ** CALLED WHEN CLIENT'S ACCESS TOKEN HAS EXPIRED
    ** CHECK REFRESH TOKEN HAS NOT PREVIOUSLY BEEN USED
    */
    PROCEDURE refreshAuthToken(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS
        l_header_value VARCHAR2(4000);
        l_refresh_token VARCHAR2(4000);
        l_token_used refresh_token.token_used%type;
        l_token_issued refresh_token.token_issued%type;
        l_token APEX_JWT.T_TOKEN;
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_scope VARCHAR2(7);
        l_email users.email%type;
        l_user_id VARCHAR2(6);
        n PLS_INTEGER;
    BEGIN
        l_header_value:=OWA_UTIL.get_cgi_env('Authorization');
        l_refresh_token:=SUBSTR(l_header_value, 8);
        l_email:=OWA_UTIL.get_cgi_env('email');

        getKeyAndScope(pWebsiteId, l_email, l_jwt_enc_key, l_user_id, l_scope);

        l_token:=apex_jwt.DECODE (p_value => l_refresh_token, p_signature_key => UTL_RAW.cast_to_raw(l_jwt_enc_key));

        apex_jwt.VALIDATE (p_token => l_token, p_iss => 'ORDS');

        -- Check refresh token has not previously been used 
        l_token_used:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_refresh_token,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);
        
        SELECT COUNT(*)
          INTO n
          FROM refresh_token
         WHERE token_used=l_token_used;

        -- Refresh token has already been used - return access denied - disable use of the refresh token issued with compromised token
        IF (n=1) THEN
            pck_core.log('Attempted refresh token reuse for '|| l_email || ' in ' || pWebsiteId);
            BEGIN 
                INSERT INTO refresh_token(user_id, token_used, token_issued)
                SELECT l_user_id, token_issued, 'COMPROMISED'
                  FROM refresh_token
                 WHERE token_used=l_token_used
                   AND token_issued<>'COMPROMISED';
                
                EXCEPTION WHEN dup_val_on_index THEN NULL;
            END;
            pck_core.sendAccessDenied(pStatus);
            RETURN;
        END IF;

        issueJWTTokens(pWebsiteId, l_email, FALSE, l_user_id, l_token_issued);

        INSERT INTO refresh_token(user_id, token_used, token_issued)
            VALUES (l_user_id, l_token_used, l_token_issued);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /* END OF JWT AUTHENTICATION METHOD */


    FUNCTION BASE64URL_DECODE(p_input IN varchar2) RETURN RAW DETERMINISTIC PARALLEL_ENABLE AS
        PRAGMA UDF;
        v_pad_count NUMBER(1);
    BEGIN
        -- This decodes Base64URL encoded value to RAW
        v_pad_count:= 4 - mod(length(p_input), 4);
        IF v_pad_count = 4 THEN
            v_pad_count:= 0;
        END IF;
        RETURN utl_encode.base64_decode(
            UTL_I18N.STRING_TO_RAW(
                replace(
                    replace(
                        rpad(p_input, length(p_input) + v_pad_count, '=')
                    , '_', '/')
                , '-', '+')
            , 'AL32UTF8')
        );
    END;

    FUNCTION MAKE_PKCS1_RSA_KEY(v_key_modulus_binary IN RAW /* raw(256) 2048 bits*/, v_public_key_exponent_binary IN RAW /* raw(3) 24 bits*/) RETURN RAW DETERMINISTIC PARALLEL_ENABLE AS
        PRAGMA UDF;
        c_tag_INTEGER raw(1) := hextoraw('02'); -- ASN.1 Universal Primitive Tag: 02 (INTEGER)
        c_tag_SEQUENCE raw(1) := hextoraw('30'); -- ASN.1 Universal Constructed Tag: 16 (SEQUENCE)
        c_2_LENGTH_BYTES_NEEDED raw(1) := hextoraw('82');
        c_3_VALUE_BYTES raw(1) := hextoraw('03');
        c_257_VALUE_BYTES raw(2) := hextoraw('0101');
        c_266_VALUE_BYTES raw(2) := hextoraw('010A');
        c_LEADING_ZERO_OF_INTEGER raw(1) := hextoraw('00');
    BEGIN
        -- From https://asktom.oracle.com/pls/apex/asktom.search?tag=plsql-only-ways-to-do-json-token-validation#9547303400346607729
        -- https://stackoverflow.com/questions/18039401/how-can-i-transform-between-the-two-styles-of-public-key-format-one-begin-rsa
        -- RSA (256) PKCS#1
        RETURN utl_encode.base64_encode(
            utl_raw.concat(
                r1 => c_tag_SEQUENCE -- 1 byte Tag
                , r2 => c_2_LENGTH_BYTES_NEEDED -- 1 byte Length Bytes
                , r3 => c_266_VALUE_BYTES -- 2 bytes Length
                , r4 => c_tag_INTEGER -- 1 byte Tag
                , r5 => c_2_LENGTH_BYTES_NEEDED -- 1 byte Length Bytes
                , r6 => c_257_VALUE_BYTES -- 2 bytes Length
                , r7 => c_LEADING_ZERO_OF_INTEGER -- 1 byte
                , r8 => v_key_modulus_binary -- 256 bytes
                , r9 => c_tag_INTEGER -- 1 byte Tag
                , r10 => c_3_VALUE_BYTES -- 1 byte Length
                , r11 => v_public_key_exponent_binary -- 3 bytes
            )
        );
    END;

    PROCEDURE UPDATE_JWKS_KEY_CACHE(p_iss IN VARCHAR2) AS
        PRAGMA AUTONOMOUS_TRANSACTION;
        v_lock_handle varchar2(128);
    BEGIN
        -- This updates local jwks_key_cache from Google IDP
        -- Executed when JWT signing key is not found in table jwks_key_cache
        --
        -- Avoid simultaneoud executions
        dbms_lock.allocate_unique('APEX_JWT_RS256_JWKS_UPDATE', v_lock_handle);
        IF dbms_lock.request(lockhandle=>v_lock_handle, timeout=>6, release_on_commit=>true) IN (0,4) THEN
            MERGE INTO jwks_key_cache t
            USING (
                WITH jwks AS 
                (
                    SELECT jwks_uri FROM JSON_TABLE(apex_web_service.make_rest_request(p_iss || '/.well-known/openid-configuration', 'GET'), '$' COLUMNS (jwks_uri))
                )
                SELECT kid, kty, e, alg, n
                FROM jwks, JSON_TABLE(apex_web_service.make_rest_request(jwks_uri, 'GET'), '$.keys[*]' COLUMNS (kid, kty, e, alg, n)) 
                ) s
            ON (s.kid = t.kid)
            WHEN NOT MATCHED THEN
                INSERT (kid, alg, kty, e, n, pkcs1)
                VALUES (s.kid, s.alg, s.kty, s.e, s.n,
                    make_pkcs1_rsa_key(base64url_decode(s.n), base64url_decode(s.e)));
            COMMIT;
        END IF;
    END;

    FUNCTION DECODE_AND_VALIDATE(p_jwt IN VARCHAR2, p_iss IN VARCHAR2, p_aud IN VARCHAR2, p_payload OUT VARCHAR2) RETURN boolean IS
        l_token apex_jwt.t_token;
        l_json_content json_object_t;
        l_kid jwks_key_cache.kid%type;
        l_key jwks_key_cache.pkcs1%type;
    BEGIN
        -- This will split and decode JWT token
        l_token := apex_jwt.decode(p_jwt);

        -- This will check if token has not expired and contains expected issued and audience
        BEGIN
            apex_jwt.validate(p_token=>l_token, p_iss=>p_iss, p_aud=>p_aud);
            EXCEPTION WHEN VALUE_ERROR THEN
                pck_core.log('Token validation error - check issuer or timeout');
                RETURN FALSE;
        END;

        -- Parsing the header to see if it is signed using RS256
        l_json_content:= json_object_t.parse(l_token.header);
        IF l_json_content.get_string('alg') != 'RS256' THEN
            pck_core.log('Only RS256 signed JWT tokens allowed');
            RETURN FALSE;
        END IF;

        -- Try fetching the key from cache.
       l_kid:= l_json_content.get_string('kid');
        FOR i IN 1..2 LOOP
            -- If first time key is not in cache, then update key cache from ISS.
            -- If second time key is also not present, then something is wrong and just fail.
            BEGIN
                SELECT pkcs1 INTO l_key FROM jwks_key_cache WHERE kid = l_kid;
                EXIT;
            EXCEPTION
                WHEN no_data_found THEN
                    IF i = 1 THEN
                        update_jwks_key_cache(p_iss);
                    ELSE
                        pck_core.log('Required key not found in JWKS cache table');
                        RETURN FALSE;
                    END IF;
            END;
        END LOOP;

        -- Validate the signature
        IF NOT DBMS_CRYPTO.VERIFY(
                src => UTL_I18N.STRING_TO_RAW(
                        SUBSTR(p_jwt, 1, INSTR(p_jwt, '.', 1, 2)-1) -- Operate on the input string itself, since v_token is already decoded, but JWT signs the encoded message
                    , 'al32utf8'), 
                sign => base64url_decode(l_token.signature),
                pub_key => l_key,
                pubkey_alg => dbms_crypto.KEY_TYPE_RSA,
                sign_alg => dbms_crypto.SIGN_SHA256_RSA)
        THEN
            pck_core.log('JWT signature failed cryptographic verification');
            RETURN FALSE;
        END IF;

        -- Signature is valid - return decoded JWT payload
        p_payload:= l_token.payload;
        RETURN TRUE;
    EXCEPTION
        WHEN others THEN
            -- In case of any exception, usually from APEX_JWT.validate or DBMS_CRYPTO - fail
            pck_core.log_error; RAISE;
    END;
    
    PROCEDURE subscribe IS
        l_header_value     VARCHAR2(4000);
        l_bearer_token     VARCHAR2(4000);
        l_token            APEX_JWT.T_TOKEN;
        l_valid_token       BOOLEAN;
        l_jwt_payload       VARCHAR2(2000);
        l_user_object      APEX_JSON.T_VALUES;
        l_email            users.email%type;
        l_iss              VARCHAR2(250):='https://accounts.google.com';
        l_google_client_id users.google_client_id%type;
    BEGIN
        --get the authorization header value from the request
        l_header_value := OWA_UTIL.get_cgi_env('Authorization');
        
        -- Check if the Authorization header exists and starts with 'Bearer '
        IF (l_header_value IS NULL OR INSTR(l_header_value, 'Bearer ') <> 1) THEN
            APEX_JSON.open_object;
            APEX_JSON.write ('success', FALSE);
            APEX_JSON.write ('message', 'Error - no bearer token in Authorization header');
            APEX_JSON.close_object;
            RETURN;
        END IF;


        /* Get Admmin user's Google client id name (repository owner) */
        SELECT u.google_client_id
          INTO l_google_client_id
          FROM apex_workspace_apex_users w, users u 
         WHERE w.email=u.email
           AND w.is_admin='Yes'
           FETCH FIRST ROW ONLY;

        -- Extract and validate the JWT token
        l_bearer_token := SUBSTR(l_header_value, 8);
        IF (decode_and_validate(l_bearer_token, l_iss, l_google_client_id, l_jwt_payload)) THEN
            apex_json.parse (l_user_object, l_jwt_payload);
            l_email:=apex_json.get_varchar2 (p_path => 'email', p_values => l_user_object);

            MERGE INTO users t
            USING (SELECT email, name FROM JSON_TABLE(l_jwt_payload, '$' COLUMNS (email, name)) ) s
            ON (s.email = t.email)
            WHEN MATCHED THEN UPDATE
                SET t.last_login_date=current_timestamp, t.last_login_provider=l_iss
            WHEN NOT MATCHED THEN
                INSERT (email, name, last_login_date, last_login_provider)
                VALUES (s.email, s.name, current_timestamp, l_iss);
            COMMIT;

            APEX_JSON.open_object;
            APEX_JSON.write ('success', TRUE);
            APEX_JSON.write ('email', l_email);
            APEX_JSON.close_object;
        ELSE
            APEX_JSON.open_object;
            APEX_JSON.write ('success', FALSE);
            APEX_JSON.write ('message', 'Error - invalid token');
            APEX_JSON.close_object;
        END IF;
    END;
  
    /*
    ** This procedure is called in the Post-authentication of Social Sign-in processing.
    ** First-time authenticated users are inserted into the USERS table
    */
    PROCEDURE insert_user_login(p_auth_provider IN VARCHAR2, p_email IN VARCHAR2, p_name IN VARCHAR2) IS
        l_user_id users.id%type;
        --l_user_name users.name%type;
        --l_client_tz users.client_tz%type;
        --l_client_tz_session users.client_tz%type:=apex_util.get_session_state('APP_CLIENT_TZ');
        l_workspace_id apex_workspaces.workspace_id%type;
    BEGIN
        SELECT workspace_id
          INTO l_workspace_id
          FROM apex_workspaces
         WHERE workspace = (
                SELECT workspace
                FROM apex_applications
                WHERE application_id = APEX_APPLICATION.G_FLOW_ID
                );
        /*
        FOR C IN (
            SELECT apex_session_id
              FROM apex_workspace_sessions
             WHERE workspace_id = l_workspace_id
               AND user_name = APEX_APPLICATION.G_USER
               AND apex_session_id != APEX_APPLICATION.G_INSTANCE
        )
        LOOP
            apex_session.delete_session(p_session_id => C.apex_session_id);
            DELETE session_data WHERE session_id=C.apex_session_id;
        END LOOP;
        */

        INSERT INTO user_login(auth_provider,email,name,apex_session_id) VALUES (p_auth_provider, p_email, p_name, APEX_APPLICATION.G_INSTANCE);

        BEGIN
            SELECT id
              INTO l_user_id
              FROM users 
             WHERE email=p_email;

            UPDATE users 
               SET name=p_name, last_login_date=current_timestamp, last_login_provider=p_auth_provider 
             WHERE id=l_user_id;

            EXCEPTION WHEN NO_DATA_FOUND 
            THEN
                INSERT INTO users(email, name, last_login_date, last_login_provider) 
                VALUES (p_email, p_name, current_timestamp, p_auth_provider);
        END;
        COMMIT;
    END;    

    PROCEDURE post_auth_facebook IS
    BEGIN
        insert_user_login('Facebook', apex_json.get_varchar2('email'), apex_json.get_varchar2('name'));
        EXCEPTION WHEN OTHERS THEN pck_core.log_error; RAISE;
    END;

    PROCEDURE post_auth_google IS
    BEGIN
        insert_user_login('Google', apex_json.get_varchar2('email'), apex_json.get_varchar2('name'));
        EXCEPTION WHEN OTHERS THEN pck_core.log_error; RAISE;
    END; 

    /* 
    ** Linked-In API has separate entry points for profile name and email addres.
    ** Support very rude. API is bollocks. Gave up on Linkedin but keep the code just in case
    */
    /*
    PROCEDURE post_auth_linkedin IS
        l_name users.name%type;
        l_email users.email%type;
        l_clob CLOB;
        l_names owa_cookie.vc_arr;
        l_vals owa_cookie.vc_arr;
        l_num_vals INTEGER;
    BEGIN
        pck_cms.log('starting'); 
        l_name:=apex_json.get_varchar2('localizedFirstName') || ' ' || apex_json.get_varchar2('localizedLastName');

        apex_web_service.g_request_headers.delete();
        pck_cms.log('Before make_rest_request');
        l_clob := apex_web_service.make_rest_request(
            p_url => 'https://api.linkedin.com/v2/emailAddress?q=members&projection=(elements*(handle~))',
            p_http_method => 'GET',
            p_credential_static_id => 'Linkedin',
            p_token_url => 'https://www.linkedin.com/oauth/v2/accessToken');
        pck_cms.log(l_clob);

        SELECT emailAddress INTO l_email 
          FROM JSON_TABLE(l_clob format json, '$'
            COLUMNS emailAddress VARCHAR2(100)  PATH '$.elements[0]."handle~".emailAddress');    

        apex_application.g_user:=l_email;

        insert_user_login('Linkedin', l_email, l_name);        
    END;   
    */

    /*
    **  Gave up on Azure too - can't remember why though.
    */
    /*
    PROCEDURE post_auth_azure IS
    BEGIN
        insert_user_login('Azure', apex_json.get_varchar2('userPrincipalName'), apex_json.get_varchar2('givenName') || ' ' || apex_json.get_varchar2('surname'));
    END; 
    */

END PCK_SEC;
/