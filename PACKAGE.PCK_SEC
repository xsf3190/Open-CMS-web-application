CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_SEC" AS 
    --
    REFRESH_TOKEN_EXP CONSTANT PLS_INTEGER:=(24*60*60)*90; --90 DAYS
    --
    ACCESS_TOKEN_EXP CONSTANT PLS_INTEGER:=60*5; -- 5 MINUTES
    --
    TYPE t_session_data IS RECORD (
        url VARCHAR2(100),
        timezone VARCHAR2(30),
        user_id NUMBER,
        scope VARCHAR2(7),
        iss VARCHAR2(10),
        sub VARCHAR2(100),
        cld_cloud_name users.cld_cloud_name%type,
        cld_upload_preset users.cld_upload_preset%type,
        client_secret user_ords_clients.client_secret%type,
        cpu_used_session NUMBER
    );

    g_session_user_id NUMBER;
    g_website_id NUMBER;
    --
    FUNCTION addMenuButton(pUriTemplate IN VARCHAR2, pMethod IN VARCHAR2, pLabel IN VARCHAR2, pData IN VARCHAR2 DEFAULT null) RETURN VARCHAR2;
    --
    PROCEDURE checkAuthStatus(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE createAuthRequest(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    FUNCTION getSessionData(pWebsiteId IN website.id%type) RETURN t_session_data;
    --
    PROCEDURE getUserinfoForm(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE logout(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE refreshAuthToken(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE updateCpuUsed(pWebsiteid IN website.id%type, pUserid IN users.id%type, pCpuUsedStart IN website_cpu.cpu_used%type);
    --
    PROCEDURE verifyAuthRequest(pWebsiteId IN website.id%type, pRequestType IN VARCHAR2, pUserId IN NUMBER, pVerifyData IN VARCHAR2, pStatus OUT NUMBER);
    --
END PCK_SEC;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_SEC" AS

    PROCEDURE getUserinfoForm(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_json JSON_OBJECT_T;
        l_delete_button VARCHAR2(2000);
        l_header VARCHAR2(200);
        l_footer VARCHAR2(500);
        l_article LONG;
        l_svg icon.svg%type;
        l_viewbox icon.viewbox%type;
        n PLS_INTEGER:=0;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        l_header:='<span>Domains hosted for ' || l_session_data.sub || '</span>';

        l_article:=
        '<div class="table-wrapper" tabindex="0" role="region" aria-labelledby="table-caption">' ||
            '<table>' ||
                '<caption id="table-caption">Platform CPU Usage for current month</caption>' ||
                '<tr><th>Domain Name</th><th>Last Activity</th><th>Pages</th><th>Media</th><th class="align-right">Usage</th><th>Delete</th></tr>';

        FOR C IN (
            WITH cpu AS
            (
                SELECT website_id, SUM(cpu_used) cpu_used FROM website_cpu WHERE period_start=TRUNC(current_timestamp,'mm') GROUP BY website_id
            ),
            activity AS
            (
                SELECT id, MAX(updated_date) as last_activity
                FROM
                (
                    SELECT w.id, w.nav_updated_date, w.logo_updated_date, w.favicon_updated_date, wa.header_html_updated, wa.footer_html_updated, 
                        a.updated_date as article_updated_date, wf.updated_date as font_updated_date, 
                        NVL(w.netlify_last_published_custom, w.netlify_last_published) last_published,
                    (SELECT MAX(visit_date) FROM page_hit WHERE website_id=w.id) as visit_date
                    FROM website w, website_article wa, article a, website_font wf
                    WHERE wa.website_id=w.id
                    AND wa.article_id=NVL(a.parent_id,a.id)
                    AND wf.website_id=w.id
                    AND w.domain_name<>'es-modules'
                )
                UNPIVOT  (updated_date FOR COL IN (visit_date,last_published,nav_updated_date, logo_updated_date, favicon_updated_date, header_html_updated, footer_html_updated, article_updated_date, font_updated_date))
                GROUP BY id
            )
            SELECT w.id, w.domain_name, w.contact_email, a.last_activity, ROUND(RATIO_TO_REPORT(c.cpu_used) OVER ()*100,1) cpu_used_pct,
                (SELECT COUNT(*) FROM article a, website_article wa WHERE wa.website_id=w.id AND (a.id=wa.article_id OR a.parent_id=wa.article_id)) nb_pages,
                (SELECT COUNT(*) FROM article a, website_article wa, asset_used au WHERE wa.website_id=w.id AND au.article_id=a.id AND (a.id=wa.article_id OR a.parent_id=wa.article_id)) nb_images
             FROM website w,  cpu c, activity a
             WHERE w.id=c.website_id(+)
               AND w.id=a.id
               AND w.domain_name<>'es-modules'
             ORDER BY domain_name
        ) LOOP
            IF (l_session_data.scope='admin' OR C.contact_email=l_session_data.sub) THEN
                IF (NVL(C.cpu_used_pct,0)=0) THEN
                    n:=n+1;
                    IF (n=1) THEN
                        SELECT svg, viewbox INTO l_svg, l_viewbox FROM icon WHERE id='delete';
                    END IF;
                    l_delete_button:=
                    '<button type="button" class="button delete" data-websiteid="' || C.id || '" data-button-variant="round-icon" style="--button-border-color:red" aria-labelledby="delete-' || C.id || '">' || 
                        '<span id="delete-' || C.id || '" hidden>Delete unused website '|| C.domain_name || '</span>'||
                        '<svg aria-hidden="true" width="1em" viewBox="' || l_viewbox || '" style="fill:red">' || l_svg || '</svg>' ||         
                    '</button>';
                ELSE
                    l_delete_button:=null;
                END IF;
                l_article:=l_article ||
                '<tr>' || 
                    '<td>' || C.domain_name || '</td>' || 
                    '<td>' || apex_util.get_since(C.last_activity, false) || '</td>' || 
                    '<td>' || C.nb_pages || '</td>' || 
                    '<td>' || C.nb_images || '</td>' || 
                    '<td class="align-right">' || NVL(C.cpu_used_pct,0) || '%</td>' || 
                    '<td class="align-center">' || l_delete_button || '</td>' || 
                '</tr>';
            END IF;
        END LOOP;
        l_article:=l_article ||
        '</table></div>';

        l_footer:=
        '<div class="flex-items">' ||
            '<button type="button" class="button logout">Log Out</button>' ||
            '<span aria-live="polite"></span>' ||
            '<span class="loader icon"></span>'||
        '</div>';

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header', l_header);
        l_json.put('article', l_article);
        l_json.put('footer', l_footer);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;

    PROCEDURE logout(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_json JSON_OBJECT_T;
        l_dropdown LONG;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);
        
        DELETE refresh_token WHERE website_id=pWebsiteId AND user_id=l_session_data.user_id;
        
        FOR C IN (
            SELECT o.label, o.uri_template, o.method
              FROM ords_endpoints o
             WHERE INSTR(o.uri_template,'authenticate')>0
        ) LOOP
            l_dropdown:=l_dropdown ||
            pck_sec.addMenuButton(C.uri_template, C.method, C.label);
        END LOOP;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('dropdown',l_dropdown);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;

    /*
    ** JWT Issuer claim
    */
    FUNCTION getJWTIssuer(pWebsiteId IN website.id%type) RETURN VARCHAR2 IS
    BEGIN
        RETURN ('ORDS.' || pWebsiteid);
    END;

    /*
    ** GET SESSION DETAILS OF JWT RESOURCE ACCESS REQUEST
    */
    FUNCTION getSessionData(pWebsiteId IN website.id%type) RETURN t_session_data IS
        l_session_data t_session_data;
        l_bearer_token varchar2(500);
        l_token APEX_JWT.T_TOKEN;

    BEGIN
    -- pck_core.log('getSessionData:'||OWA_UTIL.get_cgi_env('HTTP_USER_AGENT'));
        l_session_data.cpu_used_session:=dbms_utility.get_cpu_time();

        l_bearer_token := SUBSTR(OWA_UTIL.get_cgi_env('Authorization'), 8);
        /*
        **  "sub" in the token is the user's email address which we need to get user_ords_clients.client_secret
        */
        l_token:=apex_jwt.DECODE(p_value=>l_bearer_token, p_signature_key=>null);

        FOR C IN (
            SELECT j.iss, j.sub, j.aud, j.iat, j.exp, j.jti, j.cld_cloud_name, j.cld_upload_preset, o.client_secret
              FROM JSON_TABLE(l_token.payload, '$' COLUMNS(iss, sub, aud, iat, exp, jti, cld_cloud_name, cld_upload_preset)) j, user_ords_clients o
             WHERE j.sub=o.name
        ) LOOP

            l_token:=apex_jwt.DECODE(p_value=>l_bearer_token, p_signature_key=>UTL_RAW.cast_to_raw(C.client_secret));

            apex_jwt.VALIDATE (p_token=>l_token, p_iss=>getJWTIssuer(pWebsiteid));

            /* Token valid */
            l_session_data.user_id:=C.jti;
            l_session_data.scope:=C.aud;
            l_session_data.iss:=C.iss;
            l_session_data.sub:=C.sub;
            l_session_data.cld_cloud_name:=C.cld_cloud_name;
            l_session_data.cld_upload_preset:=C.cld_upload_preset;
            l_session_data.client_secret:=C.client_secret;
            l_session_data.timezone:=OWA_UTIL.get_cgi_env('timezone');
            l_session_data.url:=OWA_UTIL.get_cgi_env('url');
        END LOOP;

        g_session_user_id:=l_session_data.user_id;
        g_website_id:=pWebsiteid;

        RETURN (l_session_data);

    END;

    /*
    ** USAGE ACCOUNTING BASED ON CPU USED IN SESSION
    */
    PROCEDURE updateCpuUsed(pWebsiteid IN website.id%type, pUserid IN users.id%type, pCpuUsedStart IN website_cpu.cpu_used%type) IS
        l_cpu_used_session website_cpu.cpu_used%type;
        l_owner VARCHAR2(30);
        l_calling_procedure website_cpu.calling_procedure%type;
        l_lineno NUMBER;
        l_caller VARCHAR2(30);
        n PLS_INTEGER;
    BEGIN
        l_cpu_used_session:=dbms_utility.get_cpu_time()-pCpuUsedStart;

        owa_util.who_called_me(l_owner, l_calling_procedure, l_lineno, l_caller);
        
        UPDATE website_cpu
           SET cpu_used=cpu_used+l_cpu_used_session, 
               call_count=call_count+1,
               updated_date=current_timestamp
         WHERE website_id=pWebsiteid
           AND period_start=TRUNC(current_timestamp,'mm')
           AND calling_procedure=l_calling_procedure;
        
        n:=SQL%ROWCOUNT;

        IF (n=0) THEN
            INSERT INTO website_cpu (website_id, period_start, calling_procedure, user_id, cpu_used)
                VALUES (pWebsiteid, TRUNC(current_timestamp,'mm'), l_calling_procedure, pUserid, l_cpu_used_session);
        END IF;
    END;

    /*
    ** USER SUBMITS EMAIL ADDRESS TO BE AUTHENTICATED
    ** - CREATE RANDOM PASSCODE OR MAGIC LINK AND SEND TO EMAIL ADDRESS
    */
    PROCEDURE createAuthRequest(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_email users.email%type;
        l_url varchar2(100);
        l_request_type VARCHAR2(10);
        l_user_id users.id%type;
        l_ords_user_id user_ords_clients.id%type;
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_token VARCHAR2(4000);
        l_digits   CONSTANT VARCHAR2(10) := '0123456789';
        l_passcode VARCHAR2(6);
        l_passcode_hash raw(32);
        l_token_hash raw(32);
        l_sendmail_subject VARCHAR2(200);
        l_sendmail_body LONG;
        l_sendmail_link VARCHAR2(500);
        l_return_message VARCHAR2(100);
        l_json JSON_OBJECT_T;
    BEGIN
    -- pck_core.log('createAuthRequest:'||OWA_UTIL.get_cgi_env('HTTP_USER_AGENT'));
        SELECT o.client_secret, LOWER(j.email) email, j.url, j.request_type, NVL(u.id,0) user_id, NVL(o.id,0) ords_user_id
          INTO l_jwt_enc_key, l_email, l_url, l_request_type, l_user_id, l_ords_user_id
          FROM user_ords_clients o, users u, JSON_TABLE(pBodyText, '$' COLUMNS email, url, request_type) j
         WHERE o.name(+)=LOWER(j.email)
           AND u.email(+)=LOWER(j.email);

        IF (l_ords_user_id=0) THEN
            OAUTH.create_client(
                p_name            => l_email,
                p_grant_type      => 'client_credentials',
                p_owner           => 'mark.russellbrown@gmail.com',
                p_support_email   => 'mark.russellbrown@gmail.com',
                p_description     => 'Provide secure secret in order to encrypt JWT tokens',
                p_privilege_names => 'api_priv'
            );
            SELECT client_secret INTO l_jwt_enc_key FROM user_ords_clients WHERE name=l_email;
        END IF;
        

        CASE l_request_type
            WHEN 'magic' THEN
                l_token:=apex_jwt.ENCODE (
                    p_iss       => getJWTIssuer(pWebsiteid),
                    p_sub       => l_email,
                    p_aud       => 'xxx',
                    p_iat_ts    => current_timestamp,
                    p_exp_sec   => ACCESS_TOKEN_EXP,
                    p_signature_key => UTL_RAW.cast_to_raw (l_jwt_enc_key)
                );
                l_token_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_token,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

                IF (l_user_id=0) THEN
                    INSERT INTO users (email, login_status, token) VALUES (l_email, 'AUTH WAITING', l_token_hash) RETURNING id INTO l_user_id;
                ELSE
                    UPDATE users SET login_status='AUTH WAITING', token=l_token_hash, updated_date=current_timestamp WHERE id=l_user_id;
                END IF;
                
                l_sendmail_link:=pck_core.getRestUrl || 'authenticate/' || pWebsiteId || '?request=magic&user=' || l_user_id || '&verify=' || l_token;
                l_sendmail_body:=pck_email.emailTemplate(l_request_type, l_url, l_sendmail_link);

            WHEN 'passcode' THEN
                FOR i IN 1..6 LOOP
                    l_passcode:=l_passcode || SUBSTR(l_digits,FLOOR(DBMS_RANDOM.VALUE(1, LENGTH(l_digits) + 1)),1);
                END LOOP;

                /* Store passcode as hash for later comparison with user-entered value */
                l_passcode_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_passcode,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

                IF (l_user_id=0) THEN
                    INSERT INTO users (email, login_status, passcode) 
                    VALUES (l_email, 'AUTH WAITING', l_passcode_hash)
                    RETURNING id INTO l_user_id;
                ELSE
                    UPDATE users SET login_status='AUTH WAITING', passcode=l_passcode_hash, updated_date=current_timestamp WHERE id=l_user_id;
                END IF;

                l_sendmail_body:=pck_email.emailTemplate(l_request_type, l_url, l_passcode);
        END CASE;

        pck_email.sendmail(l_email, 'Login to ' || l_url, l_sendmail_body);

        IF (l_request_type='passcode') THEN
            l_return_message:='Passcode sent to your inbox';
        ELSE
            l_return_message:='Link sent to your inbox';
        END IF;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('message',l_return_message);
        l_json.put('userid',l_user_id);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** DETERMINE ROLE (I.E. SCOPE) OF LOGGED ON USER
    */
    FUNCTION getScope(pWebsiteId IN website.id%type, pEmail IN VARCHAR2) RETURN VARCHAR2
    IS
        l_scope VARCHAR2(7);
    BEGIN
        FOR C IN (
            SELECT 'admin' as scope 
              FROM apex_workspace_apex_users
             WHERE is_admin='Yes'
               AND email=pEmail
        ) LOOP
            l_scope:=C.scope;
        END LOOP;

        IF (l_scope IS NULL) THEN
            FOR C IN (
                SELECT 'owner' as scope
                  FROM website w
                 WHERE w.id=pWebsiteId
                   AND w.contact_email=pEmail) LOOP
                l_scope:=C.scope;
            END LOOP;
        END IF;
        
        RETURN (NVL(l_scope,'visitor'));
    END;

    FUNCTION addMenuButton(pUriTemplate IN VARCHAR2, pMethod IN VARCHAR2, pLabel IN VARCHAR2, pData IN VARCHAR2 DEFAULT null) RETURN VARCHAR2 IS
        TYPE tt_editor_symbols IS RECORD(
            symbol VARCHAR2(10),
            color  VARCHAR2(20)
        );
        /*
        ** Table of REST endpoints and corresponding icons / colors
        */

        TYPE t_editor_symbols IS TABLE OF tt_editor_symbols INDEX BY VARCHAR2(30);
        l_editor_symbols t_editor_symbols:=t_editor_symbols(
            'authenticate'=>tt_editor_symbols('&#x1F464;','blue'),
            'change-url'=>tt_editor_symbols('&#9883;','brown'),
            'create-website'=>tt_editor_symbols('&#43;','brown'),
            'ecology'=>tt_editor_symbols('&#127807;',''),
            'edit-codepen'=>tt_editor_symbols('&#9917;','crimson'),
            'edit-content'=>tt_editor_symbols('&#9998;','crimson'),
            'edit-pages'=>tt_editor_symbols('&#128214;','brown'),
            'link-domain'=>tt_editor_symbols('&#9884;','brown'),
            'message'=>tt_editor_symbols('&#9993;','brown'),
            'publish-website'=>tt_editor_symbols('&#127760;','darkgreen'), 
            'userinfo'=>tt_editor_symbols('&#x1F464;','blue'),
            'visits'=>tt_editor_symbols('&#128202;','rebeccapurple'),
            'page-options'=>tt_editor_symbols('&#x2637;','rebeccapurple')
        );
        l_route VARCHAR2(30):=SUBSTR(pUriTemplate,1,INSTR(pUriTemplate,'/')-1);
    BEGIN
        RETURN ('<button type="button" role="menuitem" tabindex="-1"' ||
                ' data-endpoint="' || pUriTemplate || '"' ||
                ' data-method="' ||pMethod || '"' ||
                CASE WHEN pData IS NOT NULL THEN pData END ||
                '>' || 
                '<span aria-hidden="true" style="color:' || l_editor_symbols(l_route).color || '">' || l_editor_symbols(l_route).symbol || '</span>' ||
                pLabel || 
                '</button>');
    END;

    FUNCTION buildMenulist(pWebsiteid IN NUMBER, pScope IN VARCHAR2, pEmail IN VARCHAR2) RETURN VARCHAR2 IS
        l_html CLOB;
        FUNCTION addMenuitem(pEndpoint IN VARCHAR2, pColor IN VARCHAR2, pIcon IN VARCHAR2, pText IN VARCHAR2, pParentId IN NUMBER DEFAULT NULL) RETURN VARCHAR2 IS
        BEGIN
            RETURN (
                '<button type="button" role="menuitem" tabindex="-1" ' || 
                    'data-endpoint="' || pEndpoint || '"' ||
                    CASE WHEN pParentId IS NOT NULL THEN
                    ' data-parent-id="' || pParentId || '"' 
                    END ||
                    '>' ||
                    '<span aria-hidden="true" style="color:' || pColor || '">' || pIcon || '</span>' || pText ||
                    '</button>'
            );
        END;
    BEGIN
        l_html:=l_html || addMenuitem('userinfo/:ID', 'blue', 'üë§', pEmail);
        l_html:=l_html || addMenuitem('edit-content/:ID/:PAGE', 'blue', '‚úé', 'Edit Content');
        l_html:=l_html || addMenuitem('upload-media/:ID/:PAGE', 'pink', '‚úé', 'Upload Media');
        l_html:=l_html || addMenuitem('publish-website/:ID', 'darkgreen', 'üåê', 'Publish Website');
        l_html:=l_html || addMenuitem('visits/:ID', 'rebeccapurple', 'üìä', 'Website Analytics');
        l_html:=l_html || addMenuitem('logo/:ID', 'red', 'üìä', 'Logo');
        l_html:=l_html || addMenuitem('edit-pages/:ID/:PAGE', 'brown', 'üìñ', 'Manage Pages');
        l_html:=l_html || addMenuitem('page-options/:ID/:PAGE', 'red', '‚ò∑', 'Page Options');
        
        FOR C IN (SELECT article_id, navigation_label FROM website_article WHERE website_id=pWebsiteid AND collection_type<>'N/A') LOOP
            l_html:=l_html || addMenuitem('new-collection-item/:ID', 'cyan', 'B', 'New ' || C.navigation_label, C.article_id);
        END LOOP;

        l_html:=l_html || addMenuitem('create-website/:ID/:PAGE', 'red', 'üåê', 'New Website');
        l_html:=l_html || addMenuitem('edit-codepen/:ID/:PAGE', 'cyan', '‚öΩ', 'Codepen Editor');

        RETURN (l_html);
    END;

    /*
    ** BUILD FINGERPRINT FOR CLIENT DEVICE AND LOCATION
    */
    FUNCTION getFingerprint(pRemoteAddr IN OUT VARCHAR2, pUserAgent IN OUT VARCHAR2, pTimezone IN OUT VARCHAR2) RETURN RAW IS
        l_fingerprint fingerprint.fingerprint%type;
        l_remote_addr security_log.remote_addr%type:=OWA_UTIL.get_cgi_env ('REMOTE_ADDR');
        l_user_agent  security_log.user_agent%type:=OWA_UTIL.get_cgi_env ('sec-ch-ua') || OWA_UTIL.get_cgi_env ('sec-ch-ua-platform');
        l_timezone security_log.timezone%type:=OWA_UTIL.get_cgi_env ('timezone');
    BEGIN
        l_fingerprint:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_remote_addr || l_user_agent || l_timezone,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);
        pRemoteAddr:=l_remote_addr;
        pUserAgent:=l_user_agent;
        pTimezone:=l_timezone;
        RETURN (l_fingerprint);
    END;

    /*
    ** USER AUTHENTICATED SUCCESSFULLY 
    ** - CREATE FINGERPRINT
    ** - ISSUE NEW PAIR OF TOKENS TO RETURN TO CLIENT
    */
    PROCEDURE issueJWTTokens(pWebsiteId IN website.id%type, pEmail IN users.email%type, pUserId IN users.id%type, pCldCloudName IN users.cld_cloud_name%type, pCldUploadPreset IN users.cld_upload_preset%type) IS 
        l_new_token VARCHAR2 (4000);
        l_new_refresh VARCHAR2 (4000);
        l_new_refresh_hash refresh_token.token_issued%type;
        l_url VARCHAR2(1000);
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_issuer VARCHAR2(15);
        l_scope VARCHAR2(7);
        l_cld_cloud_name users.cld_cloud_name%type:=pCldCloudName;
        l_cld_upload_preset users.cld_upload_preset%type:=pCldUploadPreset;

        l_fingerprint fingerprint.fingerprint%type;
        l_remote_addr fingerprint.remote_addr%type;
        l_user_agent  fingerprint.user_agent%type;
        l_timezone fingerprint.timezone%type;

        l_json JSON_OBJECT_T;
        n PLS_INTEGER;
    BEGIN
    for i in 1..nvl(owa.num_cgi_vars, 0) loop
        if (instr(owa.cgi_var_name(i),'sec-ch-ua')>0) then
            pck_core.log(owa.cgi_var_name(i) || ': ' || owa.cgi_var_val(i));
        end if;
    end loop;
    -- pck_core.log('issueJWTTokens:'||OWA_UTIL.get_cgi_env('HTTP_USER_AGENT'));
        l_fingerprint:=getFingerprint(l_remote_addr, l_user_agent, l_timezone);

        UPDATE fingerprint 
           SET last_used_date=current_timestamp
         WHERE user_id=pUserid
           AND fingerprint=l_fingerprint;

        n:=sql%rowcount;
        IF (n=0) THEN
            INSERT INTO fingerprint(user_id, fingerprint, last_used_date, remote_addr, user_agent, timezone)
            VALUES (pUserid, l_fingerprint, current_timestamp, l_remote_addr, l_user_agent, l_timezone);
        END IF;

        SELECT client_secret 
          INTO l_jwt_enc_key
          FROM user_ords_clients
         WHERE name=pEmail;

        IF (l_cld_cloud_name IS NULL) THEN
            SELECT u.cld_cloud_name, u.cld_upload_preset
              INTO l_cld_cloud_name, l_cld_upload_preset
              FROM users u, apex_workspace_apex_users w
             WHERE w.email=u.email
               AND w.is_admin='Yes'
             ORDER BY date_created
             FETCH FIRST ROW ONLY;
        END IF;

        l_issuer:=getJWTIssuer(pWebsiteid);
        l_scope:=getScope(pWebsiteId,pEmail);

        l_new_refresh:=apex_jwt.ENCODE (
            p_iss       => l_issuer,
            p_sub       => pEmail,
            p_aud       => l_scope,
            p_jti       => pUserId,
            p_iat_ts    => current_timestamp,
            p_exp_sec   => REFRESH_TOKEN_EXP,
            p_other_claims => '"cld_cloud_name":' || apex_json.stringify(l_cld_cloud_name) || ',"cld_upload_preset":' || apex_json.stringify(l_cld_upload_preset),
            p_signature_key => UTL_RAW.cast_to_raw (l_jwt_enc_key)
        );

        l_new_token:=apex_jwt.ENCODE (
            p_iss       => l_issuer,
            p_sub       => pEmail,
            p_aud       => l_scope,
            p_jti       => pUserId,
            p_iat_ts    => current_timestamp,
            p_exp_sec   => ACCESS_TOKEN_EXP,
            p_signature_key => UTL_RAW.cast_to_raw (l_jwt_enc_key)
        );

        l_new_refresh_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_new_refresh,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

        INSERT INTO refresh_token(user_id, website_id, token_issued) VALUES (pUserId, pWebsiteId, l_new_refresh_hash);
        
        APEX_JSON.open_object;
        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('token', l_new_token);
        l_json.put('refresh', l_new_refresh);
        l_json.put('menulist', buildMenulist(pWebsiteId, l_scope, pEmail));
        apex_util.prn(l_json.to_clob,false);
    END;

    /*
    ** VERIFY PASSCODE OR TOKEN SENT BY USER MATCHES THOSE ORIGINALLY SENT BY EMAIL
    ** SET STATUS IN USERS TABLE TO "AUTH OK"
    **  - FOR VERIFIED PASSCODE, RETURN JWT ACCESS AND REFRESH TOKENS
    **  - FOR VERIFIED TOKEN, MESSAGE IS SENT IN NEW WINDOWS TAB
    */
    PROCEDURE verifyAuthRequest(pWebsiteId IN website.id%type, pRequestType IN VARCHAR2, pUserId IN NUMBER, pVerifyData IN VARCHAR2, pStatus OUT NUMBER) IS
        l_email users.email%type;
        l_domain users.domain%type;
        l_cld_cloud_name users.cld_cloud_name%type;
        l_cld_upload_preset users.cld_upload_preset%type;
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_verify_data_hash RAW(32);
        l_token APEX_JWT.T_TOKEN;
        l_token_issued refresh_token.token_issued%type;
        l_url VARCHAR2(1000);
        l_url_base VARCHAR2(100);
        l_sendmail_body LONG;
    BEGIN
        /* 
        ** Check issued and received tokens are the same 
        */        
        FOR C IN (
            SELECT u.email, u.cld_cloud_name, u.cld_upload_preset, DECODE(pRequestType,'magic',token,'passcode',passcode) original_data_hash, o.client_secret 
              FROM users u, user_ords_clients o
             WHERE u.id=pUserId
               AND o.name=u.email
        ) 
        LOOP
            l_email:=C.email;
            l_cld_cloud_name:=C.cld_cloud_name;
            l_cld_upload_preset:=C.cld_upload_preset;
            l_verify_data_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(pVerifyData,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);
            l_jwt_enc_key:=C.client_secret;
            IF (l_verify_data_hash<>C.original_data_hash) THEN
                CASE pRequestType
                    WHEN 'passcode' THEN
                        APEX_JSON.open_object;
                        APEX_JSON.write ('success', FALSE);
                        APEX_JSON.write ('message', 'Passcode does not match');
                        APEX_JSON.close_object;
                    WHEN 'magic' THEN
                        htp.p('<h1>Token expired. Login again.</h1>');
                END CASE;
                RETURN;
            END IF;
        END LOOP;
        
        CASE pRequestType
            /* 
            ** Decode and Validate the token returned from email client
            */
            WHEN 'magic' THEN
                
                l_token:=apex_jwt.DECODE (
                    p_value   => pVerifyData,
                    p_signature_key   => UTL_RAW.cast_to_raw (l_jwt_enc_key)
                );

                BEGIN
                    apex_jwt.VALIDATE (p_token => l_token, p_iss => getJWTIssuer(pWebsiteid));
                    EXCEPTION 
                        WHEN VALUE_ERROR THEN
                            htp.p('<h1>Token expired or not current. Check you sent from most recent message received. Login again</h1>');
                            RETURN;
                        WHEN OTHERS THEN 
                            pck_core.log(sqlerrm);
                            htp.p('<h1>' || sqlerrm || '</h1>');
                            RETURN;
                END;
                
                htp.p('<h1 style="font-family:system-ui;font-size:2rem;color:rebeccapurple">Authenticated Successfully</h1>');
            
            WHEN 'passcode' THEN
                issueJWTTokens(pWebsiteId, l_email, pUserId, l_cld_cloud_name, l_cld_upload_preset);
        END CASE;

        UPDATE users SET login_status='AUTH OK', last_login_date=current_timestamp, last_login_provider='JWT' WHERE id=pUserId;

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /* 
    ** CLIENT HAS ClICKED ON MAGIC LINK ON MESSAGE RECEIVED IN THEIR INBOX,
    ** CAUSING THEIR USERS.LOGIN_STATUS TO BE UPDATED TO "AUTH OK".
    ** NOW WE CAN ISSUE NEW PAIR OF TOKENS
    */
    PROCEDURE checkAuthStatus(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_email users.email%type;
        l_user_id users.id%type;
        l_cld_cloud_name users.cld_cloud_name%type;
        l_cld_upload_preset users.cld_upload_preset%type;
        l_time_since_sent number;
        l_token_issued refresh_token.token_issued%type;
        l_json JSON_OBJECT_T;
    BEGIN
    -- pck_core.log('checkAuthStatus:'||OWA_UTIL.get_cgi_env('HTTP_USER_AGENT'));
        SELECT u.id, LOWER(u.email), u.cld_cloud_name, u.cld_upload_preset, extract (minute from (current_timestamp - coalesce(u.updated_date, u.created_date)))
          INTO l_user_id, l_email, l_cld_cloud_name, l_cld_upload_preset, l_time_since_sent
          FROM users u, JSON_TABLE(pBodyText, '$' COLUMNS email) j
         WHERE u.email=j.email
           AND u.login_status='AUTH OK';

        issueJWTTokens(pWebsiteId, l_email, l_user_id, l_cld_cloud_name, l_cld_upload_preset);

        pStatus:=200;

        EXCEPTION 
            WHEN NO_DATA_FOUND THEN
                l_json:= new JSON_OBJECT_T;
                l_json.put('success',true);
                IF (l_time_since_sent * 60 >= ACCESS_TOKEN_EXP) THEN
                    l_json.put('expired', TRUE);
                END IF;
                apex_util.prn(l_json.to_clob,false);

            WHEN OTHERS THEN
                pck_core.log_error(pStatus);
    END;

    /*
    ** CREATE NEW PAIR OF ACCESS / REFRESH TOKENS
    ** CALLED WHEN CLIENT'S ACCESS TOKEN HAS EXPIRED
    ** REFRESH TOKEN SENT IN THE REQUEST MUST EXIST FOR ONLY ONE USER/WEBSITE
    */
    PROCEDURE refreshAuthToken(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS 
        l_refresh_token VARCHAR2(4000);
        l_refresh_token_hash refresh_token.token_issued%type;
        l_token_issued refresh_token.token_issued%type;
        l_token APEX_JWT.T_TOKEN;
        l_new_token VARCHAR2 (4000);
        l_new_refresh VARCHAR2 (4000);
        l_fingerprint fingerprint.fingerprint%type;
        l_access_denied_user security_log.user_id%type;
        l_access_denied_message security_log.message%type;
        l_aud VARCHAR2(7);
        l_remote_addr fingerprint.remote_addr%type;
        l_user_agent  fingerprint.user_agent%type;
        l_timezone fingerprint.timezone%type;
        l_json JSON_OBJECT_T;
        n PLS_INTEGER;
    BEGIN
        l_refresh_token:=SUBSTR(OWA_UTIL.get_cgi_env('Authorization'), 8);

        l_fingerprint:=getFingerprint(l_remote_addr, l_user_agent, l_timezone);

        /* Decode refresh token without client secret to get the email (i.e. sub claim) */
        l_token:=apex_jwt.DECODE(p_value=>l_refresh_token, p_signature_key=>null);

        FOR C IN (
            SELECT j.iss, j.sub, j.jti, j.cld_cloud_name, j.cld_upload_preset, o.client_secret
              FROM JSON_TABLE(l_token.payload, '$' COLUMNS(iss, sub, jti, cld_cloud_name, cld_upload_preset)) j, user_ords_clients o
             WHERE j.sub=o.name
        ) LOOP
            /* Check user has previously authenticated with their current fingerprint  */
            SELECT COUNT(*) 
              INTO n
              FROM fingerprint
             WHERE user_id=C.jti
               AND fingerprint=l_fingerprint;
            IF (n=0) THEN
                l_access_denied_user:=C.jti;
                l_access_denied_message:='Access denied from unknown device or location. Login again';
                EXIT;
            END IF;

            /* Decode refresh token WITH client secret */
            l_token:=apex_jwt.DECODE(p_value=>l_refresh_token, p_signature_key=>UTL_RAW.cast_to_raw(C.client_secret));

            /* Check token not expired and issuer is valid */
            apex_jwt.VALIDATE (p_token=>l_token, p_iss=>getJWTIssuer(pWebsiteid));

            /* Check token has been registered for user */
            l_token_issued:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_refresh_token,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);
            SELECT COUNT(*) INTO n 
              FROM refresh_token 
             WHERE token_issued=l_token_issued 
               AND website_id=pWebsiteid 
               AND user_id=C.jti;
            IF (n=0) THEN
                l_access_denied_user:=C.jti;
                l_access_denied_message:='Invalid refresh token';
                EXIT;
            END IF;

            -- Delete old token before replacing it
            DELETE refresh_token 
             WHERE website_id=pWebsiteid 
               AND user_id=C.jti;

            /* User scope may have changed since last refresh cycle */
            l_aud:=getScope(pWebsiteid, C.sub);

            -- Create new refresh and access tokens
            l_new_token:=apex_jwt.ENCODE (
                p_iss       => C.iss,
                p_sub       => C.sub,
                p_aud       => l_aud,
                p_jti       => C.jti,
                p_iat_ts    => current_timestamp,
                p_exp_sec   => ACCESS_TOKEN_EXP,
                p_signature_key => UTL_RAW.cast_to_raw (C.client_secret)
            );

            l_new_refresh:=apex_jwt.ENCODE (
                p_iss       => C.iss,
                p_sub       => C.sub,
                p_aud       => l_aud,
                p_jti       => C.jti,
                p_iat_ts    => current_timestamp,
                p_exp_sec   => REFRESH_TOKEN_EXP,
                p_other_claims => '"cld_cloud_name":' || apex_json.stringify(C.cld_cloud_name) ||
                                  ',"cld_upload_preset":' || apex_json.stringify(C.cld_upload_preset),
                p_signature_key => UTL_RAW.cast_to_raw (C.client_secret)
            );

            -- Rotate the user's refresh token for the website
            INSERT INTO refresh_token(website_id, user_id, token_issued)
                VALUES (pWebsiteid, C.jti, dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_new_refresh,'AL32UTF8'), typ=>dbms_crypto.hash_sh256));
        END LOOP;

        IF (l_access_denied_user IS NULL) THEN
            l_json:= new JSON_OBJECT_T;
            l_json.put('success',true);
            l_json.put('token', l_new_token);
            l_json.put('refresh', l_new_refresh);
            apex_util.prn(l_json.to_clob,false);
            pStatus:=200;
        ELSE
            INSERT INTO security_log(website_id, user_id, token, message, remote_addr, user_agent, timezone) 
            VALUES (pWebsiteid, l_access_denied_user, l_refresh_token, l_access_denied_message, l_remote_addr, l_user_agent, l_timezone);
            pStatus:=401;
        END IF;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

END PCK_SEC;
/