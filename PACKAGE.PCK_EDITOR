CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_EDITOR" is
    --
    PROCEDURE deletePageOption(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE deployWebsite(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE getDeployWebsiteForm(pWebsiteId IN website.id%type,pStatus OUT NUMBER);
    --
    PROCEDURE getPageOptionsForm(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE getDeploymentStatus(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE getEditorContent(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE getPages(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE publishPageOption(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE setAssetUsed(pArticleId IN article.id%type, pHtml IN OUT NOCOPY CLOB);
    --
    PROCEDURE updateEditorContent(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE updatePageOption(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE updatePages(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_EDITOR" is

    TYPE tt_labels IS RECORD (
            article_id website_article.article_id%type,
            navigation_label website_article.navigation_label%type,
            display_order website_article.display_order%type
        );
    TYPE t_labels IS TABLE OF tt_labels;

    /*
    ** ARTICLE LINK OPTIONS SELECT LIST
    */
    FUNCTION getLinkOptions(pParentId IN article.id%type, pArticleid IN article.id%type) RETURN VARCHAR2 IS
        l_options CLOB;
        n PLS_INTEGER:=0;
    BEGIN
        FOR C IN (
            WITH checked AS
            (
                SELECT article_id_2 FROM article_link WHERE article_id_1=pArticleid
            )
            SELECT a.id, a.title, l.article_id_2
              FROM article a, checked l
             WHERE a.parent_id=pParentId
               AND a.id<>pArticleid
               AND a.id=l.article_id_2(+)
             ORDER BY a.title
        ) LOOP
            n:=n+1;
            l_options:=l_options ||
            '<input type="checkbox" name="link" id="link' || n || '" value="' || C.id || '"' || CASE WHEN C.article_id_2 IS NOT NULL THEN ' checked' END || '>' || 
            '<label for="link' || n || '">' || C.title || '</label>';
        END LOOP;
        RETURN (l_options);
    END;

    /*
    ** CREATE HTML FOR PAGE OPTIONS FORM
    */
    PROCEDURE getPageOptionsForm(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_header LONG;
        l_article CLOB;
        l_footer LONG;
        l_favicon VARCHAR2(10);
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        /* Form depends on Page type - i.e. collection item, collection index, normal */

        FOR C IN (
            SELECT wa.collection_type, wa.navigation_label, a.id, a.parent_id, a.title, a.excerpt, w.logo, w.favicon, w.logo_inline_size, w.img_border_radius
              FROM website w, website_article wa, article a
             WHERE w.id=pWebsiteId 
               AND wa.website_id=w.id 
               AND a.id=pArticleId
               AND wa.article_id=NVL(a.parent_id,a.id)
        ) LOOP
            CASE 
                /* Updating Page Options for a selected BLOG */
                WHEN (C.parent_id IS NOT NULL AND C.collection_type='BLOG') THEN
                    l_header:='<h4>'|| INITCAP(C.navigation_label) || ': ' || C.title || '</h4>';
                    l_article:=
                    '<input type="hidden" name="dml" value="update">' ||
                    '<p>This form enables you to:</p>' || 
                    '<ol>' ||
                        '<li>Edit the excerpt that appears on the index page and in the "related Posts" section</li>' ||
                        '<li>Create links to "related ' || C.navigation_label || ' Posts"</li>' ||
                        '<li>Permanently delete this Post</li>' ||
                    '</ol>' ||
                    '<fieldset>' ||
                        '<legend>Excerpt (maximum 160 characters)</legend>' ||
                        '<textarea name="excerpt" style="min-width:50%" maxlength="160">' || C.excerpt || '</textarea>' ||
                    '</fieldset>' ||
                    '<fieldset>' ||
                        '<legend>Link to related '|| C.navigation_label || 's</legend>' ||
                        '<p><a href="#skip">Skip to next</a></p>' ||
                            getLinkOptions(C.parent_id, pArticleId) ||
                    '</fieldset>' ||
                    '<p id="skip"></p>';
                /* Creating new BLOG item */
                WHEN (C.parent_id IS NULL AND C.collection_type<>'N/A') THEN
                    l_header:='<h4>Create New '|| INITCAP(C.navigation_label) || '</h4>';
                    l_article:=
                    '<p>Use this form to create a new ' || C.navigation_label ||
                    '</p>' ||
                    '<input type="hidden" name="dml" value="insert">' ||
                    '<div>' ||
                        '<label for="title">Enter title (maximum 100 characters)</label>' ||
                        '<input type="text" maxlength="100" id="title" name="title" value="">' ||
                    '</div>' ||
                    '<div>' ||
                        '<label for="excerpt">Enter excerpt (maximum 160 characters)</label>' ||
                        '<textarea id="excerpt" name="excerpt" style="min-width:50%" maxlength="160"></textarea>' ||
                    '</div>';
                ELSE
                    l_favicon:=regexp_substr(C.favicon,'<text.+?>(.+?)</text>',1,1,null,1);
                    l_header:='<h4>Page Options</h4>';
                    l_article:=
                    '<p>Use this form to:</p>' || 
                    '<ol>' ||
                        '<li>Set a Logo</li>' ||
                        '<li>Set a Favicon. Either SVG or emoji (default is ðŸŒ¿)</li>' ||
                        '<li>Set image round borders</li>' ||
                    '</ol>' ||
                    '<fieldset>' ||
                        '<legend>Logo (maximum 4000 characters. SVG only)</legend>' ||
                        '<textarea name="logo" style="min-width:100%;font-family:monospace;font-size:var(--step--2)" maxlength="4000">' || C.logo || '</textarea>' ||
                        '<input type="range" id="logo-inline-size" name="logo-inline-size" min="3" max="22" value="' || regexp_substr(C.logo_inline_size,'\d+') || '">' ||
                        '<label for="logo-inline-size">Adjust Width</label>' ||
                    '</fieldset>' ||
                    '<fieldset>' ||
                        '<legend>Favicon (maximum 4000 characters. SVG or emoji)</legend>' ||
                        '<textarea name="favicon" style="min-width:100%;font-family:monospace;font-size:var(--step--2)" maxlength="4000">' || NVL(l_favicon,C.favicon) || '</textarea>' ||
                    '</fieldset>' ||
                    '<fieldset>' ||
                        '<legend>Set image round borders</legend>' ||
                        '<input type="range" id="img-border-radius" name="img-border-radius" min="0" max="30" value="' || regexp_substr(C.img_border_radius,'\d+') || '">' ||
                    '</fieldset>';
            END CASE;

            l_footer:=
            '<button type="button" class="button publish" data-processing="Publishing editor site, wait a few seconds...">' ||
                'PUBLISH' ||
            '</button>' ||
            '<span aria-live="polite"></span>' ||
            '<span class="loader icon"></span>' ||
            CASE WHEN (C.parent_id IS NOT NULL AND C.collection_type='BLOG') THEN
            '<button type="button" class="button delete" style="--button-bg:red">' ||
                'DELETE ' || UPPER(C.navigation_label) ||
            '</button>'
            END;
        END LOOP;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header',l_header);
        l_json.put('article',l_article);
        l_json.put('footer',l_footer);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /* 
    **  UPDATE PAGE OPTIONS
    */
    PROCEDURE updatePageOption(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_json JSON_OBJECT_T;
        l_name VARCHAR2(30);
        l_value VARCHAR2(4000);
        l_emoji_svg VARCHAR2(200):='<svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 100 100"><text y=".9em" font-size="90">#EMOJI#</text></svg>';
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT name, value 
          INTO l_name, l_value
          FROM JSON_TABLE(pBodyText, '$' COLUMNS (name, value));

        FOR C IN (
            SELECT wa.collection_type, wa.navigation_label, a.id, a.parent_id, a.title, a.excerpt
              FROM website_article wa, article a
             WHERE wa.website_id=pWebsiteId 
               AND a.id=pArticleId
               AND wa.article_id=NVL(a.parent_id,a.id)
        ) LOOP
            CASE 
                WHEN (C.parent_id IS NOT NULL AND C.collection_type='BLOG') THEN
                    IF (l_name='excerpt') THEN
                        UPDATE article SET excerpt=l_value, updated_date=current_timestamp  WHERE id=pArticleid;
                        UPDATE article SET updated_date=current_timestamp  WHERE id=C.parent_id;
                    END IF;
                    IF (l_name='link') THEN
                        UPDATE article SET updated_date=current_timestamp WHERE id=pArticleid;
                        BEGIN
                            INSERT INTO article_link(article_id_1, article_id_2) VALUES (pArticleid, l_value);
                            EXCEPTION WHEN DUP_VAL_ON_INDEX THEN
                                DELETE article_link WHERE article_id_1=pArticleid AND article_id_2=l_value;
                        END;
                    END IF;
                WHEN (C.parent_id IS NULL AND C.collection_type='N/A') THEN
                    IF (l_name='logo') THEN
                        UPDATE website SET logo=l_value, logo_updated_date=current_timestamp WHERE id=pWebsiteid;
                    END IF;
                    IF (l_name='favicon') THEN
                        /* Build svg if emoji character */
                        IF (LENGTH(l_value)=1) THEN
                            l_value:=REPLACE(l_emoji_svg,'#EMOJI#',l_value);
                        END IF;
                        UPDATE website SET favicon=l_value, favicon_updated_date=current_timestamp WHERE id=pWebsiteid;
                    END IF;
                    IF (l_name='logo-inline-size') THEN
                        UPDATE website SET logo_inline_size=l_value || 'cqi', css_updated_date=current_timestamp WHERE id=pWebsiteid;
                    END IF;
                    IF (l_name='img-border-radius') THEN
                        UPDATE website SET img_border_radius=l_value || 'px', css_updated_date=current_timestamp WHERE id=pWebsiteid;
                    END IF;
            END CASE;
        END LOOP;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /* 
    **  DELETE COLLECTION ITEM
    */
    PROCEDURE deletePageOption(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_json JSON_OBJECT_T;
        l_link VARCHAR2(200);
        n PLS_INTEGER;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        /* FORCE NEXT DEPLOYMENT TO REBUILD INDEX PAGE */
        FOR C IN (
            SELECT w.netlify_site_id, apex_string_util.get_slug(wa.navigation_label) navigation_label_slug, a.parent_id
              FROM website w, website_article wa, article a
             WHERE w.id=pWebsiteId
               AND wa.website_id=w.id
               AND wa.article_id=a.parent_id
               AND a.id=pArticleId
        ) LOOP
            UPDATE article SET updated_date=current_timestamp WHERE id=C.parent_id;
            DELETE article WHERE id=pArticleid;
            n:=SQL%ROWCOUNT;
            pck_core.log('DELETE ARTICLE '||pArticleid||' rowcount:'||n);
            /* Deploy editor site with new collection item */ 
            pck_deploy.runDeployment(pWebsiteId=>pWebsiteId, pSiteid=>C.netlify_site_id, pRestUrl=>pck_core.getRestURL);
            /* Link to new collection item */
            l_link:='<a href="/' || C.navigation_label_slug || '" rel="noopener noreferrer" aria-describedby="open-in-same-tab">' || C.navigation_label_slug || ' Deleted. Go to new index</a>';
        END LOOP;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('link',l_link);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /* 
    **  PUBLISH FROM PAGE OPTIONS DIALOG
    */
    PROCEDURE publishPageOption(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_json JSON_OBJECT_T;
        l_dml VARCHAR2(6); -- [update|insert]
        l_title VARCHAR2(160);
        l_title_slug VARCHAR2(160);
        l_excerpt article.excerpt%type;
        l_link VARCHAR2(250);
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        /*
        IF NOT (pck_deploy.CanDeploy(pWebsiteid)) THEN
            l_json:= new JSON_OBJECT_T;
            l_json.put('success',true);
            l_json.put('message','NOTHING NEW TO PUBLISH');
            apex_util.prn(l_json.to_clob,false);
            pStatus:=200;
            RETURN;
        END IF;
        */

        SELECT dml, title, apex_string_util.get_slug(title), excerpt
          INTO l_dml, l_title, l_title_slug, l_excerpt
          FROM JSON_TABLE(pBodyText, '$' COLUMNS(dml, title, excerpt));

        /* VALIDATION */
        IF (l_dml='insert' AND l_title IS NULL) THEN
            l_json:= new JSON_OBJECT_T;
            l_json.put('success',true);
            l_json.put('message','TITLE MISSING');
            apex_util.prn(l_json.to_clob,false);
            pStatus:=200;
            RETURN;
        END IF;

        /*
        **  1. INSERT article for new collection item
        **  2. Deploy editor site with new collection item
        **  3. Return URL of new collection item
        */
        IF (l_dml='insert') THEN
            INSERT INTO article(id, parent_id, author_user_id, title, excerpt, body_html) 
            VALUES (seq_article.nextval, pArticleId, l_session_data.user_id, l_title, l_excerpt, '<h2>' || l_title || '</h2>');

            UPDATE article SET updated_date=current_timestamp WHERE id=pArticleId;

            FOR C IN (
                SELECT w.netlify_site_id, apex_string_util.get_slug(wa.navigation_label) navigation_label_slug
                  FROM website w, website_article wa 
                 WHERE w.id=pWebsiteId
                   AND wa.website_id=w.id
                   AND wa.article_id=pArticleId
            ) LOOP
                /* Deploy editor site with new collection item */ 
                pck_deploy.runDeployment(pWebsiteId=>pWebsiteId, pSiteid=>C.netlify_site_id, pResturl=>pck_core.getResturl);
                /* Link to new collection item */
                l_link:='<a href="/' || C.navigation_label_slug || '/' || l_title_slug || '" rel="noopener noreferrer" aria-describedby="open-in-same-tab">Open new ' || C.navigation_label_slug || '</a>';
            END LOOP;
        END IF;
        IF (l_dml='update') THEN
            /* pArticleid is the blog item */
            FOR C IN (
                SELECT w.netlify_site_id, apex_string_util.get_slug(a.title) title_slug, apex_string_util.get_slug(wa.navigation_label) navigation_label_slug
                  FROM website w, article a , website_article wa
                 WHERE w.id=pWebsiteId
                   AND a.id=pArticleId
                   AND wa.website_id=w.id
                   AND wa.article_id=a.parent_id
            ) LOOP
                /* Deploy editor site with updated collection item */ 
                pck_deploy.runDeployment(pWebsiteId=>pWebsiteId, pSiteid=>C.netlify_site_id, pResturl=>pck_core.getResturl);
                /* Link to new collection item */
                l_link:='<a href="/' || C.navigation_label_slug || '/' || C.title_slug || '" rel="noopener noreferrer" aria-describedby="open-in-same-tab">Open updated ' || C.navigation_label_slug || '</a>';
            END LOOP;
        END IF;

        /* PAGE OPTIONS */
        IF (l_dml IS NULL) THEN
            FOR C IN (
                SELECT w.netlify_site_id, CASE WHEN wa.display_order>1 THEN apex_string_util.get_slug(wa.navigation_label) END navigation_label_slug
                  FROM website w, website_article wa 
                 WHERE w.id=pWebsiteId
                   AND wa.website_id=w.id
                   AND wa.article_id=pArticleId
            ) LOOP
                pck_deploy.runDeployment(pWebsiteId=>pWebsiteId, pSiteid=>C.netlify_site_id, pResturl=>pck_core.getResturl);
                l_link:='<a style="color:green; font-weight:800" href="/' || C.navigation_label_slug || '" rel="noopener noreferrer" aria-describedby="open-in-same-tab">PUBLISHED SUCCESSFULLY</a>';
            END LOOP;
        END IF;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('link',l_link);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /* 
    **  GET DEPLOY WEBSITE FORM DATA
    */
    PROCEDURE getDeployWebsiteForm(pWebsiteId IN website.id%type,pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_header LONG;
        l_article LONG;
        l_footer LONG;
        n PLS_INTEGER;
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        /* 
        ** Check no other session currently deploying - simultaneous deployments not allowed on Netlify free plan 
        */
        FOR C IN (SELECT job_name FROM user_scheduler_running_jobs WHERE job_name LIKE 'DEPLOY_%') LOOP
            l_json:= new JSON_OBJECT_T;
            l_json.put('success',true);
            l_json.put('header', '<small>Problem publishing website</small>');
            l_json.put('article', '<p>Someone else is currently deploying on this shared platform</p>');
            l_json.put('footer', '<small>Try again later</small>');
            apex_util.prn(l_json.to_clob,false);
            RETURN;
        END LOOP;

        /*
        ** CHECK WHICH DEPLOY REQUEST IS VALID
        */
       
        FOR C IN (
                SELECT netlify_last_published, netlify_last_published_custom, netlify_site_id, netlify_site_id_custom
                  FROM website
                 WHERE id=pWebsiteid
        ) LOOP
            IF (pck_deploy.canDeploy(pWebsiteid)) THEN
                l_header:='<h4>PUBLISH EDITOR SITE</h4>';
                n:=0;
                FOR C1 IN (
                    SELECT wa.navigation_label || NVL2(a.parent_id,'/'||a.title,null) item
                      FROM website w, website_article wa, article a
                     WHERE w.id=pWebsiteid
                       AND wa.website_id=w.id
                       AND wa.article_id=NVL(a.parent_id,a.id)
                       AND (w.nav_updated_date>w.netlify_last_published OR a.updated_date>w.netlify_last_published OR wa.header_html_updated>w.netlify_last_published OR wa.footer_html_updated>w.netlify_last_published)
                    ORDER BY wa.display_order, a.title nulls FIRST
                ) LOOP
                    n:=n+1;
                    IF (n=1) THEN
                        l_article:=
                        '<p>Content changes since last published on ' || TO_CHAR(C.netlify_last_published,'fmdd Mon yyyy, hh24:mi') || '</p>' ||
                         '<ul>';
                    END IF;
                    l_article:=l_article || '<li>' || C1.item || '</li>';
                END LOOP;
                IF (n>0) THEN
                    l_article:=l_article || '</ul>';
                END IF;

                l_footer:=
                '<div class="flex-items space-apart">' ||
                    '<button type="button" class="button publish" data-env="TEST">PUBLISH</button>' ||
                    '<span aria-live="polite"></span>' ||
                    '<span class="loader icon"></span>' ||
                '</div>';
            ELSIF (C.netlify_last_published>NVL(C.netlify_last_published_custom, C.netlify_last_published)) THEN
                l_header:='<h4>PUBLISH LIVE SITE</h4>';
                l_article:=
                '<p>Editor site is published with all content up to date.</p>' ||
                '<p>Click GO LIVE button</p>';
                l_footer:=
                '<div class="flex-items space-apart">' ||
                    '<button type="button" class="button publish" data-env="LIVE">GO LIVE</button>' ||
                    '<span aria-live="polite"></span>' ||
                    '<span class="loader icon"></span>' ||
                '</div>';
            ELSIF (C.netlify_last_published_custom IS NULL) THEN
                l_header:='<h4>PUBLISH LIVE SITE</h4>';
                l_article:=
                '<p>Editor site is published with all content up to date.</p>' ||
                '<p>Click GO LIVE button</p>';
                l_footer:=
                '<div class="flex-items space-apart">' ||
                    '<button type="button" class="button publish" data-env="LIVE">GO LIVE</button>' ||
                    '<span aria-live="polite"></span>' ||
                    '<span class="loader icon"></span>' ||
                '</div>';
            ELSE
                l_header:='ALL CONTENT PUBLISHED';
                l_article:='<p>NOTHING NEW TO PUBLISH</p>';
                l_footer:='';
            END IF;
        END LOOP;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header', l_header);
        l_json.put('article', l_article);
        l_json.put('footer', l_footer);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;

    /* 
    **  DEPLOY WEBSITE
    */
    PROCEDURE deployWebsite(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_env VARCHAR2(4);
        l_link VARCHAR2(100);
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT NVL(env,'TEST') INTO l_env FROM JSON_TABLE(pBodyText, '$' COLUMNS(env));

        CASE l_env
            WHEN 'LIVE' THEN
                pck_deploy.deployLiveSite(pWebsiteid);
                FOR C IN (
                    SELECT domain_name FROM website WHERE id=pWebsiteId
                ) LOOP 
                    l_link:='<a href="https://' || C.domain_name || '">' || C.domain_name || ' is LIVE</a>';
                END LOOP;
            WHEN 'TEST' THEN
                pck_deploy.deployEditorSite(pWebsiteid);
                l_link:='<a href="https://' || l_session_data.url || '">Reload website</a>';
        END CASE;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('link',l_link);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        pck_sec.updateCpuUsed(pWebsiteId=>pWebsiteId, pUserid=>l_session_data.user_id, pCpuUsedStart=>l_session_data.cpu_used_session);

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    /*
     ** Get deployment status from website_deploy logging table - called from Javascript every 3 seconds
     ** Have to poll Netlify deployment status 
     */
    PROCEDURE getDeploymentStatus(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS 
        l_site_id website_deploy.site_id%type;
        l_content CLOB;
        l_clob CLOB;
        l_deployment_complete BOOLEAN:=FALSE;
        l_netlify_status website_deploy.status%type;
        l_netlify_deploy_id website.netlify_deploy_id%type;
        l_state VARCHAR2(200);
        l_job_name VARCHAR2(30);
        l_session_data pck_sec.t_session_data;
        TYPE t_deploy_log IS RECORD(
            id website_deploy.id%type,
            log_time website_deploy.log_time%type,
            message website_deploy.message%type,
            status website_deploy.status%type);
        TYPE tt_deploy_log IS TABLE OF t_deploy_log;
        l_deploy_log tt_deploy_log;          
        l_env VARCHAR2(4);
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        l_env:=pck_hosting.getEnvFromURL(l_session_data.url);

        SELECT netlify_deploy_id, CASE WHEN l_env='TEST' THEN netlify_site_id ELSE netlify_site_id_custom END
          INTO l_netlify_deploy_id, l_site_id
          FROM website
         WHERE id=pWebsiteId;

        /* Get latest deployment status if started */
        IF (l_netlify_deploy_id IS NOT NULL) THEN
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'sites/'|| l_site_id || '/deploys/' || l_netlify_deploy_id, pMethod=>'GET', pData=>l_clob);
            SELECT 'Netlify is ' || state INTO l_state FROM JSON_TABLE(l_clob, '$' COLUMNS (state)); 
            pck_deploy.logDeployment(pWebsiteId, l_site_id, l_state, pLogTime=>current_date);
        END IF;

        SELECT id, log_time, message, status
          BULK COLLECT INTO l_deploy_log
          FROM website_deploy 
         WHERE website_id=pWebsiteId
           AND site_id=l_site_id
           AND sent_ind='N'
         ORDER BY id;

        /* Now return list of all deployment status messages to client */
        FOR i IN 1..l_deploy_log.COUNT
        LOOP
            l_content:=l_content ||'<tr' || CASE WHEN l_deploy_log(i).status='NOK' THEN ' style="color:red"' END || '><td>' || TO_CHAR(l_deploy_log(i).log_time AT TIME ZONE l_session_data.timezone,'hh24:mi:ss') || '</td><td>' || l_deploy_log(i).message || '</td></tr>'; 
            IF (l_deploy_log(i).message='Netlify is ready') THEN 
                l_content:=l_content ||'<tr style="color:green"><td>' || TO_CHAR(l_deploy_log(i).log_time AT TIME ZONE l_session_data.timezone,'hh24:mi:ss') || '</td><td>SUCCESSFUL DEPLOYMENT</td></tr>'; 
                l_deployment_complete:=TRUE;
            END IF;
        END LOOP;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('completed',l_deployment_complete);
        l_json.put('content', l_content);
        apex_util.prn(l_json.to_clob,false);

        FORALL i IN 1..l_deploy_log.COUNT
            UPDATE website_deploy SET sent_ind='Y' WHERE id=l_deploy_log(i).id;

        /*
        ** DELETE ANY PREVIOUD DEPLOYMENTS
        */ 
        IF (l_deployment_complete) THEN
            

            l_job_name:=dbms_scheduler.generate_job_name('DELETE_');
            dbms_scheduler.create_job(
                job_name   => l_job_name,
                job_type   =>'STORED_PROCEDURE',
                job_action =>'pck_deploy.runDelete',
                number_of_arguments=>3,
                start_date=>systimestamp
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 1,
                argument_value => l_session_data.user_id
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 2,
                argument_value => pWebsiteId
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 3,
                argument_value => l_site_id
            );
            dbms_scheduler.enable(l_job_name);    
        END IF;

        pStatus:=200;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error(pStatus);
    END;

    /* 
    **  BUILD FORM CONTROLS TO EDIT WEBSITE PAGES
    */
    PROCEDURE getPages(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_header LONG;
        l_article CLOB;
        l_footer LONG;
        l_nav LONG;
        n PLS_INTEGER;
        n1 PLS_INTEGER;
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);
        
        l_header:='<small>Add / Delete pages, change navigation labels.</small>';

        FOR C IN (
            SELECT wa.article_id current_page, wa.collection_type, wa.navigation_label
              FROM website_article wa, article a
             WHERE wa.website_id=pWebsiteId
               AND wa.article_id=NVL(a.parent_id,a.id)
               AND a.id=pArticleId
        ) LOOP
            l_nav:=pck_deploy.buildNav(pWebsiteId, C.current_page);
            IF (l_nav IS NULL) THEN
                l_nav:='<nav><ul><li><a href="/" aria-current="page" data-id="' || pArticleId || '" data-collection="' || C.collection_type || '">' || C.navigation_label || '</a></li></ul></nav>';
            ELSE
                n:=INSTR(l_nav,C.current_page);
                n1:=INSTR(l_nav,'>',n);
                l_nav:=SUBSTR(l_nav,1,n1-1) || ' aria-current="page"' || SUBSTR(l_nav,n1);
            END IF;
        END LOOP;

        l_article:=
            l_nav ||
            '<fieldset class="flex-items">' ||
                '<div>' ||
                    '<label for="navigation-label">Navigation Label</label>' ||
                    '<input type="text" id="navigation-label" name="navigation_label" maxlength="30" value="">' ||
                '</div>' ||
                '<fieldset class="flex-items">' ||
                    '<legend>Page Type</legend>' ||
                    '<label for="collection_type-normal">Normal' ||
                    '<input type="radio" id="collection_type-normal" name="collection_type" value="N/A"></label>' ||
                    '<label for="collection_type-blog">Blog' ||
                    '<input type="radio" id="collection_type-blog" name="collection_type" value="BLOG"></label>' ||
                    '<label for="collection_type-portfolio">Portfolio' ||
                    '<input type="radio" id="collection_type-portfolio" name="collection_type" value="PORTFOLIO"></label>' ||
                '</fieldset>' ||
                '<button type="button" class="button add-page">Add Page</button>' ||
                '<button type="button" class="button delete-page">Delete Page</button>' ||
                '<button type="button" class="button deploy">Save Changes</button>' ||
                '<span class="errors" style="color:red"></span>' ||
            '</fieldset>';

        l_footer:='<small>TAKE CARE when migrating an existing website to keep the same label names</small>';

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header',l_header);
        l_json.put('article',l_article);
        l_json.put('footer',l_footer);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    /* 
    **  UPDATE WEBSITE_ARTICLE TABLE WITH ALL CHANGES 
    */
    PROCEDURE updatePages(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_body_text CLOB:=pBodyText;
        TYPE tt_arr IS RECORD(
            display_order website_article.display_order%type,
            article_id website_article.article_id%type,
            navigation_label website_article.navigation_label%type,
            collection_type website_article.collection_type%type
        );
        TYPE t_arr IS TABLE OF tt_arr;
        l_arr t_arr;
        l_current_date TIMESTAMP:=current_timestamp;
        TYPE t_article_id IS TABLE OF website_article.article_id%type;
        l_article_id t_article_id;
        l_old_nav VARCHAR2(4000);
        l_new_nav VARCHAR2(4000);
        l_do_deploy BOOLEAN;
        l_clob CLOB;
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        l_old_nav:=pck_deploy.buildNav(pWebsiteid);

        SELECT display_order, article_id, navigation_label, collection_type
          BULK COLLECT INTO l_arr
          FROM JSON_TABLE(pBodyText, '$[*]' COLUMNS (display_order FOR ORDINALITY, article_id, navigation_label, collection_type));

       -- Create ARTICLE and WEBSITE_ARTICLE rows for new pages.
        FOR i IN 1..l_arr.COUNT LOOP
            IF (l_arr(i).article_id<=0) THEN
                INSERT INTO article (id, author_user_id) 
                    VALUES (seq_article.nextval, l_session_data.user_id);
                INSERT INTO website_article (website_id, article_id, navigation_label, collection_type, user_id, display_order)
                    VALUES (pWebsiteId, seq_article.currval, l_arr(i).navigation_label, l_arr(i).collection_type, l_session_data.user_id, l_arr(i).display_order);
            END IF;
        END LOOP;

        FORALL i IN l_arr.FIRST .. l_arr.LAST
            UPDATE website_article 
               SET display_order=l_arr(i).display_order,
                   navigation_label=l_arr(i).navigation_label,
                   collection_type=l_arr(i).collection_type,
                   updated_date=l_current_date
             WHERE website_id = pWebsiteId
               AND article_id=l_arr(i).article_id;

        -- Delete pages not passed from client
        DELETE website_article WHERE website_id=pWebsiteId AND updated_date<l_current_date RETURNING article_id BULK COLLECT INTO l_article_id;

        FORALL i IN l_article_id.FIRST .. l_article_id.LAST
            DELETE article WHERE id=l_article_id(i);

        l_new_nav:=pck_deploy.buildNav(pWebsiteid);

        l_do_deploy:=NVL(l_new_nav,'x') <> NVL(l_old_nav,'x');
        
        IF (l_do_deploy) tHEN
            UPDATE website SET nav_updated_date = current_timestamp WHERE id=pWebsiteid;
        END IF;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('deploy',l_do_deploy);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;

    /* 
    **  ENSURE OWNERS GET LATEST CONTENT WHEN LOGGED IN
    */
    PROCEDURE getEditorContent(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_header_html website_article.header_html%type; 
        l_main_html article.body_html%type;
        l_footer_html website_article.footer_html%type;
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        /* RETURN NULL HTML UNLESS UPDATED AFTER LAST DEPLOYMENT */
        FOR C IN (
            SELECT body_html
              FROM article
             WHERE id=pArticleId
               AND updated_date>deployed_date
        ) LOOP
            l_main_html:=C.body_html;
        END LOOP;

        FOR C IN (
            SELECT wa.header_html, wa.header_html_updated, w.netlify_last_published
              FROM website_article wa, website w
             WHERE w.id=pWebsiteId
               AND wa.website_id=w.id
               AND wa.article_id=pArticleid
        ) LOOP
            IF (C.header_html IS NULL) THEN
                FOR C1 IN (
                    SELECT header_html 
                      FROM website_article 
                     WHERE website_id=pWebsiteId
                       AND display_order=1
                       AND header_html_updated>C.netlify_last_published
                ) LOOP
                    l_header_html:=C1.header_html;
                END LOOP;
            ELSIF (C.header_html_updated>C.netlify_last_published) THEN
                l_header_html:=C.header_html;
            END IF;
        END LOOP;

        FOR C IN (
            SELECT wa.footer_html, wa.footer_html_updated, w.netlify_last_published
              FROM website_article wa, website w
             WHERE w.id=pWebsiteId
               AND wa.website_id=w.id
               AND wa.article_id=pArticleid
        ) LOOP
            IF (C.footer_html IS NULL) THEN
                FOR C1 IN (
                    SELECT footer_html 
                      FROM website_article 
                     WHERE website_id=pWebsiteId
                       AND display_order=1
                       AND footer_html_updated>C.netlify_last_published
                ) LOOP
                    l_footer_html:=C1.footer_html;
                END LOOP;
            ELSIF (C.footer_html_updated>C.netlify_last_published) THEN
                l_footer_html:=C.footer_html;
            END IF;
        END LOOP;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header',l_header_html);
        l_json.put('main',l_main_html);
        l_json.put('footer',l_footer_html);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;

    /* 
    **  UPDATE ASSET_USED TABLE FOR ALL CLOUDINARY ASSETS INCLUDED IN CONTENT
    */
    PROCEDURE setAssetUsed(pArticleId IN article.id%type, pHtml IN OUT NOCOPY CLOB) IS
        l_res_regex VARCHAR2(50):='"https://res.cloudinary.com/(.+?)/(.+?)"';
        l_cld_cloud_name asset.cld_cloud_name%type;
        l_public_id asset.public_id%type;
        n PLS_INTEGER;

        TYPE t_asset_id IS TABLE OF asset.id%type;
        l_asset_id t_asset_id:=t_asset_id();
    BEGIN
        n:=NVL(regexp_count(pHtml,l_res_regex),0);
        FOR i IN 1..n LOOP
            l_cld_cloud_name:=regexp_substr(pHtml,l_res_regex,1,i,null,1);
            l_public_id:=regexp_substr(pHtml,l_res_regex,1,i,null,2);
            l_public_id:=REPLACE(SUBSTR(l_public_id,INSTR(l_public_id,'/',-1)+1),'.pdf');
            
            -- dbms_output.put_line(' l_cld_cloud_name:'||l_cld_cloud_name||' l_public_id:'||l_public_id);
            FOR C IN (
                SELECT id FROM asset WHERE cld_cloud_name=l_cld_cloud_name AND public_id=l_public_id
            ) LOOP
                l_asset_id.EXTEND;
                l_asset_id(l_asset_id.COUNT):=C.id;
                -- dbms_output.put_line('C.id:'||C.id||' l_cld_cloud_name:'||l_cld_cloud_name||' l_public_id:'||l_public_id);
            END LOOP;
        END LOOP;
        
        DELETE asset_used WHERE article_id=pArticleid;
        FORALL i IN 1..l_asset_id.COUNT
            INSERT INTO asset_used (asset_id, article_id) 
            VALUES (l_asset_id(i), pArticleid);
    END;

    /* 
    **  UPDATE CONTENT RECEIVED FROM CKEDITOR. 
    **  SET FLAG ON WEBSITE IF IT'S THE HOME PAGE HEADER OR FOOTER UPDATED
    */
    PROCEDURE updateEditorContent(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_word_count NUMBER;
        l_html CLOB;
        l_id VARCHAR2(6);
        l_parent_id article.parent_id%type;
        l_json JSON_OBJECT_T;

        PROCEDURE setWebsiteUpdatedDate IS
        BEGIN
            UPDATE website 
               SET nav_updated_date=current_timestamp
             WHERE id=pWebsiteId
               AND EXISTS (
                SELECT null 
                  FROM website_article
                 WHERE website_id=pWebsiteId 
                   AND article_id=pArticleid 
                   AND display_order=1
                );
        END;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT words, body_html, id
          INTO l_word_count, l_html, l_id
          FROM JSON_TABLE(pBodyText FORMAT JSON, '$' COLUMNS (body_html CLOB, id, words));

        CASE l_id
            WHEN 'header' THEN
                UPDATE website_article 
                   SET header_html=l_html,
                       header_html_updated = current_timestamp
                 WHERE website_id=pWebsiteid
                   AND article_id=pArticleId;

                setWebsiteUpdatedDate;

            WHEN 'main' THEN
                UPDATE article 
                   SET word_count=l_word_count,
                       title=SUBSTR(utl_i18n.unescape_reference(apex_escape.striphtml(regexp_substr(l_html,'<h[2-4]>(.+?)<\/h[2-4]>',1,1))),1,100),
                       excerpt=NVL(excerpt, SUBSTR(utl_i18n.unescape_reference(apex_escape.striphtml(regexp_substr(l_html,'<p>(.+?)<\/p>',1,1))),1,160)), 
                       body_html=l_html,
                       updated_date = current_timestamp
                 WHERE id=pArticleId
                 RETUrNING parent_id INTO l_parent_id;

                IF (l_parent_id IS NOT NULL) THEN
                    UPDATE article
                       SET updated_date = current_timestamp
                     WHERE id=l_parent_id;
                END IF;

                setAssetUsed(pArticleId, l_html);

            WHEN 'footer' THEN
                UPDATE website_article 
                   SET footer_html=l_html,
                       footer_html_updated = current_timestamp
                 WHERE website_id=pWebsiteid
                   AND article_id=pArticleId;

                setWebsiteUpdatedDate;
        END CASE;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('message','Saved');
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

end;
/