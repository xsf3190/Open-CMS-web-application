CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_CSS" is
    --
    PROCEDURE buildEditorTokens(pWebsiteId IN website.id%type);
    --
    PROCEDURE buildLiveTokens(pWebsiteId IN website.id%type);
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_CSS" is

    PATH_NAME CONSTANT website_cssfiles_temp.path%type:='css/website-styles.min.css';

    TYPE fluid_type_rt IS RECORD(
        property VARCHAR2(30),
        value VARCHAR2(200)
    );
    
    TYPE fluid_type_t IS TABLE OF fluid_type_rt;
    
    FUNCTION getFluidTypes(pMinFontSize IN NUMBER, pMinWidthPx IN NUMBER, pMinScale IN NUMBER, pMaxFontSize IN NUMBER, pMaxWidthPx IN NUMBER, pMaxScale IN NUMBER) RETURN fluid_type_t IS
        l_fluid_type fluid_type_t:=fluid_type_t();
        l_min_font NUMBER;
        l_max_font NUMBER;
        l_min_width NUMBER;
        l_max_width NUMBER;
        l_slope NUMBER;
        l_intersect NUMBER;
        l_clamp VARCHAR2(200);

        TYPE tt_spaces IS RECORD(dimension VARCHAR2(3), multiplier NUMBER);
        TYPE t_spaces IS TABLE OF tt_spaces INDEX BY PLS_INTEGER;
        l_spaces t_spaces:=t_spaces(
            1=>tt_spaces('3xs',0.25), 
            2=>tt_spaces('2xs',0.5), 
            3=>tt_spaces('xs',0.75), 
            4=>tt_spaces('s',1), 
            5=>tt_spaces('m',1.5), 
            6=>tt_spaces('l',2), 
            7=>tt_spaces('xl',3), 
            8=>tt_spaces('2xl',4), 
            9=>tt_spaces('3xl',6)
        );

        FUNCTION round4dp(pNumber IN NUMBER) RETURN VARCHAR2 IS
            l_number NUMBER;
        BEGIN
            l_number:=ROUND(pNumber,4);
            IF (MOD(l_number,1)=0) THEN /* integer */
                RETURN (TO_CHAR(l_number));
            ELSE
                RETURN(LTRIM(RTRIM(TO_CHAR(ROUND(pNumber,4),'fm90.0000'),'0')));
            END IF;
        END;

        PROCEDURE addFluidType(pProperty IN VARCHAR2) IS 
        BEGIN
            l_fluid_type.EXTEND;
            l_fluid_type(l_fluid_type.LAST).property:=pProperty;
            l_clamp:='clamp(' || round4dp(l_min_font) || 'rem,' || round4dp(l_intersect) || 'rem + ' || round4dp(l_slope * 100) || 'cqi,' || round4dp(l_max_font) || 'rem)';
            l_fluid_type(l_fluid_type.LAST).value:=l_clamp;
        END;

    BEGIN
        l_min_width:=pMinWidthPx/16; 
        l_max_width:=pMaxWidthPx/16;
        FOR i IN -2 .. 6 LOOP
            IF (i<0) THEN
                l_min_font:=pMinFontSize/(pMinScale**ABS(i));
                l_max_font:=pMaxFontSize/(pMaxScale**ABS(i));
            ELSE
                l_min_font:=pMinFontSize*(pMinScale**i);
                l_max_font:=pMaxFontSize*(pMaxScale**i);
            END IF;
            l_slope:=(l_max_font - l_min_font) / (l_max_width - l_min_width);
            l_intersect:= -l_min_width * l_slope + l_min_font;
            addFluidType('--step-' || i);
        END LOOP;

        FOR i IN 1..l_spaces.COUNT LOOP
            l_min_font:=pMinFontSize*l_spaces(i).multiplier;
            l_max_font:=pMaxFontSize*l_spaces(i).multiplier;
            l_slope:=(l_max_font - l_min_font) / (l_max_width - l_min_width);
            l_intersect:= -l_min_width * l_slope + l_min_font;
            addFluidType('--space-' || l_spaces(i).dimension);
        END LOOP;

        RETURN l_fluid_type;
    END;

    /*
    **  BUILD TYPOGRAPHY TOKENS
    */
    FUNCTION getTypography(pWebsiteid IN website.id%type) RETURN VARCHAR2 IS
        l_fluid_types fluid_type_t;
        l_tokens LONG;
    BEGIN
        FOR C IN (
            SELECT w.max_width, w.min_font_size, w.min_width_px, w.min_scale, w.max_font_size, w.max_width_px, w.max_scale
              FROM website w
             WHERE w.id=pWebsiteId
            )
        LOOP
            l_tokens:=
            '--max-width: ' || C.max_width || ';' ||
            '--font-family-ui: system-ui;';

            l_fluid_types:=getFluidTypes(C.min_font_size, C.min_width_px, C.min_scale, C.max_font_size, C.max_width_px, C.max_scale);
            FOR i IN 1..l_fluid_types.COUNT LOOP
                l_tokens:=l_tokens || l_fluid_types(i).property || ': ' || l_fluid_types(i).value || ';';
            END LOOP;
        END LOOP;

        -- BUILD @FONT-FACE RULES AND font-family token FOR EACH CONTEXT/FONT
        FOR C IN (
            SELECT wf.context, wf.editor_fontfaces, f.family
              FROM font f, website_font wf
             WHERE f.id=wf.font_id
               AND wf.website_id=pWebsiteId
             ORDER BY DECODE(wf.context,'logo',1,'headings',2,'text',3)
        ) LOOP
            l_tokens:=l_tokens || 
            '--font-family-' || C.context || ':"' || C.family || '",system-ui;';
        END LOOP;

        RETURN (l_tokens);
    END;
    
    /*
    **  BUILD COLOUR TOKENS
    */
    FUNCTION getColours RETURN VARCHAR2 IS
    BEGIN
        RETURN(
            '--color-bg: #fffefd;' ||
            '--color-bg-accent: #f7f5f5;' ||
            '--color-bg-code: #0f172a;' ||
            '--color-text: #020617;' ||
            '--color-text-accent: #334155;' ||
            '--color-text-code: #f1f5f9;' ||
            '--color-link: #2563eb;' ||
            '--color-link-hover: #ec4899;' ||
            '--color-theme: #ffedd5;' ||
            '--color-theme-muted: #fff5e7;' ||
            '--color-theme-accent: #fed7aa;' ||
            '--color-theme-offset: #ef4444;'
        );
    END;

    FUNCTION getDarkTheme RETURN VARCHAR2 IS
        l_css VARCHAR2(2000);
    BEGIN
        FOR i IN 1..2 LOOP
            IF (i=1) THEN
                l_css:=l_css || 
                '[data-appearance=dark] {';
            ELSE
                l_css:=l_css || 
                '@media (prefers-color-scheme: dark) {' ||
                    ':root:not([data-appearance]) {';
            END IF;
            l_css:=l_css || 
                '--color-bg:#020617;' ||
                '--color-bg-accent: #0f172a;' ||
                '--color-bg-code: #0f172a;' ||
                '--color-text: #f8fafc;' ||
                '--color-text-accent: #f1f5f9;' ||
                '--color-link: #7ab6ff;' ||
                '--color-link-hover: #93c5fd;' ||
                '--color-theme: #172554;' ||
                '--color-theme-muted: #0e1737;' ||
                '--color-theme-accent: #1e3a8a;' ||
                '--color-theme-offset: #0ea5e9;' ||
            '}' || CASE WHEN i=2 THEN '}' END;
        END LOOP;

        RETURN(l_css);
    END;

    /*
    **  BUILD EDITOR SITE FONT-FACE RULES
    */
    FUNCTION getEditorFontfaces(pWebsiteid IN website.id%type) RETURN VARCHAR2 IS
        l_fontfaces VARCHAR2(4000);
    BEGIN
        FOR C IN (
            SELECT wf.editor_fontfaces
              FROM font f, website_font wf
             WHERE f.id=wf.font_id
               AND wf.website_id=pWebsiteId
             ORDER BY DECODE(wf.context,'logo',1,'headings',2,'text',3)
        ) LOOP
            l_fontfaces:=l_fontfaces || C.editor_fontfaces;
        END LOOP;
        RETURN (l_fontfaces);
    END;

    /*
    **  BUILD LIVE SITE FONT-FACE RULES FROM TEMPORARY TABLE
    */
    FUNCTION getLiveFontfaces RETURN VARCHAR2 IS
        l_fontfaces VARCHAR2(4000);
    BEGIN
        FOR C IN (
            SELECT fontface
              FROM website_fontfiles_temp
        ) LOOP
            l_fontfaces:=l_fontfaces || C.fontface;
        END LOOP;
        RETURN (l_fontfaces);
    END;

    /* 
    ** BUILD FONT-FACE RULES AND DESIGN TOKENS FOR EDITOR SITE
    */
    PROCEDURE buildEditorTokens(pWebsiteId IN website.id%type) IS
        l_font_variation_settings VARCHAR2(4000);
        l_root_css CLOB;
        l_css website.css%type;
        l_sha1 website_cssfiles_temp.sha1%type;
    BEGIN
        l_root_css:=
        ':root {' ||
            getColours ||
            getTypography(pWebsiteid) ||
            '-webkit-text-size-adjust: none;' ||
            'text-size-adjust: none;' ||
            '--focus-outline: 2px solid var(--color-link);';

        -- USERS CHOOSE FROM 3 DIFFERENT LOGO OPTIONS - IMAGE, SVG AND FONT
        FOR C IN (
            SELECT w.logo_type,
                    w.logo_img_inline_size, w.logo_img_border_radius, w.logo_img_corner_shape,
                    w.logo_svg_inline_size,
                    w.logo_font_size
              FROM website w
             WHERE w.id=pWebsiteId
            )
        LOOP
            l_root_css:=l_root_css || 
            '--logo-img-inline-size: ' || 'var(' || C.logo_img_inline_size || ');' ||
            '--logo-img-border-radius: ' || C.logo_img_border_radius || ';' ||
            '--logo-img-corner-shape: ' || C.logo_img_corner_shape || ';' ||
            '--logo-svg-inline-size: ' || 'var(' || C.logo_svg_inline_size || ');' ||
            '--logo-font-size: ' ||  'var(--step-' || C.logo_font_size || ');';
        END LOOP;

        /*
        ** CREATE CUSTOM VARIABLES FOR ALL AVAILABLE FONT AXES
        */
        FOR C IN (
            SELECT wf.context, REPLACE(m.axis,'ital','style') axis, NVL(fa.value,m.default_value) value,
                    ROW_NUMBER() OVER (PARTITION BY wf.context ORDER BY REPLACE(m.axis,'ital','style') ) rn,
                    COUNT(*) OVER (PARTITION BY wf.context) nb
              FROM website_font wf, font_metadata m, font_axes_value fa
             WHERE wf.website_id=pWebsiteId
               AND fa.website_id(+)=wf.website_id
               AND fa.axis(+)=m.axis
               AND fa.context(+)=wf.context
             ORDER BY DECODE(context,'logo',1,'headings',2,'text',3), axis
        ) LOOP
            -- ital custom variable depends on whether font is variable
            l_root_css:=l_root_css || 
            '--' || C.context || '-font-' || C.axis || ': ';
            IF (C.axis='style') THEN
                l_root_css:=l_root_css || 
                CASE C.value WHEN 0 THEN 'normal' WHEN 1 THEN 'italic' END;
            ELSE
                l_root_css:=l_root_css || 
                C.value;
            END IF;
            l_root_css:=l_root_css || ';';
            IF (C.axis<>'style') THEN
                l_font_variation_settings:=l_font_variation_settings ||
                '"' || C.axis || '" var(--' || C.context || '-font-' || C.axis || '),';
            END IF;

            IF (C.rn=C.nb) THEN
                l_root_css:=l_root_css ||
                '--' || C.context || '-font-variation-settings: ' || RTRIM(l_font_variation_settings,',') || ';';
                l_font_variation_settings:=null;
            END IF;
        END LOOP;

        l_root_css:=l_root_css ||
        '}';
        
        /*
        l_root_css:=l_root_css || 
        '--img-border-radius: ' || C.img_border_radius || ';';
        */

        l_root_css:=l_root_css ||
        getDarkTheme;

        l_css:=getEditorFontfaces(pWebsiteid) || l_root_css;

        l_sha1:=LOWER(dbms_crypto.hash(src => l_css, typ => dbms_crypto.hash_sh1));

        INSERT INTO website_cssfiles_temp(path, sha1, content) VALUES (PATH_NAME, l_sha1, l_css);
    END;

    /*
    ** FONT-FACE RULES AND TOKENS FOR LIVE SITE
    ** FONTS ARE DEFINED IN TEMPORARY TABLE
    */
    PROCEDURE buildLiveTokens(pWebsiteId IN website.id%type) IS
        l_root_css CLOB;
        l_css CLOB;
        l_sha1 website_cssfiles_temp.sha1%type;
    BEGIN
        l_root_css:=
        ':root {' ||
            getColours ||
            getTypography(pWebsiteid) ||
            '-webkit-text-size-adjust: none;' ||
            'text-size-adjust: none;' ||
            '--focus-outline: 2px solid var(--color-link);';
        
        -- CONFIGURE ANY SELECTED LOGO OPTION
        FOR C IN (
            SELECT w.logo_type,
                    w.logo_img_inline_size, w.logo_img_border_radius, w.logo_img_corner_shape,
                    w.logo_svg_inline_size,
                    w.logo_font_size
              FROM website w
             WHERE w.id=pWebsiteId
               AND w.logo_type IS NOT NULL
            )
        LOOP
            CASE C.logo_type
                WHEN 'img' THEN
                    l_root_css:=l_root_css || 
                    '--logo-img-inline-size: ' || 'var(' || C.logo_img_inline_size || ');' ||
                    '--logo-img-border-radius: ' || C.logo_img_border_radius || ';' ||
                    '--logo-img-corner-shape: ' || C.logo_img_corner_shape || ';';
                WHEN 'svg' THEN
                    l_root_css:=l_root_css || 
                    '--logo-svg-inline-size: ' || 'var(' || C.logo_svg_inline_size || ');';
                WHEN 'font' THEN
                    l_root_css:=l_root_css || 
                    '--logo-font-size: ' ||  'var(--step-' || C.logo_font_size || ');';
            END CASE;
        END LOOP;

        FOR C IN (
            SELECT wf.context, fav.axis, fav.value,
                    ROW_NUMBER() OVER (PARTITION BY wf.context ORDER BY fav.axis ) rn,
                    COUNT(*) OVER (PARTITION BY wf.context) nb
              FROM website_font wf, font_axes_value fav
             WHERE wf.website_id=pWebsiteid
               AND fav.website_id=wf.website_id
               AND fav.context=wf.context
             ORDER BY DECODE(context,'logo',1,'headings',2,'text',3), axis
        ) LOOP
            l_root_css:=l_root_css || 
            '--' || C.context || '-font-' || C.axis || ': ' || C.value || ';';
        END LOOP;

        l_root_css:=l_root_css ||
        '}';

        l_root_css:=l_root_css ||
        getDarkTheme;

        l_css:=getLiveFontfaces || l_root_css;

        l_sha1:=LOWER(dbms_crypto.hash(src => l_css, typ => dbms_crypto.hash_sh1));

        INSERT INTO website_cssfiles_temp(path, sha1, content) VALUES (PATH_NAME, l_sha1, l_css);
    END;

end;
/