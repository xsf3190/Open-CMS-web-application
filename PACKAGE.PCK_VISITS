CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_VISITS" AS
    --
    PROCEDURE email_visits;
    --
    PROCEDURE getVisits(pWebsiteId IN website.id%type);
    --
    PROCEDURE uploadIpLookup;
    --
    PROCEDURE uploadPageHits(pWebsiteId IN website.id%type DEFAULT NULL);
    --
    PROCEDURE uploadPageVisit(pBodyText IN CLOB);
    --
END;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_VISITS" AS

    /*
     **  Procedure invoked by POST to REST end point "page-visit".
     **  Upload page visit details sent by javascript from deployed website pages
     */
    PROCEDURE uploadPageVisit(pBodyText IN CLOB) IS
        l_bodytext CLOB:=pBodyText;
        l_ip_address website_article_cwv.ip_address%type:=OWA_UTIL.get_cgi_env ('REMOTE_ADDR');
        l_this_referrer VARCHAR2(50):=LOWER(sys_context('userenv','db_name'));
    BEGIN
        INSERT INTO website_article_cwv (
            website_id, article_id, seq, referrer, ip_address, lcp, lcp_rating, cls, cls_rating, inp, inp_rating, fid, fid_rating, 
            browser, mobile, connection, url, website_loaded, duration_visible)
        SELECT website_id, article_id, seq, CASE WHEN referrer LIKE '%' || l_this_referrer || '%' THEN l_this_referrer ELSE referrer END, l_ip_address, lcp, lcp_rating, cls, cls_rating, inp, inp_rating, fid, fid_rating, 
                browser, mobile, connection, url, website_loaded, duration_visible
          FROM JSON_TABLE(l_bodytext FORMAT JSON, '$' 
            COLUMNS (website_id, article_id, seq, referrer, LCP, LCP_rating, CLS, CLS_rating, INP, INP_rating, FID, FID_rating, 
                    browser, mobile, connection, url, website_loaded, duration_visible));  

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;

    /*
     **  Keep IP_LOOKUP table up to date
     */
    PROCEDURE uploadIpLookup IS 
        l_clob clob;
        TYPE t_ip_lookup IS TABLE OF ip_lookup%rowtype;
        l_ip_lookup t_ip_lookup:=t_ip_lookup();
        l_ip_count VARCHAR2(10);
    BEGIN
        FOR C IN (SELECT DISTINCT ip_address FROM page_hit MINUS SELECT ip FROM ip_lookup )
        LOOP
            pck_api.callIpdataAPI(null,C.ip_address,'GET',l_clob);
            FOR C1 IN (SELECT city,country_code,postal,count FROM JSON_TABLE(l_clob, '$' COLUMNS(city,country_code,postal,count)))
            LOOP
                l_ip_lookup.EXTEND;
                l_ip_lookup(l_ip_lookup.LAST).ip:=C.ip_address;
                l_ip_lookup(l_ip_lookup.LAST).city:=C1.city;
                l_ip_lookup(l_ip_lookup.LAST).country_code:=C1.country_code;
                l_ip_lookup(l_ip_lookup.LAST).postal:=C1.postal;
                l_ip_count:=C1.count;
            END LOOP;
        END LOOP;
        IF (l_ip_count IS NOT NULL) THEN
            pck_core.log('IPDATA API COUNT IS ' || l_ip_count);
        END IF;
        FORALL i IN l_ip_lookup.FIRST..l_ip_lookup.LAST
            INSERT INTO ip_lookup VALUES l_ip_lookup(i);
    END;

    /*
     **  Google core web vital and website visit duration data is uploaded from client sessions to table website_article_cwv.
     **  This procedure aggregates the data into the page_hit table.
     **  Each page_hit row represents one visit from one ip address to a web page in a discrete client session.
     */
    PROCEDURE uploadPageHits(pWebsiteId IN website.id%type) IS
        n PLS_INTEGER;
    BEGIN
        DELETE website_article_cwv cwv WHERE cwv.website_id=pWebsiteId AND NOT EXISTS (SELECT null FROM article a WHERE a.id=cwv.article_id);
        n:=SQL%ROWCOUNT;
        pck_core.log('Discarded '||n||' rows from website_article_cwv referencing articles deleted since last deployment.');

        MERGE INTO page_hit t
        USING (
            SElECT 
                website_id, article_id, website_loaded, ip_address, apex_string_util.get_domain(referrer) referrer, lcp, lcp_rating, cls, cls_rating, inp, inp_rating, fid, fid_rating, NVL(duration_visible,0) duration_visible, browser, connection, url, mobile,
                to_timestamp('1970-01-01','yyyy-mm-dd') + numtodsinterval(website_loaded,'second') visit_date
            FROM
            (
                SELECT 
                    website_id, article_id, ip_address, website_loaded,  
                    MAX(CASE WHEN INSTR(referrer,url)=0 THEN referrer END) referrer, 
                    MAX(lcp) lcp, MAX(lcp_rating) lcp_rating, MAX(cls) cls, MAX(cls_rating) cls_rating, MAX(inp) inp, MAX(inp_rating) inp_rating, MAX(fid) fid, MAX(fid_rating) fid_rating, 
                    SUM(duration_visible) duration_visible, MAX(browser) browser, MAX(connection) connection, MAX(url) url, MAX(mobile) mobile
                FROM website_article_cwv 
                WHERE website_id=pWebsiteId
                GROUP BY website_id, article_id, ip_address, website_loaded
            )
        ) s
        ON (t.website_id=s.website_id AND 
            t.article_id=s.article_id AND 
            t.ip_address=s.ip_address AND 
            t.website_loaded=s.website_loaded)
        WHEN NOT MATCHED THEN
            INSERT (website_id, article_id, ip_address, website_loaded, visit_date, referrer, duration_visible, lcp, lcp_rating, cls, cls_rating, inp, inp_rating, fid, fid_rating, browser, connection, url, mobile)    
            VALUES (s.website_id, s.article_id, s.ip_address, s.website_loaded, s.visit_date, s.referrer, s.duration_visible, s.lcp, s.lcp_rating, s.cls, s.cls_rating, s.inp, s.inp_rating, s.fid, s.fid_rating, s.browser, s.connection, s.url, s.mobile)
        WHEN MATCHED THEN
            UPDATE SET t.updated_date=current_timestamp,
                t.duration_visible=t.duration_visible+s.duration_visible,
                t.lcp=CASE WHEN t.lcp IS NOT NULL AND s.lcp IS NOT NULL THEN GREATEST(s.lcp,t.lcp) ELSE COALESCE(s.lcp,t.lcp) END,
                t.lcp_rating=CASE WHEN t.lcp_rating IS NOT NULL AND s.lcp_rating IS NOT NULL THEN GREATEST(s.lcp_rating,t.lcp_rating) ELSE COALESCE(s.lcp_rating,t.lcp_rating) END,
                t.cls=CASE WHEN t.cls IS NOT NULL AND s.cls IS NOT NULL THEN GREATEST(s.cls,t.cls) ELSE COALESCE(s.cls,t.cls) END,
                t.cls_rating=CASE WHEN t.cls_rating IS NOT NULL AND s.cls_rating IS NOT NULL THEN GREATEST(s.cls_rating,t.cls_rating) ELSE COALESCE(s.cls_rating,t.cls_rating) END,
                t.inp=CASE WHEN t.inp IS NOT NULL AND s.inp IS NOT NULL THEN GREATEST(s.inp,t.inp) ELSE COALESCE(s.inp,t.inp) END,
                t.inp_rating=CASE WHEN t.inp_rating IS NOT NULL AND s.inp_rating IS NOT NULL THEN GREATEST(s.inp_rating,t.inp_rating) ELSE COALESCE(s.inp_rating,t.inp_rating) END,
                t.fid=CASE WHEN t.fid IS NOT NULL AND s.fid IS NOT NULL THEN GREATEST(s.fid,t.fid) ELSE COALESCE(s.fid,t.fid) END,
                t.fid_rating=CASE WHEN t.fid_rating IS NOT NULL AND s.fid_rating IS NOT NULL THEN GREATEST(s.fid_rating,t.fid_rating) ELSE COALESCE(s.fid_rating,t.fid_rating) END        
                ;
        n:=SQL%ROWCOUNT;
        pck_core.log('Merged '||n||' rows into page_hit table');

        DELETE website_article_cwv WHERE website_id=pWebsiteId;
        n:=SQL%ROWCOUNT;
        pck_core.log('Deleted '||n||' rows merged from website_article_cwv table');

        uploadIpLookup;
    END;

    FUNCTION cwvRating(pLCP IN VARCHAR2 DEFAULT NULL, pCLS IN VARCHAR2 DEFAULT NULL, pINP IN VARCHAR2 DEFAULT NULL) RETURN VARCHAR2 IS
        l_cwv VARCHAR2(200);
    BEGIN
        IF (pLCP IS NOT NULL) THEN
            l_cwv:=l_cwv || '<mark class="' || pLCP || '">LCP</mark>';
        ELSE
            l_cwv:=l_cwv || '<mark></mark>';
        END IF;
        IF (pCLS IS NOT NULL) THEN
            l_cwv:=l_cwv || '<mark class="' || pCLS || '">CLS</mark>';
        ELSE
            l_cwv:=l_cwv || '<mark></mark>';
        END IF;
        IF (pINP IS NOT NULL) THEN
            l_cwv:=l_cwv || '<mark class="' || pINP || '">INP</mark>';
        ELSE
            l_cwv:=l_cwv || '<mark></mark>';
        END IF;
        RETURN(l_cwv);
    END;

    FUNCTION cwvRatingOverall(pLCP IN VARCHAR2 DEFAULT NULL, pCLS IN VARCHAR2 DEFAULT NULL, pINP IN VARCHAR2 DEFAULT NULL) RETURN VARCHAR2 IS
        l_cwv VARCHAR2(200);
    BEGIN
        IF ((pLCP='good' OR pLCP IS NULL) AND (pCLS='good' OR pCLS IS NULL) AND (pINP='good' OR pINP IS NULL)) THEN
            RETURN('<span class="good">GOOD</span>');
        ELSIF ((pLCP='poor' OR pLCP IS NULL) OR (pCLS='poor' OR pCLS IS NULL) OR (pINP='poor' OR pINP IS NULL)) THEN
            RETURN('<span class="poor">POOR</span>');
        ELSE
            RETURN('<span class="needs-improvement">NEEDS<br>IMPROVEMENT</span>');
        END IF;
    END;

    
    FUNCTION sql2Html (pSql IN VARCHAR2, pBindVar IN NUMBER) RETURN CLOB
    IS
        l_cur       NUMBER:=dbms_sql.open_cursor;
        l_execute   NUMBER;
        l_col_cnt    INTEGER;
        rec_tab      DBMS_SQL.DESC_TAB;
        l_flag       NUMBER;
        l_varchar2   VARCHAR2 (4000);
        l_date       DATE;
        l_number     NUMBER;
        l_html       CLOB;
    BEGIN
        dbms_sql.parse(l_cur,pSql,dbms_sql.native);

        dbms_sql.bind_variable(l_cur,':B1',pBindVar);

        -- define columns
        
        dbms_sql.describe_columns (l_cur, l_col_cnt, rec_tab);
        FOR i IN 1 .. l_col_cnt
        LOOP
            CASE rec_tab (i).col_type
                 WHEN 1 THEN
                    dbms_sql.define_column (l_cur,i,l_varchar2,2000);
                 WHEN 2 THEN
                    dbms_sql.define_column (l_cur, i, l_number);
                 WHEN 12 THEN
                    dbms_sql.define_column (l_cur, i, l_date);
                 ELSE
                    dbms_sql.define_column (l_cur,i,l_varchar2,2000);
              END CASE;
        END LOOP;

        l_html:='<table role="presentation">';
        
       -- Print column names of dynamic sql
       l_html:=l_html || '<tr>';
       FOR i IN 1 .. l_col_cnt
       LOOP
          l_html:=l_html || '<th>' || rec_tab (i).col_name || '</th>';
       END LOOP;
       l_html:=l_html || '</tr>';


        l_execute:=dbms_sql.execute(l_cur);
        
       -- Print data fetched by query
       LOOP
          l_flag := dbms_sql.fetch_rows (l_cur);
          EXIT WHEN l_flag = 0;
          
          l_html:=l_html || '<tr>';
          FOR i IN 1 .. l_col_cnt
          LOOP
             CASE rec_tab(i).col_type
                WHEN 1 THEN
                   dbms_sql.column_value (l_cur, i, l_varchar2);
                   l_html:=l_html || '<td>' || l_varchar2 || '</td>';
                WHEN 2 THEN
                   dbms_sql.column_value (l_cur, i, l_number);
                   l_html:=l_html || '<td>' || l_number || '</td>';
                WHEN 12 THEN
                   dbms_sql.column_value (l_cur, i, l_date);
                   l_html:=l_html || '<td>' || TO_CHAR (l_date, 'DD/MM/YYYY HH24:MI:SS') || '</td>';
                ELSE
                   l_html:=l_html || '<td>' || l_varchar2 || '</td>';
             END CASE;
          END LOOP;
          l_html:=l_html || '</tr>';
       END LOOP;
       l_html:=l_html || '</table>';

       dbms_sql.close_cursor (l_cur);

       RETURN (l_html);
    END;

    /*
    **  Return log of all visits to website
    */
    PROCEDURE getVisits(pWebsiteId IN website.id%type) IS
        l_website_id website.id%type;
        l_domain_name VARCHAR2(200);
        l_content CLOB;
        l_visit_date VARCHAR2(22);
        l_sql LONG;
        l_session_data pck_core.session_data_rt;
        n PLS_INTEGER;
    BEGIN
        l_session_data:=pck_core.getSessionData();

        pck_visits.uploadPageHits(l_website_id);

        l_content:=l_content ||
        '<table role="presentation">
            <thead>
                <tr>
                    <th rowspan="2">Website Visit</th>
                    <th>Location</th>
                    <th rowspan="2">Viewing time</th>
                    <th>Browser</th>
                    <th rowspan="2">Performance</th>
                </tr>
                <tr>
                    <th>Referrer</th>
                    <th>Connection</th>
                </tr>
            </thead>
            <tbody>';

        FOR C IN (
            WITH website_hits as 
            (
                SELECT p.website_id, p.ip_address, p.visit_date, 
                        MAX(p.referrer) referrer, 
                        MAX(p.lcp_rating) lcp_rating, MAX(p.cls_rating) cls_rating, MAX(p.inp_rating) inp_rating, 
                        SUM(p.duration_visible) duration_visible, MAX(p.browser) browser, MAX(p.connection) connection, MAX(p.mobile) mobile
                  FROM website w, page_hit p
                 WHERE w.id=pWebsiteId
                   AND p.website_id=w.id
                   AND p.url=CASE WHEN w.netlify_site_id_custom IS NULL THEN REPLACE(w.domain_name,'.','-') || '.netlify.app' ELSE w.domain_name END
                 GROUP BY p.website_id, p.ip_address, p.visit_date
            )
            SELECT  
                    wh.visit_date, wh.referrer, ip.city||','||ip.country_code||','||ip.postal location, 
                    wh.browser, wh.connection, wh.mobile, wh.duration_visible, wh.lcp_rating, wh.cls_rating, wh.inp_rating
            FROM website_hits wh, ip_lookup ip
            WHERE ip.ip=wh.ip_address
            ORDER BY wh.visit_date DESC
        ) 
        LOOP
            l_content:=l_content ||
                '<tr>
                    <td rowspan="2">' || TO_CHAR(C.visit_date, 'dd Mon yyyy fmhh24:fmmi')  || '</td>
                    <td>' || LTRIM(RTRIM(C.location,','),',') || '</td>
                    <td rowspan="2">' || CASE WHEN C.duration_visible IS NOT NULL THEN TO_CHAR(C.duration_visible) || ' seconds' END || '</td>
                    <td>' || C.browser || '</td>
                    <td  rowspan="2"><div class="cwv">' || cwvRating(C.lcp_rating, C.cls_rating, C.inp_rating) || '</div></td>
                </tr>
                <tr>
                    <td>' || NVL(C.referrer,'-') || '</td>
                    <td>' || C.connection || CASE WHEN C.mobile='true' THEN ' (Mob)' END || '</td>
                </tr>';

        END LOOP;
        l_content:=l_content || '</tbody></table>';

        /* 
        ** Create page analysis report if multi-page website
        */
        n:=0;
        l_sql:='SELECT TO_CHAR(visit_date,''dd Mon yyyy fmhh24:fmmi'') "Pages Viewed",';
        FOR C1 IN (SELECT navigation_label FROM website_article WHERE website_id=pWebsiteId ORDER BY display_order) LOOP
            n:=n+1;
            l_sql:=l_sql || 'MAX(DECODE(navigation_label,''' || C1.navigation_label || ''',duration_visible)) "' || C1.navigation_label || '",';
        END LOOP;
        IF (n>1) THEN
            l_sql:=RTRIM(l_sql,',') ||
            'FROM 
                (
                SELECT ph.visit_date, ph.ip_address, wa.navigation_label, SUM(ph.duration_visible) duration_visible
                  FROM website w, page_hit ph, website_article wa
                 WHERE w.id=:B1
                   AND ph.website_id=w.id
                   AND ph.url=CASE WHEN w.netlify_site_id_custom IS NULL THEN REPLACE(w.domain_name,''.'',''-'') || ''.netlify.app'' ELSE w.domain_name END
                   AND ph.article_id=wa.article_id
                 GROUP BY ph.visit_date, ph.ip_address, wa.navigation_label
                )
             GROUP  BY visit_date
             ORDER BY visit_date DESC';

            l_content:=l_content || sql2Html(l_sql, pWebsiteId);
        END IF;
        
        l_content:=l_content || '</body></html>';

        --apex_mail.send(p_to=>C.contact_email, p_from=>'visits@'||C.domain_name, p_body=>l_body, p_body_html=>l_body_html, p_subj=>'Visits - ' || C.domain_name);
        --apex_mail.push_queue(); 


        apex_json.open_object; 
        apex_json.write('success', TRUE);
        apex_json.write('content', l_content);
        apex_json.close_object;

        EXCEPTION WHEN OTHERS THEN pck_core.log_error;
    END;

    /*
    **  EMAIL VISIT DETAILS FOR ALL WEBSISTES WITH CUSTOM DOMAIN
    */
    PROCEDURE email_visits IS
        l_body CLOB:='To view the content of this message, please use an HTML enabled mail client.'||utl_tcp.crlf;
        l_body_html CLOB:=
        '<!DOCTYPE html>
         <html>
            <head>
                <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
                <meta name="viewport" content="width=device-width, initial-scale=1.0">
                <meta name="format-detection" content="telephone=no">
                <meta name="format-detection" content="date=no">
                <meta name="format-detection" content="address=no">
                <meta name="format-detection" content="email=no">
                <style type="text/css">
                    body {
                        font-family:Georgia,sans-serif;
                        font-size:1rem;
                    }
                    td,
                    th {
                        border: 1px solid rgb(190, 190, 190);
                        padding: 0.8rem;
                    }
                    td {
                        text-align: center;
                    }
                    tr:nth-child(even) {
                        background-color: #eee;
                    }
                    th[scope="col"] {
                        background-color: #696969;
                        color: #fff;
                    }
                    th[scope="row"] {
                        background-color: #d7d9f2;
                    }
                    caption {
                        padding: 10px;
                        caption-side: top;
                    }
                    table {
                        border-collapse: collapse;
                        border: 2px solid rgb(200, 200, 200);
                        letter-spacing: 1px;
                        font-family: sans-serif;
                    }
                    span {
                        padding: 4px 8px;
                        letter-spacing: 1px;
                        font-weight: bold;
                    }
                    .good {
                        color: #000;
                        background-color: #0cce6b;
                    }
                    .needs-improvement {
                        color: #000;
                        background-color: #ffa401;
                    }
                    .poor {
                        color: #000;
                        background-color: #ff4e41;
                    }
                </style>
            </head>
        <body>';
        l_bgcolor VARCHAR2(7);
        l_sql LONG;
        n PLS_iNTEGER:=0;
    BEGIN
        FOR C IN (SELECT id, domain_name, contact_email FROM website WHERE netlify_site_id_custom IS  NOT NULL and id=1608) LOOP
            pck_visits.uploadPageHits(C.id);
            FOR C1 IN (
                WITH website_hits AS 
                (
                    SELECT website_id, ip_address, visit_date, 
                            MAX(referrer) referrer, 
                            MAX(lcp_rating) lcp_rating, MAX(cls_rating) cls_rating, MAX(inp_rating) inp_rating, MAX(fid_rating) fid_rating, 
                            SUM(duration_visible) duration_visible, MAX(browser) browser, MAX(connection) connection, MAX(mobile) mobile
                      FROM page_hit
                     WHERE website_id=C.id
                       AND url=C.domain_name
                     GROUP BY website_id, ip_address, visit_date
                ),
                summary AS
                (
                    SELECT COUNT(*) nb_hit, COUNT(DISTINCT ip_address) nb_ip, SUM(duration_visible)/COUNT(*) avg_time FROM website_hits 
                )
                SELECT  
                        wh.visit_date, wh.referrer, ip.city||','||ip.country_code||','||ip.postal location, 
                        wh.browser, wh.connection, wh.mobile, wh.duration_visible, wh.lcp_rating, wh.cls_rating, wh.inp_rating, 
                        nb_hit, nb_ip, avg_time
                  FROM website_hits wh, ip_lookup ip, summary
                 WHERE ip.ip=wh.ip_address
                 ORDER BY wh.visit_date DESC
            ) LOOP
                n:=n+1;
                IF (n=1) THEN
                    l_body_html:=l_body_html ||
                    '<table role="presentation">
                        <caption>PAGE HITS: <strong>' || C1.nb_hit || '</strong> DISTINCT VISITORS: <strong>' || C1.nb_ip || '</strong> AVERAGE VIEWING TIME: <strong>' || CEIL(C1.avg_time) || ' seconds</strong></caption>
                        <thead>
                            <tr bgcolor="#d7d9f2">
                                <th scope="col">Date</th>
                                <th scope="col">Referrer /<br>Location</th>
                                <th scope="col">Viewing Time</th>
                                <th scope="col">Performance Rating</th>
                            </tr>
                        </thead>
                        <tbody>';
                END IF;

                IF (MOD(n,2)=0) THEN
                    l_bgcolor:='#eeeeee';
                ELSE
                    l_bgcolor:=NULL;
                END IF;
                l_body_html:=l_body_html || to_clob(
                '<tr  bgcolor="' || l_bgcolor || '">
                    <td scope="row">' || TO_CHAR(C1.visit_date,'dd.mon.yyyy hh24:mi') || '</td>
                    <td>' || C1.referrer || '<br>' || C1.location || '</td>
                    <td>' || C1.duration_visible || '</td>
                    <td style="text-align:center">' || cwvRatingOverall(C1.lcp_rating, C1.cls_rating, C1.inp_rating) || '</td>
                </tr>');
            END LOOP;
            l_body_html:=l_body_html || to_clob('</tbody></table>');

        END LOOP;

        EXCEPTION WHEN OTHERS THEN pck_core.log_error;
    END;
END;
/