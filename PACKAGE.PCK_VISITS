CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_VISITS" AS
    --
    PROCEDURE daily_job;
    --
    PROCEDURE ecology(pWebsiteId IN website.id%type, pArticleid IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE getVisits(pWebsiteId IN website.id%type, pReport IN NUMBER, pPage IN NUMBER, pHandler IN VARCHAR2, pStatus OUT NUMBER);
    --
    FUNCTION pagination(pSelectedPage IN NUMBER, pTotal IN NUMBER) RETURN VARCHAR2;
    --
    PROCEDURE uploadPageVisit(pWebsiteid IN website.id%type, pArticleid IN article.id%type, pBodyText IN CLOB);
    --
END;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_VISITS" AS

    ROWS_PER_PAGE CONSTANT PLS_INTEGER:=10;

    /*
    **  PROCEDURE CALLED BY PUBLIC REST ENDPOINT
    */
    PROCEDURE ecology(pWebsiteId IN website.id%type, pArticleid IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_json JSON_OBJECT_T;
        l_body_text CLOB:=pBodyText;
        TYPE tt_metrics IS RECORD (
            entryType VARCHAR2(10), 
            name VARCHAR2(200), 
            transferSize NUMBER, 
            contentType VARCHAR2(50),
            initiatorType VARCHAR2(50)
        );
        TYPE t_metrics IS TABLE OF tt_metrics;
        l_metrics t_metrics;

        TYPE t_resources IS TABLE OF NUMBER INDEX BY VARCHAR2(5);
        l_resources t_resources:=t_resources(
            'html'=>0,
            'css'=>0,
            'js'=>0,
            'json'=>0,
            'woff2'=>0,
            'img'=>0,
            'map'=>0,
            'svg'=>0
        );
        l_lcp NUMBER;
        l_fcp NUMBER;
        l_index VARCHAR2(5);
        l_article LONG;
        l_total NUMBER:=0;
        l_75P NUMBER;
        l_filename VARCHAR2(200);
        n PLS_INTEGER;
    BEGIN
        pck_core.log('Someone clicked ecology');
        SELECT entryType, name, transferSize, contentType, initiatorType
          BULK COLLECT INTO l_metrics
          FROM JSON_TABLE(l_body_text, '$.metrics[*]' COLUMNS (entryType, name, transferSize, contentType, initiatorType))
         WHERE initiatorType NOT IN ('fetch','beacon');

        SELECT ROUND(lcp/1000,1),ROUND(fcp/1000,1)
          INTO l_lcp, l_fcp
          FROM JSON_TABLE(l_body_text, '$' COLUMNS (lcp, fcp));
        

        FOR i IN 1..l_metrics.COUNT LOOP
            -- pck_core.log('transfer:'||l_metrics(i).transferSize || ' name:'||l_metrics(i).name);
            IF (l_metrics(i).contentType='text/html' OR LOWER(l_metrics(i).entryType)='navigation') THEN
                l_resources('html'):=l_resources('html') + l_metrics(i).transferSize;
            ELSIF (l_metrics(i).initiatorType='img') THEN
                l_resources('img'):=l_resources('img') + l_metrics(i).transferSize;
            ELSE
                n:=INSTR(l_metrics(i).name,'/',-1)+1;
                l_filename:=SUBSTR(l_metrics(i).name,n);
                n:=INSTR(l_filename,'.',-1);
                IF (n=0) THEN
                    l_index:='js'; /* oddbird anchor positioning polyfill */
                ELSE
                    l_index:=SUBSTR(l_filename,n+1);
                END IF;
                l_resources(l_index):=l_resources(l_index) + l_metrics(i).transferSize;
            END IF;
        END LOOP;

        l_article:=
        '<p>The amount of data downloaded to visit web sites contributes significantly to global carbon emissions.</p>' ||
        '<p>Every 2 years a comprehensive analysis of some 16 million web sites is conducted by the HTTP Archive. Details of the last survey can be viewed at <a href="https://almanac.httparchive.org/en/2024">Web Almanac</a></p>' ||
        '<p>The chart below compares percentile values from the survey with how much data your browser downloaded to view this page.</p>' ||
        '<div style="overflow-x:auto;font-size:90%;margin-inline:auto" tabindex="0" role="group" aria-labelledby="table-caption">' ||
            '<table class="data-table" style="margin-inline:auto">' ||
                '<caption id="table-caption">Ecological impact comparison</caption>' ||
                '<tr><th>Resource</th><th>You</th><th>P10</th><th>P25</th><th>P50</th><th>P75</th><th>P90</th></tr>';

        l_index:=l_resources.FIRST;
        WHILE l_index IS NOT NULL LOOP
            IF (l_resources(l_index)>0) THEN
                l_article:=l_article ||
                '<tr><td>' || CASE WHEN l_index='woff2' THEN 'Fonts' WHEN l_index='img' THEN 'Images' ELSE UPPER(l_index) END || '</td><td style="text-align:right">' || TO_CHAR(ROUND(l_resources(l_index)/1024)) || ' KB</td></tr>';
                l_total:=l_total+l_resources(l_index);
            END IF;
            l_index:=l_resources.NEXT(l_index);
        END LOOP;
        l_total:=ROUND(l_total/1024);
        l_75P:=ROUND((l_total/3667)*100,1);
        l_article:=l_article ||
                '<tr><td><strong>TOTAL</strong></td>' || 
                    '<td style="text-align:right"><strong>' || l_total || ' KB</strong></td>' || 
                    '<td style="text-align:right"><strong>465 KB</strong></td>' || 
                    '<td style="text-align:right"><strong>995 KB</strong></td>' || 
                    '<td style="text-align:right"><strong>1,926 KB</strong></td>' || 
                    '<td style="text-align:right"><strong>3,667 KB</strong></td>' || 
                    '<td style="text-align:right"><strong>7,257 KB</strong></td>' || 
                '</tr>' ||
                '<tr><td></td>' || 
                    '<td></td>' || 
                    '<td style="text-align:right"><i>' || ROUND((l_total/465)*100) || '%</i></td>' || 
                    '<td style="text-align:right"><i>' || ROUND((l_total/995)*100) || '%</i></td>' || 
                    '<td style="text-align:right"><i>' || ROUND((l_total/1926)*100) || '%</i></td>' || 
                    '<td style="text-align:right"><i>' || l_75P || '%</i></td>' || 
                    '<td style="text-align:right"><i>' || ROUND((l_total/7257)*100,1) || '%</i></td>' || 
                '</tr>';

        l_article:=l_article ||
            '</table>' ||
        '</div>' ||
        '<p>The chart shows that you downloaded ' || l_total || ' KB which is ' || l_75P || '% of the weight of most pages on the internet, that is 3,667 KB at the 75th percentile.</p>' ||
        '<p>The environmental impact of internet usage can be vastly reduced by smaller page weights.</p>' ||
        '<p>The platform that built this web site aims to achieve maximum 100 KB pages that can be engaged with in less than 2 seconds (it took ' || NVL(l_lcp,l_fcp) || ' seconds for you to engage with this page)</p>';

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header', '<h4>Ecology Report</h4>');
        l_json.put('article', l_article);
        l_json.put('footer', '<span></span>');
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;

    PROCEDURE uploadPageVisit(pWebsiteid IN website.id%type, pArticleid IN article.id%type, pBodyText IN CLOB) IS
        l_user_agent_hash browser_lookup.user_agent_hash%type;
        l_ip_address_hash ip_lookup_hash.ip_address_hash%type;
        l_city website_visitor.city%type;
        l_country_code website_visitor.country_code%type;
        l_location VARCHAR2(100);
        l_browser browser_lookup.browser%type;
        l_clob CLOB;
        l_clob2 clob;
        l_object JSON_OBJECT_T;
        l_array JSON_ARRAY_T;    
        l_session_id page_hit.session_id%type;
        l_cpu_used_session NUMBER;
        n PLS_INTEGER;
    BEGIN
        l_cpu_used_session:=dbms_utility.get_cpu_time();

        FOR C IN (
            SELECT seq, referrer, lcp, lcp_rating, cls, cls_rating, inp, inp_rating, fcp, fcp_rating, ttfb, ttfb_rating,
                mobile, connection, url, pathname, website_loaded, duration_visible, page_weight, total_requests, first_visit, aud
          FROM JSON_TABLE(pBodyText FORMAT JSON, '$' 
            COLUMNS (seq, referrer, LCP, LCP_rating, CLS, CLS_rating, INP, INP_rating, FCP, FCP_rating, TTFB, TTFB_rating,
                    mobile, connection, url, pathname, website_loaded, duration_visible, page_weight, total_requests, first_visit, aud))
        ) LOOP
            /* 
            ** 1. Get location from IPData.co
            **    TO BE GDPR/CCPA COMPLIANT NEVER STORE VISITOR'S IP ADDRESS 
            */
            l_ip_address_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(OWA_UTIL.get_cgi_env ('REMOTE_ADDR'),'AL32UTF8'), typ=>dbms_crypto.hash_sh1);
            FOR C1 IN (
                SELECT NVL2(city,city || ',',null) || country_code location
                  FROM ip_lookup_hash
                 WHERE ip_address_hash=l_ip_address_hash
             ) LOOP
                l_location:=C1.location;
            END LOOP;
            IF (l_location IS NULL) THEN
                pck_api.callIpdataAPI(null,OWA_UTIL.get_cgi_env ('REMOTE_ADDR'),'GET',l_clob);
                FOR C2 IN (
                    SELECT city,country_code, NVL2(city,city || ',',null) || country_code AS location
                      FROM JSON_TABLE(l_clob, '$' COLUMNS(city,country_code))
                ) LOOP
                    INSERT INTO ip_lookup_hash (ip_address_hash, city, country_code)
                    VALUES (l_ip_address_hash, C2.city, C2.country_code);
                    l_location:=C2.location;
                    pck_core.log('First time IP -> '||C2.location);
                END LOOP;
            END IF;
            /* 
            ** 2. BROWSER
            */
            l_user_agent_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(OWA_UTIL.get_cgi_env('HTTP_USER_AGENT'),'AL32UTF8'), typ=>dbms_crypto.hash_sh1);
            FOR C1 IN (
                SELECT browser
                  FROM browser_lookup
                 WHERE user_agent_hash=l_user_agent_hash
             ) LOOP
                l_browser:=C1.browser;
            END LOOP;

            IF (l_browser IS NULL) THEN
                l_array:= new JSON_ARRAY_T;
                l_object:=new JSON_OBJECT_T;
                l_object.put('name','User-Agent');
                l_object.put('value',OWA_UTIL.get_cgi_env('HTTP_USER_AGENT'));
                l_array.append(l_object);
                l_object:=new JSON_OBJECT_T;
                l_object.put('headers',l_array);
                l_clob:=l_object.stringify;
                pck_api.callBrowserAPI(l_clob,l_clob2);
                FOR C1 IN (
                    SELECT simple_software_string
                      FROM JSON_TABLE(l_clob2 FORMAT JSON, '$.detection' COLUMNS (simple_software_string))
                ) LOOP
                    l_browser:=C1.simple_software_string;
                    INSERT INTO browser_lookup (browser, user_agent_hash) VALUES (l_browser, l_user_agent_hash);
                    pck_core.log('New browser -> ' || l_browser);
                END LOOP;
            END IF;
            /* 
            ** 3. SESSION_ID : 
            */
            l_session_id:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(pWebsiteid || C.url || C.website_loaded || OWA_UTIL.get_cgi_env('HTTP_REMOTE_ADDR') || OWA_UTIL.get_cgi_env('HTTP_USER_AGENT'),'AL32UTF8'), typ=>dbms_crypto.hash_sh1);

            /*
            **  seq=0 means first analytics segment for page
            **  seq>0 means analytics captured when user tabs back to the page  - used to update duration on page metric plus CWV metrics emitted after page interactions (INP)
            */
            SELECT COUNT(*) INTO n FROM dual WHERE EXISTS (SELECT null FROM page_hit WHERE website_id=pWebsiteid AND article_id=pArticleid AND session_id=l_session_id);

            IF (n=0) THEN
                INSERT INTO page_hit (
                    website_id, article_id, session_id, visit_date, url, duration_visible, first_visit,
                     ttfb, ttfb_rating, fcp, fcp_rating, lcp, lcp_rating, cls, cls_rating, inp, inp_rating,
                     referrer, browser, location, mobile, connection, page_weight, total_requests, aud, pathname
                )
                VALUES (
                    pWebsiteid, pArticleid, l_session_id, TO_TIMESTAMP('1970-01-01','yyyy-mm-dd') + numtodsinterval(C.website_loaded,'second'), C.url, C.duration_visible, C.first_visit,
                    C.ttfb, C.ttfb_rating, C.fcp, C.fcp_rating, C.lcp, C.lcp_rating, C.cls, C.cls_rating, C.inp, C.inp_rating,
                    apex_string_util.get_domain(C.referrer), l_browser, l_location, C.mobile, C.connection, C.page_weight, C.total_requests, C.aud, C.pathname
                );
            ELSE
                UPDATE page_hit
                   SET duration_visible=duration_visible+C.duration_visible,
                       ttfb=NVL(ttfb,C.ttfb), ttfb_rating=NVL(ttfb_rating,C.ttfb_rating),
                       fcp=NVL(fcp,C.fcp), fcp_rating=NVL(fcp_rating,C.fcp_rating),
                       lcp=NVL(lcp,C.lcp), lcp_rating=NVL(lcp_rating,C.lcp_rating),
                       cls=NVL(cls,C.cls), cls_rating=NVL(cls_rating,C.cls_rating),
                       inp=NVL(inp,C.inp), inp_rating=NVL(inp_rating,C.inp_rating)
                 WHERE website_id=pWebsiteid
                   AND article_id=pArticleid
                   AND session_id=l_session_id;
            END IF;
            n:=sql%rowcount;
            IF (n<>1) THEN
                pck_core.log('ERROR rowcount:'||n);
            END IF;
        END LOOP;

        pck_sec.updateCpuUsed(pWebsiteid=>pWebsiteid, pUserid=>null, pCpuUsedStart=>l_cpu_used_session);

    END;

    /*
    ** Convert seconds to hms notation, e.g. 100 -> 1m40s
    */
    FUNCTION elapsedTime(pSeconds IN NUMBER) RETUrN VARCHAR2 IS
        t_ela_time apex_t_varchar2;
        l_ela_time VARCHAR2(13);
    BEGIN
        IF (pSeconds IS NULL) THEN
            RETURN(null);
        END IF;

        IF (pSeconds>86399) THEN
            RETURN ('>24h');
        END IF;
        t_ela_time:=apex_string.split(TO_CHAR(TO_DATE(pSeconds,'sssss'),'fmhh24:mi:ss'),':');
        l_ela_time:=NULL;
        IF (t_ela_time(1)<>'0') THEN
            l_ela_time:=l_ela_time || t_ela_time(1) || 'h';
        END if;
        IF (t_ela_time(2)<>'0') THEN
            l_ela_time:=l_ela_time || ' ' ||t_ela_time(2) || 'm';
        END if;
        IF (t_ela_time(3)<>'0') THEN
            l_ela_time:=l_ela_time || ' ' || t_ela_time(3) || 's';
        END if;
        RETURN (LTRIM(l_ela_time));
    END;

    FUNCTION pagination(pSelectedPage IN NUMBER, pTotal IN NUMBER) RETURN VARCHAR2 IS
        l_html LONG;

        FUNCTION buttonHtml(pPageNo IN NUMBER, pEllipsis IN VARCHAR2 DEFAULT NULL) RETURN VARCHAR2 IS
        BEGIN
            RETURN 
                CASE WHEN pEllipsis='ellipsis-before' THEN '<span>...</span>' END || 
                '<button type="button" class="button" data-button-variant="round-icon" data-page="' || pPageNo || '" ' || CASE WHEN pPageNo=pSelectedPage THEN 'aria-pressed="true"' ELSE 'aria-pressed="false"' END|| '>' || pPageNo || '</button>' || 
                CASE WHEN pEllipsis='ellipsis-after' THEN '<span>...</span>' END;
        END;
    BEGIN
        l_html:=
        '<div class="flex-items center" role="group" aria-label="Buttons to page through visits" tabindex="0">';

        IF (pTotal<=7) THEN
            FOR i IN 1..pTotal LOOP
                l_html:=l_html || buttonHtml(i);
            END LOOP;
            l_html:=l_html || '</div>';
            RETURN (l_html);
        END IF;

        IF (pSelectedPage>1) THEN
            l_html:=l_html || '<button type="button" class="button" style="--button-font-size:70%" data-page="' || TO_CHAR(pSelectedPage-1) || '"><< Previous</button>';
        END IF;

        IF (pSelectedPage<=5) THEN
            FOR i IN 1..LEAST(pSelectedPage+2,pTotal) LOOP
                l_html:=l_html || buttonHtml(i);
            END LOOP;
            l_html:=l_html || buttonHtml(pTotal,'ellipsis-before');
        ELSE
            l_html:=l_html || buttonHtml(1,'ellipsis-after');
            /* Middle section */
            FOR i IN pSelectedPage-2..LEAST(pSelectedPage+2,pTotal) LOOP
                l_html:=l_html || buttonHtml(i);
            END LOOP;
            /* End section */
            IF (pTotal-pSelectedPage<=4) THEN
                FOR i IN pSelectedPage+3..pTotal LOOP
                    l_html:=l_html || buttonHtml(i);
                END LOOP;
            ELSE
                l_html:=l_html || buttonHtml(pTotal,'ellipsis-before');
            END IF;
        END IF;

         IF (pSelectedPage<pTotal) THEN
            l_html:=l_html || '<button type="button" class="button" style="--button-font-size:70%" data-page="' || TO_CHAR(pSelectedPage+1) || '">Next >></button>';
        END IF;

        l_html:=l_html ||
        '</div>';

        RETURN (l_html);
    END;

    /*
    **  Return log of all visits to website
    */
    PROCEDURE getWebsiteVisits(pWebsiteId IN website.id%type, pPage IN NUMBER, pHandler IN VARCHAR2, pTimeZone IN VARCHAR2, pTotalPages IN OUT NUMBER, pHtml IN OUT NOCOPY CLOB) IS
        l_content CLOB;
        l_count NUMBER;
        l_offset NUMBER;
    BEGIN
        l_offset:=(pPage-1)*ROWS_PER_PAGE;

        SELECT COUNT(DISTINCT session_id) INTO l_count FROM page_hit WHERE website_id=pWebsiteId;
        pTotalPages:=CEIL(l_count/ROWS_PER_PAGE);
        
        IF (pPage=1 AND pHandler='change') THEN
            l_content:=
            '<div class="table-wrapper" tabindex="0" role="region" aria-labelledby="table-caption">' ||
                '<table>' ||
                    '<caption id="table-caption">' || l_count || ' total site visits</caption>' ||
                    '<thead>' ||
                        '<tr>' ||
                            '<th>When</th>' ||
                            '<th>Location<span class="chip">First Visit</span></th>' ||
                            '<th>Engagement</th>' ||
                            '<th>Referrer</th>' ||
                        '</tr>' ||
                    '</thead>' ||
                    '<tbody>';
        END IF;

        FOR C IN (
            SELECT  COUNT(*) pages_viewed,
                    MAX(visit_date) visit_date,
                    MAX(first_visit) first_visit,
                    MAX(p.referrer) referrer, 
                    MAX(p.ttfb_rating) ttfb_rating, MAX(p.fcp_rating) fcp_rating,
                    MAX(p.lcp_rating) lcp_rating, MAX(p.cls_rating) cls_rating, MAX(p.inp_rating) inp_rating, 
                    MAX(p.ttfb) ttfb, MAX(p.fcp) fcp, MAX(p.lcp) lcp, MAX(p.cls) cls, MAX(p.inp) inp, 
                    MAX(p.page_weight) page_weight, 
                    MAX(p.total_requests) total_requests,
                    SUM(p.duration_visible) duration_visible, 
                    MAX(p.browser) browser, 
                    MAX(p.location) location, 
                    MAX(p.connection) connection, 
                    MAX(p.mobile) mobile,
                    MAX(w.domain_name) domain_name
              FROM page_hit p, website w
             WHERE p.website_id=pWebsiteid
               AND w.id=pWebsiteid
             GROUP BY p.session_id
             ORDER BY visit_date DESC
             OFFSET l_offset ROWS
             FETCH FIRST ROWS_PER_PAGE ROWS ONLY
        ) 
        LOOP

            l_content:=l_content ||
                '<tr>' ||
                    '<td>' || TO_CHAR(C.visit_date at time zone pTimeZone, 'dd-mm-yy fmhh24:fmmipm') || '</td>' ||
                    '<td>' || C.location || CASE WHEN C.first_visit=1 THEN '<span class="chip">First Visit</span>' END || '</td>' ||
                    '<td>' || elapsedTime(C.duration_visible) || CASE WHEN C.pages_viewed>1 THEN ' - ' || C.pages_viewed || ' pages' END || '</td>' ||
                    '<td>' || CASE WHEN INSTR(C.referrer,C.domain_name)=0 THEN C.referrer END || '</td>' ||
                '</tr>';
        END LOOP;

        IF (pPage=1) THEN
            l_content:=l_content || '</tbody></table></div>';
        END IF;

        pHtml:=l_content;
    END;

    /*
    **  First time vidits
    */
    PROCEDURE getWebsiteVisitsFirst(pWebsiteId IN website.id%type, pPage IN NUMBER, pHandler IN VARCHAR2, pTimeZone IN VARCHAR2, pTotalPages IN OUT NUMBER, pHtml IN OUT NOCOPY CLOB) IS
        l_content CLOB;
        l_offset NUMBER;
        l_count NUMBER;
    BEGIN
        l_offset:=(pPage-1)*ROWS_PER_PAGE;

        SELECT COUNT(DISTINCT session_id) INTO l_count FROM page_hit WHERE website_id=pWebsiteId AND first_visit=1;
        pTotalPages:=CEIL(l_count/ROWS_PER_PAGE);
        
        IF (pPage=1 AND pHandler='change') THEN
            l_content:=
            '<div class="table-wrapper" tabindex="0" role="region" aria-labelledby="table-caption">' ||
                '<table>' ||
                    '<caption id="table-caption">' || l_count|| ' first time visits with initial page weight and seconds loaded</caption>' ||
                    '<thead>' ||
                        '<tr>' ||
                            '<th>When</th>' ||
                            '<th>Location</th>' ||
                            '<th>Page Weight</th>' ||
                            '<th>Loaded</th>' ||
                            '<th>Connection</th>' ||
                            '<th>Browser</th>' ||
                        '</tr>' ||
                    '</thead>' ||
                    '<tbody>';
        END IF;

        FOR C IN (
            SELECT  MAX(visit_date) visit_date,
                    ROUND(NVL(MAX(p.LCP)/1000,MAX(p.FCP)/1000),1) loaded,
                    MAX(p.page_weight) page_weight, 
                    MAX(p.browser) browser, 
                    MAX(p.location) location, 
                    MAX(p.connection) || CASE WHEN MAX(p.mobile)='true' THEN '(mobile)' END connection
              FROM page_hit p
             WHERE p.website_id=pWebsiteid
             GROUP BY p.session_id
             HAVING MAX(first_visit)=1
             ORDER BY visit_date DESC
             OFFSET l_offset ROWS
             FETCH FIRST ROWS_PER_PAGE ROWS ONLY
        ) 
        LOOP
            l_content:=l_content ||
                '<tr>' ||
                    '<td>' || TO_CHAR(C.visit_date at time zone pTimeZone, 'dd-mm-yy fmhh24:fmmipm') || '</td>' ||
                    '<td>' || C.location || '</td>' ||
                    '<td>' || apex_string_util.to_display_filesize(C.page_weight) || '</td>' ||
                    '<td>' || CASE WHEN C.loaded IS NOT NULL THEN TO_CHAR(C.loaded,'fm90D0') ||  ' s' END || '</td>' ||
                    '<td>' || C.connection || '</td>' ||
                    '<td>' || C.browser || '</td>' ||
                '</tr>';
        END LOOP;

        IF (pPage=1 AND pHandler='change') THEN
            l_content:=l_content || '</tbody></table></div>';
        END IF;

        pHtml:=l_content;
    END;

    PROCEDURE getVisitsByLocation(pWebsiteId IN website.id%type, pPage IN NUMBER, pHandler IN VARCHAR2, pTotalPages IN OUT NUMBER, pRowCount IN OUT NUMBER, pHtml IN OUT NOCOPY CLOB)  IS
        l_content CLOB;
        n PLS_INTEGER:=0;
        l_offset NUMBER;
        l_count NUMBER;
    BEGIN
        l_offset:=(pPage-1)*ROWS_PER_PAGE;

        SELECT COUNT(DISTINCT SUBSTR(location,INSTR(location,',')+1)) INTO l_count FROM page_hit WHERE website_id=pWebsiteId;
        pTotalPages:=CEIL(l_count/ROWS_PER_PAGE);
        
        IF (pPage=1 AND pHandler='change') THEN
            l_content:=
            '<div class="table-wrapper" tabindex="0" role="region" aria-labelledby="table-caption">' ||
            '<table>' ||
                '<caption id="table-caption">Visits from ' || l_count || ' distinct countries</caption>' ||
                '<thead>' ||
                    '<tr>' ||
                        '<th>Location</th>' ||
                        '<th>Visits</th>' ||
                        '<th>Pct</th>' ||
                        '<th>First Visit</th>' ||
                        '<th>Last Visit</th>' ||
                    '</tr>' ||
                '</thead>' ||
                '<tbody>';
        END IF;

        FOR C IN (
            SELECT location, first_visit_date, last_visit_date, nb_visits, RATIO_TO_REPORT(nb_visits) OVER() pct, COUNT(*) OVER() total
            FROM
                (
                SELECT SUBSTR(location,INSTR(location,',')+1) location, MIN(visit_date) first_visit_date, MAX(visit_date) last_visit_date, COUNT(*) nb_visits, COUNT(*) OVER() total_visits
                  FROM page_hit 
                 WHERE website_id=pWebsiteId
                 GROUP BY SUBSTR(location,INSTR(location,',')+1)
                )
            ORDER BY nb_visits DESC, location
            OFFSET l_offset ROWS
            FETCH FIRST ROWS_PER_PAGE ROWS ONLY
        ) LOOP
            l_content:=l_content || 
            '<tr>' ||
                '<td>' || C.location || '</td>' || 
                '<td>' || C.nb_visits || '</td>' || 
                '<td>' || TO_CHAR(C.pct*100,'fm990D00') || '%</td>' ||
                '<td>' || TO_CHAR(C.first_visit_date,'dd Mon yyyy') || '</td>' ||
                '<td>' || TO_CHAR(C.last_visit_date,'dd Mon yyyy') || '</td>' ||
            '</tr>';
        END LOOP;

        IF (pPage=1 AND pHandler='change') THEN
            l_content:=l_content || 
            '</tbody></table></div>';
        END IF;

        pRowCount:=l_count;
        pHtml:=l_content;
    END;

    PROCEDURE getVisitsByBrowser(pWebsiteId IN website.id%type, pPage IN NUMBER, pHandler IN VARCHAR2, pTotalPages IN OUT NUMBER, pRowCount IN OUT NUMBER, pHtml IN OUT NOCOPY CLOB)  IS
        l_content CLOB;
        l_offset NUMBER;
        l_count NUMBER;
    BEGIN
        l_offset:=(pPage-1)*ROWS_PER_PAGE;

        SELECT COUNT(DISTINCT browser) INTO l_count FROM page_hit WHERE website_id=pWebsiteId AND browser IS NOT NULL;
        pTotalPages:=CEIL(l_count/ROWS_PER_PAGE);
        
        IF (pPage=1 AND pHandler='change') THEN
            l_content:=
            '<div class="table-wrapper" tabindex="0" role="region" aria-labelledby="table-caption">' ||
            '<table>' ||
                '<caption id="table-caption">Site visited by ' || l_count|| ' distinct browser, operating systems</caption>' ||
                '<thead>' ||
                    '<tr>' ||
                        '<th>Browser</th>' ||
                        '<th>Visits</th>' ||
                        '<th>Pct</th>' ||
                        '<th>First Visit</th>' ||
                        '<th>Last Visit</th>' ||
                    '</tr>' ||
                '</thead>' ||
                '<tbody>';
        END IF;

        FOR C IN (
                   SELECT browser, first_visit_date, last_visit_date, nb_visits, RATIO_TO_REPORT(nb_visits) OVER() pct, COUNT(*) OVER() total
                    FROM
                        (
                        SELECT browser, MIN(visit_date) first_visit_date, MAX(visit_date) last_visit_date, COUNT(*) nb_visits, COUNT(*) OVER() total_visits
                          FROM page_hit 
                         WHERE website_id=pWebsiteId
                           AND browser IS NOT NULL
                         GROUP BY browser
                        )
                    ORDER BY nb_visits DESC, browser
                    OFFSET l_offset ROWS
                    FETCH FIRST ROWS_PER_PAGE ROWS ONLY
            ) LOOP
                l_content:=l_content || 
                '<tr>' ||
                    '<td>' || C.browser || '</td>' || 
                    '<td>' || C.nb_visits || '</td>' || 
                    '<td>' || TO_CHAR(C.pct*100,'fm990D00') || '%</td>' ||
                    '<td>' || TO_CHAR(C.first_visit_date,'dd Mon yyyy') || '</td>' ||
                    '<td>' || TO_CHAR(C.last_visit_date,'dd Mon yyyy') || '</td>' ||
                '</tr>';
            END LOOP;

        IF (pPage=1 AND pHandler='change') THEN
            l_content:=l_content || 
            '</tbody></table></div>';
        END IF;

        pRowCount:=l_count;
        pHtml:=l_content;
    END;

    /* 
    **  ENTRY POINT FOR ALL VISIT REPORTS
    */
    PROCEDURE getVisits(pWebsiteId IN website.id%type, pReport IN NUMBER, pPage IN NUMBER, pHandler IN VARCHAR2, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_header LONG;
        l_article CLOB;
        l_footer LONG;
        l_total_pages NUMBER;
        l_row_count NUMBER;
        l_notification VARCHAR2(50);
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        IF (pPage=1) THEN
            l_header:=
            '<div>' ||
                '<label for="report">Select Report</label>' ||
                '<select id="report" class="select-analytics">' ||
                    '<button>' ||
                      '<selectedcontent></selectedcontent>' ||
                      '<span class="arrow"></span>' ||
                    '</button>' ||
                    '<option value="1" selected>' ||
                        '<div class="chip" aria-hidden="true">Sessions</div>' ||
                        '<div>' ||    
                            '<div class="heading">Site visits</div>' ||
                            '<div class="detail">When, where, engagement, referrer</div>' ||
                        '</div>' ||
                    '</option>' ||
                    '<option value="2">' ||
                        '<div class="chip" aria-hidden="true">First</div>' ||
                        '<div>' ||    
                            '<div class="heading">First time visits</div>' ||
                            '<div class="detail">When, where, page weight, load time, connection speed</div>' ||
                        '</div>' ||
                    '</option>' ||
                    '<option value="3">' ||
                        '<div class="chip" aria-hidden="true">Countries</div>' ||
                        '<div>' ||    
                            '<div class="heading">Visits by Country</div>' ||
                            '<div class="detail">Visit counts by country code including date of first and last visit</div>' ||
                        '</div>' ||
                    '</option>' ||
                    '<option value="4">' ||
                        '<div class="chip" aria-hidden="true">Browsers</div>' ||
                        '<div>' ||    
                            '<div class="heading">Visits by Browser / operating system</div>' ||
                            '<div class="detail">Visit counts by browser including date of first and last visit</div>' ||
                        '</div>' ||
                    '</option>' ||
                '</select>' ||
            '</div>';
        END IF;

        CASE pReport
            WHEN 1 THEN
                getWebsiteVisits(pWebsiteId, pPage, pHandler, l_session_data.timezone,l_total_pages, l_article);
            WHEN 2 THEN
                getWebsiteVisitsFirst(pWebsiteId, pPage, pHandler, l_session_data.timezone,l_total_pages, l_article);
            WHEN 3 THEN
                getVisitsByLocation(pWebsiteId, pPage, pHandler, l_total_pages, l_row_count, l_article);
            WHEN 4 THEN
                getVisitsByBrowser(pWebsiteId, pPage, pHandler, l_total_pages, l_row_count, l_article);
        END CASE;

        l_notification:=(pPage-1)*ROWS_PER_PAGE+1 || ' through ' || LEAST(l_row_count,pPage*ROWS_PER_PAGE);

        l_json:= new JSON_OBJECT_T;
        
        l_json.put('success',true);
        l_json.put('header', l_header);
        l_json.put('article', l_article);
        l_json.put('footer', pagination(pPage,l_total_pages) || '<div class="flex-items center" id="notification" aria-live="assertive"></div>');
        l_json.put('notification','Showing rows ' || l_notification);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        pck_sec.updateCpuUsed(pWebsiteId=>pWebsiteId, pUserid=>l_session_data.user_id, pCpuUsedStart=>l_session_data.cpu_used_session);

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    /*
    **  DAILY JOB TO PROCESS VISIT DATA. NOTHING YET.
    */
    PROCEDURE daily_job IS
        n PLS_INTEGER;
    BEGIN
        null;
    END;
    
END;
/