CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_LOGO" as
    
    PROCEDURE getLogoForm(pWebsiteId IN website.id%type, pLogoType IN VARCHAR2, pStatus OUT NUMBER);
    --
    PROCEDURE updateLogo(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_LOGO" as

    PROCEDURE buildImageLogoForm(pWebsiteId IN website.id%type, pHeader IN OUT VARCHAR2, pArticle IN OUT NOCOPY CLOB) IS
        l_logo_type website.logo_type%type;
        l_logo_img_id website.logo_img_id%type;
    BEGIN
        pHeader:='<span>Create Website Logo from an uploaded image</span>';

        SELECT w.logo_type, w.logo_img_id
          INTO l_logo_type, l_logo_img_id
          FROM website w
         WHERE w.id=pWebsiteid;

        pArticle:=
        '<fieldset class="flow">' ||
            '<legend>Select and configure image logo</legend>' ||
            '<div>' ||
                '<select id="logo-img-id">' ||
                    '<button>' ||
                        '<selectedcontent></selectedcontent>' ||
                        '<span class="arrow"></span>' ||
                    '</button>' ||
                    '<div class="option-container">' ||
                        '<option value="" hidden>' ||
                            '<span>Search Images</span>' ||
                        '</option>';

                FOR C IN (
                    SELECT wa.article_id, a.id, a.cld_cloud_name, a.public_id
                      FROM website_article wa, asset a
                     WHERE wa.website_id=pWebsiteid
                       AND a.article_id=wa.article_id
                       AND a.format='avif'
                     ORDER BY a.created_date DESC
                )
                LOOP
                    pArticle:=pArticle || 
                        '<option value="' || TO_CHAR(C.id) || '">' || 
                            '<img src="https://res.cloudinary.com/' || C.cld_cloud_name || '/w_200/' || C.public_id || '" alt="">' ||
                        '</option>';
                END LOOP;
                

        pArticle:=pArticle || 
                    '</div>' ||
                '</select>' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-img-corner-shape">Change border shape:</label>' ||
                '<input type="range" id="logo-img-corner-shape" min="-5" value="0" max="5" step="0.1" />' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-img-border-radius">Adjust effect:</label>' ||
                '<input type="range" id="logo-img-border-radius" min="0" value="45" max="90" step="1" />' ||
            '</div>' ||
        '</fieldset>';
    END;

    /*
    ** CREATE HTML FOR VARIABLE FONT AXES
    */
    FUNCTION buildFontVariations(pWebsiteid IN website.id%type, pFontid IN font.id%type, pContext IN website_font.context%type) RETURN VARCHAR2 IS
        l_variations LONG;
        n PLS_INTEGER:=0;
    BEGIN
        l_variations:=
        '<div class="font-variations flow">';
        FOR C IN (
            SELECT fa.axis, fa.axis_start, fa.axis_end, fm.display_name, fv.value
              FROM font_axes fa, font_metadata fm, font_variation fv
             WHERE fa.font_id=pFontid
               AND fm.axis=fa.axis
               AND fv.axis(+)=fa.axis
               AND fv.website_id(+)=pWebsiteid
               AND fv.context(+)=pContext
             ORDER BY fa.axis
        ) LOOP
            n:=n+1;
            l_variations:=l_variations || 
            '<div>';
            -- For accessibility implement toggle as button
            IF (C.axis_end-C.axis_start=1) THEN
                l_variations:=l_variations || 
                    '<button type="button" id="' || C.axis || '" aria-pressed="' || CASE WHEN C.value='0' THEN 'false' ELSE 'true' END || '">' || C.display_name || '</button>';
            ELSE
                l_variations:=l_variations || 
                    '<label for="' || C.axis || '">' || C.display_name || '</label>' ||
                    '<input type="range" id="' || C.axis || '" min="' || TO_CHAR(C.axis_start) || '"' || CASE WHEN C.value IS NOT NULL THEN ' value="' || TO_CHAR(C.value) || '"' END || ' max="'|| TO_CHAR(C.axis_end) || '" step="1" />';
            END IF;
            l_variations:=l_variations || 
            '</div>';
        END LOOP;
        -- Build weight select lisy for static fonts
        IF (n=0) THEN
            l_variations:=l_variations || 
            '<div>' ||
                '<label for="wght">Weight</label>' ||
                '<select id="wght">' ||
                    '<button>' ||
                      '<selectedcontent></selectedcontent>' ||
                      '<span class="arrow"></span>' ||
                    '</button>';
                    FOR C IN (SELECT wght_normal, wght_italic FROM font_static_wght WHERE font_id=pFontid ORDER BY 1)
                    LOOP
                        IF (C.wght_italic IS NOT NULL) THEN
                            n:=1;
                        END IF;
                        l_variations:=l_variations || 
                        '<option value="' || TO_CHAR(C.wght_normal) || '">' || 
                            CASE C.wght_normal
                                WHEN 100 THEN 'Thin'
                                WHEN 200 THEN 'Extra Light'
                                WHEN 300 THEN 'Light'
                                WHEN 400 THEN 'Normal'
                                WHEN 500 THEN 'Medium'
                                WHEN 600 THEN 'Semi Bold'
                                WHEN 700 THEN 'Bold'
                                WHEN 800 THEN 'Extra Bold'
                                WHEN 900 THEN 'Ultra Bold'
                            END
                        || '</option>';
                    END LOOP;
                l_variations:=l_variations || 
                '</select>' ||
            '</div>';
            IF (n=1) THEN
                l_variations:=l_variations || 
                '<div>' ||
                    '<button type="button" id="ital" aria-pressed="0">Italic</button>' ||
                '</div>';
            END IF;
        END IF;
        l_variations:=l_variations || 
        '</div>';

        RETURN (l_variations);
    END;

    /*
    ** CREATE HTML FOR FONT LOGO FORM
    */
    PROCEDURE buildFontLogoForm(pWebsiteId IN website.id%type, pHeader IN OUT VARCHAR2, pArticle IN OUT NOCOPY CLOB) IS
        l_font_id website_font.font_id%type;
        l_logo_font_size website.logo_font_size%type;
        l_logo_font_text website.logo_font_text%type;
    BEGIN
        pHeader:='<span>Create Website Logo using selected font</span>';

        SELECT w.logo_font_size, NVL(w.logo_font_text,w.domain_name), wf.font_id
          INTO l_logo_font_size, l_logo_font_text, l_font_id
          FROM website w, website_font wf
         WHERE w.id=pWebsiteid
           AND wf.website_id(+)=w.id
           AND wf.context(+)='logo';

        pArticle:=
        '<fieldset class="flow">' ||
            '<legend>Enter logo text and select font</legend>' ||
            '<div>' ||
                '<label for="logo-font-id">Select Font</label>' ||
                '<select id="logo-font-id">' ||
                    '<button>' ||
                      '<selectedcontent></selectedcontent>' ||
                      '<span class="arrow"></span>' ||
                    '</button>';

                FOR C IN (SELECT id, family FROM font ORDER BY 2)
                LOOP
                    pArticle:=pArticle || 
                    '<option value="' || TO_CHAR(C.id) || '"' || CASE WHEN C.id=l_font_id THEN ' selected' END || '>' || C.family || '</option>';
                END LOOP;

        pArticle:=pArticle || 
                '</select>' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-font-text">Logo text</label>' ||
                '<input type="text" id="logo-font-text" value="' || l_logo_font_text || '"/>' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-font-size">Adjust size</label>' ||
                '<input type="range" id="logo-font-size" min="2" value="' || TO_CHAR(l_logo_font_size) || '" max="5" step="0.1" />' ||
            '</div>' ||
            buildFontVariations(pWebsiteid, l_font_id, 'logo');

        pArticle:=pArticle || 
        '</fieldset>';
    END;


    /*
    ** CREATE HTML FOR PAGE OPTIONS FORM
    */
    PROCEDURE getLogoForm(pWebsiteId IN website.id%type,  pLogoType IN VARCHAR2, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_header LONG;
        l_article CLOB;
        l_footer LONG;
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        CASE pLogoType
            WHEN 'image' THEN buildImageLogoForm(pWebsiteId, l_header, l_article);
            WHEN 'font' THEN buildFontLogoForm(pWebsiteId, l_header, l_article);
        END CASE;

        l_footer:=
        '<div class="flex-items">' ||
            '<button type="button" class="button publish" data-processing="Publishing editor site, wait a few seconds...">' || 'PUBLISH' || '</button>' ||
            '<span aria-live="polite"></span>' ||
            '<span class="loader icon"></span>' ||
        '</div>';

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header',l_header);
        l_json.put('article',l_article);
        l_json.put('footer',l_footer);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** UPDATE WEBSITE LOGO. 
    ** IMAGE | FONT | SVG  ARE MUTUALLY EXCLUSIVE.
    */
    PROCEDURE updateLogo(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_logo_type website.logo_type%type;
        l_column_name user_tab_columns.column_name%type;
        l_column_value VARCHAR2(100);
        l_table_updated PLS_INTEGER;
        l_menu font.menu%type;
        l_menu_text font.menu%type;
        l_variations LONG;
        l_fontfaces VARCHAR2(1000);
        l_api_url VARCHAR2(500);
        l_text VARCHAR2(100);
        l_urls JSON_ARRAY_T;
        l_fontface JSON_OBJECT_T;
        l_json JSON_OBJECT_T;
        n PLS_INTEGER;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT REPLACE(column_name,'-','_'), 
                CASE column_name 
                    WHEN 'logo-img-corner-shape' THEN 'superellipse(' || column_value || ')' 
                    WHEN 'logo-img-border-radius' THEN column_value || 'px'
                    WHEN 'logo-font-size' THEN column_value || 'cqi'
                    ELSE column_value 
                END
          INTO l_column_name, l_column_value 
          FROM JSON_TABLE(pBodyText, '$' COLUMNS (column_name, column_value));

        IF (INSTR(l_column_name,'font')>0) THEN
            l_logo_type:='font';
        ELSIF (INSTR(l_column_name,'img')>0) THEN
            l_logo_type:='img';
        ELSIF (INSTR(l_column_name,'svg')>0) THEN
            l_logo_type:='svg';
        END IF;

        SELECT COUNT(*) INTO n FROM user_tab_columns WHERE table_name='WEBSITE' AND column_name=UPPER(l_column_name);

        IF (n=1) THEN
            EXECUTE IMMEDIATE 'UPDATE website SET logo_updated_date=current_timestamp, logo_type=:B1,' || l_column_name || '=:B2 WHERE id=:B3' USING l_logo_type, l_column_value, pWebsiteId;
        ELSE
            UPDATE font_variation 
               SET updated_date=current_timestamp, 
                   value=l_column_value
             WHERE website_id=pWebsiteid
               AND context='logo'
               AND axis=l_column_name;
            n:=sql%rowcount;
            IF (n=0) THEN
                INSERT INTO font_variation(website_id, context, axis, value)
                VALUES (pWebsiteid, 'logo', l_column_name, l_column_value);
            END IF;
        END IF;

        /* FONT LOGO CHANGE */
        IF (l_column_name='logo_font_id') THEN
            UPDATE website_font 
               SET font_id=l_column_value, updated_date=current_timestamp
             WHERE website_id=pWebsiteid
               AND context='logo';
            n:=sql%rowcount;
            IF (n=0) THEN
                INSERT INTO website_font(website_id, font_id, context)
                VALUES (pWebsiteid,l_column_value, 'logo');
            END IF;
            /*
            ** BUILD FONT PROPERTIES FORM
            */

            /*
            ** Build input range controls for variable font axes
            */
            l_variations:=buildFontVariations(pWebsiteid, l_column_value, 'logo');

            /*
            ** Build Google Fonts CSS API url
            */
            n:=0;
            FOR C IN (
                SELECT 'https://fonts.googleapis.com/css2?family=' ||
                        REPLACE(f.family,' ','+') || ':' || 
                        LISTAGG(fa.axis,',') WITHIN GROUP (ORDER BY fa.axis) || 
                        '@' AS axes,
                        LISTAGG(CASE WHEN fa.axis='ital' THEN '0' ELSE fa.axis_start||'..'||fa.axis_end END,',') WITHIN GROUP (ORDER BY fa.axis) AS ranges
                  FROM font f, font_axes fa
                 WHERE f.id=TO_NUMBER(l_column_value)
                   AND fa.font_id=f.id
                 GROUP BY f.family
            ) LOOP
                l_api_url:=C.axes || C.ranges;
                n:=1;
                IF (INSTR(C.axes,'ital')>0) THEN
                    l_api_url:=l_api_url || ';' || '1' || SUBSTR(C.ranges,2);
                    n:=2;
                END IF;
            END LOOP;

            /* "n" is number of variable font files */

            /*
            ** Set text parameter to support all European languages
            */
            l_text:='abcdefghijklmnopqrstuvwxyz';
            l_text:=l_text || UPPER(l_text);
            l_text:=l_text || '0123456789';
            -- more to come 

            l_api_url:=l_api_url || '&text=' || utl_url.escape(l_text,TRUE,'UTF-8');
            pck_api.callGoogleAPI(pUrl=>l_api_url, pData=>l_fontfaces);

            UPDATE website_font SET editor_fontfaces=l_fontfaces WHERE website_id=pWebsiteid AND font_id=l_column_value AND context='logo';

            l_urls:= new JSON_ARRAY_T;
            FOR i IN 1..n LOOP
                l_fontface:= new JSON_OBJECT_T;
                l_fontface.put('family',REGEXP_SUBSTR(l_fontfaces,'font-family: (.+?);',1,i,null,1));
                l_fontface.put('style',REGEXP_SUBSTR(l_fontfaces,'font-style: (.+?);',1,i,null,1));
                l_fontface.put('weight',REGEXP_SUBSTR(l_fontfaces,'font-weight: (.+?);',1,i,null,1));
                l_fontface.put('stretch',REGEXP_SUBSTR(l_fontfaces,'font-stretch: (.+?);',1,i,null,1));
                l_fontface.put('source',REPLACE(REPLACE(REGEXP_SUBSTR(l_fontfaces,'url(.+?)\)',1,i),'(','("'),')','")'));

                l_urls.append(l_fontface);
            END LOOP;
        END IF;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('urls',l_urls);
        l_json.put('variations',l_variations);
        l_json.put('message',l_column_name || ' updated successfully to ' || l_column_value);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;
end;
/