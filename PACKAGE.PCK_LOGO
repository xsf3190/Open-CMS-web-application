CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_LOGO" as
    
    PROCEDURE getLogoForm(pWebsiteId IN website.id%type, pLogoType IN VARCHAR2, pStatus OUT NUMBER);
    --
    PROCEDURE updateLogo(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_LOGO" as

    PROCEDURE buildImageLogoForm(pWebsiteId IN website.id%type, pHeader IN OUT VARCHAR2, pArticle IN OUT NOCOPY CLOB) IS
        l_logo_type website.logo_type%type;
        l_logo_img_id website.logo_img_id%type;
    BEGIN
        pHeader:='<span>Create Website Logo from an uploaded image</span>';

        SELECT w.logo_type, w.logo_img_id
          INTO l_logo_type, l_logo_img_id
          FROM website w
         WHERE w.id=pWebsiteid;

        pArticle:=
        '<fieldset class="flow">' ||
            '<legend>Select and configure image logo</legend>' ||
            '<div>' ||
                '<select id="logo-img-id">' ||
                    '<button>' ||
                        '<selectedcontent></selectedcontent>' ||
                        '<span class="arrow"></span>' ||
                    '</button>' ||
                    '<div class="option-container">' ||
                        '<option value="" hidden>' ||
                            '<span>Search Images</span>' ||
                        '</option>';

                FOR C IN (
                    SELECT wa.article_id, a.id, a.cld_cloud_name, a.public_id
                      FROM website_article wa, asset a
                     WHERE wa.website_id=pWebsiteid
                       AND a.article_id=wa.article_id
                       AND a.format='avif'
                     ORDER BY a.created_date DESC
                )
                LOOP
                    pArticle:=pArticle || 
                        '<option value="' || TO_CHAR(C.id) || '">' || 
                            '<img src="https://res.cloudinary.com/' || C.cld_cloud_name || '/w_200/' || C.public_id || '" alt="">' ||
                        '</option>';
                END LOOP;
                

        pArticle:=pArticle || 
                    '</div>' ||
                '</select>' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-img-corner-shape">Change border shape:</label>' ||
                '<input type="range" id="logo-img-corner-shape" min="-5" value="0" max="5" step="0.1" />' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-img-border-radius">Adjust effect:</label>' ||
                '<input type="range" id="logo-img-border-radius" min="0" value="45" max="90" step="1" />' ||
            '</div>' ||
        '</fieldset>';
    END;

    FUNCTION buildAxisInput(pAxis IN VARCHAR2, pMin IN NUMBER, pMax IN NUMBER, pValue IN NUMBER DEFAULT NULL) RETURN VARCHAR2 IS
    BEGIN
        RETURN(
        '<div>' ||
            '<label for="' || pAxis || '">Adjust weight</label>' ||
            '<input type="range" id="' || pAxis || '" min="' || TO_CHAR(pMin) || '" value="' || TO_CHAR(pValue) || '" max="'|| TO_CHAR(pMax) || '" step="1" />' ||
        '</div>');
    END;

    FUNCTION buildFontVariations(pWebsiteid IN website.id%type, pFontid IN font.id%type, pContext IN website_font.context%type) RETURN VARCHAR2 IS
        l_variations LONG;
    BEGIN
        FOR C IN (
            SELECT f.slnt_start, f.slnt_end, f.wdth_start, f.wdth_end, f.wght_start, f.wght_end, fv.axis, fv.value
              FROM font f, font_variation fv
             WHERE f.id=pFontid
               AND fv.website_id=pWebsiteid
               AND fv.context=pContext
        ) LOOP
            IF (C.wght_start IS NOT NULL) THEN
                l_variations:=l_variations || 
                '<div>' ||
                    '<label for="logo-font-wght">Adjust weight</label>' ||
                    '<input type="range" id="logo-font-wght" min="' || TO_CHAR(C.wght_start) || '" value="' || TO_CHAR(C.wght) || '" max="'|| TO_CHAR(C.wght_end) || '" step="1" />' ||
                '</div>';

            END IF;
        END LOOP;
    END;

    PROCEDURE buildFontLogoForm(pWebsiteId IN website.id%type, pHeader IN OUT VARCHAR2, pArticle IN OUT NOCOPY CLOB) IS
        l_logo_type website.logo_type%type;
        l_font_id website_font.font_id%type;
        l_logo_font_size website.logo_font_size%type;
        l_logo_font_text website.logo_font_text%type;
    BEGIN
        pHeader:='<span>Create Website Logo using selected font</span>';

        SELECT w.logo_type, w.logo_font_size, w.logo_font_text, wf.font_id
          INTO l_logo_type, l_logo_font_size, l_logo_font_text, l_font_id
          FROM website w, website_font wf
         WHERE w.id=pWebsiteid
           AND wf.website_id(+)=w.id
           AND wf.context(+)='logo';

        pArticle:=
        '<fieldset class="flow">' ||
            '<legend>Enter logo text and select font</legend>' ||
            '<div>' ||
                '<label for="logo-font-id">Select Font</label>' ||
                '<select id="logo-font-id">' ||
                    '<button>' ||
                      '<selectedcontent></selectedcontent>' ||
                      '<span class="arrow"></span>' ||
                    '</button>';

                FOR C IN (SELECT id, family FROM font ORDER BY 2)
                LOOP
                    pArticle:=pArticle || 
                    '<option value="' || TO_CHAR(C.id) || '"' || CASE WHEN C.id=l_font_id THEN ' selected' END || '>' || C.family || '</option>';
                END LOOP;

        pArticle:=pArticle || 
                '</select>' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-font-text">Logo text</label>' ||
                '<input type="text" id="logo-font-text" value="' || l_logo_font_text || '"/>' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-font-size">Adjust size</label>' ||
                '<input type="range" id="logo-font-size" min="2" value="' || TO_CHAR(l_logo_font_size) || '" max="5" step="0.1" />' ||
            '</div>' ||
            '<div class="font-variations">' || buildFontVariations(pWebsiteid, l_font_id, 'logo') || '</div>';

        pArticle:=pArticle || 
        '</fieldset>';
    END;


    /*
    ** CREATE HTML FOR PAGE OPTIONS FORM
    */
    PROCEDURE getLogoForm(pWebsiteId IN website.id%type,  pLogoType IN VARCHAR2, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_header LONG;
        l_article CLOB;
        l_footer LONG;
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        CASE pLogoType
            WHEN 'image' THEN buildImageLogoForm(pWebsiteId, l_header, l_article);
            WHEN 'font' THEN buildFontLogoForm(pWebsiteId, l_header, l_article);
        END CASE;

        l_footer:=
        '<div class="flex-items">' ||
            '<button type="button" class="button publish" data-processing="Publishing editor site, wait a few seconds...">' || 'PUBLISH' || '</button>' ||
            '<span aria-live="polite"></span>' ||
            '<span class="loader icon"></span>' ||
        '</div>';

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header',l_header);
        l_json.put('article',l_article);
        l_json.put('footer',l_footer);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** UPDATE WEBSITE LOGO. 
    ** IMAGE | FONT | SVG  ARE MUTUALLY EXCLUSIVE.
    */
    PROCEDURE updateLogo(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_logo_type website.logo_type%type;
        l_column_name user_tab_columns.column_name%type;
        l_column_value VARCHAR2(100);
        l_menu font.menu%type;
        l_menu_text font.menu%type;
        l_font_axis BOOLEAN;
        l_axes_inputs LONG;
        l_axes_names VARCHAR2(100);
        l_axes_ranges VARCHAR2(100);
        l_api_url VARCHAR2(100);
        l_text VARCHAR2(100);
        l_json JSON_OBJECT_T;
        n PLS_INTEGER;

        
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT REPLACE(column_name,'-','_'), 
                CASE column_name 
                    WHEN 'logo-img-corner-shape' THEN 'superellipse(' || column_value || ')' 
                    WHEN 'logo-img-border-radius' THEN column_value || 'px'
                    WHEN 'logo-font-size' THEN column_value || 'cqi'
                    ELSE column_value 
                END
          INTO l_column_name, l_column_value 
          FROM JSON_TABLE(pBodyText, '$' COLUMNS (column_name, column_value));

        IF (INSTR(l_column_name,'font')>0) THEN
            l_logo_type:='font';
        ELSIF (INSTR(l_column_name,'img')>0) THEN
            l_logo_type:='img';
        ELSIF (INSTR(l_column_name,'svg')>0) THEN
            l_logo_type:='svg';
        END IF;

        l_font_axis:=TRUE;
        FOR C IN (
            SELECT null FROM user_tab_columns WHERE table_name='WEBSITE' AND column_name=UPPER(l_column_name) 
        ) LOOP
            EXECUTE IMMEDIATE 'UPDATE website SET logo_updated_date=current_timestamp, logo_type=:B1,' || l_column_name || '=:B2 WHERE id=:B3' USING l_logo_type, l_column_value, pWebsiteId;
            l_font_axis:=FALSE;
        END LOOP;

        IF (l_font_axis) THEN
            UPDATE font_variation 
               SET updated_date=current_timestamp,
                   value=l_column_value
             WHERE website_id=pWebsiteid
               AND context='logo'
               AND axis=l_column_name;
            n:=sql%rowcount;
            IF (n=0) THEN
                INSERT INTO font_variation(website_id, context, axis, value)
                VALUES (pWebsiteid, 'logo', l_column_name, l_column_value);
            END IF;
        END IF;

        /* FONT LOGO CHANGE */
        IF (l_column_name='logo_font_id') THEN
            UPDATE website_font 
               SET font_id=l_column_value, updated_date=current_timestamp
             WHERE website_id=pWebsiteid
               AND context='logo';
            n:=sql%rowcount;
            IF (n=0) THEN
                INSERT INTO website_font(website_id, font_id, context)
                VALUES (pWebsiteid,l_column_value, 'logo');
            END IF;
            /*
            ** USE GOOGLE MENU URL FOR THE FONT IF LOGO TEXT NULL
            */
            FOR C IN (
                SELECT w.logo_font_text, f.menu, f.family, f.ital_start, f.ital_end, f.slnt_start, f.slnt_end, f.wdth_start, f.wdth_end, f.wght_start, f.wght_end
                  FROM website w, font f 
                 WHERE w.id=pWebsiteid 
                   AND f.id=l_column_value
            ) LOOP
                IF (C.logo_font_text IS NULL) THEN
                    l_menu_text:='url("' || C.menu || '")';
                    EXIT;
                END IF;
                /*
                ** Build input range controls for all variable font axes
                */
                IF (C.ital_start IS NOT NULL) THEN
                    l_axes_inputs:=l_axes_inputs || buildAxisInput('ital', C.ital_start, C.ital_end);
                    l_axes_names:=l_axes_names || 'ital,';
                    l_axes_ranges:=l_axes_ranges || C.ital_start || ','; 
                END IF;
                IF (C.slnt_start IS NOT NULL) THEN
                    l_axes_inputs:=l_axes_inputs || buildAxisInput('slnt', C.slnt_start, C.slnt_end);
                    l_axes_names:=l_axes_names || 'slnt,';
                    l_axes_ranges:=l_axes_ranges || C.slnt_start || '..' || C.slnt_end || ',';
                END IF;
                IF (C.wdth_start IS NOT NULL) THEN
                    l_axes_inputs:=l_axes_inputs || buildAxisInput('wdth', C.wdth_start, C.wdth_end);
                    l_axes_names:=l_axes_names || 'wdth,';
                    l_axes_ranges:=l_axes_ranges || C.wdth_start || '..' || C.wdth_end || ',';
                END IF;
                IF (C.wght_start IS NOT NULL) THEN
                    l_axes_inputs:=l_axes_inputs || buildAxisInput('wght', C.wght_start, C.wght_end);
                    l_axes_names:=l_axes_names || 'wght,';
                    l_axes_ranges:=l_axes_ranges || C.wght_start || '..' || C.wght_end || ',';
                END IF;

                /* Separate tuples for binary italics axis */
                l_axes_names:=RTRIM(l_axes_names,',');
                l_axes_ranges:=RTRIM(l_axes_ranges,',');

                IF (C.ital_end IS NOT NULL) THEN
                    l_axes_ranges:=l_axes_ranges || ';' || C.ital_end || SUBSTR(l_axes_ranges,2);
                END IF;
                /*
                ** Set text parameter to support all European languages
                */
                l_text:='abcdefghijklmnopqrstuvwxyz';
                l_text:=l_text || UPPER(l_text);
                -- more to come 

                l_api_url:='https://fonts.googleapis.com/css2?family=' || 
                    REPLACE(C.family,' ','_') || ':' ||
                    l_axes_names || '@' || l_axes_ranges || '&text=' || utl_url.escape(l_text,TRUE,'UTF-8');
            END LOOP;
        END IF;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('menu',l_menu);
        l_json.put('menutext',l_menu_text);
        l_json.put('axes_inputs',l_axes_inputs);
        l_json.put('message',l_column_name || ' updated successfully to ' || l_column_value);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;
end;
/