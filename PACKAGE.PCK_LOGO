CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_LOGO" as
    
    PROCEDURE getLogoForm(pWebsiteId IN website.id%type, pLogoType IN VARCHAR2, pStatus OUT NUMBER);
    --
    PROCEDURE updateLogo(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_LOGO" as

    PROCEDURE buildImageLogoForm(pWebsiteId IN website.id%type, pHeader IN OUT VARCHAR2, pArticle IN OUT NOCOPY CLOB) IS
        l_logo_type website.logo_type%type;
        l_logo_asset_id website.logo_asset_id%type;
    BEGIN
        pHeader:='<span>Set Website Logo from uploaded image</span>';

        SELECT w.logo_type, w.logo_asset_id
          INTO l_logo_type, l_logo_asset_id
          FROM website w
         WHERE w.id=pWebsiteid;

        pArticle:=
        '<fieldset class="flow">' ||
            '<legend>Select and configure image logo</legend>' ||
            '<div>' ||
                '<select id="logo-img">' ||
                    '<button>' ||
                        '<selectedcontent></selectedcontent>' ||
                        '<span class="arrow"></span>' ||
                    '</button>' ||
                    '<div class="option-container">' ||
                        '<option value="" hidden>' ||
                            '<span>Search Images</span>' ||
                        '</option>';

                FOR C IN (
                    SELECT wa.article_id, a.id, a.cld_cloud_name, a.public_id
                      FROM website_article wa, asset a
                     WHERE wa.website_id=pWebsiteid
                       AND a.article_id=wa.article_id
                       AND a.format='avif'
                     ORDER BY a.created_date DESC
                )
                LOOP
                    pArticle:=pArticle || 
                        '<option value="' || TO_CHAR(C.id) || '">' || 
                            '<img src="https://res.cloudinary.com/' || C.cld_cloud_name || '/w_200/' || C.public_id || '" alt="">' ||
                        '</option>';
                END LOOP;
                

        pArticle:=pArticle || 
                    '</div>' ||
                '</select>' ||
            '</div>' ||
            '<div>' ||
                '<label for="superellipse-slider">Adjust for different shapes:</label>' ||
                '<input type="range" id="superellipse-slider" min="-5" value="0" max="5" step="0.1" />' ||
            '</div>' ||
            '<div>' ||
                '<label for="radius-slider">Adjust effect:</label>' ||
                '<input type="range" id="radius-slider" min="0" value="45" max="90" step="1" />' ||
            '</div>' ||
        '</fieldset>';
        
    END;

    PROCEDURE buildFontLogoForm(pWebsiteId IN website.id%type, pHeader IN OUT VARCHAR2, pArticle IN OUT NOCOPY CLOB) IS
        l_logo_type website.logo_type%type;
        l_font_id website_font.font_id%type;
        l_font_text article_chars.chars_normal%type;
    BEGIN
        pHeader:='<span>Set Website Logo to font</span>';

        SELECT w.logo_type, wf.font_id, ac.chars_normal 
          INTO l_logo_type, l_font_id, l_font_text
          FROM website w, website_article wa, website_font wf, article_chars ac
         WHERE w.id=pWebsiteid
           AND wa.website_id=w.id
           AND wa.display_order=1
           AND wf.website_id(+)=w.id
           AND wf.context(+)='logo'
           AND ac.article_id(+)=wa.article_id
           AND ac.context(+)='logo';

        pArticle:=
        '<fieldset class="flex-items">' ||
            '<legend>Enter logo text and select font</legend>' ||
            '<div>' ||
                '<label for="logo-font-id">Select Font</label>' ||
                '<select id="logo-font-id">' ||
                    '<button>' ||
                      '<selectedcontent></selectedcontent>' ||
                      '<span class="arrow"></span>' ||
                    '</button>';

                FOR C IN (SELECT id, family FROM font ORDER BY 2)
                LOOP
                    pArticle:=pArticle || 
                    '<option value="' || TO_CHAR(C.id) || '"' || CASE WHEN C.id=l_font_id THEN ' selected' END || '>' || C.family || '</option>';
                END LOOP;

        pArticle:=pArticle || 
                '</select>' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-text">Logo text</label>' ||
                '<input type="text" id="logo-text" value="' || l_font_text || '"/>' ||
            '</div>' ||
        '</fieldset>';
    END;


    /*
    ** CREATE HTML FOR PAGE OPTIONS FORM
    */
    PROCEDURE getLogoForm(pWebsiteId IN website.id%type,  pLogoType IN VARCHAR2, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_header LONG;
        l_article CLOB;
        l_footer LONG;
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        CASE pLogoType
            WHEN 'image' THEN buildImageLogoForm(pWebsiteId, l_header, l_article);
            WHEN 'font' THEN buildFontLogoForm(pWebsiteId, l_header, l_article);
        END CASE;

        l_footer:=
        '<div class="flex-items">' ||
            '<button type="button" class="button publish" data-processing="Publishing editor site, wait a few seconds...">' || 'PUBLISH' || '</button>' ||
            '<span aria-live="polite"></span>' ||
            '<span class="loader icon"></span>' ||
        '</div>';

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header',l_header);
        l_json.put('article',l_article);
        l_json.put('footer',l_footer);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** UPDATE WEBSITE LOGO. METHODS ARE MUTUALLY EXCLUSIVE.
    */
    PROCEDURE updateLogo(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_home_article_id article.id%type;
        l_message LONG;
        l_json JSON_OBJECT_T;
        n PLS_INTEGER;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

         FOR C IN (
            SELECT logo_type, asset_id, font_id, font_text, svg
            FROM JSON_TABLE(pBodyText, '$' COLUMNS (logo_type, asset_id, font_id, font_text, svg))
        ) LOOP
            UPDATE website
               SET logo_type=C.logo_type,
                   logo_asset_id=C.asset_id,
                   logo_svg=C.svg,
                   logo_updated_date=current_timestamp
             WHERE id=pWebsiteid;

            SELECT article_id
              INTO l_home_article_id
              FROM website_article
             WHERE website_id=pWebsiteid
               AND display_order=1;

            IF (C.logo_type='font') THEN
                IF (C.font_text IS NOT NULL) THEN
                    UPDATE article_chars
                       SET chars_normal=C.font_text, updated_date=current_timestamp
                     WHERE article_id=l_home_article_id
                       AND context='logo';
                    n:=sql%rowcount;
                    IF (n=0) THEN
                        INSERT INTO article_chars(article_id, context, chars_normal)
                        VALUES (l_home_article_id, 'logo', C.font_text);
                    END IF;
                END IF;
                IF (C.font_id IS NOT NULL) THEN
                    UPDATE article 
                       SET updated_date=current_timestamp
                     WHERE id=l_home_article_id;
         
                    UPDATE website_font 
                       SET font_id=C.font_id, updated_date=current_timestamp
                     WHERE website_id=pWebsiteid
                       AND context='logo';
                    n:=sql%rowcount;
                    IF (n=0) THEN
                        INSERT INTO website_font(website_id, font_id, context)
                        VALUES (pWebsiteid, C.font_id, 'logo');
                    END IF;
                END IF;
            ELSE
                DELETE website_font WHERE website_id=pWebsiteid AND context='logo';
                DELETE article_chars WHERE article_id=l_home_article_id AND context='logo';
            END IF;

            IF (C.logo_type IS NOT NULL) THEN
                l_message:='Logo ' || C.logo_type || ' updated successfully';
            ELSE
                l_message:='Logo Removed';
            END IF;
        END LOOP;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('message',l_message);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;
end;
/