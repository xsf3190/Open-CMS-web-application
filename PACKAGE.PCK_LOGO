CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_LOGO" as
    
    PROCEDURE getLogoForm(pWebsiteId IN website.id%type, pLogoType IN VARCHAR2, pStatus OUT NUMBER);
    --
    PROCEDURE updateLogo(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_LOGO" as

    PROCEDURE buildImageLogoForm(pWebsiteId IN website.id%type, pHeader IN OUT VARCHAR2, pArticle IN OUT NOCOPY CLOB) IS
        l_logo_type website.logo_type%type;
        l_logo_img_id website.logo_img_id%type;
    BEGIN
        pHeader:='<span>Create Website Logo from an uploaded image</span>';

        SELECT w.logo_type, w.logo_img_id
          INTO l_logo_type, l_logo_img_id
          FROM website w
         WHERE w.id=pWebsiteid;

        pArticle:=
        '<fieldset class="flow">' ||
            '<legend>Select and configure image logo</legend>' ||
            '<div>' ||
                '<select id="logo-img-id">' ||
                    '<button>' ||
                        '<selectedcontent></selectedcontent>' ||
                        '<span class="arrow"></span>' ||
                    '</button>' ||
                    '<div class="option-container">' ||
                        '<option value="" hidden>' ||
                            '<span>Search Images</span>' ||
                        '</option>';

                FOR C IN (
                    SELECT wa.article_id, a.id, a.cld_cloud_name, a.public_id
                      FROM website_article wa, asset a
                     WHERE wa.website_id=pWebsiteid
                       AND a.article_id=wa.article_id
                       AND a.format='avif'
                     ORDER BY a.created_date DESC
                )
                LOOP
                    pArticle:=pArticle || 
                        '<option value="' || TO_CHAR(C.id) || '">' || 
                            '<img src="https://res.cloudinary.com/' || C.cld_cloud_name || '/w_200/' || C.public_id || '" alt="">' ||
                        '</option>';
                END LOOP;
                

        pArticle:=pArticle || 
                    '</div>' ||
                '</select>' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-img-corner-shape">Change border shape:</label>' ||
                '<input type="range" id="logo-img-corner-shape" min="-5" value="0" max="5" step="0.1" />' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-img-border-radius">Adjust effect:</label>' ||
                '<input type="range" id="logo-img-border-radius" min="0" value="45" max="90" step="1" />' ||
            '</div>' ||
        '</fieldset>';
        
    END;

    PROCEDURE buildFontLogoForm(pWebsiteId IN website.id%type, pHeader IN OUT VARCHAR2, pArticle IN OUT NOCOPY CLOB) IS
        l_logo_type website.logo_type%type;
        l_font_id website_font.font_id%type;
        l_logo_font_size website.logo_font_size%type;
        l_font_text article_chars.chars_normal%type;
    BEGIN
        pHeader:='<span>Create Website Logo using selected font</span>';

        SELECT w.logo_type, w.logo_font_size, wf.font_id, ac.chars_normal 
          INTO l_logo_type, l_logo_font_size, l_font_id, l_font_text
          FROM website w, website_article wa, website_font wf, article_chars ac
         WHERE w.id=pWebsiteid
           AND wa.website_id=w.id
           AND wa.display_order=1
           AND wf.website_id(+)=w.id
           AND wf.context(+)='logo'
           AND ac.article_id(+)=wa.article_id
           AND ac.context(+)='logo';

        pArticle:=
        '<fieldset class="flow">' ||
            '<legend>Enter logo text and select font</legend>' ||
            '<div>' ||
                '<label for="logo-font-id">Select Font</label>' ||
                '<select id="logo-font-id">' ||
                    '<button>' ||
                      '<selectedcontent></selectedcontent>' ||
                      '<span class="arrow"></span>' ||
                    '</button>';

                FOR C IN (SELECT id, family FROM font ORDER BY 2)
                LOOP
                    pArticle:=pArticle || 
                    '<option value="' || TO_CHAR(C.id) || '"' || CASE WHEN C.id=l_font_id THEN ' selected' END || '>' || C.family || '</option>';
                END LOOP;

        pArticle:=pArticle || 
                '</select>' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-font-text">Logo text</label>' ||
                '<input type="text" id="logo-font-text" value="' || l_font_text || '"/>' ||
            '</div>' ||
            '<div>' ||
                '<label for="logo-font-size">Adjust size</label>' ||
                '<input type="range" id="logo-font-size" min="2" value="' || TO_CHAR(l_logo_font_size) || '" max="5" step="0.1" />' ||
            '</div>';

        FOR C IN (
            SELECT wf.wght, f.wght_start, f.wght_end, wf.wdth, f.wdth_start, f.wdth_end, wf.slnt, f.slnt_start, f.slnt_end
              FROM website_font wf, font f
             WHERE website_id=pWebsiteid
               AND wf.context='logo'
               AND f.id=wf.font_id
        ) LOOP
            IF (C.wght_start IS NOT NULL) THEN
                pArticle:=pArticle ||
                '<div>' ||
                    '<label for="logo-font-wght">Adjust weight</label>' ||
                    '<input type="range" id="logo-font-wght" min="' || TO_CHAR(C.wght_start) || '" value="' || TO_CHAR(C.wght) || '" max="'|| TO_CHAR(C.wght_end) || '" step="1" />' ||
                '</div>';

            END IF;
        END LOOP;

        pArticle:=pArticle || 
        '</fieldset>';
    END;


    /*
    ** CREATE HTML FOR PAGE OPTIONS FORM
    */
    PROCEDURE getLogoForm(pWebsiteId IN website.id%type,  pLogoType IN VARCHAR2, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_header LONG;
        l_article CLOB;
        l_footer LONG;
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        CASE pLogoType
            WHEN 'image' THEN buildImageLogoForm(pWebsiteId, l_header, l_article);
            WHEN 'font' THEN buildFontLogoForm(pWebsiteId, l_header, l_article);
        END CASE;

        l_footer:=
        '<div class="flex-items">' ||
            '<button type="button" class="button publish" data-processing="Publishing editor site, wait a few seconds...">' || 'PUBLISH' || '</button>' ||
            '<span aria-live="polite"></span>' ||
            '<span class="loader icon"></span>' ||
        '</div>';

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header',l_header);
        l_json.put('article',l_article);
        l_json.put('footer',l_footer);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** UPDATE WEBSITE LOGO. METHODS ARE MUTUALLY EXCLUSIVE.
    */
    PROCEDURE updateLogo(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_home_article_id article.id%type;
        l_logo_type website.logo_type%type;
        l_column_name user_tab_columns.column_name%type;
        l_column_value VARCHAR2(100);
        l_menu font.menu%type;
        l_menu_text font.menu%type;
        l_json JSON_OBJECT_T;
        n PLS_INTEGER;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT REPLACE(column_name,'-','_'), 
                CASE column_name 
                    WHEN 'logo-img-corner-shape' THEN 'superellipse(' || column_value || ')' 
                    WHEN 'logo-img-border-radius' THEN column_value || 'px'
                    WHEN 'logo-font-size' THEN column_value || 'cqi'
                    ELSE column_value 
                END
          INTO l_column_name, l_column_value 
          FROM JSON_TABLE(pBodyText, '$' COLUMNS (column_name, column_value));

        IF (INSTR(l_column_name,'font')>0) THEN
            l_logo_type:='font';
        ELSIF (INSTR(l_column_name,'img')>0) THEN
            l_logo_type:='img';
        ELSIF (INSTR(l_column_name,'svg')>0) THEN
            l_logo_type:='svg';
        END IF;

        IF (l_logo_type='font') THEN
            UPDATE website 
               SET logo_updated_date=current_timestamp,
                   logo_type=l_logo_type, 
                   logo_font_size=CASE WHEN l_column_name='logo_font_size' THEN l_column_value ELSE logo_font_size END,
                   logo_font_wght=CASE WHEN l_column_name='logo_font_wght' THEN TO_NUMBER(l_column_value) ELSE logo_font_wght END  
             WHERE id=pWebsiteId;

            SELECT article_id
              INTO l_home_article_id
              FROM website_article
             WHERE website_id=pWebsiteid
               AND display_order=1;

            IF (l_column_name='logo_font_text') THEN
                UPDATE article_chars
                   SET chars_normal=l_column_value, updated_date=current_timestamp
                 WHERE article_id=l_home_article_id
                   AND context='logo';
                n:=sql%rowcount;
                IF (n=0) THEN
                    INSERT INTO article_chars(article_id, context, chars_normal)
                    VALUES (l_home_article_id, 'logo', l_column_value);
                END IF;

                pck_fonts.getFontFiles(pWebsiteId, pContext=>'logo', pArticleId=>l_home_article_id);
                
                SELECT 'url("' || src_url || '")' 
                  INTO l_menu_text
                  FROM website_fontface 
                 WHERE website_id=pWebsiteid 
                   AND context='logo';
            END IF;

            IF (l_column_name='logo_font_id') THEN
                UPDATE article 
                   SET updated_date=current_timestamp
                 WHERE id=l_home_article_id;
     
                UPDATE website_font 
                   SET font_id=l_column_value, updated_date=current_timestamp
                 WHERE website_id=pWebsiteid
                   AND context='logo';
                n:=sql%rowcount;
                IF (n=0) THEN
                    INSERT INTO website_font(website_id, font_id, context)
                    VALUES (pWebsiteid,l_column_value, 'logo');
                END IF;
                SELECT 'url("' || menu || '")' INTO l_menu FROM font WHERE id=TO_NUMBER(l_column_value);
            END IF;
        ELSE
            EXECUTE IMMEDIATE 'UPDATE website SET logo_updated_date=current_timestamp, logo_type=:B1,' || l_column_name || '=:B2 WHERE id=:B3' USING l_logo_type, l_column_value, pWebsiteId;
        END IF;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('menu',l_menu);
        l_json.put('menutext',l_menu_text);
        l_json.put('message','Logo ' || l_logo_type || ' updated successfully');
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;
end;
/