CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_FONTS" as
    --
    PROCEDURE getCharsUsed(pArticleId IN article.id%type, pContent IN OUT NOCOPY CLOB);
    --
    PROCEDURE getFontFiles(pWebsiteId IN website.id%type);
    --
    PROCEDURE getFonts(pWebsiteId IN website.id%type, pContext IN website_font.context%type, pCategory IN font.category%type, pStatus IN OUT NUMBER);
    --
    FUNCTION getLanguage(pLanguageCode IN VARCHAR2) RETURN CLOB;
    --
    PROCEDURE loadGoogleFonts;
    --
    PROCEDURE loadGoogleFontMetadata;
    --
    PROCEDURE updateFont(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    FUNCTION buildApiUrl(pWebsiteid IN website.id%type, pFontid IN font.id%type, pDefaultWght IN NUMBER)  RETURN JSON_ARRAY_T;
end "PCK_FONTS";
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_FONTS" as

    /*
    **  LANGUAGE SUPPORT
    */
    FUNCTION getLanguage(pLanguageCode IN VARCHAR2) RETURN CLOB IS
        l_unicode_range language.unicode_range%type;
        l_urlencoded language.urlencoded%type;
        l_range_split pls_integer;
        r1 pls_integer;
        r2 pls_integer;
        l_text CLOB;
        l_chars CLOB;
    BEGIN
        SELECT unicode_range, urlencoded INTO l_unicode_range, l_urlencoded FROM language WHERE code=pLanguageCode;
        IF (l_urlencoded IS NOT NULL) THEN
            RETURN (l_urlencoded);
        END IF;
        FOR C IN (SELECT LTRIM(column_value,'U+') AS range FROM TABLE(apex_string.split(l_unicode_range,', '))) 
        LOOP
            l_range_split:=INSTR(C.range,'-');
            IF (l_range_split>0) THEN
                r1:=TO_NUMBER(SUBSTR(C.range,1,l_range_split-1),'XXXX');
                r2:=TO_NUMBER(SUBSTR(C.range,l_range_split+1),'XXXX');
                FOR i IN r1..r2 LOOP
                    l_text:=l_text || '\' || LPAD(LTRIM(TO_CHAR(i,'XXXX')),4,'0');
                END LOOP;
            ELSE
                l_text:=l_text || '\' || LPAD(LTRIM(TO_CHAR(TO_NUMBER(C.range,'XXXX'),'XXXX')),4,'0');
            END IF;
        END LOOP;
        l_chars:=UNISTR(l_text);
        l_urlencoded:=utl_url.escape(l_chars,TRUE,'UTF-8');
        UPDATE language SET urlencoded=l_urlencoded, characters=l_chars WHERE code=pLanguageCode;
        RETURN (l_urlencoded);
    END;

    /* 
    ** Return distict characters sorted 
    */
    FUNCTION distinct_chars(pCharacters IN VARCHAR2) RETURN VARCHAR2 IS
        l_chars article_chars.chars_normal%type;
    BEGIN
        SELECT LISTAGG(DISTINCT SUBSTR(pCharacters, LEVEL, 1), NULL) WITHIN GROUP (ORDER BY SUBSTR(pCharacters, LEVEL, 1))
        INTO l_chars
        FROM dual
        CONNECT BY LEVEL <= LENGTH(pCharacters);
        RETURN(l_chars);
    END;

    /*
    **  COLLECT DISTINCT CHARACTERS USED IN AN ARTICLE - SAVE IN TABLE ARTICLE_CHARS AS WELL AS GLOBAL VARIABLES FOR REFERENCE IN DEPLOYMENT
    */
    PROCEDURE getCharsUsed(pArticleId IN article.id%type, pContent IN OUT NOCOPY CLOB) IS

        l_text CLOB:=pContent;

        hd pls_integer;
        it pls_integer;
        bi pls_integer;
        b pls_integer;

        l_heading LONG;
        l_heading_regex varchar2(30):='<h[1-4](.+?)</h[1-4]>';
        l_italic_regex varchar2(15):='<i>(.+?)</i>';
        l_code_regex varchar2(30):='<code(.+?)</code>';
        l_nbsp_regex varchar2(30):='<p>&nbsp;</p>';
        l_bold_italic_regex varchar2(30):='<i><strong>(.+?)</strong></i>';
        l_bold_regex varchar2(30):='<strong>(.+?)</strong>';

        l_headings LONG;
        l_headings_italic LONG;
        l_chars_normal LONG;
        l_chars_italic LONG;
        l_chars_bold_normal LONG;
        l_chars_bold_italic LONG;

        l_first_heading article_chars.first_heading%type;
        l_second_heading article_chars.second_heading%type;

    BEGIN
        DELETE article_chars WHERE article_id=pArticleId;
        
        /* 
        ** REMOVE <code></code>  AND <p>&nbsp;</p>
        ** 1 - start at beginnning
        ** 0 - replace all occurrences
        ** 'n' - ignore line breaks 
        */
        l_text:=REGEXP_REPLACE(l_text,l_code_regex,null,1,0,'n');
        l_text:=REGEXP_REPLACE(l_text,l_nbsp_regex,null,1,0);

        hd:=NVL(REGEXP_COUNT(l_text,l_heading_regex),0);

        FOR i IN 1..hd LOOP
            l_heading:=REGEXP_SUBSTR(l_text,l_heading_regex,1,i);
            it:=REGEXP_COUNT(l_heading,l_italic_regex);

            FOR j IN 1..it LOOP
                l_headings_italic:=l_headings_italic || utl_i18n.unescape_reference(apex_escape.striphtml(REGEXP_SUBSTR(l_heading,l_italic_regex,1,j)));
            END LOOP;

            l_heading:=REGEXP_REPLACE(l_heading,l_italic_regex,null,1,0);
            l_heading:=utl_i18n.unescape_reference(apex_escape.striphtml(l_heading));

            l_headings:=l_headings || l_heading;

            IF (i=1) THEN
                l_first_heading:=SUBSTR(l_heading,1,160);
            END IF;
            IF (i=2) THEN
                l_second_heading:=SUBSTR(l_heading,1,160);
            END IF;
        END LOOP;
        l_headings_italic:=distinct_chars(l_headings_italic);
        l_headings:=distinct_chars(l_headings);

        INSERT INTO article_chars(article_id, context, chars_italic, chars_normal, first_heading, second_heading)
        VALUES (pArticleId, 'headings', l_headings_italic, l_headings, l_first_heading, l_second_heading);

        l_text:=REGEXP_REPLACE(l_text,l_heading_regex,null,1,0);

        /*
        **  NOW PROCESS REST OF THE TEXT
        */

        bi:=NVL(REGEXP_COUNT(l_text, l_bold_italic_regex),0);
        FOR i IN 1..bi LOOP
            l_chars_bold_italic:=l_chars_bold_italic || utl_i18n.unescape_reference(apex_escape.striphtml(REGEXP_SUBSTR(l_text,l_bold_italic_regex,1,i)));
        END LOOP;
        l_text:=REGEXP_REPLACE(l_text,l_bold_italic_regex,null,1,0);

        it:=NVL(REGEXP_COUNT(l_text,l_italic_regex),0);
        FOR i IN 1..it LOOP
            l_chars_italic:=l_chars_italic || utl_i18n.unescape_reference(apex_escape.striphtml(REGEXP_SUBSTR(l_text,l_italic_regex,1,i)));
        END LOOP;
        l_text:=REGEXP_REPLACE(l_text,l_italic_regex,null,1,0);

        b:=NVL(REGEXP_COUNT(l_text,l_bold_regex),0);
        FOR i IN 1..b LOOP
            l_chars_bold_normal:=l_chars_bold_normal || utl_i18n.unescape_reference(apex_escape.striphtml(REGEXP_SUBSTR(l_text,l_bold_regex,1,i)));
        END LOOP;
        l_text:=REGEXP_REPLACE(l_text,l_bold_regex,null,1,0);
        /*
        ** THE REST IS NORMAL !
        */
        l_chars_normal:=distinct_chars(utl_i18n.unescape_reference(apex_escape.striphtml(l_text)));
        l_chars_bold_normal:=distinct_chars(l_chars_bold_normal);
        l_chars_italic:=distinct_chars(l_chars_italic);
        l_chars_bold_italic:=distinct_chars(l_chars_bold_italic);

        INSERT INTO article_chars(article_id, context, chars_bold_italic, chars_bold_normal, chars_italic, chars_normal)
        VALUES (pArticleId, 'text', l_chars_bold_italic, l_chars_bold_normal, l_chars_italic, l_chars_normal);   
    END;

    /*
     *  DOWNLOAD OPTIMIZED @font-face RULES FROM GOOGLE
    */
    PROCEDURE downloadFontface(pWebsiteid IN website.id%type, pContext IN VARCHAR2, pItalic IN VARCHAR2, pText IN VARCHAR2, pStyles IN VARCHAR2, pFontface OUT VARCHAR2) IS
        l_api_url VARCHAR2(200);
        l_api_val VARCHAR2(100);
        l_static_wght_normal font_axes_value.value%type;
        l_static_wght_italic font_axes_value.value%type;
        l_fontface CLOB;
        l_blob BLOB;
        l_blob_size NUMBER;
        l_url website_fontfiles_temp.path%type;
        l_url_new website_fontfiles_temp.path%type;
        l_sha1 website_fontfiles_temp.sha1%type;
        n PLS_INTEGER;

        FUNCTION getClosestNumberInRange(pFontid IN NUMBER, pWght IN NUMBER) RETURN NUMBER IS
        BEGIN
            FOR C IN (
                SELECT axis_start, axis_end FROM font_axes WHERE font_id=pFontid AND axis='wght'
            ) LOOP
                IF (pWght < C.axis_start) THEN
                    RETURN C.axis_start;
                ELSIF (pWght > C.axis_end) THEN
                    RETURN C.axis_end;
                ELSE
                    RETURN pWght;
                END IF;
            END LOOP;
        END;

    BEGIN
        FOR C IN (
            SELECT wf.font_id, f.family, f.variable
              FROM website_font wf, font f
             WHERE wf.website_id=pWebsiteId
               AND wf.context=pContext
               AND f.id=wf.font_id
        ) LOOP
            l_api_url:='https://fonts.googleapis.com/css2?family=' || REPLACE(C.family,' ','+') || ':';

            CASE C.variable
                WHEN 1 THEN
                    FOR C1 IN (
                        SELECT NVL(fav.axis,fa.axis) axis, fav.value, fm.default_value
                          FROM font_axes fa, font_axes_value fav, font_metadata fm
                         WHERE fa.font_id=C.font_id
                           AND fm.axis=fa.axis
                           AND fav.website_id(+)=pWebsiteId
                           AND fav.context(+)=pContext
                           AND fav.axis(+)=fa.axis
                         ORDER BY CASE WHEN ASCII(fa.axis)>100 THEN 1 ELSE 2 END, axis
                    ) LOOP
                        l_api_url:=l_api_url || C1.axis || ',';
                        CASE C1.axis
                            WHEN 'ital' THEN
                                l_api_val:=l_api_val|| pItalic || ',';
                            WHEN 'wght' THEN
                                IF (C1.value is NULL) THEN
                                    CASE pContext
                                        WHEN 'logo' THEN
                                            l_api_val:=l_api_val || '400,';
                                        WHEN 'headings' THEN
                                            l_api_val:=l_api_val || getClosestNumberInRange(C.font_id, 700) || ',';
                                        WHEN 'text' THEN
                                            IF INSTR(pStyles,'bold')>0 THEN
                                                l_api_val:=l_api_val || getClosestNumberInRange(C.font_id, 700) || ',';
                                            ELSE
                                                l_api_val:=l_api_val || '400,';
                                            END IF;
                                    END CASE;
                                ELSE
                                    l_api_val:=l_api_val || C1.value || ',';
                                END IF;
                            ELSE
                                l_api_val:=l_api_val || NVL(C1.value,C1.default_value) || ',';
                        END CASE;
                    END LOOP;
                    l_api_url:=RTRIM(l_api_url,',') || '@' || RTRIM(l_api_val,',');

                WHEN 0 THEN
                    BEGIN
                        SELECT fav.value
                          INTO l_static_wght_normal
                          FROM font_axes_value fav
                         WHERE fav.website_id=pWebsiteId
                           AND fav.context=pContext
                           AND fav.axis='wght';

                        l_api_url:=l_api_url || 'ital,wght@' || pItalic || ',' || l_static_wght_normal;

                    EXCEPTION WHEN NO_DATA_FOUND THEN
                        SELECT wght_normal, wght_italic
                          INTO l_static_wght_normal, l_static_wght_italic
                          FROM font_static_wght
                         WHERE font_id=C.font_id
                         ORDER BY ABS(wght_normal - CASE WHEN pContext='headings' THEN 700 ELSE 400 END)
                         FETCH FIRST ROW ONLY;

                    END;
                    l_api_url:=l_api_url || 'ital,wght@' || pItalic || ',' || l_static_wght_normal;
            END CASE;

            l_api_url:=l_api_url || '&display=swap';
            l_api_url:=l_api_url || '&text=' || pText;

            pck_api.callGoogleAPI(pUrl=>l_api_url, pData=>l_fontface);

            l_url:=REGEXP_SUBSTR(l_fontface,'url\((.+?)\)',1,1,null,1);

            l_blob:=apex_web_service.make_rest_request_b(p_url=>l_url, p_http_method=>'GET');
            l_blob_size:=dbms_lob.getlength(l_blob);

            l_sha1:=LOWER(dbms_crypto.hash(src => l_blob, typ => dbms_crypto.hash_sh1));
            l_url_new:='/fonts/' || pContext || '.' || pStyles || '.woff2';

            pFontface:=
                chr(10) || 
                '/* ' || chr(10) || 
                C.family || ' used for ' || pContext || ' - ' || LENGTH(pText) || ' ' || pStyles || ' characters - ' || apex_string_util.to_display_filesize(l_blob_size) || chr(10) || 
                l_api_url || chr(10) ||
                '*/' || chr(10) ||
                l_fontface;

            SELECT COUNT(*) INTO n FROM dual WHERE EXISTS (SELECT null  FROM website_files WHERE sha1=l_sha1 AND path=l_url_new);
            IF (n=0) THEN
                INSERT INTO website_fontfiles_temp(path, sha1, fontface, content) values (l_url_new, l_sha1, REPLACE(l_fontface, l_url, l_url_new), l_blob);
            END IF;
            
        END LOOP;
    END;

    /* 
    **  BUILD OPTIMIZED FONT FILES FOR EACH FONT USED IN A LIVE WEBSITE
    **  NB. EDITOR SITES USE FULL LANGUAGE FONTS DIRECTLY FROM GOOGLE
    */
    PROCEDURE getFontFiles(pWebsiteId IN website.id%type) IS
        TYPE tt_chars_used IS RECORD (
            normal article_chars.chars_normal%type,
            italic article_chars.chars_italic%type,
            bold_normal article_chars.chars_bold_normal%type,
            bold_italic article_chars.chars_bold_italic%type
        );
        TYPE t_chars_used IS TABLE OF tt_chars_used INDEX BY VARCHAR2(40);
        l_chars_used t_chars_used:=t_chars_used( 
            'headings' => tt_chars_used(null,null,null,null), 
            'text' => tt_chars_used(null,null,null,null), 
            'logo' => tt_chars_used(null,null,null,null));

        l_fontface VARCHAR2(1000);
        l_fontfaces CLOB;
        l_context VARCHAR2(8);
        l_html_size PLS_INTEGER:=0;
        l_pages PLS_INTEGER:=0;
        l_cpu_used_start NUMBER;
        l_elapsed NuMBER;

    BEGIN
        l_cpu_used_start:=dbms_utility.get_cpu_time();

        /*
        **  POPULATE ARTICLE_CHARS TABLE FOR EACH ARTICLE WITH CONTENT
        */
        FOR C IN (
            SELECT a.id, a.body_html || wa.header_html || wa.footer_html AS content
              FROM website_article wa, article a
             WHERE wa.website_id=pWebsiteId
               AND wa.article_id=NVL(a.parent_id,a.id)
               AND a.body_html IS NOT NULL
        ) LOOP
            l_pages:=l_pages+1;
            l_html_size:=l_html_size + dbms_lob.getlength(C.content);
            getCharsUsed(pArticleid=>C.id, pContent=>C.content);
        END LOOP; 

        /* Get distinct set of characters in website for each font style / weight for each context */
        FOR C IN (
            SELECT ac.context, ac.chars_normal, ac.chars_italic, ac.chars_bold_normal, ac.chars_bold_italic 
              FROM article_chars ac, website_article wa, article a
             WHERE wa.website_id=pWebsiteId
               AND wa.article_id=NVL(a.parent_id,a.id)
               AND ac.article_id=a.id
        ) LOOP
            l_chars_used(C.context).bold_italic:=distinct_chars(l_chars_used(C.context).bold_italic || C.chars_bold_italic);
            l_chars_used(C.context).bold_normal:=distinct_chars(l_chars_used(C.context).bold_normal || C.chars_bold_normal);
            l_chars_used(C.context).italic:=distinct_chars(l_chars_used(C.context).italic || C.chars_italic);
            l_chars_used(C.context).normal:=distinct_chars(l_chars_used(C.context).normal || C.chars_normal);
        END LOOP;

        -- Logo text is a column in WEBSITE table
        FOR C IN (
            SELECT logo_font_text FROM website WHERE id=pWebsiteId AND logo_font_text IS NOT NULL
        ) LOOP
            l_chars_used('logo').normal:=distinct_chars(l_chars_used('logo').normal || C.logo_font_text);
        END LOOP;

        l_context:=l_chars_used.FIRST;
        WHILE l_context IS NOT NULL LOOP
            IF (l_chars_used(l_context).normal IS NOT NULL) THEN
                downloadFontface(pWebsiteid, l_context, '0', l_chars_used(l_context).normal, 'normal', l_fontface);
                l_fontfaces:=l_fontfaces || l_fontface;
            END IF;

            IF (l_chars_used(l_context).italic IS NOT NULL) THEN
                downloadFontface(pWebsiteid, l_context,  '1', l_chars_used(l_context).italic, 'italic', l_fontface);
                l_fontfaces:=l_fontfaces || l_fontface;
            END IF;

            IF (l_chars_used(l_context).bold_normal IS NOT NULL) THEN
                downloadFontface(pWebsiteid, l_context,  '0', l_chars_used(l_context).bold_normal, 'bold.normal', l_fontface);
                l_fontfaces:=l_fontfaces || l_fontface;
            END IF;
            
            IF (l_chars_used(l_context).bold_italic IS NOT NULL) THEN
                downloadFontface(pWebsiteid, l_context,  '1', l_chars_used(l_context).bold_italic, 'bold.italic', l_fontface);
                l_fontfaces:=l_fontfaces || l_fontface;
            END IF;
            
            l_context:=l_chars_used.NEXT(l_context);
        END LOOP;

        l_elapsed:=(dbms_utility.get_cpu_time() - l_cpu_used_start)/100;

        l_fontfaces:='/* Scanned ' || l_pages || ' pages in ' || TO_CHAR(l_elapsed)|| ' seconds (' || apex_string_util.to_display_filesize(l_html_size) || ' html) */' || chr(10) || chr(10) || l_fontfaces;
        
        -- dbms_output.put_line(l_fontfaces);

    END;


    /* 
    **  BUILD HTML FORM TO SELECT WEBSITE FONTS
    */
    PROCEDURE getHTML(pWebsiteId IN website.id%type) IS
        l_header LONG;
        l_article CLOB;
        l_footer LONG;
        l_fontfaces LONG;
        l_json JSON_OBJECT_T;
        l_fonts JSON_ARRAY_T;
        l_fonts_obj JSON_OBJECT_T;
        l_selected VARCHAR2(9);
        l_styles VARCHAR2(100);
        
        TYPE t_context IS VARRAY(3) OF VARCHAR2(30);
        l_contexts t_context:=t_context('logo','headings','text');

        TYPE t_font_category IS VARRAY(5) OF VARCHAR2(30);
        l_font_categories t_font_category:=t_font_category('display','monospace','handwriting','serif','sans-serif');
    BEGIN
        FOR C IN (
            SELECT COUNT(*) total, MAX(last_modified) last_modified
            FROM font
        ) LOOP
            l_header:=
            '<a class="icon-with-text" href="https://fonts.google.com" target="a_blank" aria-label="Search on Google Fonts">' ||
				'<svg width="1em" height="1em" viewBox="0 0 512 512" aria-hidden="true" focusable="false"><path d="M416 208c0 45.9-14.9 88.3-40 122.7L502.6 457.4c12.5 12.5 12.5 32.8 0 45.3s-32.8 12.5-45.3 0L330.7 376C296.3 401.1 253.9 416 208 416 93.1 416 0 322.9 0 208S93.1 0 208 0 416 93.1 416 208zM208 352a144 144 0 1 0 0-288 144 144 0 1 0 0 288z"/></svg>' ||
				'<small>' || C.total || ' Google fonts as of '|| TO_CHAR(C.last_modified, 'dd Mon yyyy') || '</small>' ||
			'</a>';
        END LOOP;

        l_article:=
        '<fieldset class="flex-items">' ||
            '<legend>Change context</legend>';
        FOR i IN 1..l_contexts.COUNT LOOP
            l_article:=l_article || 
            '<span>' ||
		        '<input type="radio" name="context" id="' || l_contexts(i) || '"' || CASE WHEN l_contexts(i)='text' THEN ' checked' END || '>' ||
		        '<label for="' || l_contexts(i) || '">' || INITCAP(l_contexts(i)) || '</label>' ||
			'</span>';
        END LOOP;
        l_article:=l_article || 
        '</fieldset>';

        FOR C IN (
            SELECT wf.font_id, wf.context, f.category, f.family, f.bold, f.italic, f.variable, f.color
              FROM website_font wf, font f
             WHERE wf.website_id=pWebsiteId
               AND wf.font_id=f.id 
               AND wf.context='text'
        ) LOOP
            l_article:=l_article || 
            '<fieldset class="flex-items">' ||
                '<legend>Select category</legend>';
            FOR i IN 1..l_font_categories.COUNT LOOP
                l_article:=l_article || 
                '<span>' ||
    		        '<input type="radio" name="category" id="' || l_font_categories(i) || '"' || CASE WHEN l_font_categories(i)=C.category THEN ' checked' END || '>' ||
    		        '<label for="' || l_font_categories(i) || '">' || INITCAP(l_font_categories(i)) || '</label>' ||
    			'</span>';
            END LOOP;
            l_article:=l_article || 
            '</fieldset>';

        
            l_article:=l_article ||
            '<fieldset class="flex-items">' ||
                '<legend>Design features</legend>' ||
                '<span>' ||
    		        '<input type="checkbox" id="bold"' || CASE WHEN C.bold=1 THEN ' checked' END || '>' ||
    		        '<label for="bold">Bold</label>' ||
    			'</span>' ||
                '<span>' ||
    		        '<input type="checkbox" id="italic"' || CASE WHEN C.italic=1 THEN ' checked' END || '>' ||
    		        '<label for="bold">Italic</label>' ||
    			'</span>' ||
                '<span>' ||
    		        '<input type="checkbox" id="variable"' || CASE WHEN C.variable=1 THEN ' checked' END || '>' ||
    		        '<label for="variable">Finesse</label>' ||
    			'</span>' ||
                '<span>' ||
    		        '<input type="checkbox" id="color"' || CASE WHEN C.color=1 THEN ' checked' END || '>' ||
    		        '<label for="color">Color</label>' ||
    			'</span>' ||
            '</fieldset>';

            /* Fonts in category */
            l_article:=l_article || 
            '<div>' ||
                '<label for="family">Select font</label>' ||
                '<select id="family">' ||
                    '<button>' ||
                        '<selectedcontent></selectedcontent>' ||
                        '<span class="arrow"></span>' ||
                    '</button>';
            
            FOR C1 IN (
                SELECT id, family 
                  FROM font 
                 WHERE category=C.category
                   AND bold=C.bold
                   AND italic=C.italic
                   AND variable=C.variable
                   AND color=C.color
                 ORDER BY 2
            ) LOOP
                l_article:=l_article || 
                    '<option value="' || TO_CHAR(C1.id) || '"'|| CASE WHEN C1.id=C.font_id THEN ' selected' END || '>' || C1.family || '</option>';
            END LOOP;
            l_article:=l_article || 
                '</select>' ||
            '</div>';
        END LOOP;

        l_footer:=
        '<div class="flex-items">' ||
            '<button type="button" class="button publish" data-processing="Publishing editor site, wait a few seconds...">' || 'PUBLISH' || '</button>' ||
            '<span aria-live="polite"></span>' ||
            '<span class="loader icon"></span>' ||
        '</div>';

        /*
        ** RETURN DIALOG CONTENTS TO CLIENT 
        */
        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header',l_header);
        l_json.put('article',l_article);
        l_json.put('footer',l_footer);
        apex_util.prn(l_json.to_clob,false);
    END;

    /*
    ** USER CHANGES FONT CATEGORY
    */
    PROCEDURE getFontsInCategory(pCategory IN font.category%type) IS
        l_html CLOB;
        l_json JSON_OBJECT_T;

        PROCEDURE appendOption(pNumber IN NUMBER, pFontid IN NUMBER, pFamily IN VARCHAR2) IS
        BEGIN
            IF (l_html IS NULL) THEN
                l_html:='<option value="">-- Select from ' || pNumber || ' Fonts --</option>';
            END IF;
            l_html:=l_html || '<option value="' || pFontid || '">' || pFamily || '</option>';
        END;
    
    BEGIN
        CASE 
            WHEN pCategory IN ('display', 'handwriting', 'monospace', 'sans-serif', 'serif') THEN
                FOR C IN (
                    SELECT id, family, COUNT(*) OVER () nb
                    FROM font 
                    WHERE category=pCategory 
                    ORDER BY 2
                )
                LOOP
                    appendOption(C.nb, C.id, C.family);
                END LOOP;
            WHEN pCategory='variable' THEN
                FOR C IN (
                    SELECT id, family, COUNT(*) OVER () nb
                    FROM font 
                    WHERE variable=1
                    ORDER BY 2
                )
                LOOP
                    appendOption(C.nb, C.id, C.family);
                END LOOP;
            WHEN pCategory='italic' THEN
                FOR C IN (
                    SELECT id, family, COUNT(*) OVER () nb
                    FROM font 
                    WHERE italic=1
                    ORDER BY 2
                )
                LOOP
                    appendOption(C.nb, C.id, C.family);
                END LOOP;
        END CASE;
        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('content', l_html);
        apex_util.prn(l_json.to_clob,false);
    END;



    /* 
    **  BUILD OPENING DIALOG CONTENT OR LIST OF FONT FAMILIES FOR A SELECTED CATEGORY
    */
    PROCEDURE getFonts(pWebsiteId IN website.id%type, pContext IN website_font.context%type, pCategory IN font.category%type, pStatus IN OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_html CLOB;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        IF (pContext='HTML') THEN
            getHTML(pWebsiteId);
        ELSE
            getFontsInCategory(pCategory);
        END IF;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;

    /* 
    **  GET GOGGLE @font-face RULES FOR A FONT / CONTEXT
    */
    FUNCTION getFontfaces(pWebsiteid IN website.id%type, pFontid IN font.id%type, pApiurl IN VARCHAR2) RETURN JSON_ARRAY_T
    IS 
        l_fontfaces CLOB;
        l_api_url VARCHAR2(500);
        l_fontfaces_json JSON_ARRAY_T;
        l_fontface JSON_OBJECT_T;
        n PLS_INTEGER;
    BEGIN
        FOR C IN (
            SELECT w.language_code, f.family 
              FROM website w, font f 
             WHERE w.id=pWebsiteid
               AND f.id=pFontid
        ) LOOP
            l_api_url:='https://fonts.googleapis.com/css2?family=' || 
                C.family || ':' || 
                pApiurl || 
                '&text=' || pck_fonts.getLanguage(C.language_code);
        END LOOP;
            
        pck_api.callGoogleAPI(pUrl=>l_api_url, pData=>l_fontfaces);

        UPDATE website_font SET editor_fontfaces=l_fontfaces WHERE website_id=pWebsiteid AND font_id=pFontid;

        -- Fonts that include italics will be in a separate @font-face
        --
        l_fontfaces_json:= new JSON_ARRAY_T;
        n:=REGEXP_COUNT(l_fontfaces,'@font-face');
        FOR i IN 1..n LOOP
            l_fontface:= new JSON_OBJECT_T;
            l_fontface.put('style',REGEXP_SUBSTR(l_fontfaces,'font-style: (.+?);',1,i,null,1));
            l_fontface.put('weight',REGEXP_SUBSTR(l_fontfaces,'font-weight: (.+?);',1,i,null,1));
            l_fontface.put('stretch',REGEXP_SUBSTR(l_fontfaces,'font-stretch: (.+?);',1,i,null,1));
            l_fontface.put('source',REPLACE(REPLACE(REGEXP_SUBSTR(l_fontfaces,'url(.+?)\)',1,i),'(','("'),')','")'));

            l_fontfaces_json.append(l_fontface);
        END LOOP;
        RETURN(l_fontfaces_json);
    END;

    /*
    ** Build Google Fonts API url for a given font
    */
    FUNCTION buildApiUrl(pWebsiteid IN website.id%type, pFontid IN font.id%type, pDefaultWght IN NUMBER)  RETURN JSON_ARRAY_T
    IS
        l_api_url VARCHAR2(500);
    BEGIN
        FOR C IN (
            SELECT variable
              FROM font
             WHERE id=pFontid
        ) LOOP
            CASE C.variable
                WHEN 1 THEN
                    FOR C IN (
                        SELECT LISTAGG(fa.axis,',') WITHIN GROUP (ORDER BY CASE WHEN ASCII(fa.axis)>100 THEN 1 ELSE 2 END, fa.axis) ||
                               '@' AS axes,
                               LISTAGG(CASE WHEN fa.axis='ital' THEN '0' ELSE fa.axis_start||'..'||fa.axis_end END,',') WITHIN GROUP (ORDER BY CASE WHEN ASCII(fa.axis)>100 THEN 1 ELSE 2 END, fa.axis) AS ranges
                          FROM font_axes fa
                         WHERE fa.font_id=pFontid
                         GROUP BY fa.font_id
                    ) LOOP
                        l_api_url:=l_api_url || C.axes || C.ranges;
                        IF (INSTR(C.axes,'ital')>0) THEN
                            l_api_url:=l_api_url || ';' || '1' || SUBSTR(C.ranges,2);
                        END IF;
                    END LOOP;

                WHEN 0 THEN
                    FOR C IN (
                        SELECT wght_normal, wght_italic
                          FROM font_static_wght
                         WHERE font_id=pFontid
                         ORDER BY ABS(wght_normal - pDefaultWght)
                         FETCH FIRST ROW ONLY
                    ) LOOP
                        IF (C.wght_italic IS NOT NULL) THEN
                            l_api_url:=l_api_url || 'ital,wght@0,' || C.wght_normal || ';1,' || C.wght_normal;
                        ELSE
                            l_api_url:=l_api_url || 'wght@' || C.wght_normal;
                        END IF;
                    END LOOP;
            END CASE;

            RETURN (getFontfaces(pWebsiteid, pFontid, l_api_url));
        END LOOP;
    END;

    /* 
    **  USER SELECTS A NEW FONT FOR A CONTEXT
    */
    PROCEDURE updateFont(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_context website_font.context%type;
        l_font_id website_font.font_id%type;
        l_default_wght NUMBER;
        l_json JSON_OBJECT_T;
        l_urls JSON_ARRAY_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT context,font_id
          INTO l_context, l_font_id
          FROM JSON_TABLE(pBodyText, '$' COLUMNS(context,font_id));

        UPDATE website_font
           SET font_id=l_font_id, updated_date=current_timestamp
         WHERE website_id=pWebsiteId
           AND context=l_context;

        CASE l_context
            WHEN 'logo' THEN l_default_wght:=400;
            WHEN 'headings' THEN l_default_wght:=700;
            WHEN 'text' THEN l_default_wght:=400;
        END CASE;

        l_urls:=buildApiUrl(pWebsiteid, l_font_id, l_default_wght); 

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('urls',l_urls);

        apex_util.prn(l_json.to_clob,false);
        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    /*
    **  Load Google font details into FONT table. WOFF2 only. STATIC and VARIABLE. 
    */
    PROCEDURE loadGoogleFonts IS 
        l_clob CLOB;
        l_nb_fonts PLS_INTEGER;
        l_lastModified google_font.lastModified%type;
    BEGIN
        pck_api.callGoogleAPI('https://www.googleapis.com/webfonts/v1/webfonts?capability=WOFF2&capability=VF',pData=>l_clob);

        EXECUTE IMMEDIATE('truncate table google_font');

        INSERT INTO google_font(
            family, category,
            ital_start,ital_end,
            wdth_start,wdth_end,
            wght_start,wght_end,
            opsz_start,opsz_end,
            slnt_start,slnt_end,
            italic,
            subset, 
            variable, 
            axes,
            lastModified,
            menu,
            colorCapabilities
        )
        WITH 
        data AS
        (
            SELECT family, category, lastModified, menu, colorCapabilities,
                CASE WHEN variant='italic' THEN variant END italic,
                tag,"start" as tag_start, "end" as tag_end, subset
            FROM JSON_TABLE(l_clob, '$.items[*]' COLUMNS(family, category, lastModified, menu, NESTED PATH '$.variants[*]' COLUMNS (variant VARCHAR2(50) PATH '$'),  
                NESTED '$.axes[*]' COLUMNS(tag varchar2(4) PATH '$.tag', "start" number PATH '$.start', "end" number PATH '$.end'),
                NESTED PATH '$.subsets[*]' COLUMNS (subset VARCHAR2(50) PATH '$'),
                NESTED PATH '$.colorCapabilities[*]' COLUMNS (colorCapabilities VARCHAR2(50) PATH '$' )))
        ), 
        data2 as
        (
            SELECT family, category, TO_DATE(lastModified,'YYYY-MM-DD') lastModified, menu, 
                MAX(DECODE(italic,'italic',0))  as ital_start, MAX(DECODE(italic,'italic',1)) as ital_end,
                MAX(DECODE(tag,'wdth',tag_start)) as wdth_start, MAX(DECODE(tag,'wdth',tag_end)) as wdth_end, 
                MAX(DECODE(tag,'wght',tag_start)) as wght_start, MAX(DECODE(tag,'wght',tag_end)) as wght_end, 
                MAX(DECODE(tag,'opsz',tag_start)) as opsz_start, MAX(DECODE(tag,'opsz',tag_end)) as opsz_end, 
                MAX(DECODE(tag,'slnt',tag_start)) as slnt_start, MAX(DECODE(tag,'slnt',tag_end)) as slnt_end,
                LISTAGG(tag,',') WITHIN GROUP (ORDER BY null) axes,
                LISTAGG(subset,'|') WITHIN GROUP (ORDER BY subset) subset,
                LISTAGG(colorCapabilities,'|') WITHIN GROUP (ORDER BY colorCapabilities) colorCapabilities
            FROM data
            GROUP BY family, category, lastModified, menu
        )
        SELECT family, category, 
            CASE WHEN axes IS NOT NULL THEN ital_start END ital_start, 
            CASE WHEN axes IS NOT NULL THEN ital_end END ital_end,
            wdth_start, wdth_end,
            wght_start, wght_end,
            opsz_start, opsz_end,
            slnt_start, slnt_end,
            CASE WHEN INSTR(axes,'slnt')>0 THEN 1 ELSE NVL(ital_end,0) END,
            subset,
            CASE WHEN axes IS NULL THEN 0 ELSE 1 END,
            axes,
            lastModified,
            menu,
            colorCapabilities
        FROM data2;

        /* Delete any previously uploaded fonts no longer on Google */
        FOR C IN (
            SELECT COUNT(google),family
            FROM
            (
            SELECT 1 google, TO_NUMBER(null) dogface, family FROM google_font
            UNION ALL
            SELECT TO_NUMBER(null),1, family FROM font
            )
           GROUP BY family
            HAVING COUNT(google)=0
        ) LOOP
            pck_core.log('Deleting font no longer supported by Google: ' || C.family);
            DELETE font WHERE family=C.family;
        END LOOP;

        /* Update font table from staging table */
        MERGE INTO font t
        USING
        (
            SELECT family, category, lastmodified, menu, INSTR(colorcapabilities,'COLRv1') color, ital_start, ital_end, wdth_start, wdth_end, wght_start, wght_end, opsz_start, opsz_end, slnt_start, slnt_end, italic, variable, axes
            FROM google_font
        ) s
        ON (t.family=s.family)
        WHEN MATCHED THEN UPDATE SET
            t.category=s.category,
            t.menu=s.menu,
            t.color=s.color,
            t.last_modified=s.lastmodified,
            t.ital_start=s.ital_start, t.ital_end=s.ital_end,
            t.wdth_start=s.wdth_start, t.wdth_end=s.wdth_end, 
            t.wght_start=s.wght_start, t.wght_end=s.wght_end,
            t.opsz_start=s.opsz_start, t.opsz_end=s.opsz_end,
            t.slnt_start=s.slnt_start, t.slnt_end=s.slnt_end,
            t.italic=s.italic,
            t.variable=s.variable,
            t.axes=s.axes,
            t.updated_date=current_timestamp
        WHEN NOT MATCHED THEN 
            INSERT (id, family, category, menu, color, last_modified, ital_start, ital_end, wdth_start, wdth_end, wght_start, wght_end, opsz_start, opsz_end, slnt_start, slnt_end, italic, variable, axes, created_date) 
            VALUES (seq_font.nextval, s.family, s.category, s.menu, s.color, s.lastmodified, s.ital_start, s.ital_end, s.wdth_start, s.wdth_end, s.wght_start, s.wght_end, s.opsz_start, s.opsz_end, s.slnt_start, s.slnt_end, s.italic, s.variable, s.axes, current_timestamp);
        
        l_nb_fonts:=sql%rowcount;
        pck_core.log('Uploaded ' ||l_nb_fonts || ' Google Fonts');

        /*
        ** LOAD STATIC FONT WEIGHTS FOR NORMAL AND ITALIC STYLES
        */
        execute immediate 'truncate table font_static_wght';
        INSERT INTO font_static_wght
        (
            font_id, wght_normal, wght_italic
        )
        WITH 
        data AS
        (
            SELECT family, DECODE(variant,'regular','400',variant) wght
            FROM JSON_TABLE(l_clob, '$.items[*]' COLUMNS(family, axes VARCHAR2(5) EXISTS PATH '$.axes', NESTED PATH '$.variants[*]' COLUMNS (variant VARCHAR2(50) PATH '$')))
            WHERE axes='false'
        ),
        data2 AS
        (
        SELECT family, 
            CASE WHEN INSTR(wght,'italic')=0 THEN wght END wght_normal,
            CASE WHEN INSTR(wght,'italic')>0 THEN NVL(SUBSTR(wght,1,INSTR(wght,'italic')-1),400) END wght_italic
        FROM data
        ),
        data3 AS
        (
        SELECT family, 
            wght_normal,
            LEAD(wght_italic) OVER (PARTITION BY family ORDER BY null) wght_italic
        FROM data2
        )
        SELECT f.id, g.wght_normal, g.wght_italic 
        FROM data3 g, font f
        WHERE COALESCE(g.wght_normal, g.wght_italic) IS NOT NULL
        AND f.family=g.family;

        /*
        ** LOAD VARIABLE FONT AXES. 
        ** NOTE THAT "ital" AXIS ALREADY COMPUTED
        */
        DELETE font_axes;

        INSERT INTO font_axes
        (
            font_id, axis, axis_start, axis_end
        )
        WITH 
        data AS
        (
            SELECT f.id,  tag, "start" as tag_start, "end" as tag_end
            FROM font f,
                JSON_TABLE(l_clob, '$.items[*]' COLUMNS(family,
                    NESTED '$.axes[*]' COLUMNS(tag varchar2(4) PATH '$.tag', "start" number PATH '$.start', "end" number PATH '$.end'))) g
            WHERE f.family=g.family
            UNION ALL
            SELECT id, 'ital', ital_start, ital_end 
              FROM font 
             WHERE variable=1 
               AND ital_start+ital_end=1
        ) 
        SELECT id, tag, tag_start, tag_end
        FROM data
        WHERE tag IS NOT NULL
        ORDER BY id, tag;

        /*
        ** SET "bold" in "font" table
        */
        UPDATE font 
           SET bold=1 
        WHERE id IN
            (
                 SELECT font_id
                 FROM font_axes
                 WHERE axis='wght'
                UNION ALL
                SELECT font_id
                FROM
                (
                    SELECT font_id, COUNT(*)
                     FROM  font_static_wght
                    GROUP BY font_id
                    HAVING COUNT(*)>1
                )
            );
        
    END;

    /*
    **  Load Google font metadata
    */
    PROCEDURE loadGoogleFontMetadata IS 
        l_clob CLOB;
        l_nb_fonts PLS_INTEGER;
    BEGIN
        pck_api.callGoogleAPI('https://fonts.google.com/metadata/fonts',pData=>l_clob);

        EXECUTE IMMEDIATE('truncate table font_metadata');
        
        INSERT INTO font_metadata
        (
            axis, display_name, min_value, max_value, default_value, precision, description
        )
        SELECT tag AS axis, displayName AS display_name, min AS min_value, max AS max_value, defaultvalue AS default_value, precision, description
        FROM JSON_TABLE(l_clob, '$.axisRegistry[*]' COLUMNS(tag, displayName, min, max, defaultValue, precision, description));
        
    END;

end "PCK_FONTS";
/