CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_MEDIA" IS
    --
    PROCEDURE deleteArticleAssets(pArticleId IN article.id%type, pDeleteArticle IN OUT BOOLEAN);
    --
    PROCEDURE DeleteAsset(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    FUNCTION getCloudinaryUrl(pCloudname IN VARCHAR2, pResourceType IN VARCHAR2, pPublicId IN VARCHAR2, pFormat IN VARCHAR2, pWidth IN INTEGER, pDimension IN NUMBER DEFAULT NULL) RETURN VARCHAR2;
    --
    FUNCTION getImageSrcset(pAssetId IN asset.id%type, pSizes IN VARCHAR2 DEFAULT NULL) RETURN VARCHAR2;
    --
    FUNCTION getThumbnails(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pRequest IN VARCHAR2, pNbImages IN OUT NUMBER) RETURN CLOB;
    --
    PROCEDURE getThumbnails(pArticleId IN article.id%type, pDeploy IN BOOLEAN DEFAULT FALSE, pHTML IN OUT NOCOPY CLOB);
    --
    PROCEDURE getCldSignature(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pParamsToSign IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE getCldDetails(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pRequest IN VARCHAR2, pStatus OUT NUMBER);
    --
    PROCEDURE setImageBreakpoints(pAssetId IN asset.id%type);
    --
    PROCEDURE updateCldAsset(pArticleId IN article.id%type DEFAULT NULL);
    --
    PROCEDURE uploadCldMetadata(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
END;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_MEDIA" IS

    gWidthThumbnail CONSTANT INTEGER:=360;

    /*
    ** GET AUTHENTICATED USER'S CLOUDINARY DETAILS IN ORDER TO PREPARE UPLOAD WIDGET
    */
    PROCEDURE getCldDetails(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pRequest IN VARCHAR2, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_cld_cloud_name users.cld_cloud_name%type;
        l_cld_api_key users.cld_api_key%type;
        l_cld_api_secret users.cld_api_secret%type;
        l_image_max_size INTEGER;
        l_video_max_size INTEGER;
        l_google_api_key users.google_api_key%type;
        l_thumbnails CLOB;
        l_nb_images NUMBER;
        l_heading VARCHAR2(50);
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        IF (pRequest='details') THEN
            pck_api.getCloudinaryData(pUserId=>l_session_data.user_id, pCloudName=>l_cld_cloud_name, pCldApiKey=>l_cld_api_key, pCldImageMaxSize=>l_image_max_size, pCldVideoMaxSize=>l_video_max_size, pGoogleApiKey=>l_google_api_key);
            apex_json.open_object; 
            apex_json.write('success', TRUE);
            apex_json.write('cloudname', l_cld_cloud_name);
            apex_json.write('apikey', l_cld_api_key);
            apex_json.write('maxImageFileSize', l_image_max_size);
            apex_json.write('maxVideoFileSize', l_video_max_size);
            apex_json.write('googleApiKey', l_google_api_key);
            apex_json.close_object;
        ELSE
            l_thumbnails:=pck_media.getThumbnails(pWebsiteId,pArticleId, pRequest, l_nb_images);

            l_json:= new JSON_OBJECT_T;
            l_json.put('success',true);
            l_json.put('header', '<small>' || CASE WHEN l_nb_images=0 THEN 'Hint: Upload file or image first' ELSE 'Use this form to copy URL or delete unused files' END || '</small>');
            l_json.put('article', l_thumbnails);
            l_json.put('footer', '<small aria-live="polite" style="color:green">' || l_nb_images || ' image' || CASE WHEN l_nb_images>1 THEN 's' END || '</small>');
            apex_util.prn(l_json.to_clob,false);
        END IF;

        pStatus:=200;
        
        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
     **  Delete All Cloudinary media assets tagged with pArticleId
     **  Disconnect article if any of it's assets in use
     */  
    PROCEDURE deleteArticleAssets(pArticleId IN article.id%type, pDeleteArticle IN OUT BOOLEAN) IS
        n PLS_INTEGER;
    BEGIN
        SELECT COUNT(*) INTO n FROM dual WHERE EXISTS (SELECT null FROM asset a, asset_used au WHERE a.article_id=pArticleid AND au.asset_id=a.id AND au.article_id<>pArticleid);
        pDeleteArticle:=n=0;
    END;

    /*
     **  Delete Cloudinary media asset. ID in body parameter.
     */   
    PROCEDURE DeleteAsset(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_asset_id asset.id%type;
        l_cld_cloud_name asset.cld_cloud_name%type;
        l_public_id asset.public_id%type;
        l_resource_type asset.resource_type%type;
        l_cld_api_key users.cld_api_key%type;
        l_cld_api_secret users.cld_api_secret%type;
        l_timestamp NUMBER:=trunc((cast(current_timestamp at time zone 'UTC' as date) - to_date('01-jan-1970','dd-mon-yyyy')) * (86400));
        l_signature VARCHAR2(100);
        l_string_to_sign VARCHAR2(2000);  
        l_clob CLOB;
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT a.id, a.cld_cloud_name, a.public_id, a.resource_type, u.cld_api_key, u.cld_api_secret
          INTO l_asset_id, l_cld_cloud_name, l_public_id, l_resource_type, l_cld_api_key, l_cld_api_secret
          FROM asset a, users u, JSON_TABLE(pBodyText, '$' COLUMNS(id)) j
         WHERE a.id=j.id
           AND u.cld_cloud_name=a.cld_cloud_name;

        l_json:=new JSON_OBJECT_T;
        l_json.put('invalidate', 'true');
        l_string_to_sign:=l_string_to_sign || 'invalidate=true&';
        l_json.put('public_id', l_public_id);
        l_string_to_sign:=l_string_to_sign || 'public_id=' || l_public_id || '&';
        l_json.put('timestamp', l_timestamp);
        l_string_to_sign:=l_string_to_sign || 'timestamp=' || l_timestamp;

        l_string_to_sign:=l_string_to_sign || l_cld_api_secret;

        l_json.put('signature', dbms_crypto.hash(utl_raw.cast_to_raw(l_string_to_sign), dbms_crypto.HASH_SH1));
        
        l_json.put('api_key', l_cld_api_key);  
        pck_api.callCloudinaryAPI(pUserId=>null, pEndpoint=>l_cld_cloud_name || '/' || l_resource_type || '/destroy', pMethod=>'POST', pBody=>l_json.stringify, pData=>l_clob);
        
        IF (apex_web_service.g_status_code<>200) THEN
            pck_core.log(l_clob);
        END IF;

        DELETE asset WHERE id=l_asset_id;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;

    /*
    ** GENERATE USER SIGNATURE FROM AUTHENTICATED  USER'S CLOUDINARY API SECRET
    */
    PROCEDURE getCldSignature(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pParamsToSign IN CLOB, pStatus OUT NUMBER) IS
        l_params_to_sign clob:=pParamsToSign;
        l_string_to_sign varchar2(2000):=NULL; 
        l_session_data pck_sec.t_session_data;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT NVL2(filename_override,'filename_override='||filename_override||'&',NULL) ||
               NVL2(headers,'headers='||headers||'&',NULL) ||
               NVL2(source,'source='||source||'&',NULL) ||
               NVL2(tags,'tags='||tags||'&',NULL) ||
               NVL2(timestamp,'timestamp='||timestamp||'&',NULL) ||
               NVL2(use_filename,'use_filename='||use_filename,NULL)
          INTO l_string_to_sign
          FROM users, JSON_TABLE(l_params_to_sign FORMAT JSON, '$' COLUMNS filename_override,headers,source,tags,timestamp,use_filename)
         WHERE users.id=l_session_data.user_id;

        apex_json.open_object; 
        apex_json.write('success', TRUE);
        apex_json.write('signature', dbms_crypto.hash(utl_raw.cast_to_raw(l_string_to_sign || pck_api.getCloudinaryAPISecret(l_session_data.user_id)), dbms_crypto.HASH_SH1));
        apex_json.close_object;

        pStatus:=200;
        
        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** UPDATE EXISTING CLOUDINARY ASSETS FOR GIVEN ARTICLE_ID CONVERTING TO AVIF FORMAT WITH Q80 COMPRESSION
    */
    PROCEDURE updateCldAsset(pArticleId IN article.id%type) IS
        TYPE tt_assets IS RECORD(
            id asset.id%type,
            cld_cloud_name asset.cld_cloud_name%type, 
            public_id asset.public_id%type,
            cld_api_key users.cld_api_key%type, 
            cld_api_secret users.cld_api_secret%type,
            cld_upload_preset users.cld_upload_preset%type
        );
        TYPE t_assets IS TABLE OF tt_assets INDEX BY PLS_INTEGER;
        l_assets t_assets;

        l_breakpoints asset.breakpoints%type;

        l_parm_names apex_application_global.VC_ARR2;
        l_parm_values apex_application_global.VC_ARR2;
        l_timestamp NUMBER:=trunc((cast(current_timestamp at time zone 'UTC' as date) - to_date('01-jan-1970','dd-mon-yyyy')) * (86400));
        l_signature VARCHAR2(100);
        l_string_to_sign varchar2(2000);  
        l_clob CLOB;
        l_json JSON_OBJECT_T;
        l_return CLOB;
        n PLS_INTEGER:=0;
    BEGIN
        /*
        ** CLOUDINARY SIGNATURE IS URL QUERY STRING OF ALL PARAMETERS EXCEPT "api_key" ARRANGED ALPHABETICALLY BY NAME
        */

        --curl https://api.cloudinary.com/v1_1/cld-docs/image/upload -X POST --data 'file=https://www.example.com/sample.jpg&responsive_breakpoint=[{"create_derived":true,"bytes_step":20000,"min_width":200,"max_width":1000}]&timestamp=173719931&api_key=614335564976464&signature=a788d68f86a6f868af'

        
        SELECT id, cld_cloud_name, public_id, cld_api_key, cld_api_secret, cld_upload_preset
          BULK COLLECT INTO l_assets
          FROM
            ( 
                SELECT a.id, a.cld_cloud_name, a.public_id
                  FROM asset a
                 WHERE a.article_id=pArticleId
                   AND a.resource_type='image'
                   AND a.format<>'avif'
            ),
            (SELECT cld_api_key, cld_api_secret, cld_upload_preset from users where id=44);

        FOR i IN 1..l_assets.COUNT LOOP

            IF (i=1) THEN
                apex_web_service.clear_request_headers;
                apex_web_service.g_request_headers(1).name := 'Content-Type';
                apex_web_service.g_request_headers(1).value := 'application/json';
            END IF;

            l_parm_names(1):='public_id';
            l_parm_values(1):=l_assets(i).public_id;

            l_parm_names(2):='responsive_breakpoints';
            l_parm_values(2):='[{"create_derived":false,"bytes_step":10000,"min_width":200,"max_width":1200,"max_images":10}]';

            l_parm_names(3):='timestamp';    
            l_parm_values(3):=l_timestamp;

            l_parm_names(4):='upload_preset';
            l_parm_values(4):=l_assets(i).cld_upload_preset;

            l_parm_names(5):='file';
            l_parm_values(5):='https://res.cloudinary.com/' || l_assets(i).cld_cloud_name || '/image/upload/v1744548339/' || l_assets(i).public_id || '.jpg';

            l_string_to_sign:=NULL;
            FOR i IN 1..4 LOOP
                l_string_to_sign:=l_string_to_sign || l_parm_names(i) || '=' || l_parm_values(i) || '&';
            END LOOP;
            -- append user's secret to query string
            l_string_to_sign:=RTRIM(l_string_to_sign,'&') || l_assets(i).cld_api_secret;

            l_parm_names(6):='signature';    
            l_parm_values(6):=dbms_crypto.hash(utl_raw.cast_to_raw(l_string_to_sign), dbms_crypto.HASH_SH1);

            l_parm_names(7):='api_key';    
            l_parm_values(7):=l_assets(i).cld_api_key;

            l_json:=new JSON_OBJECT_T;
            FOR i IN 1..l_parm_names.COUNT LOOP
                l_json.put(l_parm_names(i), l_parm_values(i));
                --pck_core.log(l_parm_names(i) || '='|| l_parm_values(i));
            END LOOP;            

            l_return:=apex_web_service.make_rest_request(
                p_url=>'https://api.cloudinary.com/v1_1/' || l_assets(i).cld_cloud_name || '/image/upload', 
                p_http_method=>'POST', 
                p_body=>l_json.stringify);

            --pck_core.log(l_return);

            IF (apex_web_service.g_status_code<>200) THEN
                pck_core.log('Got Error ' || apex_web_service.g_status_code || ' converting ' || l_assets(i).public_id);
                pck_core.log(l_return);
                RAISE_APPLICATION_ERROR(-20000,'Error in avif conversion - see log for details');
                EXIT;
            END IF;

            /*
            l_return:=apex_web_service.make_rest_request(
                p_url=>'https://res.cloudinary.com/' || l_assets(i).cld_cloud_name || '/w_auto:breakpoints_200_1220_5_10:json/' || l_assets(i).public_id, 
                p_http_method=>'GET');

            l_assets(i).breakpoints:=LTRIM(RTRIM(SUBSTR(l_return,INSTR(l_return,':')+1),']}'),'[');
            */
            
            -- Get responsive breakpoints and update ASSET table
            SELECT LISTAGG(width,',') WiTHIN GROUP (ORDER BY width) 
              INTO l_breakpoints
              FROM JSON_TABLE(l_return, '$.responsive_breakpoints[0].breakpoints[*]' COLUMNS (width number PATH '$.width'));

            UPDATE asset 
               SET format='avif',
                   breakpoints=l_breakpoints,
                   updated_date=current_timestamp
             WHERE id=l_assets(i).id;

            COMMIT; -- Each conversion takes a long time, so commit work done as soon as possible

        END LOOP;

        pck_core.log('Updated ' || l_assets.COUNT || ' assets');
        
        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error;
    END;

    PROCEDURE setImageBreakpoints(pAssetId IN asset.id%type) IS
        l_cld_cloud_name asset.cld_cloud_name%type;
        l_public_id asset.public_id%type;
        l_url VARCHAR2(200);
        l_response CLOB;
        l_breakpoints asset.breakpoints%type;
    BEGIN
        SELECT cld_cloud_name, public_id 
          INTO l_cld_cloud_name, l_public_id
          FROM asset 
         WHERE id=pAssetId;

        l_url:='https://res.cloudinary.com/' || l_cld_cloud_name || '/w_auto:breakpoints_200_1200_5_10:json/' || l_public_id;
        l_response:=apex_web_service.make_rest_request(p_url=>l_url, p_http_method=>'GET');
        l_breakpoints:=LTRIM(RTRIM(SUBSTR(l_response,INSTR(l_response,':')+1),']}'),'[');
        
        UPDATE asset SET breakpoints=l_breakpoints WHERE id=pAssetId;
    END;

    /*
     **  RELEVANT METADATA OF ALL UPLOADED MEDIA INSERTED INTO ASSETS TABLE
     */       
    PROCEDURE uploadCldMetadata(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_bodytext CLOB:=pBodyText;
        l_article_id asset.article_id%type;
        l_job_name VARCHAR2(30);
        l_asset_id asset.id%type;
        l_url VARCHAR2(200);
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        FOR C IN (
            SELECT seq, article_id, cld_cloud_name, resource_type, public_id, width, height, bytes, format
              FROM JSON_TABLE(l_bodytext, '$.images[*]' COLUMNS (seq FOR ORDINALITY, article_id, cld_cloud_name, resource_type, public_id, width, height, bytes,  format))
        ) LOOP
            INSERT INTO asset(id, article_id, cld_cloud_name, resource_type, public_id, width, height, bytes, format, display_order, created_date, user_id)
            VALUES (seq_asset.nextval, C.article_id, C.cld_cloud_name, C.resource_type, C.public_id, C.width, C.height, C.bytes, C.format,  C.seq, current_timestamp, l_session_data.user_id);

            IF (C.format='avif') THEN
                l_job_name:=dbms_scheduler.generate_job_name('IMAGE_BREAKPOINTS_');
                dbms_scheduler.create_job(
                    job_name   => l_job_name,
                    job_type   =>'STORED_PROCEDURE',
                    job_action =>'pck_media.setImageBreakpoints',
                    number_of_arguments=>1,
                    start_date=>systimestamp
                );
                dbms_scheduler.set_job_argument_value(
                    job_name => l_job_name,
                    argument_position => 1,
                    argument_value => seq_asset.currval
                );
                dbms_scheduler.enable(l_job_name);
            END IF;
        END LOOP;

        /*
        ** RETURN URL OF LAST UPLOADED ASSET TO BE COPIED TO CLIPBOARD
        */
        l_asset_id:=seq_asset.currval;

        FOR C IN (SELECT cld_cloud_name, width, public_id, resource_type, format FROM asset WHERE id=l_asset_id) LOOP
            l_url:='https://res.cloudinary.com/' || C.cld_cloud_name || '/' || 
                CASE 
                    WHEN C.format='avif' THEN 'w_' || LEAST(C.width,1200)
                    WHEN C.format='pdf' THEN null
                    WHEN C.resource_type='raw' THEN 'raw/upload'
                END ||
                '/' || C.public_id ||
                CASE 
                    WHEN C.format='pdf' THEN '.pdf'
                END;
        END LOOP;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('url',l_url);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;
        
        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;  


    FUNCTION getMediaType(pResourceType IN VARCHAR2, pFormat IN VARCHAR2) RETURN VARCHAR2 
    IS
    BEGIN
        IF (pResourceType='image') THEN
            RETURN ('image');
        ELSIF (pResourceType='video' AND pFormat IN ('mp4')) THEN
            RETURN ('video');
        ELSIF (pResourceType='video' AND pFormat IN ('m4a','wav')) THEN
            RETURN('audio');
        ELSE
            RETURN (NULL);
        END IF;
    END;

    /*
     **  Format Cloudinary URL
     */    
    FUNCTION getCloudinaryUrl(pCloudname IN VARCHAR2, pResourceType IN VARCHAR2, pPublicId IN VARCHAR2, pFormat IN VARCHAR2, pWidth IN INTEGER, pDimension IN NUMBER DEFAULT NULL) RETURN VARCHAR2 
    IS
        l_url VARCHAR2(500);
        l_media_type VARCHAR(5);

        FUNCTION getWidth RETURN VARCHAR2 IS
            l_left PLS_INTEGER:=1;
            l_right PLS_INTEGER:=3;
            TYPE t_arr IS VARRAY(3) OF INTEGER;
            l_arr t_arr:=t_arr(1,2,3);
            l_width INTEGER:=pWidth; 
        BEGIN
            IF (pWidth=0) THEN
                RETURN NULL;
            END IF;
            IF (pDimension IS NOT NULL) THEN
                IF (pDimension=1) THEN
                    RETURN NULL;
                ELSE
                    RETURN ',w_' || ROUND(pWidth*pDimension);
                END IF;
            END IF;
            l_arr(1):=pWidth*.25;
            l_arr(2):=pWidth*.625;
            l_arr(3):=pWidth;
            IF (l_arr(1)>gWidthThumbnail) THEN
                l_arr(1):=gWidthThumbnail;
                l_arr(2):=(pWidth-gWidthThumbnail)/2;
            END IF;
            l_left:=1;
            l_right:=3;
            WHILE (l_left<l_right) LOOP
                IF (ABS(l_arr(l_left)-gWidthThumbnail) <= ABS(l_arr(l_right)-gWidthThumbnail)) THEN
                    l_right:=l_right-1;
                ELSE
                    l_left:=l_left+1;
                END IF;
            END LOOP;
            IF (l_arr(l_left)=pWidth) THEN
                RETURN NULL;
            END IF;
            RETURN ',w_' || ROUND(l_arr(l_left));
        END;
    BEGIN
        /* 
         ** Return best-fit thumbnail url of Cloudinary asset
         */
        IF (pResourceType IS NOT NULL) THEN
            l_media_type:=getMediaType(pResourceType, pFormat);
            l_url:='https://res.cloudinary.com/' || pCloudname ||
                    CASE l_media_type
                        WHEN 'image' THEN '/q_auto,f_auto' || getwidth() || '/' || pPublicId
                        WHEN 'video' THEN '/video/upload/q_auto,f_auto' || getwidth() || '/' || pPublicId || '.jpg' -- video poster
                        WHEN 'audio' THEN '/video/upload/q_auto,f_auto,w_' || gWidthThumbnail || '/fl_waveform' || '/' || pPublicId || '.png'  --audio waveform
                    END;
        END IF;        
        RETURN (l_url);
    END;

    /*
     **  Build gallery of thumbnail images for a given article
     */
    PROCEDURE getThumbnails(pArticleId IN article.id%type, pDeploy IN BOOLEAN DEFAULT FALSE, pHTML IN OUT NOCOPY CLOB)
    IS
        l_clob CLOB;
        l_user_id users.id%type;
        l_url_image VARCHAR2(300);
        l_media_type VARCHAR2(5);
        l_dimensions VARCHAR2(50);
        l_aspect_ratio NUMBER;
        n PLS_INTEGER:=0;
    BEGIN
        FOR C IN (
            SELECT a.id, a.cld_cloud_name, a.resource_type, a.public_id, TO_CHAR(a.created_date,'dd-Mon-yyyy') uploaded, 
                   DECODE(a.width,0,gWidthThumbnail,a.width) width, a.height, a.bytes, a.format
              FROM asset a
             WHERE a.article_id=pArticleId
               AND NVL(a.format,'N/A')<>'clt'
             ORDER BY a.display_order )
        LOOP
            n:=n+1;
            l_media_type:=getMediaType(C.resource_type, C.format);

            l_url_image:=getCloudinaryUrl(C.cld_cloud_name, C.resource_type, C.public_id, C.format, C.width);

            IF (C.width>0 AND C.width*.25>gWidthThumbnail) THEN
                l_dimensions:=gWidthThumbnail || ':' || ROUND((C.width-gWidthThumbnail)/2) || ':' || C.width;
            ELSE
                l_dimensions:=ROUND(C.width*.25) || ':' || ROUND(C.width*.625) || ':' || C.width;
            END IF;

            pHTML:=pHTML || TO_CLOB(
                '<li' || CASE WHEN c.width>C.height THEN ' class="landscape"' END || ' tabindex="0" data-id="' || C.id || '">
                    <img class="fullscreen" data-dimensions="'  || l_dimensions || '" data-src="' || l_url_image || '"' || CASE WHEN l_media_type='video' THEN ' style="display:none"' END || '>');

            CASE l_media_type
                WHEN 'video' THEN
                    pHTML:=pHTML || TO_CLOB(
                    '<video controls preload="metadata" poster="' || l_url_image || '">
                        <source src="' || REPLACE(l_url_image,'.jpg','.webm') || '" type="video/webm"></source>
                        <source src="' || REPLACE(l_url_image,'.jpg','.mp4') || '" type="video/mp4"></source>
                    </video>');

                WHEN 'audio' THEN
                    l_url_image:=REGEXP_REPLACE(l_url_image,',w_(\d)+\/fl_waveform',NULL);
                    pHTML:=pHTML || TO_CLOB(
                    '<audio preload="metadata" controls>
                        <source src="' || REPLACE(l_url_image,'.png','.aac') || '" type="audio/aac"></source>
                        <source src="' || REPLACE(l_url_image,'.png','.ogg') || '" type="audio/ogg"></source>
                        <source src="' || REPLACE(l_url_image,'.png','.mp3') || '" type="audio/mpeg"></source>
                        <source src="' || REPLACE(l_url_image,'.png','.wav') || '" type="audio/wav"></source>
                    </audio>');
                ELSE NULL;
            END CASE;

            IF (NOT pDeploy) THEN
                pHTML:=pHTML || TO_CLOB(
                '<button type="button" class="media-options" title="Edit Media">&#128397;</button>');
            END IF;

            pHTML:=pHTML || TO_CLOB('</li>');
        END LOOP;
    END;

    /*
     **  Build list of thumbnail images for a given article
     */
    FUNCTION getThumbnails(pWebsiteid IN website.id%type, pArticleId IN article.id%type, pRequest IN VARCHAR2, pNbImages IN OUT NUMBER) RETURN CLOB
    IS
        l_clob CLOB;
        l_src VARCHAR2(500);
        l_data_url VARCHAR2(500);
        l_file_name asset.public_id%type;
        n PLS_INTEGER:=0;
    BEGIN
        /*
        ** TABLE OF ASSETS UPLOADED TO PAGE WITH AVAILABLE COPY / DELETE OPTIONS.
        ** ONLY UNUSED ASSETS CAN BE DELETED
        */
        FOR C IN (
            SELECT a.id, a.cld_cloud_name, a.resource_type, a.public_id, a.width, a.height, a.format, a.breakpoints, a.created_date, a.bytes, au.asset_id asset_used_id, w.domain_name, wa.navigation_label
              FROM asset a, asset_used au, article ar, website_article wa, website w
             WHERE a.article_id=pArticleId
               AND au.asset_id=a.id
               AND ar.id=au.article_id
               AND wa.article_id=NVL(ar.parent_id,au.article_id)
               AND w.id=wa.website_id
               UNION ALL               
            SELECT a.id, a.cld_cloud_name, a.resource_type, a.public_id, a.width, a.height, a.format, a.breakpoints, a.created_date, a.bytes, null, null, null
              FROM asset a
             WHERE a.article_id=pArticleId
               AND NOT EXISTS (SELECT null FROM asset_used au WHERE au.asset_id=a.id)
             ORDER BY asset_used_id, created_date  DESC )
        LOOP
            n:=n+1;
            IF (n=1) THEN
                l_clob:=
                '<div style="overflow-x:auto;font-size:50%" tabindex="0" role="group" aria-labelledby="table-caption">' ||
                    '<table class="data-table">' ||
                        '<caption id="table-caption" style="text-align:left">Images and media files uploaded to this page</caption>' ||
                        '<tr><th style="text-align:left">Uploaded File</th><th style="text-align:left">Where Used</th><th>Copy URL</th><th>Delete File</th></tr>';
            END IF;
            l_clob:=l_clob ||
            '<tr><td><figure role="group" aria-labelledby="fig' || n || '">';
            /* Get widest image if breakpoints - i.e. AVIF */
            IF (C.format='avif') THEN
                FOR C1 IN (
                    SELECT breakpoint, ROW_NUMBER() OVER (ORDER BY breakpoint) rn, COUNT(*) OVER () nb  FROM (SELECT TO_NUMBER(column_value) AS breakpoint FROM TABLE(apex_string.split(C.breakpoints,','))) ORDER BY 1 DESC
                    ) LOOP
                    IF (C1.rn=1) THEN
                        l_src:='https://res.cloudinary.com/' || C.cld_cloud_name || '/w_' || C1.breakpoint || '/' || C.public_id;
                    END IF;
                    IF (C1.rn=C1.nb) THEN
                        l_data_url:='https://res.cloudinary.com/' || C.cld_cloud_name || '/w_' || C1.breakpoint || '/' || C.public_id;
                    END IF;
                END LOOP;
                l_file_name:=SUBSTR(C.public_id,1,INSTR(C.public_id,'_',-1)-1) || '.' || C.format;
                l_clob:=l_clob || 
                '<img width="' ||  C.width|| '" height="' ||  C.height|| '" draggable="false" style="pointer-events:none" src="' || l_src || '" alt="">';
            ELSIF (C.format='pdf') THEN
                l_file_name:=SUBSTR(C.public_id,1,INSTR(C.public_id,'_',-1)-1) || '.' || C.format;
                l_data_url:='https://res.cloudinary.com/' || C.cld_cloud_name || '/image/upload/' || C.public_id || '.pdf';
                l_clob:=l_clob || 
                '<svg width="576" height="512" viewBox="0 0 576 512"><path d="M208 48L96 48c-8.8 0-16 7.2-16 16l0 384c0 8.8 7.2 16 16 16l80 0 0 48-80 0c-35.3 0-64-28.7-64-64L32 64C32 28.7 60.7 0 96 0L229.5 0c17 0 33.3 6.7 45.3 18.7L397.3 141.3c12 12 18.7 28.3 18.7 45.3l0 149.5-48 0 0-128-88 0c-39.8 0-72-32.2-72-72l0-88zM348.1 160L256 67.9 256 136c0 13.3 10.7 24 24 24l68.1 0zM240 380l32 0c33.1 0 60 26.9 60 60s-26.9 60-60 60l-12 0 0 28c0 11-9 20-20 20s-20-9-20-20l0-128c0-11 9-20 20-20zm32 80c11 0 20-9 20-20s-9-20-20-20l-12 0 0 40 12 0zm96-80l32 0c28.7 0 52 23.3 52 52l0 64c0 28.7-23.3 52-52 52l-32 0c-11 0-20-9-20-20l0-128c0-11 9-20 20-20zm32 128c6.6 0 12-5.4 12-12l0-64c0-6.6-5.4-12-12-12l-12 0 0 88 12 0zm76-108c0-11 9-20 20-20l48 0c11 0 20 9 20 20s-9 20-20 20l-28 0 0 24 28 0c11 0 20 9 20 20s-9 20-20 20l-28 0 0 44c0 11-9 20-20 20s-20-9-20-20l0-128z"/></svg>';
            ELSIF (C.resource_type='raw') THEN
                l_file_name:=SUBSTR(C.public_id,1,INSTR(C.public_id,'_',-1)-1) || SUBSTR(C.public_id,INSTR(C.public_id,'.',-1));
                l_data_url:='https://res.cloudinary.com/' || C.cld_cloud_name || '/raw/upload/' || C.public_id;
                l_clob:=l_clob || 
                '<svg width="384" height="512" viewBox="0 0 384 512"><path d="M64 48l112 0 0 88c0 39.8 32.2 72 72 72l88 0 0 240c0 8.8-7.2 16-16 16L64 464c-8.8 0-16-7.2-16-16L48 64c0-8.8 7.2-16 16-16zM224 67.9l92.1 92.1-68.1 0c-13.3 0-24-10.7-24-24l0-68.1zM64 0C28.7 0 0 28.7 0 64L0 448c0 35.3 28.7 64 64 64l256 0c35.3 0 64-28.7 64-64l0-261.5c0-17-6.7-33.3-18.7-45.3L242.7 18.7C230.7 6.7 214.5 0 197.5 0L64 0zM80 104c0 13.3 10.7 24 24 24l16 0c13.3 0 24-10.7 24-24s-10.7-24-24-24l-16 0c-13.3 0-24 10.7-24 24zm0 80c0 13.3 10.7 24 24 24l32 0c13.3 0 24-10.7 24-24s-10.7-24-24-24l-32 0c-13.3 0-24 10.7-24 24zm64 56l-32 0c-17.7 0-32 14.3-32 32l0 48c0 26.5 21.5 48 48 48s48-21.5 48-48l0-48c0-17.7-14.3-32-32-32zm-16 64a16 16 0 1 1 0 32 16 16 0 1 1 0-32z"/></svg>';
            ELSE
                l_file_name:=C.public_id;
                l_clob:=l_clob || 
                '<img src="https://res.cloudinary.com/' || C.cld_cloud_name || '/w_200/' || C.public_id || '" alt="">';
            END IF;
            l_clob:=l_clob || 
                '<figcaption id="fig' || n || '">' || 
                    '<span><strong>File name: </strong>' || l_file_name || '</span><br>' ||
                    '<span><strong>Size: </strong>' || apex_string_util.to_display_filesize(C.bytes) || '</span><br>'||
                    '<span><strong>Uploaded: </strong>' || TO_CHAR(C.created_date,'dd-Mon-yyyy hh24:mi') || '</span>'||
                '</figcaption></figure></td>';

            l_clob:=l_clob ||
                '<td><small>' || CASE WHEN C.navigation_label IS NULL THEN 'UNUSED' ELSE C.domain_name || '/' || C.navigation_label END || '</small></td>';

            l_clob:=l_clob ||
                '<td><button type="button" class="button copy" data-url="' || l_data_url || '" data-file="' || l_file_name || '">COPY</button></td>';
            
            IF (C.asset_used_id IS NULL) THEN
                l_clob:=l_clob ||
                '<td><button type="button" class="button delete" data-id="' || C.id || '" data-file="' || l_file_name || '">DELETE</button></td>';
            ELSE
                l_clob:=l_clob ||
                '<td style="text-align:center">IN USE</td>';
            END IF;

            l_clob:=l_clob ||
            '</tr>';

            
            /*
            IF (C.resource_type='image') THEN
                l_clob:=l_clob || l_url_image;
            ELSIF (C.resource_type='video') THEN
                l_clob:=l_clob || 
                '<video draggable="false" controls preload="metadata" poster="' || l_url_image || '">' ||
                    '<source src="' || REPLACE(l_url_image,'.jpg','.webm') || '" type="video/webm"></source>' ||
                    '<source src="' || REPLACE(l_url_image,'.jpg','.mp4') || '" type="video/mp4"></source>' ||
                '</video>';
            ELSIF (C.resource_type='audio') THEN
                l_url_image:=REGEXP_REPLACE(l_url_image,',w_(\d)+\/fl_waveform',NULL);
                l_clob:=l_clob || 
                '<audio draggable="false" preload="metadata" controls>' ||
                    '<source src="' || REPLACE(l_url_image,'.png','.aac') || '" type="audio/aac"></source>' ||
                    '<source src="' || REPLACE(l_url_image,'.png','.ogg') || '" type="audio/ogg"></source>' ||
                    '<source src="' || REPLACE(l_url_image,'.png','.mp3') || '" type="audio/mpeg"></source>' ||
                    '<source src="' || REPLACE(l_url_image,'.png','.wav') || '" type="audio/wav"></source>' ||
                '</audio>';
            END IF;
                */
                
        END LOOP;

        IF (l_clob IS NOT NULL) THEN
            l_clob:=l_clob || 
                '</table>' ||
            '</div>';
        END IF;

        pNbImages:=n;

        RETURN (NVL(l_clob,'<p>No files have been uploaded to this page.</p>'));
    END;

    /*
     **  Build gallery of thumbnail images for a given article
     */
    FUNCTION getImageSrcset(pAssetId IN asset.id%type, pSizes IN VARCHAR2) RETURN VARCHAR2
    IS
        l_img_html LONG;
        l_img_src VARCHAR2(500);
        n PLS_INTEGER:=0;
    BEGIN
        FOR C IN (
            SELECT cld_cloud_name, resource_type, public_id, width, height, breakpoints, alt_text
              FROM asset
             WHERE id=pAssetId )
        LOOP
            l_img_html:=
            '<img width="' || C.width || '" height="' || C.height || '" draggable="false" id="' || pAssetId || '" style="margin-inline:auto;border-radius:initial"';

            IF (pSizes='any') THEN
                l_img_html:=l_img_html || ' sizes="any"';
            ELSE
                l_img_html:=l_img_html || ' sizes="(max-width: ' || LEAST(C.width,1920) || 'px) 100vw, ' || LEAST(C.width,1920) || 'px"';
            END IF;

            FOR C1 IN (SELECT column_value AS breakpoint FROM TABLE(apex_string.split(C.breakpoints,','))) LOOP
                l_img_src:='https://res.cloudinary.com/' || C.cld_cloud_name || '/w_' || C1.breakpoint || '/' || C.public_id;
                n:=n+1;
                IF (n=1) THEN
                    l_img_html:=l_img_html || 
                    ' src="' || l_img_src || '"' ||
                    ' srcset="';
                END IF;
                l_img_html:=l_img_html || l_img_src || ' ' || C1.breakpoint || 'w,';
            END LOOP;
            l_img_html:=RTRIM(l_img_html,',') || '"';

            l_img_html:=l_img_html ||
            ' alt="' || C.alt_text || '">'; 

        END LOOP;

        RETURN (l_img_html);
    END;

END;
/