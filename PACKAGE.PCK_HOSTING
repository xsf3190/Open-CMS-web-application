CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_HOSTING" is
    --
    PROCEDURE changeURL(pWebsiteId IN website.id%type, pBodytext IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE createWebsite(pWebsiteId IN website.id%type, pArticleid IN article.id%type, pBodyText IN CLOB,  pStatus OUT NUMBER);
    --
    --PROCEDURE createWebsite(pWebsiteId IN website.id%type, pUserid IN users.id%type, pEmail IN users.email%type, pUrl IN OUT VARCHAR2);
    --
    PROCEDURE deleteDNS(pWebsiteId IN website.id%type);
    --
    PROCEDURE deleteWebsite(pWebsiteid IN website.id%type);
    --
    PROCEDURE deleteWebsite(pWebsiteid IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE getChangeURL(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE getCreateWebsiteForm(pWebsiteId IN website.id%type, pArticleid IN article.id%type, pStatus OUT NUMBER);
    --
    FUNCTION getEnvFromURL(pUrl IN VARCHAR2) RETURN VARCHAR2;
    --
    PROCEDURE getDNS(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    FUNCTION getEditorDomain(pEmail IN users.email%type) RETURN VARCHAR2;
    --
    PROCEDURE getNetlifyStatus(pIncludeFiles IN BOOLEAN DEFAULT FALSE);
    --
    FUNCTION urlExists(pUrl IN VARCHAR2) RETURN BOOLEAN;
    --
    PROCEDURE waitDeployment(pWebsiteId IN website.id%type, pUserid IN users.id%type, pWaitSeconds IN NUMBER DEFAULT 10);
    --
    PROCEDURE createURLExistsJob(pUrl IN VARCHAR2, pUserid IN website.user_id%type);

    PROCEDURE runURLExists(pUrl IN VARCHAR2, pUserid IN website.user_id%type);
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_HOSTING" is

    /*
    ** DETERMINE ENVIRONMENT FROM URL
    */
    FUNCTION getEnvFromURL(pUrl IN VARCHAR2) RETURN VARCHAR2 IS
    BEGIN
        IF (INSTR(pUrl,'.netlify.app')>0) THEN
            RETURN 'TEST';
        ELSIF (INSTR(pUrl,'editor.')>0) THEN
            RETURN 'TEST';
        ELSIF (INSTR(pUrl,'cdpn.io')>0) THEN
            RETURN 'TEST';
        END IF;
        RETURN 'LIVE';
    END;

    /*
    ** POLL NETLIFY "deploys" API FOR STATUS="ready"
    */
    PROCEDURE waitDeployment(pWebsiteId IN website.id%type, pUserid IN users.id%type, pWaitSeconds IN NUMBER) IS
        l_netlify_deploy_id website.netlify_deploy_id%type;
        l_netlify_site_id website.netlify_site_id%type;
        l_clob CLOB;
        l_state VARCHAR2(20);
        l_done BOOLEAN:=FALSE;
        n PLS_INTEGER:=2;
    BEGIN
        /* WAIT A COUPLE OF SECONDS BEFORE STARTING. HOPEFULLY WILL BE "ready"  */
        dbms_session.sleep(n);

        SELECT netlify_deploy_id, netlify_site_id INTO l_netlify_deploy_id, l_netlify_site_id FROM website WHERE id=pWebsiteId;

        WHILE NOT l_done 
        LOOP
            pck_api.callNetlifyAPI(pUserId=>pUserid, pEndpoint=>'sites/'|| l_netlify_site_id || '/deploys/' || l_netlify_deploy_id, pMethod=>'GET', pData=>l_clob);
            
            SELECT state INTO l_state FROM JSON_TABLE(l_clob, '$' COLUMNS (state));

            n:=n+1;
            IF (n>=pWaitSeconds) THEN
                l_done:=TRUE;
                EXIT;
            END IF;

            IF (l_state='ready') THEN
                l_done:=TRUE;
            ELSE
                dbms_session.sleep(1);
            END IF;

        END LOOP;

        IF (n=pWaitSeconds) THEN
            RAISE_APPLICATION_ERROR(-20100,'Waited too long for Netlify Deploy');
        END IF;
    END;

    /* 
    ** CHECKS URL EXISTS
    */
    FUNCTION urlExists(pUrl IN VARCHAR2) RETURN BOOLEAN IS
        l_clob CLOB;
        l_not_found EXCEPTION;
        PRAGMA EXCEPTION_INIT(l_not_found,-29273);
    BEGIN
        apex_web_service.clear_request_headers;
        l_clob:=apex_web_service.make_rest_request(p_url=>'https://' || pUrl, p_http_method=>'HEAD');
        IF (apex_web_service.g_status_code=200) THEN
            RETURN (TRUE);
        ELSE
            RETURN (FALSE);
        END IF;
        EXCEPTION
            WHEN l_not_found then
                RETURN (FALSE);
    END;

    /* 
    ** CONVERT EMAIL ACCOUNT NAME INTO VALID SUBDOMAIN NAME
    */
    FUNCTION getEditorDomain(pEmail IN users.email%type) RETURN VARCHAR2 IS
        l_domain VARCHAR2(100);
    BEGIN
        l_domain:=SUBSTR(pEmail,1,INSTR(pEmail,'@')-1);
        l_domain:=REPLACE(REPLACE(l_domain,'.','-'),'_','-');
        RETURN(l_domain);
    END;

    /*
    ** CREATE HTML FOR FORM TO CHANGE SUBDOMAIN OF TEST URL
    */
    PROCEDURE getCreateWebsiteForm(pWebsiteId IN website.id%type, pArticleid IN article.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_header LONG;
        l_article CLOB;
        l_footer LONG;
        l_json JSON_OBJECT_T;
        TYPE t_labels IS VARRAY(6) OF website_article.navigation_label%type;
        l_labels t_labels:=t_labels('About','Services','Products','Blog','Contact');
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);
        pck_core.log('l_browser_language:' || owa_util.get_cgi_env('HTTP_ACCEPT_LANGUAGE'));

        l_header:='Create new editor website';
        l_article:=
        '<div>' ||
            '<label for="domain_name">Enter Domain Name (leave blank for website with funny auto-generated name)</label>' ||
            '<input autofocus type="text" id="domain_name" value="">' ||
        '</div>' ||
        '<div>' ||
            '<label for="contact_email">Owner email (authorised to change content)</label>' ||
            '<input type="text" id="contact_email" value="' || l_session_data.sub || '">' ||
        '</div>' ||
         '<fieldset>' ||
            '<legend>Starter pages (can add, change and delete pages in the editor)</legend>' ||
            '<p><a href="#skip">Skip to next</a></p>';
            FOR i IN 1..l_labels.COUNT LOOP
                l_article:=l_article ||
                '<input type="checkbox" id="link' || i || '" value="' || l_labels(i) || '"' || CASE WHEN i=1 THEN ' checked' END || '>' || 
                '<label for="link' || i || '">' || l_labels(i) || '</label>';
            END LOOP;
        l_article:=l_article ||
        '</fieldset>' ||
        '<p id="skip"></p>';
        l_footer:=
        '<button type="button" class="button send" data-processing="Creating new website, wait a few seconds...">' ||
            'CREATE WEBSITE' ||
        '</button>' ||
        '<span aria-live="polite"></span>' ||
        '<span class="loader icon"></span>';

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('header',l_header);
        l_json.put('article',l_article);
        l_json.put('footer',l_footer);
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /* 
    ** CREATE NEW WEBSITE WHEN REQUESTED VIA MENULIST
    */
    PROCEDURE createWebsite(pWebsiteId IN website.id%type, pArticleid IN article.id%type, pBodyText IN CLOB,  pStatus OUT NUMBER) IS
        l_domain_name website.domain_name%type;
        l_domain_name_exists NUMBER;
        l_welcome_html article.body_html%type;
        l_header_html website_article.header_html%type;
        l_footer_html website_article.footer_html%type;
        l_ssl_url VARCHAR2(100);
        l_session_data pck_sec.t_session_data;
        l_contact_email website.contact_email%type;
        l_json JSON_OBJECT_T;
        l_clob CLOB;
        l_netlify_site_id website.netlify_site_id%type;
        l_netlify_site_id_custom website.netlify_site_id_custom%type;
        l_netlify_editor_url VARCHAR2(100);
        l_netlify_live_url VARCHAR2(100);
        l_new_website_id website.id%type:=seq_website.nextval;
        l_parent_id article.parent_id%type;
        l_sendmail_body CLOB;
        n PLS_INTEGER:=0;
        l_body_text CLOB:=pBodyText;

        PROCEDURE error(pError IN VARCHAR2) IS
        BEGIN
            l_json:= new JSON_OBJECT_T;
            l_json.put('success',true);
            l_json.put('link','<span style="color:red">' || pError || '</span>');
            apex_util.prn(l_json.to_clob,false);
            pStatus:=200;
        END;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT j.domain_name, j.contact_email, CASE WHEN w.domain_name IS NULL THEN 0 ELSE 1 END
          INTO l_domain_name, l_contact_email, l_domain_name_exists
          FROM JSON_TABLE(l_body_text,  '$' COLUMNS (domain_name, contact_email)) j, website w
         WHERE j.domain_name=w.domain_name(+);

        IF (l_domain_name_exists=1) THEN
            error('Domain already taken');
            RETURN;
        END IF;

        IF (l_domain_name IS NULL) THEN
            /* FUNNY AUTO GENERATED NAME */
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'sites', pMethod=>'POST', pData=>l_clob);
            SELECT id, ssl_url, name INTO l_netlify_site_id, l_ssl_url, l_domain_name FROM JSON_TABLE(l_clob, '$' COLUMNS (id, ssl_url, name));
        ELSE
            /* REQUESTED DOMAIN NAME */
            l_json:=new JSON_OBJECT_T;
            l_json.put('name', l_domain_name || '-custom');
            l_json.put('custom_domain', l_domain_name);
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'sites', pStatusAllowed=>422, pMethod=>'POST', pBody=>l_json.stringify, pData=>l_clob);
            IF (apex_web_service.g_status_code=422) THEN
                error('Domain name invalid or already taken');
                RETURN;
            END IF;
            SELECT id INTO l_netlify_site_id_custom FROM JSON_TABLE(l_clob, '$' COLUMNS (id));
            l_netlify_live_url:=apex_string_util.get_slug(l_domain_name) || '-custom.netlify.app';
            
            l_json:=new JSON_OBJECT_T;
            l_json.put('name', l_domain_name || '-editor');
            l_json.put('custom_domain', 'editor.' || l_domain_name);
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'sites', pMethod=>'POST', pBody=>l_json.stringify, pData=>l_clob);
            SELECT id INTO l_netlify_site_id FROM JSON_TABLE(l_clob, '$' COLUMNS (id));
            l_netlify_editor_url:=apex_string_util.get_slug(l_domain_name) || '-editor.netlify.app';
            -- pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'sites/' || l_netlify_site_id || '/ssl', pStatusAllowed=>422, pMethod=>'POST', pData=>l_clob);
        END IF;

        /*
        ** GET HTML FOR WELCOME PAGE
        */ 
        SELECT REPLACE(a.body_html,'#DOMAIN_NAME#',l_domain_name), REPLACE(wa.header_html,'#DOMAIN_NAME#',l_domain_name), REPLACE(wa.footer_html,'#CONTACT_EMAIL#',l_contact_email)
          INTO l_welcome_html, l_header_html, l_footer_html
          FROM article a, website_article wa, website w 
         WHERE w.domain_name='restartnet.com'
           AND wa.website_id=w.id
           AND wa.navigation_label='Welcome'
           AND a.id=wa.article_id;

        IF (l_netlify_site_id_custom IS NOT NULL) THEN
            l_welcome_html:=REPLACE(l_welcome_html,'#NETLIFY_EDITOR_URL#',l_netlify_editor_url);
            l_welcome_html:=REPLACE(l_welcome_html,'#NETLIFY_LIVE_URL#',l_netlify_live_url);
        ELSE
            l_welcome_html:='<h2>Funny name url</h2>';
        END IF;
        /*
        ** CREATE WEBSITE IN CMS DATABASE. COPIES FONTS FROM pWebsiteid
        */
        INSERT INTO website (id, domain_name, user_id, css, esm_library, contact_email, netlify_site_id, netlify_site_id_custom, netlify_last_published, netlify_last_published_custom)
        SELECT l_new_website_id, l_domain_name, l_session_data.user_id, css, esm_library, l_contact_email, l_netlify_site_id, l_netlify_site_id_custom, current_timestamp, current_timestamp
          FROM website
         WHERE id=pWebsiteid;

        INSERT INTO website_font(website_id, font_id, context)
        SELECT l_new_website_id, font_id, context
          FROM website_font
         WHERE website_id=pWebsiteid;

        FOR C IN (
            SELECT 'Home' as page FROM dual
            UNION ALL
            SELECT page
            FROM JSON_TABLE(l_body_text, '$.pages[*]' COLUMNS (page PATH '$'))
        ) LOOP
            n:=n+1;
            IF (n=1) THEN
                INSERT INTO article (id, body_html, author_user_id, publication_date )
                    VALUES (seq_article.nextval, l_welcome_html, l_session_data.user_id, current_timestamp);
                INSERT INTO website_article (website_id, article_id, display_order, navigation_label, header_html, footer_html)
                    VALUES (l_new_website_id, seq_article.currval, n, C.page, l_header_html, l_footer_html);
            ELSE
                INSERT INTO article (id, body_html, author_user_id, publication_date)
                    VALUES (seq_article.nextval, CASE WHEN C.page<>'Blog' THEN '<h2>Heading to introduce your "' || C.page || '" page</h2><p>Use the editor to change content.</p>' END, l_session_data.user_id, current_timestamp);
                INSERT INTO website_article (website_id, article_id, display_order, navigation_label, collection_type)
                    VALUES (l_new_website_id, seq_article.currval, n, C.page, CASE WHEN C.page='Blog' THEN 'BLOG' ELSE 'N/A' END);
            END IF;

            IF (C.page='Blog') THEN
                l_parent_id:=seq_article.currval;
                INSERT INTO article (parent_id, id, author_user_id, body_html, title, excerpt, word_count, publication_date)
                    VALUES (l_parent_id, seq_article.nextval, l_session_data.user_id, '<h2>Title of your ' || C.page || ' post</h2></p><Use the editor to change content/p>', 'Title of your ' || C.page || ' post', 'Excerpt of your '|| C.page || ' post. Set using "Page Option" menu item', 11, current_timestamp);
            END IF;
        END LOOP;
        
        pck_deploy.deployEditorSite(l_new_website_id);
        IF (l_netlify_site_id_custom IS NOT NULL) THEN
            pck_deploy.deployLiveSite(l_new_website_id);
        END IF;

        /*
        IF (l_domain_name IS NOT NULL) THEN
            l_sendmail_body:=pck_email.emailTemplate('website', l_ssl_url);
            pck_email.sendmail(l_contact_email, 'Website created successfully', l_sendmail_body);
            l_ssl_url:='https://' || apex_string_util.get_slug(l_domain_name) || '-editor.netlify.app';
        END IF;
        */

        IF (l_ssl_url IS NULL) THEN
            l_ssl_url:='https://' || apex_string_util.get_slug(l_domain_name) || '-editor.netlify.app';
        END IF;

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        l_json.put('link','<a href="' || l_ssl_url || '" target="a_blank" rel="noopener noreferrer" aria-describedby="open-in-new-tab">Open editor website</a>');
        apex_util.prn(l_json.to_clob,false);

        pStatus:=200;

        pck_sec.updateCpuUsed(pUserid=>l_session_data.user_id, pCpuUsedStart=>l_session_data.cpu_used_session);

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
     * DELETE WEBSITE CALLED FROM PLSQL
    */
    PROCEDURE deleteWebsite(pWebsiteid IN website.id%type) IS
        l_clob CLOB;
    BEGIN
        /*
        **  1. DELETE HOSTING OBJECTS
        */
        FOR C IN (
            SELECT w.netlify_site_id, w.netlify_site_id_custom, w.netlify_dns_zone_id
              FROM website w
             WHERE w.id=pWebsiteId
        ) LOOP
            /* DELETE NETLIFY SITES (ALLOW 404 AS HOSTING OBJECT MAY HAVE BEEN DELETED MANUALLY)  */
            IF (C.netlify_site_id_custom IS NOT NULL) THEN
                pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'sites/' || C.netlify_site_id_custom, pMethod=>'DELETE', pStatusAllowed=>404, pData=>l_clob);
                pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'dns_zones/' || C.netlify_dns_zone_id, pMethod=>'DELETE', pStatusAllowed=>404, pData=>l_clob);
            END IF;
            
            IF (C.netlify_site_id IS NOT NULL) THEN
                pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'sites/' || C.netlify_site_id, pMethod=>'DELETE', pStatusAllowed=>404, pData=>l_clob);
            END IF;

            IF (C.netlify_dns_zone_id IS NOT NULL) THEN
                pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'dns_zones/' || C.netlify_dns_zone_id, pMethod=>'DELETE', pStatusAllowed=>404, pData=>l_clob);
            END IF;
        END LOOP;

        /* 
        ** 2. DELETE DATABASE DATA AND CLOUDINARY ASSETS 
        */
        FOR C IN (
            SELECT a.id, wa.navigation_label, a.title
              FROM website_article wa, article a
             WHERE wa.website_id=pWebsiteId
               AND wa.article_id=NVL(a.parent_id,a.id)
             ORDER BY wa.display_order
        ) LOOP
            /*
            **  DELETE CLOUDINARY ASSETS
            */
            FOR C1 IN (
                SELECT DISTINCT cld_cloud_name, resource_type FROM asset WHERE article_id=C.id
            ) LOOP
                pck_api.callCloudinaryAPI(pUserId=>null, pEndpoint=>C1.cld_cloud_name || '/resources/' || C1.resource_type || '/tags/' || C.id, pMethod=>'DELETE', pData=>l_clob);
            END LOOP;

            /* DELETE ARTICLE */
            DELETE article WHERE id=C.id;
            DELETE asset_used WHERE article_id=C.id;
        END LOOP;

        /* DELETE TOKENS AND WEBSITE */
        DELETE refresh_token WHERE website_id=pWebsiteId;
        DELETE website WHERE id=pWebsiteId;
    END;
    
    /*
     * DELETE WEBSITE CALLED FROM JAVASCRIPT FETCH
    */
    PROCEDURE deleteWebsite(pWebsiteid IN website.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_json JSON_OBJECT_T;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        deleteWebsite(pWebsiteId);

        l_json:= new JSON_OBJECT_T;
        l_json.put('success',true);
        apex_util.prn(l_json.to_clob,false);
        
        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** CREATE HTML FOR FORM TO CHANGE SUBDOMAIN OF TEST URL
    */
    PROCEDURE getChangeURL(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_subdomain website.domain_name%type;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        l_subdomain:=SUBSTR(l_session_data.url,1,INSTR(l_session_data.url,'.')-1);

        apex_json.open_object;
        apex_json.write('article', '<label for="subdomain">Enter New URL</label><input type="text" name="subdomain" required value="' || l_subdomain || '"><span class="result">' || l_session_data.url || '</span>');
        apex_json.write('footer', '<button type="button" class="button change-url">CHANGE URL</button><div class="loader visually-hidden"></div>');
        apex_json.write('success', TRUE);
        apex_json.close_object;

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** CHANGE NETLIFY SUBDOMAIN NAME IF AVAILABLE
    */
    PROCEDURE changeURL(pWebsiteId IN website.id%type, pBodytext IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_netlify_site_id website.netlify_site_id%type;
        l_subdomain website.domain_name%type;
        l_json JSON_OBJECT_T;
        l_clob CLOB;
        l_valid BOOLEAN:=TRUE;
        l_message VARCHAR2(50);
        l_new_token_domain VARCHAR2 (4000);
        l_new_refresh_domain VARCHAR2 (4000);
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT j.subdomain, w.netlify_site_id INTO l_subdomain, l_netlify_site_id  FROM JSON_TABLE(pBodytext, '$' COLUMNS (subdomain)) j, website w WHERE w.id=pWebsiteId;

        IF (urlExists(l_subdomain || '.netlify.app')) THEN
            l_message:='URL already in use. Try again';
            l_valid:=FALSE;
        ELSE
            l_json:=new JSON_OBJECT_T;
            l_json.put('name', l_subdomain);
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'sites/' || l_netlify_site_id, pMethod=>'PATCH', pBody=>l_json.stringify, pStatusAllowed=>422, pData=>l_clob);
            IF (apex_web_service.g_status_code BETWEEN 200 AND 299) THEN
                l_message:='URL successfully changed';
                UPDATE website SET domain_name=l_subdomain WHERE id=pWebsiteId;
                pck_deploy.runDeployment(pWebsiteId, l_netlify_site_id, pck_core.getResturl);
            ELSIF (apex_web_service.g_status_code=422) THEN
                l_message:='Name already taken. Try again';
                l_valid:=FALSE;
            END IF;
        END IF;

        apex_json.open_object; 
        apex_json.write('success', TRUE);
        apex_json.write('valid', l_valid);
        IF (l_valid) THEN
            l_new_token_domain:=apex_jwt.ENCODE (
                p_iss       => l_session_data.iss,
                p_sub       => l_session_data.sub,
                p_aud       => l_session_data.scope,
                p_jti       => l_session_data.user_id,
                p_iat_ts    => current_timestamp,
                p_exp_sec   => pck_sec.ACCESS_TOKEN_EXP,
                p_signature_key => UTL_RAW.cast_to_raw (l_session_data.client_secret)
            ); 
            l_new_refresh_domain:=apex_jwt.ENCODE (
                p_iss       => l_session_data.iss,
                p_sub       => l_session_data.sub,
                p_aud       => l_session_data.scope,
                p_jti       => l_session_data.user_id,
                p_iat_ts    => current_timestamp,
                p_exp_sec   => pck_sec.REFRESH_TOKEN_EXP,
                p_signature_key => UTL_RAW.cast_to_raw (l_session_data.client_secret)
            );
            apex_json.write('url', 'https://' || l_subdomain || '.netlify.app' || '?refresh=' || l_new_refresh_domain || '&token=' || l_new_token_domain);
        END IF;
        apex_json.write('message', l_message);
        apex_json.close_object;

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** GET ALL DNS ZONE SERVERS REGISTERED FOR A CUSTOM DOMAIN
    ** IF NOT REGISTERED RETURN INPUT ELEMENT AND BUTTON TO CREATE CUSTOM DOMAIN SITE
    */
    PROCEDURE getDNS(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_clob CLOB;
        l_netlify_site_id_custom website.netlify_site_id_custom%type;
        l_html VARCHAR2(500);
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT netlify_site_id_custom INTO l_netlify_site_id_custom  FROM website WHERE id=pWebsiteId;

        apex_json.open_object; 
        
        IF (l_netlify_site_id_custom IS NULL) THEN
            apex_json.write('header', '<h4>Link to custom Domain</h4>');
            apex_json.write('article', '<label for="domain_name_custom">Enter Registered Domain Name</label><input type="text" id="domain_name_custom" name="domain_name_custom" required><div class="result"></div>');
            apex_json.write('footer', '<button type="button" class="button create-dns">GET NAMESERVERS</button><div class="loader visually-hidden"></div>');
        ELSE
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'sites/' || l_netlify_site_id_custom || '/dns', pMethod=>'GET', pData=>l_clob);
            FOR C IN (SELECT dns_server  FROM JSON_TABLE(l_clob, '$' COLUMNS (NESTED PATH '$.dns_servers[*]' COLUMNS (dns_server PATH '$'))) ) 
            LOOP
                l_html:=l_html || '<li>' || C.dns_server || '</li>';
            END LOOP;
            apex_json.write('header', '<h4>Domain Name Servers</h4>');
            apex_json.write('article', '<ul>' || l_html || '</ul>');
            apex_json.write('footer', '<p>Update your domain''s nameservers</p>');
        END IF;

        apex_json.write('success', TRUE);
        apex_json.close_object;

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** DELETE DNS ZONE REGISTERED FOR A CUSTOM DOMAIN - CALLED AS PART OF WEBSITE DELETE PROCESS
    */
    PROCEDURE deleteDNS(pWebsiteId IN website.id%type) IS
        l_clob CLOB;
    BEGIN
        FOR C IN (
            SELECT netlify_dns_zone_id, user_id FROM website WHERE id=pWebsiteId AND netlify_dns_zone_id IS NOT NULL
        )
        LOOP
            pck_api.callNetlifyAPI(pUserId=>C.user_id, pEndpoint=>'dns_zones/' || C.netlify_dns_zone_id, pMethod=>'DELETE', pData=>l_clob);
        END LOOP;
    END;
    
    /*
    ** SUBMIT JOB TO RUN EVERY 10 MINUTES CHECKING URL IS ACTIVE.
    ** RUNS AT NEXT 10 MINUTE BOUNDARY AND THEN EVERY 10 MINUTES UNTIL URL EXISTS
    */
    PROCEDURE createURLExistsJob(pUrl IN VARCHAR2, pUserId IN website.user_id%type) IS
        l_job_name VARCHAR2(30);
    BEGIN
        l_job_name:=dbms_scheduler.generate_job_name('URLEXISTS_');
        dbms_scheduler.create_job(
            job_name   => l_job_name,
            job_type   =>'STORED_PROCEDURE',
            job_action =>'pck_hosting.runURLExists',
            number_of_arguments=>1,
            start_date=>systimestamp,
            repeat_interval   => 'FREQ=minutely;BYMINUTE=0,10,20,30,40,50;BYSECOND=0'
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 1,
            argument_value => pUrl
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 2,
            argument_value => pUserId
        );
        dbms_scheduler.enable(l_job_name);
    END;

    /*
    ** CHECK URL IS ACTIVE - I.E. DNS HAS SUFFICIENTLY PROPAGATED AND NETLIFY SSL CERTIFICATE HAS BEEN ISSUED
    */
    PROCEDURE runURLExists(pUrl IN VARCHAR2, pUserId IN website.user_id%type) IS
        l_job_name VARCHAR2(30);
        l_sendmail_body LONG;
    BEGIN
        IF NOT (urlExists(pUrl)) THEN
            pck_core.log('URL ' || pUrl || ' NOK. Retry in 10 minutes');
            RETURN;
        END IF;

        pck_core.log('URL ' || pUrl || ' OK. Send mail and drop job');
        l_sendmail_body:=pck_email.emailTemplate('website',pUrl);
        FOR C IN (SELECT email FROM users WHERE id=pUserId) LOOP
            pck_email.sendmail(C.email, pUrl || ' is deployed', l_sendmail_body);
        END LOOP;

        SELECT job_name INTO l_job_name FROM user_scheduler_running_jobs WHERE session_id = sys_context('USERENV', 'SID');
        dbms_scheduler.drop_job(l_job_name,defer=>true);

    END;

    /*
    ** COPY NETLIFY STATUS TO DATABASE TABLES
    */
    PROCEDURE getNetlifyStatus(pIncludeFiles IN BOOLEAN) IS
        l_admin_userid users.id%type;
        l_dns_zones CLOB;
        l_sites CLOB;
        l_files CLOB;
        l_dns_records CLOB;
    BEGIN
        SELECT u.id 
          INTO l_admin_userid
          FROM apex_workspace_apex_users w, users u 
         WHERE w.email=u.email
           AND w.is_admin='Yes'
         FETCH FIRST ROW ONLY;

        pck_api.callNetlifyAPI(pUserId=>l_admin_userid, pEndpoint=>'dns_zones', pMethod=>'GET', pData=>l_dns_zones);
        DELETE netlify_dns_zone;
        INSERT INTO netlify_dns_zone (id, name, site_id, created_at, updated_at)
        SELECT id, name, site_id, TO_TIMESTAMP_TZ(created_at, 'YYYY-MM-DD"T"HH24:MI:SS.FXFF3TZR'), TO_TIMESTAMP_TZ(updated_at, 'YYYY-MM-DD"T"HH24:MI:SS.FXFF3TZR') 
          FROM JSON_TABLE(l_dns_zones, '$[*]' COLUMNS(id, name, site_id, created_at, updated_at));

        DELETE netlify_dns_zone_server;
        INSERT INTO netlify_dns_zone_server (dns_zone_id, server)
        SELECT id, server FROM JSON_TABLE(l_dns_zones, '$[*]' COLUMNS (id, NESTED '$.dns_servers[*]' COLUMNS (server varchar2(50) path '$')));

        pck_api.callNetlifyAPI(pUserId=>l_admin_userid, pEndpoint=>'sites', pMethod=>'GET', pData=>l_sites);
        DELETE netlify_site;
        INSERT INTO netlify_site (id, name, custom_domain, url, created_at, updated_at)
        SELECT id, name, custom_domain, url, TO_TIMESTAMP_TZ(created_at, 'YYYY-MM-DD"T"HH24:MI:SS.FXFF3TZR'), TO_TIMESTAMP_TZ(updated_at, 'YYYY-MM-DD"T"HH24:MI:SS.FXFF3TZR')
          FROM JSON_TABLE(l_sites, '$[*]' COLUMNS(id, name, custom_domain, url,  created_at, updated_at));

        DELETE netlify_dns_record;
        FOR C IN (
            SELECT id FROM netlify_dns_zone
        ) LOOP
            pck_api.callNetlifyAPI(pUserId=>l_admin_userid, pEndpoint=>'dns_zones/' || C.id || '/dns_records', pMethod=>'GET', pData=>l_dns_records);
            
            INSERT INTO netlify_dns_record (id, hostname, type, value, ttl, dns_zone_id, site_id, managed)
            SELECT id, hostname, type, value, ttl, dns_zone_id, site_id, managed
              FROM JSON_TABLE(l_dns_records, '$[*]' COLUMNS(id, hostname, type, value, ttl, dns_zone_id, site_id, managed));
        END LOOP;

        IF (pIncludeFiles) THEN
            DELETE netlify_file;
            FOR C IN (
                SELECT id FROM netlify_site
            ) LOOP
                pck_api.callNetlifyAPI(pUserId=>l_admin_userid, pEndpoint=>'sites/' || C.id || '/files', pMethod=>'GET', pData=>l_files);
                INSERT INTO netlify_file (site_id, path, sha, mime_type, size_bytes)
                SELECT C.id, path, sha, mime_type, "size"
                  FROM JSON_TABLE(l_files, '$[*]' COLUMNS(path, sha, mime_type, "size"));
            END LOOP;
        END IF;

    END;

end;
/