CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_DEPLOY" as 
    --
    PROCEDURE cancelDeployment(pId IN VARCHAR2);
    --
    PROCEDURE deleteDirectory(pWebsiteId IN website.id%type, pUserId IN users.id%type);
    --
    PROCEDURE deployInfrastructure(pSenderEmail IN VARCHAR2 DEFAULT 'contact.form');
    --
    PROCEDURE deployWebsite(pBodyText IN CLOB);
    --
    PROCEDURE getDeploymentStatus(pId IN VARCHAR2);
    --
    PROCEDURE runDelete(pUserId IN users.id%type, pWebsiteId IN website.id%type, pSiteId IN website.netlify_site_id%type);
    --
    PROCEDURE runDeployment(pWebsiteId IN website.id%type, pUserId IN website.user_id%type, pSiteId IN website.netlify_site_id%type, pResturl IN VARCHAR2, pApplicationId IN VARCHAR2) ;
    --
    PROCEDURE setTerraformApikey;
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_DEPLOY" as 
    
    /*
    **  Insert row in website_deploy logging table
    */      
    PROCEDURE logDeployment(pWebsiteId IN website.id%type, pSiteId IN website_deploy.site_id%type, pMessage IN VARCHAR2, pStatus IN VARCHAR2 DEFAULT 'OK', pLogTime IN TIMESTAMP DEFAULT current_timestamp) IS 
    BEGIN
        INSERT INTO website_deploy(id, website_id, site_id, message, status, log_time) VALUES (seq_log.nextval, pWebsiteId, pSiteId, pMessage, pStatus, pLogTime);
        COMMIT;
    END; 

    /*
    ** Build and deploy website to Netlify by submitting Github action
    */    
    PROCEDURE deployWebsite(pBodyText IN CLOB) IS
        TYPE t_table_id IS RECORD(
            id NUMBER,
            display_order NUMBER);
        TYPE tt_table_id IS TABLE OF t_table_id;
        l_table_ids tt_table_id; 

        l_clob CLOB;
        l_job_name VARCHAR2(30);
        n PLS_INTEGER;
        l_body_text CLOB:=pBodyText;
        l_website_id website.id%type;
        l_site_id website.netlify_site_id%type;
        l_list LONG;
        l_url VARCHAR2(100);
        l_session_data pck_core.session_data_rt;
    BEGIN
        l_session_data:=pck_core.getSessionData();

        WITH payload AS (SELECT websiteid, env, list FROM JSON_TABLE(l_body_text, '$' COLUMNS (websiteid, env, list)))
        SELECT p.websiteid, CASE WHEN p.env='TEST' THEN w.netlify_site_id ELSE w.netlify_site_id_custom END, p.list 
          INTO l_website_id, l_site_id, l_list 
          FROM website w, payload p
         WHERE w.user_id=l_session_data.user_id
           AND w.id=p.websiteid;

        /* Reorder pages */
        SELECT column_value, rownum 
          BULK COLLECT INTO l_table_ids
          FROM TABLE(apex_string.split(l_list,':'));

        FORALL i IN l_table_ids.first .. l_table_ids.last
            UPDATE website_article 
               SET display_order=l_table_ids(i).display_order 
             WHERE website_id = l_website_id
               AND article_id=l_table_ids(i).id;

        /* Check no other session currently deploying - simultaneous deployments not allowed on Netlify free plan */
        FOR C IN (SELECT job_name FROM user_scheduler_running_jobs WHERE job_name LIKE 'DEPLOY_%') LOOP
            apex_json.open_object;
            apex_json.write('content', '<h2>Someone else is deploying ... try again later</h2>');
            apex_json.write('stop', TRUE);
            apex_json.write('success', TRUE);
            apex_json.close_object;
            RETURN;
        END LOOP;

        DELETE website_deploy WHERE website_id=l_website_id;
        UPDATE website SET netlify_deploy_id=NULL WHERE id=l_website_id;

        l_job_name:=dbms_scheduler.generate_job_name('DEPLOY_');
        dbms_scheduler.create_job(
            job_name   => l_job_name,
            job_type   =>'STORED_PROCEDURE',
            job_action =>'pck_deploy.runDeployment',
            number_of_arguments=>5,
            start_date=>systimestamp
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 1,
            argument_value => l_website_id
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 2,
            argument_value => l_session_data.user_id
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 3,
            argument_value => l_site_id
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 4,
            argument_value => pck_core.getRestUrl()
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 5,
            argument_value => l_session_data.app_id
        );
        dbms_scheduler.enable(l_job_name);

        SELECT CASE 
                WHEN netlify_site_id=l_site_id THEN REPLACE(domain_name,'.','-') || '.netlify.app'  
                WHEN netlify_site_id_custom=l_site_id THEN domain_name
               END
        INTO l_url 
        FROM website 
        WHERE id=l_website_id;

        apex_json.open_object; 
        apex_json.write('success', TRUE);
        -- <button type="button" class="cancel-deploy">CANCEL DEPLOY</button>
        apex_json.write('content', '<ol class="deploy"><li><span>' || TO_CHAR(current_timestamp,'hh24:mi:ss') || '</span><span>Starting deployment of ' || l_url || '</span></li></ol>');
        apex_json.close_object;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;

    /*
    **  DEPLOY BY UPLOADING HTML FILE CONTENT DIRECTLY TO NETLIFY
    */
    PROCEDURE runDeployment(pWebsiteId IN website.id%type, pUserId IN website.user_id%type, pSiteId IN website.netlify_site_id%type, pRestUrl IN VARCHAR2, pApplicationId IN VARCHAR2) IS
        l_clob CLOB;
        l_json JSON_OBJECT_T;
        l_files JSON_OBJECT_T;
        l_head CLOB;
        l_page CLOB;
        l_font website.font%type;
        l_deploy_css CLOB;
        l_metric_js CLOB;
        l_gallery_js CLOB;
        l_contact_js CLOB;
        l_file_name VARCHAR2(200);
        l_sha1 VARCHAR2(40);
        TYPE tt_files IS RECORD (
            lastmod article.updated_date%type,
            url VARCHAR2(200),
            file_name VARCHAR2(200),
            file_content CLOB
        );
        TYPE t_files IS TABLE OF tt_files INDEX BY VARCHAR2(40);
        l_deploy_files t_files;

        TYPE tt_labels IS RECORD (
            parent_article_id website_article.article_id%type,
            article_id website_article.article_id%type,
            navigation_label article.title%type,  -- largest that can fit here
            path_name VARCHAR2(200),
            collection_type website_article.collection_type%type,
            page_title website_article.page_title%type,
            page_description website_article.page_description%type,
            contact_form website_article.contact_form%type,
            lastmod article.updated_date%type,
            word_count article.word_count%type, 
            body_html article.body_html%type
        );
        TYPE t_labels IS TABLE OF tt_labels INDEX BY PLS_INTEGER;
        l_labels t_labels;
        l_is_media BOOLEAN:=FALSE;
        l_thumbnails CLOB;

        l_sitemap CLOB;
        l_robots CLOB;
        l_ssl_url VARCHAR2(200);
        l_url VARCHAR2(200);

        n PLS_INTEGER:=0;
        l_total_pages PLS_INTEGER;

        FUNCTION buildNavLabels(pArticleId IN website_article.article_id%type) RETURN VARCHAR2 IS
            l_nav LONG;
        BEGIN
            FOR i IN 1..l_labels.COUNT LOOP
                IF (l_labels(i).article_id=l_labels(i).parent_article_id) THEN
                    l_nav:=l_nav || 
                    '<a href="/' || CASE WHEN i>1 THEN l_labels(i).path_name  END || '" class="nav-label' || 
                    CASE WHEN l_labels(i).article_id=pArticleId THEN ' selected' END || '" data-id="' || l_labels(i).article_id || '">' || l_labels(i).navigation_label || '</a>';
                END IF;
            END LOOP;
            RETURN (l_nav);
        END;

        FUNCTION contactEmail RETURN VARCHAR2 IS
            l_contact_email website.contact_email%type;
        BEGIN
            SELECT contact_email INTO l_contact_email FROM website WHERE id=pWebsiteId;
            IF (l_contact_email IS NULL) THEN
                SELECT email INTO l_contact_email FROM users WHERE id=pUserId;
            END IF;
            RETURN (l_contact_email);
        END;

    BEGIN
        /* get javascript files */
        FOR C IN (SELECT file_name, apex_util.blob_to_clob(file_content) file_content
                    FROM apex_application_static_files 
                   WHERE application_id=pApplicationId
                     AND file_name IN ('deploy_metric.js', 'deploy_gallery.js', 'deploy_contact.js') )
        LOOP
            CASE C.file_name
                WHEN 'deploy_metric.js' THEN l_metric_js:=C.file_content;
                WHEN 'deploy_gallery.js' THEN l_gallery_js:=C.file_content;
                WHEN 'deploy_contact.js' THEN l_contact_js:=C.file_content;
            END CASE;
        END LOOP;

        
        SELECT REPLACE(font,' ','+') INTO l_font FROM website WHERE id=pWebsiteId;

        l_head:=
        '<!DOCTYPE HTML>' || chr(10) ||
        '<html lang="en">' || chr(10) ||
        '<head>' || chr(10) ||
            '<meta charset="UTF-8">' || chr(10) ||
            '<meta name="viewport" content="width=device-width, initial-scale=1.0">' || chr(10) || 
            '<title>#PAGE_TITLE#</title>' || chr(10) ||
            '<meta name="description" content=">#PAGE_DESCRIPTION#">' || chr(10) ||
            '<meta name="author" content=">www.adfreesites.com">' || chr(10) ||
            '<meta name="view-transition" content="same-origin">' || chr(10) ||
            '<link rel="icon" href="https://res.cloudinary.com/mrbapex/believeintalking_favicon_q57es4.svg" type="image/svg+xml">'  || chr(10) ||
            '<link rel="preconnect" href="https://fonts.gstatic.com">' || chr(10) ||
            '<link rel="preconnect" href="https://fonts.gstatic.com">' || chr(10) ||
            '<link rel="stylesheet" href="https://fonts.googleapis.com/css2?family=' || l_font || '&display=swap&text=1234567890ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz%20%21%22%23%24%25%26%27">' || chr(10) ||
            '<style>' || chr(10) || pck_core.buildCssRoot(pWebsiteId, pApplicationId) || chr(10) || '</style>' || chr(10) || 
        '</head>' || chr(10);

        /*
        ** Get all website content into collection
        */
        SELECT parent_article_id, article_id, navigation_label, path_name, collection_type, page_title, page_description, contact_form, lastmod, word_count, body_html
          BULK COLLECT INTO l_labels
          FROM
          (
        SELECT wa.article_id parent_article_id, wa.article_id, wa.navigation_label, apex_string_util.get_slug(wa.navigation_label) path_name, wa.collection_type, wa.page_title, wa.page_description, wa.contact_form, wa.display_order, COALESCE(a.updated_date,a.created_date) lastmod, a.word_count, a.body_html
          FROM website_article wa, article a
         WHERE wa.website_id=pWebsiteId 
           AND wa.article_id=a.id
         UNION ALL
        SELECT wa.article_id, a.id, a.title, apex_string_util.get_slug(wa.navigation_label) || '/' || apex_string_util.get_slug(a.title),  NULL, NULL, NULL, NULL, wa.display_order+NVL(a.display_order/100,0.001), COALESCE(a.deployed_date, a.updated_date) lastmod, a.word_count, a.body_html
          FROM website_article wa, article a 
         WHERE wa.website_id=pWebsiteId 
           AND a.parent_id=wa.article_id 
          )
         ORDER BY display_order, lastmod DESC;
         

        /*
        ** 1. Remove any empty <h1> element from html
        ** 2. Append to parent page links to individual blog/media entries 
        */
        FOR i IN 1..l_labels.COUNT LOOP
            l_labels(i).body_html:=REPLACE(l_labels(i).body_html ,'<h1>&nbsp;</h1>',null);
            IF (l_labels(i).collection_type='BLOG') THEN
                l_is_media:=FALSE;
                l_labels(i).body_html:=l_labels(i).body_html || pck_core.buildBlogList(pWebsiteId, l_labels(i).parent_article_id);
            ELSIF (l_labels(i).collection_type='MEDIA') THEN
                l_is_media:=TRUE;
                l_labels(i).body_html:=l_labels(i).body_html || pck_core.buildMediaList(pWebsiteId, l_labels(i).parent_article_id);
            ELSIF (l_labels(i).collection_type IS NULL AND l_is_media) THEN
                l_thumbnails:=NULL;
                pck_media.getThumbnails(l_labels(i).article_id,pDeploy=>TRUE,pHtml=>l_thumbnails);
                l_labels(i).body_html:=l_labels(i).body_html ||
                '<ol class="gallery-list">' || l_thumbnails || '</ol>
                <div class="gallery-overlay">
                    <img src="">
                    <span class="counter"></span>
                    <button type="button" class="close-fullscreen">&times;</button>
                    <button type="button" class="prev">&lt;</button>
                    <button type="button" class="next">&gt;</button>
                </div>';
            END IF;
        END LOOP;

        /* Get URL of site to be deployed in order to build navigation links */
        pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/' || pSiteId, pMethod=>'GET', pData=>l_clob);
        SELECT ssl_url INTO l_ssl_url FROM JSON_TABLE(l_clob, '$' COLUMNS(ssl_url));

        /* Get SHA1 of all files to be deployed */
        l_json:= new JSON_OBJECT_T;
        l_files:= new JSON_OBJECT_T;

        l_is_media:=FALSE;        
        FOR i IN 1..l_labels.COUNT
        LOOP
            IF (i=1) THEN
                logDeployment(pWebsiteId, pSiteId, 'Building ' || l_labels.COUNT || ' page' || CASE WHEN l_labels.COUNT>1 THEN 's' END);
            END IF;

            IF (l_labels(i).collection_type='MEDIA') THEN
                l_is_media:=TRUE;
            ELSIF (l_labels(i).parent_article_id=l_labels(i).article_id) THEN
                l_is_media:=FALSE;
            END IF;

            l_page:=REPLACE(REPLACE(l_head,'#PAGE_TITLE#',l_labels(i).page_title),'#PAGE_DESCRIPTION#',l_labels(i).page_description) || 
                '<body>' || 
                CASE WHEN l_labels.COUNT>1 THEN '<nav aria-label="Main Navigation"' || CASE WHEN l_labels(i).collection_type<>'N/A' THEN 'style="position:static"' END || '>' || buildNavLabels(l_labels(i).parent_article_id) || '</nav>' END || chr(10) || 
                l_labels(i).body_html || chr(10) || 
                CASE WHEN l_labels(i).contact_form='YES' THEN pck_core.buildContactForm(contactEmail(), l_ssl_url) END ||
                '<script type="module">
                    import {onCLS, onLCP, onINP} from ''https://unpkg.com/web-vitals?module'';
                    onCLS(addToVitalsQueue);
                    onLCP(addToVitalsQueue);
                    onINP(addToVitalsQueue);
                </script>
                <script>
                    gMetricWebsiteId = "' || pWebsiteId || '";' || chr(10) || 
                    'gMetricArticleId = "' || CASE WHEN l_labels(i).parent_article_id=l_labels(i).article_id THEN l_labels(i).parent_article_id ELSE l_labels(i).article_id END || '";' || chr(10) || 
                    'gRestUrl = "' || pRestUrl || '";' || chr(10) ||
                    l_metric_js || 
                    CASE WHEN l_is_media AND l_labels(i).parent_article_id<>l_labels(i).article_id THEN l_gallery_js END ||
                    CASE WHEN l_labels(i).contact_form='YES' THEN l_contact_js END ||
                '</script>' || chr(10) || '
                </body>' || chr(10) || 
            '</html>';

            l_sha1:=LOWER(dbms_crypto.hash(src => l_page, typ => dbms_crypto.hash_sh1));

            IF (i=1) THEN
                l_file_name:='index.html';
            ELSE
                l_file_name:=l_labels(i).path_name || '.html';
            END IF;

            l_deploy_files(l_sha1).url:=l_ssl_url || CASE WHEN i>1 THEN '/' || l_labels(i).path_name END;
            l_deploy_files(l_sha1).lastmod:=l_labels(i).lastmod;
            l_deploy_files(l_sha1).file_name:=l_file_name;
            l_deploy_files(l_sha1).file_content:=l_page;
            l_files.put(l_file_name, l_sha1);
        END LOOP;

        /* Build sitemap.xml */

        l_sitemap:='<?xml version="1.0" encoding="UTF-8"?>' || chr(10) || 
                    '<urlset xmlns="http://www.sitemaps.org/schemas/sitemap/0.9">' || chr(10);

        l_sha1:=l_deploy_files.FIRST;
        WHILE (l_sha1 IS NOT NULL) LOOP
            IF (l_sha1=l_deploy_files.FIRST) THEN
                l_robots:='User-agent: *' || chr(10) || 'Allow: /' || chr(10) || chr(10) || 'Sitemap: ' || l_deploy_files(l_sha1).url || '/sitemap.xml';
            END IF;
            l_sitemap:=l_sitemap || 
                '    <url>' || chr(10) || 
                '        <loc>' || l_deploy_files(l_sha1).url || '</loc>' || chr(10) || 
                '        <lastmod>' || TO_CHAR(l_deploy_files(l_sha1).lastmod,'YYYY-MM-DD') || '</lastmod>' || chr(10) || 
                '    </url>' || chr(10);
            l_sha1 := l_deploy_files.NEXT(l_sha1);
        END LOOP;
        l_sitemap:=l_sitemap || '</urlset>';

        logDeployment(pWebsiteId, pSiteId, 'Building sitemap.xml');
        l_sha1:=LOWER(dbms_crypto.hash(src => l_sitemap, typ => dbms_crypto.hash_sh1));
        l_deploy_files(l_sha1).file_name:='sitemap.xml';
        l_deploy_files(l_sha1).file_content:=l_sitemap;
        l_files.put('sitemap.xml', l_sha1);

        logDeployment(pWebsiteId, pSiteId, 'Building robots.txt');
        l_sha1:=LOWER(dbms_crypto.hash(src => l_robots, typ => dbms_crypto.hash_sh1));
        l_deploy_files(l_sha1).file_name:='robots.txt';
        l_deploy_files(l_sha1).file_content:=l_robots;
        l_files.put('robots.txt', l_sha1);

        l_json.put('files',l_files);

        logDeployment(pWebsiteId, pSiteId, 'Sending digest of all files to Netlify');
        pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/' || pSiteId || '/deploys', pMethod=>'POST', pBody=>l_json.stringify, pData=>l_clob);
        UPDATE website SET netlify_deploy_id=(SELECT id FROM JSON_TABLE(l_clob, '$' COLUMNS (id))) WHERE id=pWebsiteId;
        COMMIT;

        /* upload to Netlify if required - i.e. file content has changed since last deployment  */
        n:=0;
        FOR C IN (SELECT id, sha1 FROM JSON_TABLE(l_clob, '$' COLUMNS (id, NESTED '$.required[*]' COLUMNS (sha1 PATH '$'))) WHERE sha1 IS NOT NULL) LOOP
            pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'deploys/' || C.id || '/files/' || l_deploy_files(C.sha1).file_name, pMethod=>'PUT', pBody=>l_deploy_files(C.sha1).file_content, pData=>l_clob);
            logDeployment(pWebsiteId, pSiteId, 'Successfully uploaded ' || l_deploy_files(C.sha1).file_name);
            n:=n+1;
        END LOOP;

        IF (n>0) THEN
            logDeployment(pWebsiteId, pSiteId, 'Uploaded files: ' || n || ' Unchanged files: ' || TO_CHAR(l_deploy_files.COUNT-n));
        ELSE
            logDeployment(pWebsiteId, pSiteId, 'All files unchanged since last deployment');
        END IF;

        /*
        ** Update deployed_date for article belnging to collection pages
       
        UPDATE article SET deployed_date = current_timestamp
         WHERE parent_id IN (SELECT article_id FROM website_article WHERE website_id=pWebsiteId AND collection_type<>'N/A')
           AND deployed_date IS NULL;
        */

        /* test url */
        

        EXCEPTION
            WHEN OTHERS THEN
                logDeployment(pWebsiteId, pSiteId, SUBSTR(SQLERRM,1,100),'NOK');
                pck_core.log_error;
    END;   

    /*
     **  CANCEL active deployment - HAVE TO STOP JOB FIRST PERHAPS?
     */
    PROCEDURE cancelDeployment(pId IN VARCHAR2) IS 
        l_website_id website_deploy.website_id%type;
        l_site_id website_deploy.site_id%type;
        l_netlify_deploy_id website.netlify_deploy_id%type;
        l_clob CLOB;
        l_session_data pck_core.session_data_rt;
    BEGIN
        l_session_data:=pck_core.getSessionData();

        l_website_id:=SUBSTR(pId,1,INSTR(pId,',')-1);
        l_site_id:=SUBSTR(pId,INSTR(pId,',')+1);

        SELECT netlify_deploy_id INTO l_netlify_deploy_id FROM website WHERE id=l_website_id AND user_id=l_session_data.user_id;

        /* Get latest deployment status if started */
        IF (l_netlify_deploy_id IS NOT NULL) THEN
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'deploys/' || l_netlify_deploy_id || '/cancel', pMethod=>'POST', pData=>l_clob);
            pck_core.log(l_clob);
            apex_json.open_object; 
            apex_json.write('success', TRUE);
            apex_json.write('completed', TRUE);
            apex_json.write('content', '<li><span></span><span>DEPLOYMENT CANCELLED</span>');
            apex_json.close_object;
        END IF;
    END;

    /*
     **  Get deployment status from website_deploy logging table - called from Javascript every 3 seconds
     */
    PROCEDURE getDeploymentStatus(pId IN VARCHAR2) IS 
        l_website_id website_deploy.website_id%type;
        l_site_id website_deploy.site_id%type;
        l_content CLOB;
        l_clob CLOB;
        l_ssl_url VARCHAR2(100);
        l_deployment_complete BOOLEAN:=FALSE;
        l_netlify_status website_deploy.status%type;
        l_netlify_deploy_id website.netlify_deploy_id%type;
        l_state VARCHAR2(200);
        l_updated_at website.netlify_last_published%type;
        l_job_name VARCHAR2(30);
        l_session_data pck_core.session_data_rt;
        TYPE t_deploy_log IS RECORD(
            id website_deploy.id%type,
            log_time VARCHAR2(8),
            message website_deploy.message%type,
            status website_deploy.status%type);
        TYPE tt_deploy_log IS TABLE OF t_deploy_log;
        l_deploy_log tt_deploy_log;          
        l_env VARCHAR2(4);
        l_is_live BOOLEAN;
        cert_not_ready exception;
        pragma exception_init( cert_not_ready, -29273 );
    BEGIN
        l_session_data:=pck_core.getSessionData();

        l_website_id:=SUBSTR(pId,1,INSTR(pId,',')-1);
        l_env:=SUBSTR(pId,INSTR(pId,',')+1);

        SELECT netlify_deploy_id, CASE WHEN l_env='TEST' THEN netlify_site_id ELSE netlify_site_id_custom END
          INTO l_netlify_deploy_id, l_site_id
          FROM website
         WHERE id=l_website_id;

        l_is_live:=l_env='LIVE';

        /* Get latest deployment status if started */
        IF (l_netlify_deploy_id IS NOT NULL) THEN
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'sites/'|| l_site_id || '/deploys/' || l_netlify_deploy_id, pMethod=>'GET', pData=>l_clob);
            FOR C IN (SELECT state, ssl_url, TO_TIMESTAMP_TZ(updated_at, 'YYYY-MM-DD"T"HH24:MI:SS.FXFF3TZR') updated_at
                        FROM JSON_TABLE(l_clob, '$' COLUMNS (state, ssl_url, updated_at)) 
                       WHERE state='ready') 
            LOOP
                logDeployment(l_website_id, l_site_id, C.state, pLogTime=>C.updated_at);
                l_ssl_url:=C.ssl_url;
            END LOOP;
        END IF;

        SELECT id, TO_CHAR(log_time,'hh24:mi:ss') log_time, message, status
          BULK COLLECT INTO l_deploy_log
          FROM website_deploy 
         WHERE website_id=l_website_id
           AND site_id=l_site_id
           AND sent_ind='N'
         ORDER BY id;

        /* Now return list of all deployment status messages to client */
        FOR i IN 1..l_deploy_log.COUNT
        LOOP
            IF (l_deploy_log(i).message='ready') THEN 
                l_content:=l_content ||'<li><span>' || l_deploy_log(i).log_time || '</span><span>Successful deployment</span></li>'; 
                l_content:=l_content ||'<li class="ready"><span></span><span><a target="_blank" href="' || l_ssl_url || '">' || l_ssl_url || '</a></span></li>';
                IF (l_is_live) THEN
                    BEGIN
                        l_clob:=apex_web_service.make_rest_request(p_url=>l_ssl_url, p_http_method=>'GET');
                        EXCEPTION WHEN cert_not_ready THEN
                            l_content:=l_content ||'<li><span></span><span style="color:red">Certificate in process of being issued</span></li>'; 
                    END;
                END If;
                l_deployment_complete:=TRUE;
                EXIT;
            END IF;

            IF (l_deploy_log(i).status='OK') THEN 
                l_content:=l_content ||'<li><span>' || l_deploy_log(i).log_time || '</span><span>' || l_deploy_log(i).message || '</span></li>'; 
            ELSE
                l_content:=l_content ||'<li class="nok"><span>' || l_deploy_log(i).log_time || '</span><span>' || l_deploy_log(i).message || '</span></li>'; 
                l_deployment_complete:=TRUE;
            END IF;
        END LOOP;

        apex_json.open_object; 
        apex_json.write('success', TRUE);
        apex_json.write('completed', l_deployment_complete);
        apex_json.write('content', l_content);
        apex_json.close_object;

        FORALL i IN 1..l_deploy_log.COUNT
            UPDATE website_deploy SET sent_ind='Y' WHERE id=l_deploy_log(i).id;

        IF (l_deployment_complete) THEN
            IF (l_is_live) THEN
                UPDATE website 
                   SET netlify_last_published_custom = current_timestamp
                 WHERE id=l_website_id;
            ELSE
                UPDATE website 
                   SET netlify_last_published = current_timestamp
                 WHERE id=l_website_id;
            END IF;

            l_job_name:=dbms_scheduler.generate_job_name('DELETE_');
            dbms_scheduler.create_job(
                job_name   => l_job_name,
                job_type   =>'STORED_PROCEDURE',
                job_action =>'pck_deploy.runDelete',
                number_of_arguments=>3,
                start_date=>systimestamp
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 1,
                argument_value => l_session_data.user_id
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 2,
                argument_value => l_website_id
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 3,
                argument_value => l_site_id
            );
            dbms_scheduler.enable(l_job_name);    
        END IF;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;   

    PROCEDURE runDelete(pUserId IN users.id%type, pWebsiteId IN website.id%type, pSiteId IN website.netlify_site_id%type) IS
        l_clob CLOB;
    BEGIN
        pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/'|| pSiteId || '/deploys', pMethod=>'GET', pData=>l_clob);
        FOR C IN (
            SELECT id, published_at, MAX(published_at) OVER() last_published_at 
            FROM
                (
                SELECT id, TO_TIMESTAMP_TZ(published_at, 'YYYY-MM-DD"T"HH24:MI:SS.FXFF3TZR') published_at FROM JSON_TABLE(l_clob, '$[*]' COLUMNS (id, published_at))
                )
        )
        LOOP
            IF (C.published_at<C.last_published_at) THEN
                pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/'|| pSiteId || '/deploys/' || C.id, pMethod=>'DELETE', pData=>l_clob);
            END IF;
        END LOOP;
    END;

    /*
    ** Delete Github build directory - have to delete files individually before Github removes directory
    */    
    PROCEDURE deleteDirectory(pWebsiteId IN website.id%type, pUserId IN users.id%type) IS
        l_clob CLOB;
        l_domain_name website.domain_name%type;
        l_github CLOB;
        l_json JSON_OBJECT_T;
    BEGIN
        SELECT domain_name INTO l_domain_name FROM website WHERE id=pWebsiteId;
        pck_api.callGithubAPI(pUserId=>pUserId, pEndpoint=>'contents/'||l_domain_name, pMethod=>'GET', pData=>l_clob);
        FOR C IN (SELECT name, sha, message FROM  JSON_TABLE(l_clob FORMAT JSON, '$[*]' COLUMNS (name, sha, message))) LOOP
            IF (C.message IS NOT NULL) THEN 
                pck_core.log(C.message);
                RAISE_APPLICATION_ERROR(-20050,'Error in deleteDirectory - '|| C.message);
            ELSE
                l_json:=JSON_OBJECT_T.parse('{"message":"Commit by PLSQL"}');
                l_json.put('sha',C.sha);
                l_github:=l_json.to_clob; 
                pck_api.callGithubAPI(pUserId=>pUserId, pEndpoint=>'contents/' || l_domain_name || '/'||C.name, pMethod=>'DELETE', pBody=>l_github, pData=>l_clob);
            END IF;
        END LOOP;
        pck_api.resetGithubCurrentBuild(pUserId);
    END;

    /*
    ** Build and deploy AWS SES infrastructure through Terraform by submitting Github action. 
    ** Contact form sending email address must be stored in Apex Administrator description field!
    */
    PROCEDURE deployInfrastructure(pSenderEmail IN VARCHAR2) IS
        l_clob CLOB;
        l_json_clob CLOB;
        l_json JSON_OBJECT_T;
        l_action JSON_OBJECT_T;
    BEGIN
        -- Apex admin user description must hold valid domain name of sending email address
        FOR C IN (SELECT u.id, u.terraform_token, w.description ses_domain_name
                    FROM apex_workspace_apex_users w, users u 
                   WHERE w.email=u.email
                     AND w.is_admin='Yes'
                   ORDER BY date_created
                   FETCH FIRST ROW ONLY) 
        LOOP
            IF (C.ses_domain_name IS NULL) THEN
                dbms_output.put_line('APEX ADMIN USER MUST HAVE DESCRIPTION SET TO DOMAIN NAME OF SENDING EMAIL ADDRESS');
                EXIT;
            END IF;

            /* trigger the deployment using Githib action */          
            l_json:=new JSON_OBJECT_T;
            l_json.put('event_type', 'trigger_build_infra');

            l_action:=new JSON_OBJECT_T;
            l_action.put('from_email', pSenderEmail||'@'||C.ses_domain_name); --'contact.form@adfreesites.com'
            l_action.put('terraform_token', C.terraform_token);
            l_json.put('client_payload', l_action);

            --l_clob := apex_web_service.make_rest_request(p_url=>'https://api.github.com/repos/'|| C.deploy_repo || '/dispatches' ,p_http_method=>'POST',p_body=>l_json.stringify);

            /* nb; Github API 'dispatches' endpoint does not send a response */
            l_json_clob:=l_json.stringify;
            pck_api.callGithubAPI(pUserId=>C.id, pEndpoint=>'dispatches', pMethod=>'POST', pBody=>l_json_clob, pData=>l_clob);

        END LOOP;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;

/*
**  RUN this after deploying infrastructure for AWS enabling Amazon Simple Emal Service
**  Updates in Admin use row the aws gateway url and the api key in order to use the SES send mail service
*/
    PROCEDURE setTerraformApikey IS
        l_domain_name website.domain_name%type; 
        l_admin_id users.id%type;
        l_terraform_organization_name users.terraform_organization_name%type;
        l_terraform_workspace_name users.terraform_workspace_name%type;
        l_terraform_private_key users.terraform_private_key%type;
        l_aws_gateway_url VARCHAR2(200);
        l_api_key varchar2(40);
        l_api_key_link VARCHAR2(100);
        l_clob CLOB;
    BEGIN
        SELECT u.id, SUBSTR(w.description,1,INSTR(w.description,'.')-1)
          INTO l_admin_id, l_domain_name
          FROM apex_workspace_apex_users w, users u 
         WHERE w.email=u.email
           AND w.is_admin='Yes'
         ORDER BY date_created
         FETCH FIRST ROW ONLY;

        pck_api.getTerraformData(pUserId=>null, pTerraformOrganizationName=>l_terraform_organization_name, pTerraformWorkspaceName=>l_terraform_workspace_name, pTerraformPrivateKey=>l_terraform_private_key);
        pck_api.callTerraformAPI(pUserId=>null, pEndpoint=>'api/v2/organizations/' || l_terraform_organization_name || '/workspaces/' || l_terraform_workspace_name, pMethod=>'GET', pData=>l_clob);

        FOR C IN (SELECT related FROM JSON_TABLE(l_clob, '$.data.relationships.outputs.links' COLUMNS (related))) LOOP
            pck_api.callTerraformAPI(pUserId=>null, pEndpoint=>C.related, pMethod=>'GET', pData=>l_clob);

            /* invoke_url */
            FOR C1 IN (SELECT value FROM JSON_TABLE(l_clob, '$.data[*]' COLUMNS (name PATH '$.attributes.name', value PATH '$.attributes.value')) WHERE name='invoke_url') LOOP
                l_aws_gateway_url:=C1.value || 'send/' || l_domain_name;
            END LOOP;

            /* ses_dkim_tokens */
            dbms_output.put_line('FETCHING DKIM TOKENS');
            FOR C1 IN (SELECT dkim FROM JSON_TABLE(l_clob, '$.data[*]' COLUMNS (name PATH '$.attributes.name', NESTED PATH '$.attributes.value[*]' COLUMNS (dkim VARCHAR2(40) PATH '$'))) WHERE name='ses_dkim_tokens') LOOP
                dbms_output.put_line('ses_dkim_tokens:'||C1.dkim);
            END LOOP;

            /* api_key */
            FOR C1 IN (SELECT api_key_link FROM JSON_TABLE(l_clob, '$.data[*]' COLUMNS (api_key_link PATH '$.links.self', name PATH '$.attributes.name', value PATH '$.attributes.value')) WHERE name='api_key') LOOP
                l_api_key_link:=C1.api_key_link;
            END LOOP;
            pck_api.callTerraformAPI(pUserId=>null, pEndpoint=>l_api_key_link, pMethod=>'GET', pData=>l_clob);
            FOR C1 IN (SELECT value FROM JSON_TABLE(l_clob, '$.data' COLUMNS (value PATH '$.attributes.value'))) LOOP
                l_api_key:=C1.value;
            END LOOP;
        END LOOP;

        IF (l_aws_gateway_url IS NOT NULL AND l_api_key IS NOT NULL) THEN
            UPDATE users 
               SET terraform_aws_gateway_url=l_aws_gateway_url, 
                   terraform_api_key=l_api_key
             WHERE id=l_admin_id;

            dbms_output.put_line('TERRAFORM API_KEY UPDATED SUCESSFULLY');
            dbms_output.put_line('TERRAFORM AWS_GATEWAY_URL UPDATED SUCESSFULLY');
            RETURN;
        END IF;

        IF (l_api_key IS NULL) THEN
            dbms_output.put_line('FAILED TO UPDATE TERRAFORM API_KEY');
        END IF;

        IF (l_aws_gateway_url IS NULL) THEN
            dbms_output.put_line('FAILED TO UPDATE TERRAFORM AWS_GATEWAY_URL');
        END IF;
    END;
       
end;
/