CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_DEPLOY" as 
    --
    PROCEDURE cancelDeployment(pId IN VARCHAR2);
    --
    PROCEDURE deleteDirectory(pWebsiteId IN website.id%type, pUserId IN users.id%type);
    --
    PROCEDURE deployInfrastructure;
    --
    PROCEDURE deployWebsite(pBodyText IN CLOB);
    --
    PROCEDURE getDeploymentStatus(pId IN VARCHAR2);
    --
    PROCEDURE runDelete(pUserId IN users.id%type, pWebsiteId IN website.id%type, pSiteId IN website.netlify_site_id%type);
    --
    PROCEDURE runDeployment(pWebsiteId IN website.id%type, pUserId IN website.user_id%type, pSiteId IN website.netlify_site_id%type, pResturl IN VARCHAR2, pApplicationId IN VARCHAR2) ;
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_DEPLOY" as 
    
    /*
    **  Insert row in website_deploy logging table
    */      
    PROCEDURE logDeployment(pWebsiteId IN website.id%type, pSiteId IN website_deploy.site_id%type, pMessage IN VARCHAR2, pStatus IN VARCHAR2 DEFAULT 'OK', pLogTime IN TIMESTAMP DEFAULT current_timestamp) IS 
    BEGIN
        INSERT INTO website_deploy(id, website_id, site_id, message, status, log_time) VALUES (seq_log.nextval, pWebsiteId, pSiteId, pMessage, pStatus, pLogTime);
        COMMIT;
    END; 

    /*
    ** Build and deploy website to Netlify by submitting Github action
    */    
    PROCEDURE deployWebsite(pBodyText IN CLOB) IS
        TYPE t_table_id IS RECORD(
            id NUMBER,
            display_order NUMBER);
        TYPE tt_table_id IS TABLE OF t_table_id;
        l_table_ids tt_table_id; 

        l_clob CLOB;
        l_job_name VARCHAR2(30);
        n PLS_INTEGER;
        l_body_text CLOB:=pBodyText;
        l_website_id website.id%type;
        l_site_id website.netlify_site_id%type;
        l_list LONG;
        l_url VARCHAR2(100);
        l_session_data pck_core.session_data_rt;
    BEGIN
        l_session_data:=pck_core.getSessionData();

        SELECT websiteid, siteid, list INTO l_website_id, l_site_id, l_list FROM users, JSON_TABLE(l_body_text, '$' COLUMNS (websiteid, siteid, list)) WHERE users.id=l_session_data.user_id;

        /* Reorder pages */
        SELECT column_value, rownum 
          BULK COLLECT INTO l_table_ids
          FROM TABLE(apex_string.split(l_list,':'));

        FORALL i IN l_table_ids.first .. l_table_ids.last
            UPDATE website_article 
               SET display_order=l_table_ids(i).display_order 
             WHERE website_id = l_website_id
               AND article_id=l_table_ids(i).id;

        /* Check no other session currently deploying - simultaneous deployments not allowed on Netlify free plan */
        FOR C IN (SELECT job_name FROM user_scheduler_running_jobs WHERE job_name LIKE 'DEPLOY_%') LOOP
            apex_json.open_object;
            apex_json.write('content', '<h2>Someone else is deploying ... try again later</h2>');
            apex_json.write('stop', TRUE);
            apex_json.write('success', TRUE);
            apex_json.close_object;
            RETURN;
        END LOOP;

        DELETE website_deploy WHERE website_id=l_website_id;
        UPDATE website SET netlify_deploy_id=NULL WHERE id=l_website_id;

        l_job_name:=dbms_scheduler.generate_job_name('DEPLOY_');
        dbms_scheduler.create_job(
            job_name   => l_job_name,
            job_type   =>'STORED_PROCEDURE',
            job_action =>'pck_deploy.runDeployment',
            number_of_arguments=>5,
            start_date=>systimestamp
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 1,
            argument_value => l_website_id
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 2,
            argument_value => l_session_data.user_id
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 3,
            argument_value => l_site_id
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 4,
            argument_value => pck_core.getRestUrl()
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 5,
            argument_value => l_session_data.app_id
        );
        dbms_scheduler.enable(l_job_name);

        SELECT CASE 
                WHEN netlify_site_id=l_site_id THEN REPLACE(domain_name,'.','-') || '.netlify.app'  
                WHEN netlify_site_id_custom=l_site_id THEN domain_name
               END
        INTO l_url 
        FROM website 
        WHERE id=l_website_id;

        apex_json.open_object; 
        apex_json.write('success', TRUE);
        -- <button type="button" class="cancel-deploy">CANCEL DEPLOY</button>
        apex_json.write('content', '<ol class="deploy"><li><span>' || TO_CHAR(current_timestamp,'hh24:mi:ss') || '</span><span>Starting deployment of ' || l_url || '</span></li></ol>');
        apex_json.close_object;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;

    /*
    **  DEPLOY BY UPLOADING HTML FILE CONTENT DIRECTLY TO NETLIFY
    */
    PROCEDURE runDeployment(pWebsiteId IN website.id%type, pUserId IN website.user_id%type, pSiteId IN website.netlify_site_id%type, pRestUrl IN VARCHAR2, pApplicationId IN VARCHAR2) IS
        l_clob CLOB;
        l_json JSON_OBJECT_T;
        l_files JSON_OBJECT_T;
        l_head CLOB;
        l_page CLOB;
        l_deploy_css CLOB;
        l_metric_js CLOB;
        l_domain_name website.domain_name%type;
        l_file_name VARCHAR2(200);
        l_sha1 VARCHAR2(40);
        TYPE tt_files IS RECORD (
            lastmod article.updated_date%type,
            url VARCHAR2(200),
            file_name VARCHAR2(200),
            file_content CLOB
        );
        TYPE t_files IS TABLE OF tt_files INDEX BY VARCHAR2(40);
        l_deploy_files t_files;

        TYPE tt_labels IS RECORD (
            parent_article_id website_article.article_id%type,
            article_id website_article.article_id%type,
            navigation_label article.title%type,  -- largest that can fit here
            path_name VARCHAR2(200),
            collection_type website_article.collection_type%type,
            page_title website_article.page_title%type,
            page_description website_article.page_description%type,
            contact_form website_article.contact_form%type,
            lastmod article.updated_date%type,
            word_count article.word_count%type, 
            body_html article.body_html%type
        );
        TYPE t_labels IS TABLE OF tt_labels INDEX BY PLS_INTEGER;
        l_labels t_labels;

        l_sitemap CLOB;
        l_robots CLOB;
        l_ssl_url VARCHAR2(200);
        l_url VARCHAR2(200);
        l_fluid_types pck_core.fluid_type_t;
        l_fluid_css LONG;

        n PLS_INTEGER:=0;
        l_total_pages PLS_INTEGER;

        FUNCTION buildNavLabels(pArticleId IN website_article.article_id%type) RETURN VARCHAR2 IS
            l_nav LONG;
        BEGIN
            FOR i IN 1..l_labels.COUNT LOOP
                IF (l_labels(i).article_id=l_labels(i).parent_article_id) THEN
                    l_nav:=l_nav || 
                    '<a href="/' || CASE WHEN i>1 THEN l_labels(i).path_name  END || '" class="nav-label' || 
                    CASE WHEN l_labels(i).article_id=pArticleId THEN ' selected' END || '" data-id="' || l_labels(i).article_id || '">' || l_labels(i).navigation_label || '</a>';
                END IF;
            END LOOP;
            RETURN (l_nav);
        END;

        FUNCTION contactEmail RETURN VARCHAR2 IS
            l_contact_email website.contact_email%type;
        BEGIN
            SELECT contact_email INTO l_contact_email FROM website WHERE id=pWebsiteId;
            IF (l_contact_email IS NULL) THEN
                SELECT email INTO l_contact_email FROM users WHERE id=pUserId;
            END IF;
            RETURN (l_contact_email);
        END;

    BEGIN
        /* get metric js */
        SELECT apex_util.blob_to_clob(file_content)
        INTO l_metric_js
        FROM APEX_APPLICATION_STATIC_FILES 
        WHERE application_id=pApplicationId
        AND file_name='app' || pApplicationId || '_metric.min.js';

        /* get deploy css */
        SELECT apex_util.blob_to_clob(file_content)
        INTO l_deploy_css
        FROM APEX_APPLICATION_STATIC_FILES 
        WHERE application_id=pApplicationId
        AND file_name='app' || pApplicationId || '_deploy.min.css';

        /* 
        ** Build page head which is common to all website pages
        */ 
        FOR C IN (SELECT domain_name, font, max_width, color_text, color_background, color_primary, css, javascript, 
                         min_font_size, min_width_px, min_scale, max_font_size, max_width_px, max_scale
                    FROM website
                   WHERE id=pWebsiteId)
        LOOP
            l_domain_name:=C.domain_name;
            l_fluid_types:=pck_core.getFluidTypes(C.min_font_size, C.min_width_px, C.min_scale, C.max_font_size, C.max_width_px, C.max_scale);
            l_fluid_css:=chr(9) || '/* Custom properties for Fluid typography and spacing */' || chr(10);
            FOR i IN 1..l_fluid_types.COUNT LOOP
                l_fluid_css:=l_fluid_css || chr(9) || l_fluid_types(i).property || ': ' || l_fluid_types(i).value || ';' || chr(10);
            END LOOP;
            l_head:=
            '<!DOCTYPE HTML>' || chr(10) ||
            '<html lang="en">' || chr(10) ||
            '<head>' || chr(10) ||
                '<meta charset="UTF-8">' || chr(10) ||
                '<meta name="viewport" content="width=device-width, initial-scale=1.0">' || chr(10) || 
                '<title>#PAGE_TITLE#</title>' || chr(10) ||
                '<meta name="description" content=">#PAGE_DESCRIPTION#">' || chr(10) ||
                '<meta name="author" content=">www.adfreesites.com">' || chr(10) ||
                '<meta name="view-transition" content="same-origin">' || chr(10) ||
                '<script src="https://cdn.jsdelivr.net/npm/bowser@latest/es5.js" defer></script>' || chr(10) ||
                '<link rel="icon" href="https://res.cloudinary.com/mrbapex/believeintalking_favicon_q57es4.svg" type="image/svg+xml">'  || chr(10) ||
                '<link rel="preconnect" href="https://fonts.gstatic.com">' || chr(10) ||
                '<link rel="preconnect" href="https://fonts.gstatic.com">' || chr(10) ||
                '<link rel="stylesheet" href="https://fonts.googleapis.com/css2?family=' || REPLACE(C.font,' ','+') || '&display=swap">' || chr(10) ||
                '<style>' || chr(10) ||
                ':root {' || chr(10) || l_fluid_css || 
                    chr(9) || '--font-family: "' || C.font || '";' || chr(10) ||
                    chr(9) || '--max-width: ' || C.max_width || ';' || chr(10) ||
                    chr(9) || '--color-text: ' || C.color_text || ';' || chr(10) ||
                    chr(9) || '--color-background: ' || C.color_background || ';' || chr(10) ||
                    chr(9) || '--color-primary: ' || C.color_primary || ';' || chr(10) ||
                '}' || chr(10) || chr(10) ||
                    chr(10) || NVL(C.css,l_deploy_css) || chr(10) || 
                '</style>' || chr(10) || 
            '</head>' || chr(10);
        END LOOP;

        /*
        ** Get all website content into collection
        */
        SELECT parent_article_id, article_id, navigation_label, path_name, collection_type, page_title, page_description, contact_form, lastmod, word_count, body_html
          BULK COLLECT INTO l_labels
          FROM
          (
        SELECT wa.article_id parent_article_id, wa.article_id, wa.navigation_label, apex_string_util.get_slug(wa.navigation_label) path_name, wa.collection_type, wa.page_title, wa.page_description, wa.contact_form, wa.display_order, COALESCE(a.updated_date,a.created_date) lastmod, a.word_count, a.body_html
          FROM website_article wa, article a
         WHERE wa.website_id=pWebsiteId 
           AND wa.article_id=a.id
         UNION ALL
        SELECT wa.article_id, a.id, a.title, apex_string_util.get_slug(wa.navigation_label) || '/' || apex_string_util.get_slug(a.title),  NULL, NULL, NULL, NULL, wa.display_order, COALESCE(a.deployed_date, a.updated_date) lastmod, a.word_count, a.body_html
          FROM website_article wa, article a 
         WHERE wa.website_id=pWebsiteId 
           AND a.parent_id=wa.article_id 
          )
         ORDER BY display_order, lastmod DESC;
         

        /*
        ** 1. Remove any empty <h1> element.
        ** 2. Append to parent page links to blog entries 
        */
        FOR i IN 1..l_labels.COUNT LOOP
           l_labels(i).body_html:=REPLACE(l_labels(i).body_html ,'<h1>&nbsp;</h1>',null);
            IF (l_labels(i).collection_type='BLOG') THEN
                l_labels(i).body_html:=l_labels(i).body_html || pck_core.buildBlogList(pWebsiteId, l_labels(i).parent_article_id);
            END IF;
        END LOOP;

        /* Get URL of site to be deployed in order to build navigation links */
        pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/' || pSiteId, pMethod=>'GET', pData=>l_clob);
        SELECT ssl_url INTO l_ssl_url FROM JSON_TABLE(l_clob, '$' COLUMNS(ssl_url));

        /* Get SHA1 of all files to be deployed */
        l_json:= new JSON_OBJECT_T;
        l_files:= new JSON_OBJECT_T;

        FOR i IN 1..l_labels.COUNT
        LOOP
            IF (i=1) THEN
                logDeployment(pWebsiteId, pSiteId, 'Building ' || l_labels.COUNT || ' page' || CASE WHEN l_labels.COUNT>1 THEN 's' END);
            END IF;
            --dbms_session.sleep(1);

            l_page:=REPLACE(REPLACE(l_head,'#PAGE_TITLE#',l_labels(i).page_title),'#PAGE_DESCRIPTION#',l_labels(i).page_description) || 
                '<body>' || 
                CASE WHEN l_labels.COUNT>1 THEN '<nav aria-label="Main Navigation">' || buildNavLabels(l_labels(i).parent_article_id) || '</nav>' END || chr(10) || 
                l_labels(i).body_html || chr(10) || 
                CASE WHEN l_labels(i).contact_form='YES' THEN pck_core.buildContactForm(contactEmail()) END ||
                '<script type="module">
                    import {onCLS, onLCP, onINP} from ''https://unpkg.com/web-vitals?module'';
                    onCLS(addToVitalsQueue);
                    onLCP(addToVitalsQueue);
                    onINP(addToVitalsQueue);
                </script>
                <script type="module">
                    import tgwf from ''https://cdn.skypack.dev/@tgwf/co2'';
                    const emissions = new tgwf.co2()
                    const bytesSent = (2.6 * 1024 * 1024) // 2.6MB expressed in bytes
                    const greenHost = true // Is the data transferred from a green host?
                    let estimatedCO2 = emissions.perByte(bytesSent, greenHost).toFixed(3)
                    console.log("estimatedCO2",estimatedCO2) ;
                </script>
                <script>
                    gMetricWebsiteId = "' || pWebsiteId || '";' || chr(10) || 
                    'gMetricArticleId = "' || CASE WHEN l_labels(i).parent_article_id=l_labels(i).article_id THEN l_labels(i).parent_article_id ELSE l_labels(i).article_id END || '";' || chr(10) || 
                    'gMetricVisitUrl = "' || pRestUrl || 'page-visit";' || chr(10) ||
                    l_metric_js || 
                '</script>' || chr(10) || '
                </body>' || chr(10) || 
            '</html>';

            l_sha1:=LOWER(dbms_crypto.hash(src => l_page, typ => dbms_crypto.hash_sh1));

            IF (i=1) THEN
                l_file_name:='index.html';
            ELSE
                l_file_name:=l_labels(i).path_name || '.html';
            END IF;

            l_deploy_files(l_sha1).url:=l_ssl_url || CASE WHEN i>1 THEN '/' || l_labels(i).path_name END;
            l_deploy_files(l_sha1).lastmod:=l_labels(i).lastmod;
            l_deploy_files(l_sha1).file_name:=l_file_name;
            l_deploy_files(l_sha1).file_content:=l_page;
            l_files.put(l_file_name, l_sha1);
        END LOOP;

        /* Build sitemap.xml */

        l_sitemap:='<?xml version="1.0" encoding="UTF-8"?>' || chr(10) || 
                    '<urlset xmlns="http://www.sitemaps.org/schemas/sitemap/0.9">' || chr(10);

        l_sha1:=l_deploy_files.FIRST;
        WHILE (l_sha1 IS NOT NULL) LOOP
            IF (l_sha1=l_deploy_files.FIRST) THEN
                l_robots:='User-agent: *' || chr(10) || 'Allow: /' || chr(10) || chr(10) || 'Sitemap: ' || l_deploy_files(l_sha1).url || '/sitemap.xml';
            END IF;
            l_sitemap:=l_sitemap || 
                '    <url>' || chr(10) || 
                '        <loc>' || l_deploy_files(l_sha1).url || '</loc>' || chr(10) || 
                '        <lastmod>' || TO_CHAR(l_deploy_files(l_sha1).lastmod,'YYYY-MM-DD') || '</lastmod>' || chr(10) || 
                '    </url>' || chr(10);
            l_sha1 := l_deploy_files.NEXT(l_sha1);
        END LOOP;
        l_sitemap:=l_sitemap || '</urlset>';

        logDeployment(pWebsiteId, pSiteId, 'Building sitemap.xml');
        l_sha1:=LOWER(dbms_crypto.hash(src => l_sitemap, typ => dbms_crypto.hash_sh1));
        l_deploy_files(l_sha1).file_name:='sitemap.xml';
        l_deploy_files(l_sha1).file_content:=l_sitemap;
        l_files.put('sitemap.xml', l_sha1);

        logDeployment(pWebsiteId, pSiteId, 'Building robots.txt');
        l_sha1:=LOWER(dbms_crypto.hash(src => l_robots, typ => dbms_crypto.hash_sh1));
        l_deploy_files(l_sha1).file_name:='robots.txt';
        l_deploy_files(l_sha1).file_content:=l_robots;
        l_files.put('robots.txt', l_sha1);

        l_json.put('files',l_files);

        logDeployment(pWebsiteId, pSiteId, 'Sending digest of all files to Netlify');
        pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/' || pSiteId || '/deploys', pMethod=>'POST', pBody=>l_json.stringify, pData=>l_clob);
        UPDATE website SET netlify_deploy_id=(SELECT id FROM JSON_TABLE(l_clob, '$' COLUMNS (id))) WHERE id=pWebsiteId;
        COMMIT;

        /* upload to Netlify if required - i.e. file content has changed since last deployment  */
        n:=0;
        FOR C IN (SELECT id, sha1 FROM JSON_TABLE(l_clob, '$' COLUMNS (id, NESTED '$.required[*]' COLUMNS (sha1 PATH '$'))) WHERE sha1 IS NOT NULL) LOOP
            pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'deploys/' || C.id || '/files/' || l_deploy_files(C.sha1).file_name, pMethod=>'PUT', pBody=>l_deploy_files(C.sha1).file_content, pData=>l_clob);
            logDeployment(pWebsiteId, pSiteId, 'Successfully uploaded ' || l_deploy_files(C.sha1).file_name);
            n:=n+1;
        END LOOP;

        IF (n>0) THEN
            logDeployment(pWebsiteId, pSiteId, 'Uploaded files: ' || n || ' Unchanged files: ' || TO_CHAR(l_deploy_files.COUNT-n));
        ELSE
            logDeployment(pWebsiteId, pSiteId, 'All files unchanged since last deployment');
        END IF;

        /*
        ** Update deployed_date for article belnging to collection pages
        */
        UPDATE article SET deployed_date = current_timestamp
         WHERE parent_id IN (SELECT article_id FROM website_article WHERE website_id=pWebsiteId AND collection_type<>'N/A')
           AND deployed_date IS NULL;

        EXCEPTION
            WHEN OTHERS THEN
                logDeployment(pWebsiteId, pSiteId, SUBSTR(SQLERRM,1,100),'NOK');
                pck_core.log_error;
    END;   

    /*
     **  CANCEL active deployment - HAVE TO STOP JOB FIRST PERHAPS?
     */
    PROCEDURE cancelDeployment(pId IN VARCHAR2) IS 
        l_website_id website_deploy.website_id%type;
        l_site_id website_deploy.site_id%type;
        l_netlify_deploy_id website.netlify_deploy_id%type;
        l_clob CLOB;
        l_session_data pck_core.session_data_rt;
    BEGIN
        l_session_data:=pck_core.getSessionData();

        l_website_id:=SUBSTR(pId,1,INSTR(pId,',')-1);
        l_site_id:=SUBSTR(pId,INSTR(pId,',')+1);

        SELECT netlify_deploy_id INTO l_netlify_deploy_id FROM website WHERE id=l_website_id AND user_id=l_session_data.user_id;

        /* Get latest deployment status if started */
        IF (l_netlify_deploy_id IS NOT NULL) THEN
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'deploys/' || l_netlify_deploy_id || '/cancel', pMethod=>'POST', pData=>l_clob);
            pck_core.log(l_clob);
            apex_json.open_object; 
            apex_json.write('success', TRUE);
            apex_json.write('completed', TRUE);
            apex_json.write('content', '<li><span></span><span>DEPLOYMENT CANCELLED</span>');
            apex_json.close_object;
        END IF;
    END;

    /*
     **  Get deployment status from website_deploy logging table - called from Javascript every 3 seconds
     */
    PROCEDURE getDeploymentStatus(pId IN VARCHAR2) IS 
        l_website_id website_deploy.website_id%type;
        l_site_id website_deploy.site_id%type;
        l_content CLOB;
        l_clob CLOB;
        l_ssl_url VARCHAR2(100);
        l_deployment_complete BOOLEAN:=FALSE;
        l_netlify_status website_deploy.status%type;
        l_netlify_deploy_id website.netlify_deploy_id%type;
        l_state VARCHAR2(200);
        l_updated_at website.netlify_last_published%type;
        l_job_name VARCHAR2(30);
        l_session_data pck_core.session_data_rt;
        TYPE t_deploy_log IS RECORD(
            id website_deploy.id%type,
            log_time VARCHAR2(8),
            message website_deploy.message%type,
            status website_deploy.status%type);
        TYPE tt_deploy_log IS TABLE OF t_deploy_log;
        l_deploy_log tt_deploy_log;          
    BEGIN
        l_session_data:=pck_core.getSessionData();

        l_website_id:=SUBSTR(pId,1,INSTR(pId,',')-1);
        l_site_id:=SUBSTR(pId,INSTR(pId,',')+1);

        SELECT netlify_deploy_id INTO l_netlify_deploy_id FROM website WHERE id=l_website_id;

        /* Get latest deployment status if started */
        IF (l_netlify_deploy_id IS NOT NULL) THEN
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'sites/'|| l_site_id || '/deploys/' || l_netlify_deploy_id, pMethod=>'GET', pData=>l_clob);
            FOR C IN (SELECT state, ssl_url, TO_TIMESTAMP_TZ(updated_at, 'YYYY-MM-DD"T"HH24:MI:SS.FXFF3TZR') updated_at
                        FROM JSON_TABLE(l_clob, '$' COLUMNS (state, ssl_url, updated_at)) 
                       WHERE state='ready') 
            LOOP
                logDeployment(l_website_id, l_site_id, C.state, pLogTime=>C.updated_at);
                l_ssl_url:=C.ssl_url;
            END LOOP;
        END IF;

        SELECT id, TO_CHAR(log_time,'hh24:mi:ss') log_time, message, status
          BULK COLLECT INTO l_deploy_log
          FROM website_deploy 
         WHERE website_id=l_website_id
           AND site_id=l_site_id
           AND sent_ind='N'
         ORDER BY id;

        /* Now return list of all deployment status messaages to client */
        FOR i IN 1..l_deploy_log.COUNT
        LOOP
            IF (l_deploy_log(i).message='ready') THEN 
                l_content:=l_content ||'<li><span>' || l_deploy_log(i).log_time || '</span><span>Successful deployment</span></li>'; 
                l_content:=l_content ||'<li class="ready"><span></span><span><a target="_blank" href="' || l_ssl_url || '">' || l_ssl_url || '</a></span></li>';
                l_deployment_complete:=TRUE;
                EXIT;
            END IF;

            IF (l_deploy_log(i).status='OK') THEN 
                l_content:=l_content ||'<li><span>' || l_deploy_log(i).log_time || '</span><span>' || l_deploy_log(i).message || '</span></li>'; 
            ELSE
                l_content:=l_content ||'<li class="nok"><span>' || l_deploy_log(i).log_time || '</span><span>' || l_deploy_log(i).message || '</span></li>'; 
                l_deployment_complete:=TRUE;
            END IF;
        END LOOP;

        apex_json.open_object; 
        apex_json.write('success', TRUE);
        apex_json.write('completed', l_deployment_complete);
        apex_json.write('content', l_content);
        apex_json.close_object;

        FORALL i IN 1..l_deploy_log.COUNT
            UPDATE website_deploy SET sent_ind='Y' WHERE id=l_deploy_log(i).id;

        IF (l_deployment_complete) THEN
            l_job_name:=dbms_scheduler.generate_job_name('DELETE_');
            dbms_scheduler.create_job(
                job_name   => l_job_name,
                job_type   =>'STORED_PROCEDURE',
                job_action =>'pck_deploy.runDelete',
                number_of_arguments=>3,
                start_date=>systimestamp
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 1,
                argument_value => l_session_data.user_id
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 2,
                argument_value => l_website_id
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 3,
                argument_value => l_site_id
            );
            dbms_scheduler.enable(l_job_name);    
        END IF;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;   

    PROCEDURE runDelete(pUserId IN users.id%type, pWebsiteId IN website.id%type, pSiteId IN website.netlify_site_id%type) IS
        l_clob CLOB;
    BEGIN
        UPDATE website 
           SET netlify_last_published = CASE WHEN netlify_site_id=pSiteId THEN current_timestamp ELSE netlify_last_published END,
               netlify_last_published_custom = CASE WHEN netlify_site_id_custom=pSiteId THEN current_timestamp ELSE netlify_last_published_custom END
         WHERE id=pWebsiteId;

        pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/'|| pSiteId || '/deploys', pMethod=>'GET', pData=>l_clob);
        FOR C IN (
            SELECT id, published_at, MAX(published_at) OVER() last_published_at 
            FROM
                (
                SELECT id, TO_TIMESTAMP_TZ(published_at, 'YYYY-MM-DD"T"HH24:MI:SS.FXFF3TZR') published_at FROM JSON_TABLE(l_clob, '$[*]' COLUMNS (id, published_at))
                )
        )
        LOOP
            IF (C.published_at<C.last_published_at) THEN
                pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/'|| pSiteId || '/deploys/' || C.id, pMethod=>'DELETE', pData=>l_clob);
            END IF;
        END LOOP;
    END;

    /*
    ** Delete Github build directory - have to delete files individually before Github removes directory
    */    
    PROCEDURE deleteDirectory(pWebsiteId IN website.id%type, pUserId IN users.id%type) IS
        l_clob CLOB;
        l_domain_name website.domain_name%type;
        l_github CLOB;
        l_json JSON_OBJECT_T;
    BEGIN
        SELECT domain_name INTO l_domain_name FROM website WHERE id=pWebsiteId;
        pck_api.callGithubAPI(pUserId=>pUserId, pEndpoint=>'contents/'||l_domain_name, pMethod=>'GET', pData=>l_clob);
        FOR C IN (SELECT name, sha, message FROM  JSON_TABLE(l_clob FORMAT JSON, '$[*]' COLUMNS (name, sha, message))) LOOP
            IF (C.message IS NOT NULL) THEN 
                pck_core.log(C.message);
                RAISE_APPLICATION_ERROR(-20050,'Error in deleteDirectory - '|| C.message);
            ELSE
                l_json:=JSON_OBJECT_T.parse('{"message":"Commit by PLSQL"}');
                l_json.put('sha',C.sha);
                l_github:=l_json.to_clob; 
                pck_api.callGithubAPI(pUserId=>pUserId, pEndpoint=>'contents/' || l_domain_name || '/'||C.name, pMethod=>'DELETE', pBody=>l_github, pData=>l_clob);
            END IF;
        END LOOP;
        pck_api.resetGithubCurrentBuild(pUserId);
    END;

    /*
    ** Build and deploy infrastructure through Terraform by submitting Github action. 
    ** Do this once and each time we recycle the public key that is used to sign a website's contact email
    */
    PROCEDURE deployInfrastructure IS
        l_clob CLOB;
        l_json_clob CLOB;
        l_json JSON_OBJECT_T;
        l_action JSON_OBJECT_T;
        l_session_data pck_core.session_data_rt;
    BEGIN
        l_session_data:=pck_core.getSessionData();

        FOR C IN (SELECT terraform_token, replace(utl_raw.cast_to_varchar2(utl_encode.base64_encode(utl_raw.cast_to_raw(terraform_public_key))),chr(13)||chr(10),null) terraform_public_key
                    FROM users
                   WHERE id=l_session_data.user_id) 
        LOOP
            /* trigger the deployment using Githib action */          
            l_json:=new JSON_OBJECT_T;
            l_json.put('event_type', 'trigger_build_infra');

            l_action:=new JSON_OBJECT_T;
            l_action.put('from_email', 'funnyface@markrussellbrown.com');
            l_action.put('public_key', C.terraform_public_key);
            l_action.put('terraform_token', C.terraform_token);
            l_json.put('client_payload', l_action);

            --l_clob := apex_web_service.make_rest_request(p_url=>'https://api.github.com/repos/'|| C.deploy_repo || '/dispatches' ,p_http_method=>'POST',p_body=>l_json.stringify);

            /* nb; Github API 'dispatches' endpoint does not send a response */
            l_json_clob:=l_json.stringify;
            pck_api.callGithubAPI(pUserId=>l_session_data.user_id, pEndpoint=>'dispatches', pMethod=>'POST', pBody=>l_json_clob, pData=>l_clob);

            apex_json.open_object; 
            apex_json.write('success', TRUE);
            apex_json.close_object;
        END LOOP;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;       
end;
/