CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_DEPLOY" as 
    --
    FUNCTION buildColorScheme RETURN LONG;
    --
    FUNCTION buildContentDialog RETURN VARCHAR2;
    --
    FUNCTION buildDropdown(pWebsiteId IN website.id%type) RETURN VARCHAR2;
    --
    FUNCTION buildLoginForm RETURN VARCHAR2;
    --
    FUNCTION buildNav(pWebsiteId IN website.id%type, pArticleid IN website_article.article_id%type DEFAULT NULL) RETURN VARCHAR2;
    --
    PROCEDURE buildPageCollection(pCollectionType IN website_article.collection_type%type, pArticleid website_article.article_id%type, pHtml IN OUT NOCOPY CLOB);
    --
    FUNCTION canDeploy(pWebsiteid IN website.id%type) RETURN BOOLEAN;
    --
    PROCEDURE deleteDirectory(pWebsiteId IN website.id%type, pUserId IN users.id%type);
    --
    PROCEDURE deployInfrastructure(pSenderEmail IN VARCHAR2 DEFAULT 'contact.form');
    --
    PROCEDURE deployEditorSite(pWebsiteid IN website.id%type);
     --
    PROCEDURE deployLiveSite(pWebsiteid IN website.id%type);
    --
    PROCEDURE logDeployment(pWebsiteId IN website.id%type, pSiteId IN website_deploy.site_id%type, pMessage IN VARCHAR2, pStatus IN VARCHAR2 DEFAULT 'OK', pLogTime IN TIMESTAMP DEFAULT current_timestamp);
    --
    PROCEDURE runDelete(pUserId IN users.id%type, pWebsiteId IN website.id%type, pSiteId IN website.netlify_site_id%type);
    --
    PROCEDURE runDeployment(pWebsiteId IN website.id%type, pSiteId IN website.netlify_site_id%type, pRestUrl IN VARCHAR2);
    --
    PROCEDURE setTerraformApikey;
    --
    PROCEDURE updateCommonModules(pWebsiteid IN website.id%type DEFAULT null);
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_DEPLOY" as 

    PROCEDURE updateCommonModules(pWebsiteid IN website.id%type) IS
        l_domain_name website.domain_name%type;
        l_ref_css_path website_files.path%type:='/css/core-styles.min.css';
        l_ref_js_path website_files.path%type :='/javascript/deploy_start.min.js';
        l_ref_css_sha1 website_files.sha1%type;
        l_ref_js_sha1 website_files.sha1%type;
        l_files JSON_OBJECT_T;
        l_json JSON_OBJECT_T;
        l_clob CLOB;

        PROCEDURE check_deploy IS
            l_errors PLS_INTEGER:=0;
        BEGIN
            FOR C IN (
                SELECT id, sha1 
                  FROM JSON_TABLE(l_clob, '$' COLUMNS (id, NESTED '$.required[*]' COLUMNS (sha1 PATH '$'))) 
                 WHERE sha1 IS NOT NULL
            ) LOOP
                /* SIGNALS ERROR IN PROCESS BECAUSE REFERENCE SITE DEPLOYMENT SHOULD HAVE LOADED THESE SHA1 */
                l_errors:=l_errors+1;
                dbms_output.put_line('!!required sha!!:' || C.sha1);
            END LOOP;
            IF (l_errors>0) THEN
                RAISE_APPLICATION_ERROR(-20000,'updateCommonModules FAILED. ONE OR MORE SHA1 REQUIRED.');
            END IF;
        END;

        PROCEDURE deploy(pWebsiteid IN website.id%type, pDomainName IN website.domain_name%type, pSiteid IN website.netlify_site_id%type) IS
            TYPE tt_deploy IS RECORD (
                path website_files.path%type,
                sha1 website_files.sha1%type
            );
            TYPE t_deploy IS TABLE OF tt_deploy;
            l_deploy t_deploy;

            l_deploy_css PLS_INTEGER:=0;
            l_deploy_js PLS_INTEGER:=0;
            l_current_timestamp TIMESTAMP WITH TIME ZONE:=current_timestamp;
        BEGIN
            SELECT path, sha1
              BULK COLLECT INTO l_deploy
              FROM website_files
             WHERE website_id=pWebsiteid
               AND site_id=pSiteid;

            FOR i IN 1..l_deploy.COUNT LOOP
                IF (l_deploy(i).path=l_ref_css_path AND l_deploy(i).sha1<>l_ref_css_sha1) THEN
                    l_deploy_css:=1;
                    l_deploy(i).sha1:=l_ref_css_sha1;
                END IF;
                IF (l_deploy(i).path=l_ref_js_path AND l_deploy(i).sha1<>l_ref_js_sha1) THEN
                    l_deploy_js:=1;
                    l_deploy(i).sha1:=l_ref_js_sha1;
                END IF;
            END LOOP;

            IF (l_deploy_js+l_deploy_css=0) THEN
                dbms_output.put_line(pDomainName || ' - common modules all up to date');
                RETURN;
            END IF;

            dbms_output.put_line('START DEPLOY ' || pDomainName || ' Site:' || pSiteid);
            l_files:=new JSON_OBJECT_T;
            FOR i IN 1..l_deploy.COUNT LOOP
                l_files.put(l_deploy(i).path, l_deploy(i).sha1);
            END LOOP;
            l_json:=new JSON_OBJECT_T;
            l_json.put('files',l_files);
            pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'sites/' || pSiteid || '/deploys', pMethod=>'POST', pBody=>l_json.stringify, pData=>l_clob);
            check_deploy;

            IF (l_deploy_css=1) THEN
                UPDATE website_files SET sha1=l_ref_css_sha1, deployed_date=l_current_timestamp WHERE website_id=pWebsiteid AND site_id=pSiteid AND path=l_ref_css_path;
            END IF;
            IF (l_deploy_js=1) THEN
                UPDATE website_files SET sha1=l_ref_js_sha1, deployed_date=l_current_timestamp WHERE website_id=pWebsiteid AND site_id=pSiteid AND path=l_ref_js_path;
            END IF;

            IF (pWebsiteid IS NULL) THEN
                COMMIT;
            END IF;
        END;
    BEGIN
        /* 
        ** Process requires a reference website
        */
        SELECT MAX(DECODE(wf.path,l_ref_css_path,wf.sha1)), MAX(DECODE(wf.path,l_ref_js_path,wf.sha1))
          INTO l_ref_css_sha1, l_ref_js_sha1
          FROM website_files wf, website w
         WHERE w.domain_name='restartnet.com' 
           AND wf.path IN (l_ref_css_path,l_ref_js_path) 
           AND wf.website_id=w.id
           AND wf.site_id=w.netlify_site_id;

        FOR C IN (
            SELECT w.id, w.domain_name, w.netlify_site_id, w.netlify_site_id_custom
              FROM website w
             WHERE w.id=NVL(pWebsiteid,w.id)
        ) LOOP
            deploy(C.id, C.domain_name, C.netlify_site_id);
            IF (C.netlify_site_id_custom IS NOT NULL) THEN
                deploy(C.id, C.domain_name, C.netlify_site_id_custom);
            END IF;
        END LOOP;
    END;

    PROCEDURE deployEditorSite(pWebsiteid IN website.id%type) IS
    BEGIN        
        UPDATE website SET netlify_deploy_id=NULL WHERE id=pWebsiteId;

        FOR C IN (
            SELECT netlify_site_id
              FROM website 
             WHERE id=pWebsiteid
        ) LOOP
            DELETE website_deploy WHERE website_id=pWebsiteId AND site_id=C.netlify_site_id;
            pck_deploy.runDeployment(pWebsiteid, C.netlify_site_id, pck_core.getRestURL);
            pck_deploy.runDelete(null,pWebsiteid,C.netlify_site_id);
        END LOOP;
    END;

    PROCEDURE deployLiveSite(pWebsiteid IN website.id%type) IS
        l_files JSON_OBJECT_T;
        l_json JSON_OBJECT_T;
        l_clob CLOB;
    BEGIN
        FOR C IN (
            SELECT netlify_site_id, netlify_site_id_custom
              FROM website 
             WHERE id=pWebsiteid
        ) LOOP
            l_files:=new JSON_OBJECT_T;
            FOR C1 IN (
                SELECT path, sha1
                  FROM website_files
                 WHERE website_id=pWebsiteid
                   AND site_id=C.netlify_site_id
            ) LOOP
                l_files.put(C1.path, C1.sha1);
            END LOOP;
            l_json:=new JSON_OBJECT_T;
            l_json.put('files',l_files);
            pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'sites/' || C.netlify_site_id_custom || '/deploys', pMethod=>'POST', pBody=>l_json.stringify, pData=>l_clob);
            
            /* Cancel deploy if any file required */
            FOR C1 IN (
                SELECT id, sha1 
                  FROM JSON_TABLE(l_clob, '$' COLUMNS (id, NESTED '$.required[*]' COLUMNS (sha1 PATH '$'))) 
                 WHERE sha1 IS NOT NULL
            ) LOOP
                pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'deploys/' || C1.id || '/cancel', pMethod=>'POST', pData=>l_clob);
                RAISE_APPLICATION_ERROR(-20000,'pck_deploy.deployLiveSite FAILED. ONE OR MORE SHA1 REQUIRED.');
            END LOOP;

            UPDATE website
               SET netlify_last_published_custom=current_timestamp
             WHERE id=pWebsiteId;

            pck_deploy.runDelete(null,pWebsiteid,C.netlify_site_id_custom);
        END LOOP;
    END;

    FUNCTION getSha256(pContent IN CLOB) RETURN VARCHAR2 IS
        l_sha256 RAW(92);
    BEGIN
        l_sha256:=dbms_crypto.hash(src=>pContent, typ=>dbms_crypto.hash_sh256);
        RETURN ('sha256-' || utl_raw.cast_to_varchar2(utl_encode.base64_encode(l_sha256)));
    END;

    /*
    **  Insert row in website_deploy logging table
    */      
    PROCEDURE logDeployment(pWebsiteId IN website.id%type, pSiteId IN website_deploy.site_id%type, pMessage IN VARCHAR2, pStatus IN VARCHAR2 DEFAULT 'OK', pLogTime IN TIMESTAMP DEFAULT current_timestamp) IS 
    BEGIN
        INSERT INTO website_deploy(id, website_id, site_id, message, status, log_time) VALUES (seq_log.nextval, pWebsiteId, pSiteId, pMessage, pStatus, pLogTime);
        COMMIT;
    END; 

    /*
    **  Determine whether editor (TEST) website can be deployed based on different source data last update dates
    */  
    FUNCTION canDeploy(pWebsiteid IN website.id%type) RETURN BOOLEAN IS
        l_netlify_last_published TIMESTAMP WITH LOCAL TIME ZONE;
        l_last_updated_date TIMESTAMP WITH LOCAL TIME ZONE;
    BEGIN
        WITH data AS
        (
            SELECT w.css_updated_date, w.nav_updated_date, w.logo_updated_date, w.favicon_updated_date, wa.header_html_updated, wa.footer_html_updated, a.updated_date as article_updated_date, wf.updated_date as font_updated_date, w.netlify_last_published, CAST(a.last_updated_on AS TIMESTAMP WITH TIME ZONE) css2_updated_date
            FROM website w, website_article wa, article a, website_font wf, apex_application_static_files a
            WHERE w.id=pWebsiteid
            AND wa.website_id=w.id
            AND wa.article_id=NVL(a.parent_id,a.id)
            AND wf.website_id=w.id
            AND a.application_id=101
            AND a.file_name='deploy.min.css'
        )
        SELECT MAX(netlify_last_published),MAX(updated_date)
        INTO l_netlify_last_published, l_last_updated_date
        FROM data
        UNPIVOT  (updated_date FOR COL IN (css_updated_date, nav_updated_date, logo_updated_date, favicon_updated_date, header_html_updated, footer_html_updated, article_updated_date, font_updated_date, css2_updated_date));

        RETURN (l_last_updated_date>l_netlify_last_published);
    END;

    /*
     **  Build Login Form
     */
    FUNCTION buildLoginForm RETURN VARCHAR2 IS
    BEGIN
        RETURN(
            '<dialog class="login-email" style="--max-width:40rem" closedBy="any">' ||
                '<form>' ||
                    '<input type="hidden" name="url" value="">' ||
                    '<input type="hidden" name="request_type" value="">' ||
                    '<header class="flex-items space-between">' ||
                        '<h2>Log in</h2>' ||
                        '<button type="button" class="button close no-focus" data-button-variant="round-icon">' ||
                            '<svg viewBox="0 0 18 18" class="icon" aria-hidden="true" focusable="false">' ||
                                '<line x1="18" y1="0" x2="0" y2="18"/><line x1="0" y1="0" x2="18" y2="18"/>' ||
                            '</svg>' ||
                        '</button>' ||
                    '</header>' ||
                    '<article class="flow">' ||
                        '<div>' ||
                            '<label for="emailInput">Email</label>' ||
                            '<input type="email" id="emailInput" name="email" required maxlength="50" autocapitalize="none" autocorrect="off" autocomplete="on">' ||
                            '<output for="email" class="sendmail-result"></output>' ||
                        '</div>' ||
                        '<div class="visually-hidden">' ||
                            '<label for="passcodeInput">Enter Passcode</label>' ||
                            '<input type="text" id="passcodeInput" name="passcode" pattern="\d{6}" inputmode="numeric" maxlength="6" autocomplete="one-time-code">' ||
                            '<output for="passcode" class="passcode-result"></output>' ||
                        '</div>' ||
                        '<div class="loader visually-hidden"></div>' ||
                    '</article>' ||
                    '<footer class="flex-items space-apart no-wrap">' ||
                        '<button class="button sendmail-magic" type="button">Send Link</button>' ||
                        '<div><hr><span>OR</span><hr></div>' ||
                        '<button class="button sendmail-passcode" type="button">Send Code</button>' ||
                        '<button class="button validate-passcode visually-hidden" type="button">Submit Passcode</button>' ||
                    '</footer>' ||
                '</form>' ||
             '</dialog>');
    END;

    /*
     **  Build Content Dialog - include pagination button
     */
    FUNCTION buildContentDialog RETURN VARCHAR2 IS
    BEGIN
        RETURN(
            '<dialog class="output" closedBy="any">' ||
                '<form method="dialog">' ||
                    '<header class="flex-items space-between no-wrap">' ||
                        '<div></div>' ||
                        '<button type="button" class="button close no-focus" data-button-variant="round-icon">' ||
                            '<svg viewBox="0 0 18 18" class="icon" aria-hidden="true" focusable="false">' ||
                                '<line x1="18" y1="0" x2="0" y2="18"/><line x1="0" y1="0" x2="18" y2="18"/>' ||
                            '</svg>' ||
                        '</button>' ||
                    '</header>' ||
                    '<article class="flow"></article>' ||
                    '<footer class="flex-items space-between">' ||
                    '</footer>' ||
                '</form>' ||
             '</dialog>');
    END;

    /*
    **  Build Colour Scheme Switcher component
    */
    FUNCTION buildColorScheme RETURN LONG IS
        l_html LONG;
        l_index VARCHAR2(6);
        TYPE t_schemes IS TABLE OF LONG INDEX BY VARCHAR2(6);
        l_schemes t_schemes:=t_schemes(
            'system'=>
                '<rect width="256" height="256" fill="none"></rect>' ||
                '<rect x="24" y="40" width="208" height="160" rx="24"></rect>' ||
                '<path d="M160,216H96a8,8,0,0,0,0,16h64a8,8,0,0,0,0-16Z"></path>',
            'light'=>
                '<rect width="256" height="256" fill="none"></rect>' ||
                '<path d="M120,40V16a8,8,0,0,1,16,0V40a8,8,0,0,1-16,0Zm8,24a64,64,0,1,0,64,64A64.07,64.07,0,0,0,128,64ZM58.34,69.66A8,8,0,0,0,69.66,58.34l-16-16A8,8,0,0,0,42.34,53.66Zm0,116.68-16,16a8,8,0,0,0,11.32,11.32l16-16a8,8,0,0,0-11.32-11.32ZM192,72a8,8,0,0,0,5.66-2.34l16-16a8,8,0,0,0-11.32-11.32l-16,16A8,8,0,0,0,192,72Zm5.66,114.34a8,8,0,0,0-11.32,11.32l16,16a8,8,0,0,0,11.32-11.32ZM48,128a8,8,0,0,0-8-8H16a8,8,0,0,0,0,16H40A8,8,0,0,0,48,128Zm80,80a8,8,0,0,0-8,8v24a8,8,0,0,0,16,0V216A8,8,0,0,0,128,208Zm112-88H216a8,8,0,0,0,0,16h24a8,8,0,0,0,0-16Z"></path>',
            'dark'=>
                '<rect width="256" height="256" fill="none"></rect>' ||
                '<path d="M240,96a8,8,0,0,1-8,8H216v16a8,8,0,0,1-16,0V104H184a8,8,0,0,1,0-16h16V72a8,8,0,0,1,16,0V88h16A8,8,0,0,1,240,96ZM144,56h8v8a8,8,0,0,0,16,0V56h8a8,8,0,0,0,0-16h-8V32a8,8,0,0,0-16,0v8h-8a8,8,0,0,0,0,16Zm65.14,94.33A88.07,88.07,0,0,1,105.67,46.86a8,8,0,0,0-10.6-9.06A96,96,0,1,0,218.2,160.93a8,8,0,0,0-9.06-10.6Z"></path>'
        );
    BEGIN
        l_html:=
        '<color-scheme class="theme-machine">' ||
            '<fieldset class="control-appearance">' ||
            '<legend class="visually-hidden">Select appearance</legend>';
        l_index:=l_schemes.LAST;
        WHILE l_index IS NOT NULL LOOP
            l_html:=l_html ||
            '<input id="appearance-' || l_index || '" value="' || CASE WHEN l_index<>'system' THEN l_index END || '" type="radio" name="appearance" class="visually-hidden">' ||
            '<label for="appearance-' || l_index || '">' ||
                '<svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 256 256" width="24" height="24" fill="currentcolor" aria-hidden="true" class="icon">';
                    l_html:=l_html || l_schemes(l_index) ||
                '</svg>' ||
            '</label>';
            l_index:=l_schemes.PRIOR(l_index);
        END LOOP;
        l_html:=l_html ||
            '<div class="highlighter"></div>' ||
            '</fieldset>' ||
        '</<color-scheme>';
        RETURN(l_html);
    END;

    /*
    **  Build Dropdown Component with "Log In" and "Ecology" buttons
    */
    FUNCTION buildDropdown(pWebsiteId IN website.id%type) RETURN VARCHAR2 IS
        l_html LONG;
        l_uri_template ords_endpoints.uri_template%type;
    BEGIN
        l_html:=
        '<button type="button" popovertarget="menulist" id="menulist-btn" aria-label="Actions menu">' ||
            'Menu' ||
            '<svg height="1rem" viewBox="0 0 32 32" preserveAspectRatio="none" aria-hidden="true" focusable="false">' ||
              '<rect class="top" x="0" y="0" width="32" height="6" fill="currentColor" />' ||
              '<rect class="middle" x="0" y="13" width="32" height="6" fill="currentColor" />' ||
              '<rect class="bottom" x="0" y="26" width="32" height="6" fill="currentColor" />' ||
            '</svg>' ||
        '</button>' ||
        '<div id="menulist" role="menu" aria-labelledby="menulist-btn" popover>';
        
        FOR C IN (
            SELECT o.label, o.uri_template, o.method
              FROM ords_endpoints o
             WHERE INSTR(o.uri_template,'authenticate')>0
                OR INSTR(o.uri_template,'ecology')>0
        ) LOOP
            l_html:=l_html ||
            pck_sec.addMenuButton(C.uri_template, C.method, C.label);
        END LOOP;
        l_html:=l_html ||
        '</div>';

        RETURN(l_html);
    END;

    /*
    ** BUILD NAVIGATION ELEMENT COMMON TO ALL PAGES
    */
    FUNCTION buildNav(pWebsiteId IN website.id%type, pArticleid IN website_article.article_id%type DEFAULT NULL) RETURN VARCHAR2 IS
        l_nav VARCHAR2(4000);
        l_nav_length PLS_INTEGER:=0;
        l_article_id article.id%type;
        n PLS_INTEGER:=0;
    BEGIN
        l_nav:=
        '<nav aria-label="Main">' ||
            '<ul>';
        FOR C IN (
            SELECT navigation_label, collection_type, article_id, ROW_NUMBER() OVER (ORDER BY display_order) rn
              FROM website_article 
             WHERE website_id=pWebsiteId
             ORDER BY display_order
        ) LOOP
            n:=n+1;
            l_nav:=l_nav ||
            '<li>' ||
                '<a href="/' || CASE WHEN C.rn>1 THEN apex_string_util.get_slug(C.navigation_label) END || '"' ||
                ' data-id="' || C.article_id || '" data-collection="' || C.collection_type || '"' ||
                CASE WHEN pArticleid=C.article_id THEN ' aria-current="page"' END || '>' ||
                C.navigation_label || 
                '</a>' ||
            '</li>';
        END LOOP;
        l_nav:=l_nav || 
            '</ul>' ||
        '</nav>';

        IF (n>1) THEN
            RETURN (l_nav);
        ELSE
            RETURN (null);
        END IF;
    END;

    /*
    ** GET THE header_html FOR THE FIRST PAGE IN WEBSITE FOR USE ON THOSE PAGES NOT HAVING THEIR OWN HEADER
    */
    FUNCTION buildCommonHeader(pWebsiteId IN website.id%type) RETURN website_article.header_html%type IS
        l_header_html website_article.header_html%type;
    BEGIN
        SELECT header_html 
          INTO l_header_html 
          FROM website_article 
         WHERE website_id=pWebsiteId
           AND display_order=(SELECT MIN(display_order) FROM website_article WHERE website_id=pWebsiteId);

        RETURN (l_header_html);
    END;

    /*
    ** GET THE footer_html FOR THE FIRST PAGE IN WEBSITE FOR USE ON THOSE PAGES NOT HAVING THEIR OWN FOOTER
    */
    FUNCTION buildCommonFooter(pWebsiteId IN website.id%type) RETURN website_article.footer_html%type IS
        l_footer_html website_article.footer_html%type;
    BEGIN
        SELECT footer_html 
          INTO l_footer_html 
          FROM website_article 
         WHERE website_id=pWebsiteId
           AND display_order=(SELECT MIN(display_order) FROM website_article WHERE website_id=pWebsiteId);

        RETURN (l_footer_html);
    END;

    /*
    ** GET WEBSITE FAVICON. DEFAULTS TO FIRST 2 LETTERS OF DOMAIN
    ** RETURN FAVICON AS ESCAPED SVG CODE IN XML FORMAT
    */
    FUNCTION buildFavicon(pWebsiteId IN website.id%type) RETURN website.favicon%type IS
        l_favicon_template website.favicon%type:=
        '<svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 100 100"><text y="1em" fill="darkgreen" font-family="system-ui" font-size="90">#FAVICON#</text></svg>';
        l_favicon website.favicon%type;
    BEGIN
        FOR C IN (
            SELECT NVL(favicon, UPPER(SUBSTR(domain_name,1,2))) favicon FROM website WHERE id=pWebsiteid
        ) LOOP
            IF (INSTR(C.favicon,'svg')=0) THEN
                l_favicon:=REPLACE(l_favicon_template,'#FAVICON#', C.favicon);
            ELSE
                l_favicon:=C.favicon;
            END IF;
        END LOOP;
        
        l_favicon:='data:image/svg+xml,' || REPLACE(REPLACE(l_favicon,'"','%22'),'#','%23');

        RETURN (l_favicon);
    END;

    /*
    ** SET "loading="lazy" ON ALL <img> ELEMENTS EXCEPT THE FIRST WHICH WE ASSUME WILL BE IN VIEWPORT
    */
    PROCEDURE setImgSrcset(pArticleid IN article.id%type, pMaxWidth IN website.max_width%type, pBodyHtml IN OUT NOCOPY article.body_html%type) IS
        l_img_width_regex varchar2(100):='class=".*?image_resized".+?width:(.+?)%.+?src="(.+?)"';
        l_img_regex varchar2(50):='<img.+?src="(.+?)"';
        l_img_width varchar2(10);
        l_img_width_pct NUMBER;
        l_max_width_px NUMBER;
        l_srcset VARCHAR2(4000);
        l_sizes VARCHAR2(100);
        l_loading VARCHAR2(16);
        l_fetchpriority VARCHAR2(4);
        l_img_src_tmp VARCHAR2(200);
        n pls_integer;
        n1 pls_integer;
        n2 pls_integer;

        TYPE tt_img_src IS RECORD (
            img_src VARCHAR2(200),
            public_id asset.public_id%type,
            article_id article.id%type,
            asset_id asset.id%type
        );
        TYPE t_img_src IS TABLE OF tt_img_src;
        l_img_src t_img_src:=t_img_src();

    BEGIN
    
        n:=NVL(regexp_count(pBodyHtml,l_img_regex),0);

        /* BUILD COLLECTION OF CLOUDINARY IMAGES FOUND IN HTML */
        FOR i IN 1..n LOOP
            l_img_src_tmp:=regexp_substr(pBodyHtml,l_img_regex,1,i,null,1);
            IF (l_img_src_tmp LIKE 'https://res.cloudinary.com%') THEN
                l_img_src.EXTEND;
                l_img_src(l_img_src.COUNT).img_src:=l_img_src_tmp;
                l_img_src(l_img_src.COUNT).public_id:=SUBSTR(l_img_src_tmp,INSTR(l_img_src_tmp,'/',-1)+1);
            END IF;
        END LOOP;

        IF (n>0) THEN
            /* CONVERT CONTAINER WIDTH TO PX */
            n1:=INSTR(pMaxWidth,'rem');
            IF (n1>0) THEN
                l_max_width_px:=SUBSTR(pMaxWidth,1,n1-1)*16;
            ELSE
                l_max_width_px:=pMaxWidth;
            END IF;
            /* remove padding 2rem */
            l_max_width_px:=l_max_width_px-32;
        END IF;
        

        FOR i IN 1..l_img_src.COUNT LOOP
            l_srcset:=null;
            FOR C IN (
                SELECT id, article_id, cld_cloud_name, breakpoints, height, width FROM asset WHERE public_id=l_img_src(i).public_id
            ) LOOP
                l_img_src(i).article_id:=C.article_id;
                l_img_src(i).asset_id:=C.id;
                FOR C1 IN (
                        SELECT breakpoint FROM (SELECT TO_NUMBER(column_value) AS breakpoint FROM TABLE(apex_string.split(C.breakpoints,','))) ORDER BY 1 DESC
                    ) LOOP
                        l_srcset:=l_srcset || 'https://res.cloudinary.com/' || C.cld_cloud_name || '/w_' || C1.breakpoint || '/' || l_img_src(i).public_id || ' ' || C1.breakpoint || 'w,';
                END LOOP;
                l_srcset:=RTRIM(l_srcset,',');
            END LOOP;

            IF (i=1) THEN
                l_loading:='eager';
                l_fetchpriority:='high';
                /* IF FIRST IMAGE HAS WIDTH PERCENTAGE */
                IF (regexp_substr(pBodyHtml,l_img_width_regex,1,1,null,2)=l_img_src(i).img_src) THEN
                    l_img_width:=regexp_substr(pBodyHtml,l_img_width_regex,1,1,null,1);
                    l_img_width_pct:=l_img_width/100;
                    l_sizes:='(width >= ' || l_max_width_px || ') ' || TO_CHAR(ROUND(l_img_width_pct * l_max_width_px)) || 'px,' || l_img_width || 'vw';
                ELSE
                    /* OTHERWISE USE INTRINSIC SIZE FROM IMG URL */
                    -- pck_core.log(l_img_src(i).img_src);
                    n1:=INSTR(l_img_src(i).img_src,'/',1,4);
                    n2:=INSTR(l_img_src(i).img_src,'/',1,5);
                    l_img_width:=SUBSTR(l_img_src(i).img_src,n1+3,n2-n1-3);
                    l_img_width_pct:=ROUND((l_img_width/l_max_width_px)*100);
                    l_sizes:='(width >= ' || l_max_width_px || ') ' || l_img_width || 'px,' || l_img_width_pct || 'vw';
                END IF;
            ELSE
                l_loading:='lazy';
                l_fetchpriority:='auto';
                l_sizes:='auto';
            END IF;

            pBodyHtml:=regexp_replace(pBodyHtml,'src="' || l_img_src(i).img_src || '"',
                'src="' || l_img_src(i).img_src || '"' || ' srcset="' || l_srcset || '"' || ' loading="' || l_loading || '"' || ' sizes="' || l_sizes || '"' || ' fetchpriority="' || l_fetchpriority || '"',
                i,0);
        END LOOP;
    END;


    /*
    ** BUILD PAGE COLLECTION. ANY IMAGES ARE DECORATIVE, HENCE EMPTY ALT TEXT 
    */
    PROCEDURE buildPageCollection(pCollectionType IN website_article.collection_type%type, pArticleid website_article.article_id%type, pHtml IN OUT NOCOPY CLOB) IS
        n PLS_INTEGER:=0;
    BEGIN
        pHtml:='<ul class="grid" role="list">';
        FOR C IN (
            WITH covers AS
               (
                SELECT article_id, cld_cloud_name, public_id, width, height
                  FROM
                    (
                    SELECT id, MIN(id) OVER (PARTITION BY article_id) min_id, article_id, cld_cloud_name, public_id, width, height
                      FROM asset
                     WHERE article_id IN (SELECT id FROM article WHERE parent_id=pArticleid)
                    )
                 WHERE id=min_id
               )
            SELECT apex_string_util.get_slug(wa.navigation_label) path, art.title, art.excerpt, NVL(art.publication_date, art.updated_date) publication_date, art.word_count, ass.cld_cloud_name, ass.public_id, ass.width, ass.height
              FROM website_article wa, article art, covers ass
             WHERE wa.article_id=pArticleid
               AND art.parent_id=wa.article_id
               AND art.id=ass.article_id(+)
             ORDER BY publication_date DESC
        ) 
        LOOP
            n:=n+1;
            pHtml:=pHtml || 
            '<li class="card">';
            IF (C.cld_cloud_name IS NOT NULL) THEN
                pHtml:=pHtml || 
                '<img loading="' || CASE WHEN n=1 THEN 'eager' ELSE 'lazy' END || '" src="https://res.cloudinary.com/' || C.cld_cloud_name || '/w_400/' || C.public_id || '.avif" width="'|| C.width || '" height="' || C.height || '" alt="">';
            END IF;
            pHtml:=pHtml || 
                '<div class="text">' ||
                    '<h2>' ||
                        '<a href="/' || C.path || '/' || apex_string_util.get_slug(C.title) || '">' || C.title || '</a>' ||
                    '</h2>' ||
                    '<p>' || C.excerpt || '</p>' ||
                '</div>' ||
                '<footer>' ||
                    '<span>' || TO_CHAR(C.publication_date,'fmdd Month, yyyy') || '</span>' ||
                    '<span>' || C.word_count || ' words</span>' ||
                '</footer>' ||
            '</li>';
        END LOOP;
        pHtml:=pHtml || '</ul>';
    END;

    /*
    **  <nav>
    **  <header>
    **  <main>
    **  <footer>
    */
    PROCEDURE buildPageBody(
        pWebsiteId IN website.id%type, 
        pArticleid IN article.id%type,
        pNav IN VARCHAR2, 
        pColorScheme IN VARCHAR2,
        pDropdown in VARCHAR2, 
        pIntegrity IN VARCHAR2, 
        pCollectionType IN website_article.collection_type%type, 
        pCommonHeader IN website_article.header_html%type, 
        pCommonFooter IN website_article.footer_html%type,
        pBody IN OUT NOCOPY CLOB
    ) IS
        l_title article_chars.first_heading%type;
        l_nav VARCHAR2(4000);
        l_header website_article.header_html%type;
        l_main article.body_html%type;
        l_footer website_article.footer_html%type;
        l_collection CLOB;
        l_article_links LONG;
        l_logo LONG;
        l_viewbox VARCHAR2(50);
        l_width PLS_INTEGER;
        l_height PLS_INTEGER;
        l_clob CLOB;

        n PLS_INTEGER;
        n1 PLS_INTEGER;
    BEGIN
        pBody:=null;
        FOR C IN (
            SELECT NVL(wa.header_html,pCommonHeader) header_html, a.body_html, NVL(wa.footer_html,pCommonFooter) footer_html, a.excerpt, a.parent_id, w.max_width, NVL2(w.logo,'1',null) logo, NVL2(w.favicon,'1',null) favicon
              FROM website_article wa, article a, website w
             WHERE w.id=pWebsiteId
               AND wa.website_id=w.id
               AND wa.article_id=NVL(a.parent_id,pArticleId)
               AND a.id=pArticleId
        ) LOOP
            IF (pNav IS NOT NULL) THEN
                IF (C.parent_id IS NULL) THEN
                    n:=INSTR(pNav,pArticleid);
                    n1:=INSTR(pNav,'>',n);
                    l_nav:=SUBSTR(pNav,1,n1-1) || ' aria-current="page"' || SUBSTR(pNav,n1);
                ELSE
                    l_nav:=pNav;
                END IF;
            END IF;

            l_header:='<header id="header" class="ck-content"' || CASE WHEN C.header_html IS NULL THEN ' nocontent' END || '>' || C.header_html || '</header>';
            l_footer:='<footer id="footer" class="ck-content"' || CASE WHEN C.footer_html IS NULL THEN ' nocontent' END || '>' || C.footer_html || '</footer>';
            
            l_clob:=C.header_html || C.body_html || C.footer_html || '<p>' || C.excerpt || '</p>';
            pck_fonts.getCharsUsed(pArticleId, l_clob);

            IF (C.parent_id IS NOT NULL) THEN
                n:=0;
                FOR C1 IN (
                    SELECT a.id, a.title, a.excerpt, apex_string_util.get_slug(wa.navigation_label) navigation_label_slug, apex_string_util.get_slug(a.title) title_slug
                      FROM article a, article_link al, website_article wa
                     WHERE al.article_id_1=pArticleid
                       AND a.id=al.article_id_2
                       AND wa.article_id=C.parent_id
                       AND wa.website_id=pWebsiteId
                ) LOOP
                    n:=n+1;
                    IF (n=1) THEN
                        l_article_links:=
                        '<section>' ||
                            '<h2>Related Posts</h2>' ||
                            '<p>Here are some other posts that may be of interest</p>' ||
                            '<ul>';
                    END IF;
                    l_article_links:=l_article_links ||
                            '<li>' ||
                                '<h3><a href="/' || C1.navigation_label_slug || '/' || C1.title_slug || '">' || C1.title || '</a></h3>' ||
                                '<p>' || C1.excerpt || '</p>' ||
                            '</li>';
                END LOOP;
                IF (n>0) THEN
                    l_article_links:=l_article_links || 
                        '</ul>' ||
                    '</section>';
                END IF;
            END IF;
            
            l_main:='<main id="main" class="ck-content flow" tabindex="-1"' || CASE WHEN C.body_html IS NULL THEN ' nocontent' END || '>' || C.body_html || '</main>';
            
            setImgSrcset(pArticleId, C.max_width, l_main);

            /*
            ** OPTIMAL TITLE LENGTH IS 60 CHARACTERS, I.E. 580PX
            */
            FOR C1 IN (
                SELECT first_heading, second_heading FROM article_chars WHERE article_id=pArticleid AND context='headings'
            ) LOOP
                l_title:=C1.second_heading;
                IF (l_title IS NOT NULL) THEN
                    l_title:=l_title || '|' || C1.first_heading;
                END IF;
            END LOOP;

            IF (pCollectionType='BLOG') THEN
                buildPageCollection(pCollectionType, pArticleid, l_collection);
            END IF;

            /*
            ** CONFIGURE LOGO. ONLY SUPPORTED LOGO IS SVG WITH viewBox ATTRIBUTE OR EMOJI
            */
            IF (C.logo IS NULL) THEN
                l_logo:='<span class="logo"></span>';
            ELSE
                -- l_viewbox:=regexp_substr(pLogo,'viewBox="(.+?)"',1,1,null,1);
                -- l_width:=regexp_substr(l_viewbox,'[^ ]+',1) + regexp_substr(l_viewbox,'[^ ]+',1,3);
                -- l_height:=regexp_substr(l_viewbox,'[^ ]+',1,2) + regexp_substr(l_viewbox,'[^ ]+',1,4);
                l_logo:='<span class="logo"><img src="/images/logo.svg" alt="Image of website logo" fetchpriority="high"></span>';
            END IF;

            pBody:=
            '<!DOCTYPE HTML>' ||
            '<html lang="en">' ||
                '<head>' ||
                    '<meta charset="UTF-8">' ||
                    '<meta name="viewport" content="width=device-width, initial-scale=1">' ||
                    '<title>' || l_title || '</title>' ||
                    '<meta name="author" content="restartnet.com">' ||
                    '<meta name="mobile-web-app-capable" content="yes">' ||
                    '<link rel="stylesheet" href="/css/website-styles.min.css">' ||
                    '<link rel="stylesheet" href="/css/core-styles.min.css">' ||
                    '<link rel="stylesheet" href="/css/ckeditor.min.css">' ||
                    '<link rel="icon" href="/images/favicon.svg">' ||
                    '<script src="/javascript/deploy_start.min.js" integrity="' || pIntegrity || '" defer></script>' ||
                '</head>' ||
                '<body data-websiteid="' || pWebsiteId || '" data-articleid="' || pArticleid || '">' || 
                    '<a href="#main" class="skip-link">Skip to main content</a>' ||
                    '<div class="topnav flex-items space-between">' ||
                        l_logo || l_nav || pColorScheme || pDropdown || 
                    '</div>' ||    
                    l_header || l_main || l_collection || l_article_links || l_footer || buildLoginForm || buildContentDialog ||
                '</body>' ||
                '<form action="https://codepen.io/pen/define" method="POST" target="_blank"><input type="hidden" name="data" value=""></form>' ||
                '</html>';
        END LOOP;
    END;

    /*
    **  DEPLOY BY UPLOADING HTML FILE CONTENT DIRECTLY TO NETLIFY
    */
    PROCEDURE runDeployment(pWebsiteId IN website.id%type, pSiteId IN website.netlify_site_id%type, pRestUrl IN VARCHAR2) IS
        
        l_clob CLOB;
        l_files JSON_OBJECT_T;
        
        TYPE tt_files IS RECORD (
            file_name VARCHAR2(200),
            file_content CLOB,
            file_content_b BLOB,
            md5 VARCHAR2(32),
            content_length NUMBER
        );
        TYPE t_files IS TABLE OF tt_files INDEX BY VARCHAR2(40);
        l_deploy_files t_files;

        l_script_sha256 VARCHAR2(500);
        l_font_download BLOB;

        l_sha1 VARCHAR2(40);
        n PLS_INTEGER;

        l_dogface_requested CLOB;
        l_dogface_required CLOB;
        l_dogface_endpoint VARCHAR2(200);
        l_dogface_empty_clob CLOB:=EMPTY_CLOB();
        l_dogface_empty_blob BLOB:=EMPTY_BLOB();

        /* START OF NEW */

        l_nav VARCHAR2(4000);
        l_color_scheme LONG;
        l_dropdown VARCHAR2(4000);
        l_common_header website_article.header_html%type;
        l_common_footer website_article.footer_html%type;

        /* END NEW */

        /* CLOB SIGNATURE */
        PROCEDURE addDeploy(pFilename IN VARCHAR2, pFileContent IN OUT NOCOPY CLOB) IS
            l_sha1 website_files.sha1%type;
        BEGIN
            dbms_output.put_line('new version : '||pFilename);
            l_sha1:=LOWER(dbms_crypto.hash(src => pFileContent, typ => dbms_crypto.hash_sh1));
            l_deploy_files(l_sha1).file_name:=pFilename;
            l_deploy_files(l_sha1).file_content:=pFileContent;
            l_files.put(pFilename, l_sha1);
            -- IF (pEsmLibrary='test') THEN
            --     l_deploy_files(l_sha1).md5:=LOWER(dbms_crypto.hash(src => pFileContent, typ => dbms_crypto.hash_md5));
            --     l_deploy_files(l_sha1).content_length:=dbms_lob.getlength(pFileContent);
            -- END IF;
        END;

        /* BLOB SIGNATURE */
        PROCEDURE addDeploy(pFilename IN VARCHAR2, pFileContent IN OUT NOCOPY BLOB) IS
            l_sha1 website_files.sha1%type;
        BEGIN
            -- dbms_output.put_line('new version: '||pFilename);
            l_sha1:=LOWER(dbms_crypto.hash(src => pFileContent, typ => dbms_crypto.hash_sh1));
            l_deploy_files(l_sha1).file_name:=pFilename;
            l_deploy_files(l_sha1).file_content_b:=pFileContent;
            l_files.put(pFilename, l_sha1);
            -- IF (pEsmLibrary='test') THEN
            --     l_deploy_files(l_sha1).md5:=LOWER(dbms_crypto.hash(src => pFileContent, typ => dbms_crypto.hash_md5));
            --     l_deploy_files(l_sha1).content_length:=dbms_lob.getlength(pFileContent);
            -- END IF;
        END;

        /* 
        ** 1. SEND TO NETLIFY A LIST OF SHA1 OF EACH FILE IN THE DEPLOYMENT 
        ** 2. UPLOAD THOSE FILES INDICATED BY NETLIFY AS REQUIRED 
        ** 3. UPDATE CONTROL TABLE WEBSITE_FILES TO FACILITATE FUTURE DEPLOYMENTS
        */
        PROCEDURE sendNetlifyDeploy IS
            l_json JSON_OBJECT_T;
            l_keys JSON_KEY_LIST;
            l_filename VARCHAR2(200);
            l_sha1 VARCHAR2(40);
            l_clob CLOB;
        BEGIN
            l_json:= new JSON_OBJECT_T;
            l_json.put('files',l_files);

            pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'sites/' || pSiteId || '/deploys', pMethod=>'POST', pBody=>l_json.stringify, pData=>l_clob);

            /* NEED THIS FOR ASYNC DEPLOYMENT STATUS MONITORING */
            UPDATE website SET netlify_deploy_id=(SELECT id FROM JSON_TABLE(l_clob, '$' COLUMNS (id))) WHERE id=pWebsiteId;
            COMMIT;

            FOR C IN (
                SELECT id, sha1 
                  FROM JSON_TABLE(l_clob, '$' COLUMNS (id, NESTED '$.required[*]' COLUMNS (sha1 PATH '$'))) 
                 WHERE sha1 IS NOT NULL
            ) LOOP
                dbms_output.put_line('Uploading ' || l_deploy_files(C.sha1).file_name);
                logDeployment(pWebsiteId, pSiteId, 'Uploading ' || l_deploy_files(C.sha1).file_name);
                IF (l_deploy_files(C.sha1).file_content IS NOT NULL) THEN
                    pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'deploys/' || C.id || '/files/' || l_deploy_files(C.sha1).file_name, pMethod=>'PUT', pBody=>l_deploy_files(C.sha1).file_content,pData=>l_clob);
                ELSIF (l_deploy_files(C.sha1).file_content_b IS NOT NULL) THEN
                    pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'deploys/' || C.id || '/files/' || l_deploy_files(C.sha1).file_name, pMethod=>'PUT', pBody=>l_deploy_files(C.sha1).file_content_b,pData=>l_clob);
                ELSE
                    RAISE_APPLICATION_ERROR(-20090,'Logic error - file_content clob and blob both null');
                END IF;
            END LOOP;

            /* 
            ** UPDATE WEBSITE_FILES WITH THIS SITE DEPLOYMENT 
            */
            DELETE website_files WHERE website_id=pWebsiteId AND site_id=pSiteId;
            l_keys:=l_files.get_keys;
            FOR i in 1..l_keys.COUNT LOOP
                l_filename:=l_keys(i);
                l_sha1:=l_files.get_string(l_keys(i));
                INSERT INTO website_files(website_id, site_id, path, sha1, mime_type)
                VALUES (pWebsiteId, pSiteId, l_filename, l_sha1, SUBSTR(l_filename,INSTR(l_filename,'.',-1)+1));
            END LOOP;
        END;

    BEGIN
        /* For logging when called by scheduler */
        pck_sec.g_website_id:=pWebsiteId;

        /*
        ** WEBSITE SHOULD ALWAYS HAVE ENTRIES IN TABLE WEBSITE_FILES FROM PREVIOUS DEPLOYMENT
        **
        ** 1. submit all asset paths and their sha1 to hosting service
        ** 2. receive array of assets to upload - i.e new path or sha1
        ** 3. upload assets set in Netlify "required" array
        **
        **  Each website page comprises following assets:
        **
        **  1. html
        **  2. fonts
        **  3. css stylesheets
        **  4. javascript starter
        **  5. _headers file including strict Content Security Policy
        **  6. sitemap.xml
        **  7. robots.txt
        */

        l_files:= new JSON_OBJECT_T;

        /*
        **  1. BUILD JAVASCRIPT START SCRIPT FIRST TO GET ITS SHA256 INTEGRITY VALUE WHICH IS INCLUDED IN THE HEAD OF ALL HTML FILES
        */
        FOR C IN (
            WITH source AS
            (
             SELECT '/javascript/' || file_name AS deploy_filename, apex_util.blob_to_clob(file_content) file_content, CAST(last_updated_on AS TIMESTAMP WITH TIME ZONE) updated_date
               FROM apex_application_static_files 
              WHERE application_id=101
                AND file_name IN ('deploy_start.min.js')
            )
            SELECT s.deploy_filename, REPLACE(REPLACE(s.file_content,'#RESTURL#',pRestUrl),'#JSLIB#','https://es-modules.netlify.app/' || LOWER(w.esm_library) || '/') file_content, 
                    s.updated_date, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR s.updated_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM source s, website_files wf, website w
             WHERE w.id=pWebsiteid
               AND wf.website_id(+)=w.id
               AND wf.path(+)=s.deploy_filename
               AND wf.site_id(+)=pSiteid
        ) LOOP
            l_script_sha256:=getSha256(C.file_content);

            IF (C.build=0) THEN
                l_sha1:=LOWER(dbms_crypto.hash(src => C.file_content, typ => dbms_crypto.hash_sh1));
                IF (l_sha1=C.sha1) THEN
                    l_files.put(C.deploy_filename, C.sha1);
                    EXIT;
                END IF;
            END IF;
            /* ENSURE CORRECT INTEGRITY VALUE INCLUDED IN ALL HTML FILES */
            DELETE website_files WHERE website_id=pWebsiteid AND site_id=pSiteid AND mime_type='html';

            logDeployment(pWebsiteId, pSiteId, 'Download JAVASCRIPT - '|| C.deploy_filename);
            addDeploy(pFilename=>C.deploy_filename, pFileContent=>C.file_content);
        END LOOP;

        /*
        **  2. Build LOGO and FAVICON files
        */
        FOR C IN (
            WITH source AS
            (
                SELECT '/images/logo.svg' AS deploy_filename, logo AS file_content, logo_updated_date AS updated_date
                  FROM website
                 WHERE id=pWebsiteid
                   AND logo IS NOT NULL
                 UNION ALL
                SELECT '/images/favicon.svg', favicon, favicon_updated_date
                  FROM website
                 WHERE id=pWebsiteid
            )
            SELECT s.deploy_filename, s.file_content, s.updated_date, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR s.updated_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM source s, website_files wf
             WHERE wf.website_id(+)=pWebsiteid
               AND wf.site_id(+)=pSiteid
               AND wf.path(+)=s.deploy_filename
               AND wf.mime_type(+)='image'
        ) LOOP
            IF (C.build=0) THEN
                l_files.put(C.deploy_filename, C.sha1);
                CONTINUE;
            END IF;

            IF (C.file_content IS NOT NULL) THEN
                logDeployment(pWebsiteId, pSiteId, 'Download IMAGE - ' || C.deploy_filename);
                addDeploy(pFilename=>C.deploy_filename, pFileContent=>C.file_content);
            END IF;
        END LOOP;
        
        /*
        **  3. Build HTML files
        */
        n:=0;
        FOR C IN (
            WITH content AS
            (
                SELECT wa.article_id, wa.collection_type, '/' || CASE WHEN wa.display_order=1 THEN 'index' ELSE apex_string_util.get_slug(wa.navigation_label) END || '.html' path, 
                        GREATEST(NVL(w.nav_updated_date,w.updated_date),a.updated_date,wa.updated_date) updated_date
                  FROM article a, website_article wa, website w
                 WHERE w.id=pWebsiteId
                   AND wa.website_id=w.id
                   AND a.id=wa.article_id
                UNION ALL 
                SELECT a.id, null, '/' || apex_string_util.get_slug(wa.navigation_label) || '/' || apex_string_util.get_slug(a.title) || '.html' path, 
                        GREATEST(NVL(w.nav_updated_date,w.updated_date),a.updated_date,wa.updated_date) updated_date
                  FROM article a, website_article wa, website w
                 WHERE w.id=pWebsiteId
                   AND wa.website_id=w.id
                   AND a.parent_id=wa.article_id
            )
            SELECT C.article_id, C.path, C.collection_type, f.sha1, 
                    CASE WHEN f.deployed_date IS NULL OR c.updated_date > f.deployed_date THEN 1 ELSE 0 END build
              FROM content c, website_files f
             WHERE f.website_id(+)=pWebsiteId
               AND f.site_id(+)=pSiteid
               AND f.path(+)=c.path
               AND f.mime_type(+)='html'
        ) LOOP
            IF (C.build=0) THEN
                l_files.put(C.path, C.sha1);
                CONTINUE;
            END IF;
            n:=n+1;
            IF (n=1) THEN
                l_nav:=buildNav(pWebsiteId);
                -- l_logo:=buildLogo(pWebsiteId);
                l_color_scheme:=buildColorscheme();
                l_dropdown:=buildDropdown(pWebsiteId);
                l_common_header:=buildCommonHeader(pWebsiteid);
                l_common_footer:=buildCommonFooter(pWebsiteid);
                -- l_favicon:=buildFavicon(pWebsiteid);
            END IF;
            buildPageBody(pWebsiteId, C.article_id, l_nav, l_color_scheme, l_dropdown, l_script_sha256, C.collection_type, l_common_header, l_common_footer, l_clob);
            addDeploy(pFilename=>C.path, pFileContent=>l_clob);
        END LOOP;

        /*
        **  4. Build FONT files
        */
        pck_fonts.getFontFiles(pWebsiteid, pSiteId);  /* Sets website_fontfaces.created_date */
        
        FOR C IN (
            SELECT ff.deploy_filename, ff.src_url, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR ff.created_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM website_fontface ff, website_files wf
             WHERE ff.website_id=pWebsiteid
               AND wf.website_id(+)=pWebsiteid
               AND wf.site_id(+)=pSiteid
               AND wf.path(+)=ff.deploy_filename
               AND wf.mime_type(+)='woff2'
        ) LOOP
            IF (C.build=0) THEN
                l_files.put(C.deploy_filename, C.sha1);
                CONTINUE;
            END IF;

            /* 
            **  DEPLOY FONT FILE FROM GOOGLE
            */
            logDeployment(pWebsiteId, pSiteId, 'Download Google Font - '|| C.deploy_filename);
            l_font_download:=apex_web_service.make_rest_request_b(p_url=>C.src_url, p_http_method=>'GET');
            addDeploy(pFilename=>C.deploy_filename, pFileContent=>l_font_download);
        END LOOP;
        
        /*
        **  5. Build CSS files - core, ckeditor and website-specific styles
        */
        pck_css.buildWebsiteCss(pWebsiteid); -- sets website.css_updated_date

        FOR C IN (
            WITH source AS
            (
                SELECT '/css/' || CASE WHEN file_name='deploy.min.css' THEN 'core-styles.min.css' ELSE file_name END AS deploy_filename, apex_util.blob_to_clob(file_content) file_content, CAST(last_updated_on AS TIMESTAMP WITH TIME ZONE) updated_date
                  FROM apex_application_static_files 
                 WHERE application_id=101
                   AND file_name IN ('deploy.min.css','ckeditor.min.css')
                UNION ALL
                SELECT '/css/website-styles.min.css', css, css_updated_date
                  FROM website
                 WHERE id=pWebsiteid
            )
            SELECT s.deploy_filename, s.file_content, s.updated_date, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR s.updated_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM source s, website_files wf
             WHERE wf.website_id(+)=pWebsiteid
               AND wf.site_id(+)=pSiteid
               AND wf.path(+)=s.deploy_filename
               AND wf.mime_type(+)='css'
        ) LOOP
            IF (C.build=0) THEN
                l_files.put(C.deploy_filename, C.sha1);
                CONTINUE;
            END IF;

            logDeployment(pWebsiteId, pSiteId, 'Download CSS - '|| C.deploy_filename);
            addDeploy(pFilename=>C.deploy_filename, pFileContent=>C.file_content);
        END LOOP;

        /*
        **  6. BUILD _headers INCLUDING CONTENT SECURITY POLICY
        */
        l_script_sha256:='''' || l_script_sha256 || '''';
        FOR C IN (
            WITH source AS
            (
             SELECT '/' || REPLACE(file_name,'.txt') AS deploy_filename, apex_util.blob_to_clob(file_content) file_content, CAST(last_updated_on AS TIMESTAMP WITH TIME ZONE) updated_date
               FROM apex_application_static_files 
              WHERE application_id=101
                AND file_name='_headers.txt'
            )
            SELECT s.deploy_filename, REPLACE(REPLACE(s.file_content,'#RESTURL#',pRestUrl),'#SHA256#',l_script_sha256) file_content, s.updated_date, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR s.updated_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM source s, website_files wf
             WHERE wf.website_id(+)=pWebsiteid
               AND wf.site_id(+)=pSiteid
               AND wf.path(+)=s.deploy_filename
        ) LOOP
            IF (C.build=0) THEN
                l_sha1:=LOWER(dbms_crypto.hash(src => C.file_content, typ => dbms_crypto.hash_sh1));
                IF (l_sha1=C.sha1) THEN
                    l_files.put(C.deploy_filename, C.sha1);
                    EXIT;
                END IF;
            END IF;

            logDeployment(pWebsiteId, pSiteId, 'Download CONTENT SECURITY HEADERS - '|| C.deploy_filename);
            addDeploy(pFilename=>C.deploy_filename, pFileContent=>C.file_content);
        END LOOP;

        /*
        **  7. BUILD SITEMAP
        */
        FOR C IN (
            WITH data AS 
            (
                SELECT 'https://' || w.domain_name ||
                        CASE WHEN wa.display_order>1 THEN '/' || apex_string_util.get_slug(wa.navigation_label) END AS path1, 
                        CASE WHEN wa.display_order>1 THEN apex_string_util.get_slug(ac.first_heading) END AS path2, 
                        TO_CHAR(NVL(a.updated_date,wa.updated_date),'yyyy-mm-dd') lastmod
                  FROM website w, website_article wa, article a, article_chars ac
                 WHERE w.id=pWebsiteid
                   AND wa.website_id=w.id
                   AND a.parent_id(+)=wa.article_id
                   AND ac.article_id(+)=a.id
                   AND ac.context(+)='headings'
                 ORDER BY wa.display_order
            ),
            xml AS
            (
                SELECT 
                    XMLElement("urlset", XMLAttributes('http://www.sitemaps.org/schemas/sitemap/0.9' as "xmlns"),
                        XMLAgg(
                            XMLElement("url",
                                XMLElement("loc",path1 || CASE WHEN path2 IS NOT NULL THEN '/' || path2 END),
                                XMLElement("lastmod",lastmod)
                            )
                        )
                    ) as sitemap
                FROM data
            ),
            source AS
            (
                SELECT '/sitemap.xml' AS deploy_filename, sitemap, nav_updated_date updated_date 
                  FROM website, xml
                 WHERE id=pWebsiteid
            )
            SELECT s.deploy_filename, s.sitemap.getClobVal() sitemap, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR s.updated_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM source s, website_files wf
             WHERE wf.website_id(+)=pWebsiteid
               AND wf.site_id(+)=pSiteid
               AND wf.path(+)=s.deploy_filename
        ) LOOP
            IF (C.build=0) THEN
                l_files.put(C.deploy_filename, C.sha1);
            ELSE
                logDeployment(pWebsiteId, pSiteId, 'Download SITEMAP.XML - '|| C.deploy_filename);
                addDeploy(pFilename=>C.deploy_filename, pFileContent=>C.sitemap);
            END IF;
        END LOOP;

        /*
        **  8. BUILD robots.txt TO DISCOURAGE AI CRAWLERS
        */
        FOR C IN (
            WITH source AS
            (
             SELECT '/' || file_name AS deploy_filename, apex_util.blob_to_clob(file_content) file_content, CAST(last_updated_on AS TIMESTAMP WITH TIME ZONE) updated_date
               FROM apex_application_static_files 
              WHERE application_id=101
                AND file_name='robots.txt'
            )
            SELECT s.deploy_filename, s.file_content, s.updated_date, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR s.updated_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM source s, website_files wf
             WHERE wf.website_id(+)=pWebsiteid
               AND wf.site_id(+)=pSiteid
               AND wf.path(+)=s.deploy_filename
        ) LOOP
            IF (C.build=0) THEN
                l_files.put(C.deploy_filename, C.sha1);
            ELSE
                logDeployment(pWebsiteId, pSiteId, 'Download robots.txt');
                addDeploy(pFilename=>C.deploy_filename, pFileContent=>C.file_content);
            END IF;
        END LOOP;

        /*
        **  START DEPLOYMENT NOW THAT ALL WEBSITE FILES HAVE BEEN PREPARED
        */
        sendNetlifyDeploy;

        /*
        ** AT THIS POINT NETLIFY DEPLOYMENT IS COMPLETE
        ** - UPDATE RELEVANT DEPLOYED_DATE IN ALL WEBSITE ARTICLES 
        ** - UPDATE RELEVANT LAST_PUBLISHED_DATE FOR THE WEBSITE 
        */
        UPDATE article 
           SET deployed_date=current_timestamp
         WHERE id IN
            (
                SELECT a.id 
                  FROM article a, website_article wa
                 WHERE wa.website_id=pWebsiteid 
                   AND wa.article_id=NVL(a.parent_id,a.id)
            );

        UPDATE website
           SET netlify_last_published=current_timestamp
         WHERE id=pWebsiteId;

        EXCEPTION
            WHEN OTHERS THEN
                logDeployment(pWebsiteId, pSiteId, SUBSTR(SQLERRM,1,100),'NOK');
                pck_core.log_error;
    END;

    /*
    **  DELETE OLD DEPLOYMENTS FROM NETLIFY SYSTEM
    */
    PROCEDURE runDelete(pUserId IN users.id%type, pWebsiteId IN website.id%type, pSiteId IN website.netlify_site_id%type) IS
        l_clob CLOB;
    BEGIN
        pck_sec.g_session_user_id:=pUserId;
        pck_sec.g_website_id:=pWebsiteId;
        
        pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/'|| pSiteId || '/deploys', pMethod=>'GET', pData=>l_clob);
        FOR C IN (
            SELECT id, published_at, MAX(published_at) OVER() last_published_at 
            FROM
                (
                SELECT id, TO_TIMESTAMP_TZ(published_at, 'YYYY-MM-DD"T"HH24:MI:SS.FXFF3TZR') published_at FROM JSON_TABLE(l_clob, '$[*]' COLUMNS (id, published_at))
                )
        )
        LOOP
            IF (C.published_at<C.last_published_at) THEN
                pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/'|| pSiteId || '/deploys/' || C.id, pMethod=>'DELETE', pData=>l_clob);
            END IF;
        END LOOP;
    END;

    /*
    ** Delete Github build directory - have to delete files individually before Github removes directory
    */    
    PROCEDURE deleteDirectory(pWebsiteId IN website.id%type, pUserId IN users.id%type) IS
        l_clob CLOB;
        l_domain_name website.domain_name%type;
        l_github CLOB;
        l_json JSON_OBJECT_T;
    BEGIN
        SELECT domain_name INTO l_domain_name FROM website WHERE id=pWebsiteId;
        pck_api.callGithubAPI(pUserId=>pUserId, pEndpoint=>'contents/'||l_domain_name, pMethod=>'GET', pData=>l_clob);
        FOR C IN (SELECT name, sha, message FROM  JSON_TABLE(l_clob FORMAT JSON, '$[*]' COLUMNS (name, sha, message))) LOOP
            IF (C.message IS NOT NULL) THEN 
                pck_core.log(C.message);
                RAISE_APPLICATION_ERROR(-20050,'Error in deleteDirectory - '|| C.message);
            ELSE
                l_json:=JSON_OBJECT_T.parse('{"message":"Commit by PLSQL"}');
                l_json.put('sha',C.sha);
                l_github:=l_json.to_clob; 
                pck_api.callGithubAPI(pUserId=>pUserId, pEndpoint=>'contents/' || l_domain_name || '/'||C.name, pMethod=>'DELETE', pBody=>l_github, pData=>l_clob);
            END IF;
        END LOOP;
        pck_api.resetGithubCurrentBuild(pUserId);
    END;

    /*
    ** Build and deploy AWS SES infrastructure through Terraform by submitting Github action. 
    ** Contact form sending email address must be stored in Apex Administrator description field!
    */
    PROCEDURE deployInfrastructure(pSenderEmail IN VARCHAR2) IS
        l_clob CLOB;
        l_json_clob CLOB;
        l_json JSON_OBJECT_T;
        l_action JSON_OBJECT_T;
    BEGIN
        -- Apex admin user description must hold valid domain name of sending email address
        FOR C IN (SELECT u.id, u.terraform_token, w.description ses_domain_name
                    FROM apex_workspace_apex_users w, users u 
                   WHERE w.email=u.email
                     AND w.is_admin='Yes'
                   ORDER BY date_created
                   FETCH FIRST ROW ONLY) 
        LOOP
            IF (C.ses_domain_name IS NULL) THEN
                dbms_output.put_line('APEX ADMIN USER MUST HAVE DESCRIPTION SET TO DOMAIN NAME OF SENDING EMAIL ADDRESS');
                EXIT;
            END IF;

            /* trigger the deployment using Githib action */          
            l_json:=new JSON_OBJECT_T;
            l_json.put('event_type', 'trigger_build_infra');

            l_action:=new JSON_OBJECT_T;
            l_action.put('from_email', pSenderEmail||'@'||C.ses_domain_name); --'contact.form@adfreesites.com'
            l_action.put('terraform_token', C.terraform_token);
            l_json.put('client_payload', l_action);

            --l_clob := apex_web_service.make_rest_request(p_url=>'https://api.github.com/repos/'|| C.deploy_repo || '/dispatches' ,p_http_method=>'POST',p_body=>l_json.stringify);

            /* nb; Github API 'dispatches' endpoint does not send a response */
            l_json_clob:=l_json.stringify;
            pck_api.callGithubAPI(pUserId=>C.id, pEndpoint=>'dispatches', pMethod=>'POST', pBody=>l_json_clob, pData=>l_clob);

        END LOOP;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;

/*
**  RUN this after deploying infrastructure for AWS enabling Amazon Simple Email Service
**  Updates the Admin user's "aws gateway url" and "api key" required for SES send mail service
*/
    PROCEDURE setTerraformApikey IS
        l_domain_name website.domain_name%type; 
        l_admin_id users.id%type;
        l_aws_gateway_url VARCHAR2(200);
        l_api_key varchar2(40);
        l_api_key_link VARCHAR2(100);
        l_clob CLOB;
    BEGIN
        SELECT u.id, SUBSTR(w.description,1,INSTR(w.description,'.')-1)
          INTO l_admin_id, l_domain_name
          FROM apex_workspace_apex_users w, users u 
         WHERE w.email=u.email
           AND w.is_admin='Yes'
         ORDER BY date_created
         FETCH FIRST ROW ONLY;

        pck_api.callTerraformAPI(pUserId=>null, pEndpoint=>'api/v2/organizations/Florent/workspaces/Mark_CMS', pMethod=>'GET', pData=>l_clob);

        FOR C IN (SELECT related FROM JSON_TABLE(l_clob, '$.data.relationships.outputs.links' COLUMNS (related))) LOOP
            pck_api.callTerraformAPI(pUserId=>null, pEndpoint=>C.related, pMethod=>'GET', pData=>l_clob);

            /* invoke_url */
            FOR C1 IN (SELECT value FROM JSON_TABLE(l_clob, '$.data[*]' COLUMNS (name PATH '$.attributes.name', value PATH '$.attributes.value')) WHERE name='invoke_url') LOOP
                l_aws_gateway_url:=C1.value || 'send/' || l_domain_name;
            END LOOP;

            /* ses_dkim_tokens */
            dbms_output.put_line('FETCHING DKIM TOKENS');
            FOR C1 IN (SELECT dkim FROM JSON_TABLE(l_clob, '$.data[*]' COLUMNS (name PATH '$.attributes.name', NESTED PATH '$.attributes.value[*]' COLUMNS (dkim VARCHAR2(40) PATH '$'))) WHERE name='ses_dkim_tokens') LOOP
                dbms_output.put_line('ses_dkim_tokens:'||C1.dkim);
            END LOOP;

            /* api_key */
            FOR C1 IN (SELECT api_key_link FROM JSON_TABLE(l_clob, '$.data[*]' COLUMNS (api_key_link PATH '$.links.self', name PATH '$.attributes.name', value PATH '$.attributes.value')) WHERE name='api_key') LOOP
                l_api_key_link:=C1.api_key_link;
            END LOOP;
            pck_api.callTerraformAPI(pUserId=>null, pEndpoint=>l_api_key_link, pMethod=>'GET', pData=>l_clob);
            FOR C1 IN (SELECT value FROM JSON_TABLE(l_clob, '$.data' COLUMNS (value PATH '$.attributes.value'))) LOOP
                l_api_key:=C1.value;
            END LOOP;
        END LOOP;

        IF (l_aws_gateway_url IS NOT NULL AND l_api_key IS NOT NULL) THEN
            UPDATE users 
               SET terraform_aws_gateway_url=l_aws_gateway_url, 
                   terraform_api_key=l_api_key
             WHERE id=l_admin_id;

            dbms_output.put_line('TERRAFORM API_KEY UPDATED SUCESSFULLY');
            dbms_output.put_line('TERRAFORM AWS_GATEWAY_URL UPDATED SUCESSFULLY');
            RETURN;
        END IF;

        IF (l_api_key IS NULL) THEN
            dbms_output.put_line('FAILED TO UPDATE TERRAFORM API_KEY');
        END IF;

        IF (l_aws_gateway_url IS NULL) THEN
            dbms_output.put_line('FAILED TO UPDATE TERRAFORM AWS_GATEWAY_URL');
        END IF;
    END;
       
end;
/