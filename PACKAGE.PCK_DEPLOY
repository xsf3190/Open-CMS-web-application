CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_DEPLOY" as 
    --
    FUNCTION buildContentDialog RETURN VARCHAR2;
    --
    FUNCTION buildDropdown(pWebsiteId IN website.id%type) RETURN VARCHAR2;
    --
    FUNCTION buildLoginForm RETURN VARCHAR2;
    --
    FUNCTION buildLogo(pWebsiteId IN website.id%type) RETURN VARCHAR2;
    --
    FUNCTION buildNav(pWebsiteId IN website.id%type, pArticleid IN website_article.article_id%type DEFAULT NULL) RETURN VARCHAR2;
    --
    PROCEDURE deleteDirectory(pWebsiteId IN website.id%type, pUserId IN users.id%type);
    --
    PROCEDURE deployInfrastructure(pSenderEmail IN VARCHAR2 DEFAULT 'contact.form');
    --
    PROCEDURE logDeployment(pWebsiteId IN website.id%type, pSiteId IN website_deploy.site_id%type, pMessage IN VARCHAR2, pStatus IN VARCHAR2 DEFAULT 'OK', pLogTime IN TIMESTAMP DEFAULT current_timestamp);
    --
    PROCEDURE runDelete(pUserId IN users.id%type, pWebsiteId IN website.id%type, pSiteId IN website.netlify_site_id%type);
    --
    PROCEDURE runDeployment(pWebsiteId IN website.id%type, pUserId IN website.user_id%type, pEnv IN VARCHAR2, pSiteId IN website.netlify_site_id%type, pRestUrl IN VARCHAR2);
    --
    PROCEDURE setTerraformApikey;
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_DEPLOY" as 

    FUNCTION getSha256(pContent IN CLOB) RETURN VARCHAR2 IS
        l_sha256 RAW(92);
    BEGIN
        l_sha256:=dbms_crypto.hash(src=>pContent, typ=>dbms_crypto.hash_sh256);
        RETURN ('sha256-' || utl_raw.cast_to_varchar2(utl_encode.base64_encode(l_sha256)));
    END;

    /*
    **  Insert row in website_deploy logging table
    */      
    PROCEDURE logDeployment(pWebsiteId IN website.id%type, pSiteId IN website_deploy.site_id%type, pMessage IN VARCHAR2, pStatus IN VARCHAR2 DEFAULT 'OK', pLogTime IN TIMESTAMP DEFAULT current_timestamp) IS 
    BEGIN
        INSERT INTO website_deploy(id, website_id, site_id, message, status, log_time) VALUES (seq_log.nextval, pWebsiteId, pSiteId, pMessage, pStatus, pLogTime);
        COMMIT;
    END; 

    /*
     **  Build Login Form
     */
    FUNCTION buildLoginForm RETURN VARCHAR2 IS
    BEGIN
        RETURN(
            '<dialog class="login-email" style="--max-width:40rem" closedBy="any">' ||
                '<form>' ||
                    '<input type="hidden" name="url" value="">' ||
                    '<input type="hidden" name="request_type" value="">' ||
                    '<header class="flex-items space-between">' ||
                        '<h2>Log in</h2>' ||
                        '<button type="button" class="button close no-focus" data-button-variant="round-icon">' ||
                            '<svg viewBox="0 0 18 18" class="icon" aria-hidden="true" focusable="false">' ||
                                '<line x1="18" y1="0" x2="0" y2="18"/><line x1="0" y1="0" x2="18" y2="18"/>' ||
                            '</svg>' ||
                        '</button>' ||
                    '</header>' ||
                    '<article class="flow">' ||
                        '<div>' ||
                            '<label for="emailInput">Email</label>' ||
                            '<input type="email" id="emailInput" name="email" required maxlength="50" autocapitalize="none" autocorrect="off" autocomplete="on">' ||
                            '<output for="email" class="sendmail-result"></output>' ||
                        '</div>' ||
                        '<div class="visually-hidden">' ||
                            '<label for="passcodeInput">Enter Passcode</label>' ||
                            '<input type="text" id="passcodeInput" name="passcode" pattern="\d{6}" inputmode="numeric" maxlength="6" autocomplete="one-time-code">' ||
                            '<output for="passcode" class="passcode-result"></output>' ||
                        '</div>' ||
                        '<div class="loader visually-hidden"></div>' ||
                    '</article>' ||
                    '<footer class="flex-items space-apart no-wrap">' ||
                        '<button class="button sendmail-magic" type="button">Send Link</button>' ||
                        '<div><hr><span>OR</span><hr></div>' ||
                        '<button class="button sendmail-passcode" type="button">Send Code</button>' ||
                        '<button class="button validate-passcode visually-hidden" type="button">Submit Passcode</button>' ||
                    '</footer>' ||
                '</form>' ||
             '</dialog>');
    END;

    /*
     **  Build Content Dialog - include pagination button
     */
    FUNCTION buildContentDialog RETURN VARCHAR2 IS
    BEGIN
        RETURN(
            '<dialog class="output" closedBy="any">' ||
                '<form method="dialog">' ||
                    '<header class="flex-items space-between no-wrap">' ||
                        '<div></div>' ||
                        '<button type="button" class="button close no-focus" data-button-variant="round-icon">' ||
                            '<svg viewBox="0 0 18 18" class="icon" aria-hidden="true" focusable="false">' ||
                                '<line x1="18" y1="0" x2="0" y2="18"/><line x1="0" y1="0" x2="18" y2="18"/>' ||
                            '</svg>' ||
                        '</button>' ||
                    '</header>' ||
                    '<article class="flow"></article>' ||
                    '<footer class="flex-items space-between">' ||
                    '</footer>' ||
                '</form>' ||
             '</dialog>');
    END;

    /*
    **  Build Dropdown Component with "Log In" and "Ecology" buttons
    */
    FUNCTION buildDropdown(pWebsiteId IN website.id%type) RETURN VARCHAR2 IS
        l_html LONG;
        l_uri_template ords_endpoints.uri_template%type;
    BEGIN
        pck_sec.refreshOrdsEndpoints(pWebsiteId);
        
        FOR C IN (
            SELECT o.uri_template
              FROM api_endpoint a, role r, ords_endpoints o
             WHERE a.website_id=pWebsiteId 
               AND a.role_id=r.id 
               AND r.name='visitor'
               AND o.label='Log In'
               AND a.template_id=o.template_id
        ) LOOP
            l_html:=
                '<button type="button" popovertarget="menulist" id="menulist-btn" style="anchor-name:--menulist-btn" aria-label="Actions menu">' ||
                    'Menu' ||
                    '<svg height="1rem" viewBox="0 0 32 32" preserveAspectRatio="none" aria-hidden="true" focusable="false">' ||
                      '<rect class="top" x="0" y="0" width="32" height="6" fill="currentColor" />' ||
                      '<rect class="middle" x="0" y="13" width="32" height="6" fill="currentColor" />' ||
                      '<rect class="bottom" x="0" y="26" width="32" height="6" fill="currentColor" />' ||
                    '</svg>' ||
                '</button>' ||
                '<div id="menulist" role="menu" aria-labelledby="menulist-btn" popover>' ||
                    '<button role="menuitem" tabindex="-1" type="button">' ||
                        '<span>&#127793;</span>' ||
                        'Ecology' ||
                    '</button>' ||
                    '<button role="menuitem" tabindex="-1" type="button" class="login-btn" data-endpoint="' || C.uri_template || '">' ||
                        '<svg viewBox="0 0 512 512"><use href="#userinfo"/></svg>' ||
                        'Log In' ||
                    '</button>' ||
                '</div>';
        END LOOP;
            
        RETURN(l_html);
    END;


    FUNCTION buildLogo(pWebsiteId IN website.id%type) RETURN VARCHAR2 IS
        l_logo website.logo%type;
        l_viewbox VARCHAR2(50);
        l_width PLS_INTEGER;
        l_height PLS_INTEGER;
        l_logo_styles VARCHAR2(100);
    BEGIN
        SELECT logo, regexp_substr(logo,'viewBox="(.+?)"',1,1,null,1) 
          INTO l_logo, l_viewbox 
          FROM website 
         WHERE id=pWebsiteId;
       
        IF (l_logo IS NOT NULL) THEN
            l_width:=regexp_substr(l_viewbox,'[^ ]+',1) + regexp_substr(l_viewbox,'[^ ]+',1,3);
            l_height:=regexp_substr(l_viewbox,'[^ ]+',1,2) + regexp_substr(l_viewbox,'[^ ]+',1,4);
            l_logo_styles:='style="margin-block-start:1rem;margin-inline-start:1rem;height:3rem;aspect-ratio:' || l_width || '/' || l_height || '"';
        END IF;

        RETURN ('<span class="logo"' || l_logo_styles || '>' || l_logo || '</span>');
    END;

    /*
    ** BUILD NAVIGATION ELEMENT COMMON TO ALL PAGES
    */
    FUNCTION buildNav(pWebsiteId IN website.id%type, pArticleid IN website_article.article_id%type DEFAULT NULL) RETURN VARCHAR2 IS
        l_nav VARCHAR2(4000);
        l_nav_length PLS_INTEGER:=0;
        l_article_id article.id%type;
        n PLS_INTEGER:=0;
    BEGIN
        l_nav:=
        '<nav aria-label="Main">' ||
            '<ul role="list">';
        FOR C IN (
            SELECT navigation_label, collection_type, article_id, ROW_NUMBER() OVER (ORDER BY display_order) rn
              FROM website_article 
             WHERE website_id=pWebsiteId
             ORDER BY display_order
        ) LOOP
            n:=n+1;
            l_nav:=l_nav ||
            '<li>' ||
                '<a href="/' || CASE WHEN C.rn>1 THEN apex_string_util.get_slug(C.navigation_label) END || '"' ||
                ' data-id="' || C.article_id || '" data-collection="' || C.collection_type || '"' ||
                CASE WHEN pArticleid=C.article_id THEN ' aria-current="page"' END || '>' ||
                C.navigation_label || 
                '</a>' ||
            '</li>';
        END LOOP;
        l_nav:=l_nav || 
            '<ul>' ||
        '</nav>';

        IF (n>1) THEN
            RETURN (l_nav);
        ELSE
            RETURN (null);
        END IF;
    END;

    /*
    ** GET THE header_html FOR THE FIRST PAGE IN WEBSITE FOR USE ON THOSE PAGES NOT HAVING THEIR OWN HEADER
    */
    FUNCTION buildCommonHeader(pWebsiteId IN website.id%type) RETURN website_article.header_html%type IS
        l_header_html website_article.header_html%type;
    BEGIN
        SELECT header_html 
          INTO l_header_html 
          FROM website_article 
         WHERE website_id=pWebsiteId
           AND display_order=(SELECT MIN(display_order) FROM website_article WHERE website_id=pWebsiteId);

        RETURN (l_header_html);
    END;

    /*
    ** GET THE footer_html FOR THE FIRST PAGE IN WEBSITE FOR USE ON THOSE PAGES NOT HAVING THEIR OWN FOOTER
    */
    FUNCTION buildCommonFooter(pWebsiteId IN website.id%type) RETURN website_article.footer_html%type IS
        l_footer_html website_article.footer_html%type;
    BEGIN
        SELECT footer_html 
          INTO l_footer_html 
          FROM website_article 
         WHERE website_id=pWebsiteId
           AND display_order=(SELECT MIN(display_order) FROM website_article WHERE website_id=pWebsiteId);

        RETURN (l_footer_html);
    END;

    /*
    ** GET WEBSITE FAVICON. DEFAULTS TO FIRST 2 LETTERS OF DOMAIN
    ** RETURN FAVICON AS ESCAPED SVG CODE IN XML FORMAT
    */
    FUNCTION buildFavicon(pWebsiteId IN website.id%type) RETURN website.favicon%type IS
        l_favicon_template website.favicon%type:=
        '<svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 100 100"><text y="1em" fill="darkgreen" font-family="system-ui" font-size="90">#FAVICON#</text></svg>';
        l_favicon website.favicon%type;
    BEGIN
        FOR C IN (
            SELECT NVL(favicon, UPPER(SUBSTR(domain_name,1,2))) favicon FROM website WHERE id=pWebsiteid
        ) LOOP
            IF (INSTR(C.favicon,'svg')=0) THEN
                l_favicon:=REPLACE(l_favicon_template,'#FAVICON#', C.favicon);
            ELSE
                l_favicon:=C.favicon;
            END IF;
        END LOOP;
        
        l_favicon:='data:image/svg+xml,' || REPLACE(REPLACE(l_favicon,'"','%22'),'#','%23');

        RETURN (l_favicon);
    END;

    /*
    ** SET "loading="lazy" ON ALL <img> ELEMENTS EXCEPT THE FIRST WHICH WE ASSUME WILL BE IN VIEWPORT
    */
    PROCEDURE setImgSrcset(pArticleid IN article.id%type, pMaxWidth IN website.max_width%type, pBodyHtml IN OUT NOCOPY article.body_html%type) IS
        l_img_width_regex varchar2(100):='class=".*?image_resized".+?width:(.+?)%.+?src="(.+?)"';
        l_img_regex varchar2(50):='<img.+?src="(.+?)"';
        l_img_width varchar2(10);
        l_img_width_pct NUMBER;
        l_max_width_px NUMBER;
        l_srcset VARCHAR2(4000);
        l_sizes VARCHAR2(100);
        l_loading VARCHAR2(16);
        l_fetchpriority VARCHAR2(4);
        l_img_src_tmp VARCHAR2(200);
        n pls_integer;
        n1 pls_integer;
        n2 pls_integer;

        TYPE tt_img_src IS RECORD (
            img_src VARCHAR2(200),
            public_id asset.public_id%type,
            article_id article.id%type,
            asset_id asset.id%type
        );
        TYPE t_img_src IS TABLE OF tt_img_src;
        l_img_src t_img_src:=t_img_src();

    BEGIN
        n:=NVL(regexp_count(pBodyHtml,l_img_regex),0);

        /* BUILD COLLECTION OF CLOUDINARY IMAGES FOUND IN HTML */
        FOR i IN 1..n LOOP
            l_img_src_tmp:=regexp_substr(pBodyHtml,l_img_regex,1,i,null,1);
            IF (l_img_src_tmp LIKE 'https://res.cloudinary.com%') THEN
                l_img_src.EXTEND;
                l_img_src(l_img_src.COUNT).img_src:=l_img_src_tmp;
                l_img_src(l_img_src.COUNT).public_id:=SUBSTR(l_img_src_tmp,INSTR(l_img_src_tmp,'/',-1)+1);
            END IF;
        END LOOP;

        IF (n>0) THEN
            /* CONVERT CONTAINER WIDTH TO PX */
            n1:=INSTR(pMaxWidth,'rem');
            IF (n1>0) THEN
                l_max_width_px:=SUBSTR(pMaxWidth,1,n1-1)*16;
            ELSE
                l_max_width_px:=pMaxWidth;
            END IF;
            /* remove padding 2rem */
            l_max_width_px:=l_max_width_px-32;
        END IF;
        

        FOR i IN 1..l_img_src.COUNT LOOP
            l_srcset:=null;
            FOR C IN (
                SELECT id, article_id, cld_cloud_name, breakpoints, height, width FROM asset WHERE public_id=l_img_src(i).public_id
            ) LOOP
                l_img_src(i).article_id:=C.article_id;
                l_img_src(i).asset_id:=C.id;
                FOR C1 IN (
                        SELECT breakpoint FROM (SELECT TO_NUMBER(column_value) AS breakpoint FROM TABLE(apex_string.split(C.breakpoints,','))) ORDER BY 1 DESC
                    ) LOOP
                        l_srcset:=l_srcset || 'https://res.cloudinary.com/' || C.cld_cloud_name || '/w_' || C1.breakpoint || '/' || l_img_src(i).public_id || ' ' || C1.breakpoint || 'w,';
                END LOOP;
                l_srcset:=RTRIM(l_srcset,',');
            END LOOP;

            IF (i=1) THEN
                l_loading:='eager';
                l_fetchpriority:='high';
                /* IF FIRST IMAGE HAS WIDTH PERCENTAGE */
                IF (regexp_substr(pBodyHtml,l_img_width_regex,1,1,null,2)=l_img_src(i).img_src) THEN
                    l_img_width:=regexp_substr(pBodyHtml,l_img_width_regex,1,1,null,1);
                    l_img_width_pct:=l_img_width/100;
                    l_sizes:='(width >= ' || l_max_width_px || ') ' || TO_CHAR(ROUND(l_img_width_pct * l_max_width_px)) || 'px,' || l_img_width || 'vw';
                ELSE
                    /* OTHERWISE USE INTRINSIC SIZE FROM IMG URL */
                    n1:=INSTR(l_img_src(i).img_src,'/',1,4);
                    n2:=INSTR(l_img_src(i).img_src,'/',1,5);
                    l_img_width:=SUBSTR(l_img_src(i).img_src,n1+3,n2-n1-3);
                    l_img_width_pct:=ROUND((l_img_width/l_max_width_px)*100);
                    l_sizes:='(width >= ' || l_max_width_px || ') ' || l_img_width || 'px,' || l_img_width_pct || 'vw';
                END IF;
            ELSE
                l_loading:='lazy';
                l_fetchpriority:='auto';
                l_sizes:='auto';
            END IF;

            pBodyHtml:=regexp_replace(pBodyHtml,'src="' || l_img_src(i).img_src || '"',
                'src="' || l_img_src(i).img_src || '"' || ' srcset="' || l_srcset || '"' || ' loading="' || l_loading || '"' || ' sizes="' || l_sizes || '"' || ' fetchpriority="' || l_fetchpriority || '"',
                i,0);
        END LOOP;
    END;


    /*
    ** BUILD BLOG COLLECTION
    */
    PROCEDURE buildPageCollection(pCollectionType IN website_article.collection_type%type, pArticleid website_article.article_id%type, pHtml IN OUT NOCOPY CLOB) IS
    BEGIN
        pHtml:='<ol class="grid grid__auto_fit" role="list">';
        FOR C IN (SELECT COUNT(*) OVER () nb, ROW_NUMBER() OVER (ORDER BY null) rn,
                            apex_string_util.get_slug(wa.navigation_label) path, wa.collection_date_format, art.body_html, art.title, art.excerpt, art.updated_date, art.word_count, art.cover_icon_id, ass.cld_cloud_name, ass.resource_type, ass.public_id, ass.format, ass.width, ass.height, ass.alt_text
                    FROM website_article wa, article art, asset ass
                   WHERE wa.article_id=pArticleId
                     AND art.parent_id=wa.article_id
                     AND ass.id(+)=art.cover_asset_id
                   ORDER BY art.updated_date DESC ) 
        LOOP
            pHtml:=pHtml || 
            '<li class="card">';
            IF (C.cld_cloud_name IS NOT NULL) THEN
                pHtml:=pHtml || 
                '<img loading="' || CASE WHEN C.rn=1 THEN 'eager' ELSE 'lazy' END || '" src="' || pck_media.getCloudinaryUrl(C.cld_cloud_name, C.resource_type, C.public_id, C.format, C.width) || '" width="'|| C.width || '" height="' || C.height || '" alt="' || C.alt_text || '">';
            ELSIF (C.cover_icon_id IS NOT NULL) THEN
                pHtml:=pHtml || 
                '<svg class="icon" aria-hidden="true" focusable="false"><use href="#' || C.cover_icon_id || '"></use></svg>';
            END IF;
            pHtml:=pHtml || 
                '<div class="flow">' ||
                    '<h2>' ||
                        '<a href="/' || C.path || '/' || apex_string_util.get_slug(C.title) || '">' || C.title || '</a>' ||
                    '</h2>' ||
                    '<p>' || CASE WHEN LENGTH(C.excerpt)<160 THEN C.excerpt ELSE SUBSTR(C.excerpt,1,INSTR(C.excerpt,' ',-1)) || '...' END  || '</p>' ||
                '</div>' ||
                '<small>' ||
                    CASE WHEN NVL(C.collection_date_format,'NONE')<>'NONE' THEN
                    '<span>' || TO_CHAR(NVL(C.updated_date,current_timestamp),'fm'||C.collection_date_format) || '</span>'
                    END ||
                    '<span>' || C.word_count || ' words</span>' ||
                '</small>' ||
            '</li>';
        END LOOP;
        pHtml:=pHtml || '</ol>';
    END;

    /*
    **  <nav>
    **  <header>
    **  <main>
    **  <footer>
    */
    PROCEDURE buildPageBody(
        pWebsiteId IN website.id%type, 
        pArticleid IN article.id%type, 
        pLogo IN VARCHAR2, 
        pNav IN VARCHAR2, 
        pDropdown in VARCHAR2, 
        pEnv IN VARCHAR2, 
        pRestUrl IN VARCHAR2, 
        pEsmLibrary IN VARCHAR2, 
        pIntegrity IN VARCHAR2, 
        pCollectionType IN website_article.collection_type%type, 
        pCommonHeader IN website_article.header_html%type, 
        pCommonFooter IN website_article.footer_html%type,
        pFavicon IN website.favicon%type,
        pBody IN OUT NOCOPY CLOB
    ) IS
        l_title article_chars.first_heading%type;
        l_nav VARCHAR2(4000);
        l_header website_article.header_html%type;
        l_main article.body_html%type;
        l_footer website_article.footer_html%type;
        l_collection CLOB;
        l_clob CLOB;
        l_importmap VARCHAR2(200);

        n PLS_INTEGER;
        n1 PLS_INTEGER;
    BEGIN
        pBody:=null;
        FOR C IN (
            SELECT NVL(wa.header_html,pCommonHeader) header_html, a.body_html, NVL(wa.footer_html,pCommonFooter) footer_html, NVL(a.parent_id,pArticleId) nav_id, w.max_width
              FROM website_article wa, article a, website w
             WHERE w.id=pWebsiteId
               AND wa.website_id=w.id
               AND wa.article_id=NVL(a.parent_id,pArticleId)
               AND a.id=pArticleId
        ) LOOP
            IF (pNav IS NOT NULL) THEN
                n:=INSTR(pNav,C.nav_id);
                n1:=INSTR(pNav,'>',n);
                l_nav:=SUBSTR(pNav,1,n1-1) || ' aria-current="page"' || SUBSTR(pNav,n1);
            END IF;

            l_header:='<header id="header" class="ck-content"' || CASE WHEN C.header_html IS NULL THEN ' nocontent' END || '>' || C.header_html || '</header>';
            l_footer:='<footer id="footer" class="ck-content"' || CASE WHEN C.footer_html IS NULL THEN ' nocontent' END || '>' || C.footer_html || '</footer>';
            
            l_clob:=C.header_html || C.body_html || C.footer_html;
            pck_fonts.getCharsUsed(pArticleId, l_clob);
            
            l_main:='<main id="main" class="ck-content flow"' || CASE WHEN C.body_html IS NULL THEN ' nocontent' END || '>' || C.body_html || '</main>';
            
            setImgSrcset(pArticleId, C.max_width, l_main);

            /*
            ** OPTIMAL TITLE LENGTH IS 60 CHARACTERS, I.E. 580PX
            */
            FOR C1 IN (
                SELECT first_heading, second_heading FROM article_chars WHERE article_id=pArticleid AND context='headings'
            ) LOOP
                l_title:=C1.second_heading;
                IF (l_title IS NOT NULL) THEN
                    l_title:=l_title || '|' || C1.first_heading;
                END IF;
            END LOOP;

            IF (pCollectionType='BLOG') THEN
                buildPageCollection(pCollectionType, pArticleid, l_collection);
            END IF;

            l_importmap:='https://es-modules.netlify.app/' || LOWER(pEsmLibrary) || '/importmap.json';

            /*
            ** FAVICON SVG MUST BE PURE XML WITH NO <STYLE> OR COMMENTS AND ESCAPED QUOTE AND HASH CHARACTERS
            */
            pBody:=
            '<!DOCTYPE HTML>' ||
            '<html lang="en">' ||
                '<head>' ||
                    '<meta charset="UTF-8">' ||
                    '<meta name="viewport" content="width=device-width, initial-scale=1">' ||
                    '<title>' || l_title || '</title>' ||
                    '<meta name="author" content="restartnet.com">' ||
                    '<meta name="mobile-web-app-capable" content="yes">' ||

                    '<link rel="stylesheet" href="/css/website-styles.min.css">'; /* TO CHANGE */
                    FOR C1 IN (SELECT deploy_filename FROM css ORDER BY link_order) LOOP
                        pBody:=pBody ||
                        '<link rel="stylesheet" href="' || C1.deploy_filename || '">';
                    END LOOP;

                    pBody:=pBody ||
                    '<link rel="icon" href="' || pFavicon || '">' ||
                    '<script src="/javascript/deploy_start.min.js" integrity="' || pIntegrity || '" defer></script>' ||
                    
                '</head>' ||
                '<body class="flow" data-websiteid="' || pWebsiteId || '" data-articleid="' || pArticleid || '" data-resturl="' || pRestUrl || '" data-importmap="' || l_importmap || '">' || 
                    '<div class="topnav flex-items space-between">' ||
                        pLogo || l_nav || pDropdown || 
                    '</div>' ||    
                    l_header || l_main || l_collection || l_footer || buildLoginForm || buildContentDialog ||
                '</body>' ||
                '<svg width="0" height="0">' ||
                    '<symbol id="userinfo"><path d="M224 248a120 120 0 1 0 0-240 120 120 0 1 0 0 240zm-29.7 56C95.8 304 16 383.8 16 482.3 16 498.7 29.3 512 45.7 512l356.6 0c16.4 0 29.7-13.3 29.7-29.7 0-98.5-79.8-178.3-178.3-178.3l-59.4 0z" fill="blue"/></symbol>';
                IF (pEnv='TEST') THEN
                    pBody:=pBody || 
                    '<symbol id="visits"><path d="M192 80c0-26.5 21.5-48 48-48l32 0c26.5 0 48 21.5 48 48l0 352c0 26.5-21.5 48-48 48l-32 0c-26.5 0-48-21.5-48-48l0-352zM0 272c0-26.5 21.5-48 48-48l32 0c26.5 0 48 21.5 48 48l0 160c0 26.5-21.5 48-48 48l-32 0c-26.5 0-48-21.5-48-48L0 272zM432 96l32 0c26.5 0 48 21.5 48 48l0 288c0 26.5-21.5 48-48 48l-32 0c-26.5 0-48-21.5-48-48l0-288c0-26.5 21.5-48 48-48z" fill="SlateGray"/></symbol>' ||
                    '<symbol id="edit-content"><path d="M36.4 353.2c4.1-14.6 11.8-27.9 22.6-38.7l181.2-181.2 33.9-33.9c16.6 16.6 51.3 51.3 104 104l33.9 33.9-33.9 33.9-181.2 181.2c-10.7 10.7-24.1 18.5-38.7 22.6L30.4 510.6c-8.3 2.3-17.3 0-23.4-6.2S-1.4 489.3 .9 481L36.4 353.2zm55.6-3.7c-4.4 4.7-7.6 10.4-9.3 16.6l-24.1 86.9 86.9-24.1c6.4-1.8 12.2-5.1 17-9.7L91.9 349.5zm354-146.1c-16.6-16.6-51.3-51.3-104-104L308 65.5C334.5 39 349.4 24.1 352.9 20.6 366.4 7 384.8-.6 404-.6S441.6 7 455.1 20.6l35.7 35.7C504.4 69.9 512 88.3 512 107.4s-7.6 37.6-21.2 51.1c-3.5 3.5-18.4 18.4-44.9 44.9z" fill="red"/></symbol>' ||
                    '<symbol id="publish-website"><path d="M351.9 280l-190.9 0c2.9 64.5 17.2 123.9 37.5 167.4 11.4 24.5 23.7 41.8 35.1 52.4 11.2 10.5 18.9 12.2 22.9 12.2s11.7-1.7 22.9-12.2c11.4-10.6 23.7-28 35.1-52.4 20.3-43.5 34.6-102.9 37.5-167.4zM160.9 232l190.9 0C349 167.5 334.7 108.1 314.4 64.6 303 40.2 290.7 22.8 279.3 12.2 268.1 1.7 260.4 0 256.4 0s-11.7 1.7-22.9 12.2c-11.4 10.6-23.7 28-35.1 52.4-20.3 43.5-34.6 102.9-37.5 167.4zm-48 0C116.4 146.4 138.5 66.9 170.8 14.7 78.7 47.3 10.9 131.2 1.5 232l111.4 0zM1.5 280c9.4 100.8 77.2 184.7 169.3 217.3-32.3-52.2-54.4-131.7-57.9-217.3L1.5 280zm398.4 0c-3.5 85.6-25.6 165.1-57.9 217.3 92.1-32.7 159.9-116.5 169.3-217.3l-111.4 0zm111.4-48C501.9 131.2 434.1 47.3 342 14.7 374.3 66.9 396.4 146.4 399.9 232l111.4 0z" fill="violet"/></symbol>' ||
                    '<symbol id="edit-pages"><path d="M0 64C0 46.3 14.3 32 32 32l128 0c88.4 0 160 71.6 160 160S248.4 352 160 352l-96 0 0 96c0 17.7-14.3 32-32 32S0 465.7 0 448L0 64zM64 288l96 0c53 0 96-43 96-96s-43-96-96-96l-96 0 0 192z" fill="brown"/></symbol>' ||
                    '<symbol id="change-url"><path d="M512 32C512 140.1 435.4 230.3 333.6 251.4 325.7 193.3 299.6 141 261.1 100.5 301.2 40 369.9 0 448 0l32 0c17.7 0 32 14.3 32 32zM0 96C0 78.3 14.3 64 32 64l32 0c123.7 0 224 100.3 224 224l0 192c0 17.7-14.3 32-32 32s-32-14.3-32-32l0-160C100.3 320 0 219.7 0 96z" fill="green"/></symbol>' ||
                    '<symbol id="create-website""><path d="M256 64c0-17.7-14.3-32-32-32s-32 14.3-32 32l0 160-160 0c-17.7 0-32 14.3-32 32s14.3 32 32 32l160 0 0 160c0 17.7 14.3 32 32 32s32-14.3 32-32l0-160 160 0c17.7 0 32-14.3 32-32s-14.3-32-32-32l-160 0 0-160z" fill="blue"/></symbol>' ||
                    '<symbol id="edit-codepen"><path d="M502.3 159.7l-234-156c-8-4.9-16.5-5-24.6 0l-234 156c-6 4-9.7 11.1-9.7 18.3L0 334c0 7.1 3.7 14.3 9.7 18.3l234 156c8 4.9 16.5 5 24.6 0l234-156c6-4 9.7-11.1 9.7-18.3l0-156c0-7.1-3.7-14.3-9.7-18.3zM278 63.1l172.3 114.9-76.9 51.4-95.4-63.7 0-102.6zm-44 0L234 165.7 138.6 229.4 61.7 178 234 63.1zM44 219.1L99.1 256 44 292.8 44 219.1zM234 448.8L61.7 334 138.6 282.6 234 346.3 234 448.8zM256 308l-77.7-52 77.7-52 77.7 52-77.7 52zm22 140.9l0-102.6 95.4-63.7 76.9 51.4-172.3 114.9zm190-156l-55.1-36.9 55.1-36.9 0 73.7z" fill="orange"/></symbol>' ||
                    '<symbol id="link-domain"><path d="M351.9 280l-190.9 0c2.9 64.5 17.2 123.9 37.5 167.4 11.4 24.5 23.7 41.8 35.1 52.4 11.2 10.5 18.9 12.2 22.9 12.2s11.7-1.7 22.9-12.2c11.4-10.6 23.7-28 35.1-52.4 20.3-43.5 34.6-102.9 37.5-167.4zM160.9 232l190.9 0C349 167.5 334.7 108.1 314.4 64.6 303 40.2 290.7 22.8 279.3 12.2 268.1 1.7 260.4 0 256.4 0s-11.7 1.7-22.9 12.2c-11.4 10.6-23.7 28-35.1 52.4-20.3 43.5-34.6 102.9-37.5 167.4zm-48 0C116.4 146.4 138.5 66.9 170.8 14.7 78.7 47.3 10.9 131.2 1.5 232l111.4 0zM1.5 280c9.4 100.8 77.2 184.7 169.3 217.3-32.3-52.2-54.4-131.7-57.9-217.3L1.5 280zm398.4 0c-3.5 85.6-25.6 165.1-57.9 217.3 92.1-32.7 159.9-116.5 169.3-217.3l-111.4 0zm111.4-48C501.9 131.2 434.1 47.3 342 14.7 374.3 66.9 396.4 146.4 399.9 232l111.4 0z" fill="violet"/></symbol>' ||
                    '<symbol id="message"><path d="M48 64c-26.5 0-48 21.5-48 48 0 15.1 7.1 29.3 19.2 38.4l208 156c17.1 12.8 40.5 12.8 57.6 0l208-156c12.1-9.1 19.2-23.3 19.2-38.4 0-26.5-21.5-48-48-48L48 64zM0 196L0 384c0 35.3 28.7 64 64 64l384 0c35.3 0 64-28.7 64-64l0-188-198.4 148.8c-34.1 25.6-81.1 25.6-115.2 0L0 196z"/></symbol>';
                END IF;
                pBody:=pBody || 
                '</svg>';
                IF (pEnv='TEST') THEN
                    pBody:=pBody || 
                    '<form action="https://codepen.io/pen/define" method="POST" target="_blank"><input type="hidden" name="data" value=""></form>';
                END IF;
            pBody:=pBody || 
            '</html>';
        END LOOP;
    END;

    /*
    **  DEPLOY BY UPLOADING HTML FILE CONTENT DIRECTLY TO NETLIFY
    */
    PROCEDURE runDeployment(pWebsiteId IN website.id%type, pUserId IN website.user_id%type, pEnv IN VARCHAR2, pSiteId IN website.netlify_site_id%type, pRestUrl IN VARCHAR2) IS
        
        l_clob CLOB;
        l_files JSON_OBJECT_T;
        
        TYPE tt_files IS RECORD (
            file_name VARCHAR2(200),
            file_content CLOB,
            file_content_b BLOB,
            md5 VARCHAR2(32),
            content_length NUMBER
        );
        TYPE t_files IS TABLE OF tt_files INDEX BY VARCHAR2(40);
        l_deploy_files t_files;

        l_script_sha256 VARCHAR2(500);
        l_font_download BLOB;

        l_sha1 VARCHAR2(40);
        n PLS_INTEGER;

        l_dogface_requested CLOB;
        l_dogface_required CLOB;
        l_dogface_endpoint VARCHAR2(200);
        l_dogface_empty_clob CLOB:=EMPTY_CLOB();
        l_dogface_empty_blob BLOB:=EMPTY_BLOB();

        /* START OF NEW */

        l_nav VARCHAR2(4000);
        l_logo website.logo%type;
        l_favicon website.favicon%type;
        l_dropdown VARCHAR2(4000);
        l_common_header website_article.header_html%type;
        l_common_footer website_article.footer_html%type;

        /* END NEW */

        /* CLOB SIGNATURE */
        PROCEDURE addDeploy(pFilename IN VARCHAR2, pFileContent IN OUT NOCOPY CLOB) IS
            l_sha1 website_files.sha1%type;
        BEGIN
            dbms_output.put_line('new version : '||pFilename);
            l_sha1:=LOWER(dbms_crypto.hash(src => pFileContent, typ => dbms_crypto.hash_sh1));
            l_deploy_files(l_sha1).file_name:=pFilename;
            l_deploy_files(l_sha1).file_content:=pFileContent;
            l_files.put(pFilename, l_sha1);
            -- IF (pEsmLibrary='test') THEN
            --     l_deploy_files(l_sha1).md5:=LOWER(dbms_crypto.hash(src => pFileContent, typ => dbms_crypto.hash_md5));
            --     l_deploy_files(l_sha1).content_length:=dbms_lob.getlength(pFileContent);
            -- END IF;
        END;

        /* BLOB SIGNATURE */
        PROCEDURE addDeploy(pFilename IN VARCHAR2, pFileContent IN OUT NOCOPY BLOB) IS
            l_sha1 website_files.sha1%type;
        BEGIN
            dbms_output.put_line('new version: '||pFilename);
            l_sha1:=LOWER(dbms_crypto.hash(src => pFileContent, typ => dbms_crypto.hash_sh1));
            l_deploy_files(l_sha1).file_name:=pFilename;
            l_deploy_files(l_sha1).file_content_b:=pFileContent;
            l_files.put(pFilename, l_sha1);
            -- IF (pEsmLibrary='test') THEN
            --     l_deploy_files(l_sha1).md5:=LOWER(dbms_crypto.hash(src => pFileContent, typ => dbms_crypto.hash_md5));
            --     l_deploy_files(l_sha1).content_length:=dbms_lob.getlength(pFileContent);
            -- END IF;
        END;

        /* 
        ** SEND TO NETLIFY LIST OF SHA1 OF EACH FILE IN THE DEPLOYMENT 
        */
        PROCEDURE sendNetlifyDeploy IS
            l_json JSON_OBJECT_T;
            l_keys JSON_KEY_LIST;
            l_filename VARCHAR2(200);
            l_sha1 VARCHAR2(40);
            l_clob CLOB;
        BEGIN
            l_json:= new JSON_OBJECT_T;
            l_json.put('files',l_files);

            pck_api.callNetlifyAPI(pUserId=>pUserid, pEndpoint=>'sites/' || pSiteId || '/deploys', pMethod=>'POST', pBody=>l_json.stringify, pData=>l_clob);

            /* NEED THIS FOR ASYNC DEPLOYMENT STATUS MONITORING */
            UPDATE website SET netlify_deploy_id=(SELECT id FROM JSON_TABLE(l_clob, '$' COLUMNS (id))) WHERE id=pWebsiteId;
            COMMIT;

            FOR C IN (
                SELECT id, sha1 
                  FROM JSON_TABLE(l_clob, '$' COLUMNS (id, NESTED '$.required[*]' COLUMNS (sha1 PATH '$'))) 
                 WHERE sha1 IS NOT NULL
            ) LOOP
                dbms_output.put_line('Uploading ' || l_deploy_files(C.sha1).file_name);
                logDeployment(pWebsiteId, pSiteId, 'Uploading ' || l_deploy_files(C.sha1).file_name);
                IF (l_deploy_files(C.sha1).file_content IS NOT NULL) THEN
                    pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'deploys/' || C.id || '/files/' || l_deploy_files(C.sha1).file_name, pMethod=>'PUT', pBody=>l_deploy_files(C.sha1).file_content,pData=>l_clob);
                ELSIF (l_deploy_files(C.sha1).file_content_b IS NOT NULL) THEN
                    pck_api.callNetlifyAPI(pUserId=>null, pEndpoint=>'deploys/' || C.id || '/files/' || l_deploy_files(C.sha1).file_name, pMethod=>'PUT', pBody=>l_deploy_files(C.sha1).file_content_b,pData=>l_clob);
                ELSE
                    RAISE_APPLICATION_ERROR(-20090,'Logic error - file_content clob and blob both null');
                END IF;
            END LOOP;

            /* 
            ** UPDATE WEBSITE_FILES WITH THIS SITE DEPLOYMENT 
            */
            DELETE website_files WHERE website_id=pWebsiteId AND site_id=pSiteId;
            l_keys:=l_files.get_keys;
            FOR i in 1..l_keys.COUNT LOOP
                l_filename:=l_keys(i);
                l_sha1:=l_files.get_string(l_keys(i));
                INSERT INTO website_files(website_id, site_id, path, sha1, mime_type)
                VALUES (pWebsiteId, pSiteId, l_filename, l_sha1, SUBSTR(l_filename,INSTR(l_filename,'.',-1)+1));
            END LOOP;
        END;

    BEGIN
        /* For logging when called by scheduler */
        pck_sec.g_session_user_id:=pUserId;
        pck_sec.g_website_id:=pWebsiteId;

        /*
        ** WEBSITE SHOULD ALWAYS HAVE ENTRIES IN TABLE WEBSITE_FILES FROM PREVIOUS DEPLOYMENT
        **
        ** 1. submit all asset paths and their sha1 to hosting service
        ** 2. receive array of assets to upload - i.e new path or sha1
        ** 3. upload assets set in Netlify "required" array
        **
        **  Each website page comprises following assets:
        **
        **  1. html
        **  2. fonts
        **  3. css stylesheets
        **  4. javascript starter and modules
        **  5. _headers file including strict Content Security Policy
        **  6. robots.txt, sitemap.xml
        */

        -- l_json:= new JSON_OBJECT_T;
        l_files:= new JSON_OBJECT_T;

        /*
        **  0. Build JAVASCRIPT FILES FIRST TO GET SHA256 INTEGRITY VALUE OF THE STARTER SCRIPT WHICH WE INCLUDE IN HEAD OF HTML FILES
        */
        FOR C IN (
            WITH source AS
            (
             SELECT '/javascript/' || file_name AS deploy_filename, apex_util.blob_to_clob(file_content) file_content, CAST(last_updated_on AS TIMESTAMP WITH TIME ZONE) updated_date
               FROM apex_application_static_files 
              WHERE application_id=101
                AND file_name IN ('deploy_start.min.js', 'deploy_main.min.js', 'deploy_web_vitals5.min.js', 'deploy_menulist.min.js')
            )
            SELECT s.deploy_filename, s.file_content, s.updated_date, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR s.updated_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM source s, website_files wf
             WHERE wf.website_id(+)=pWebsiteid
               AND wf.path(+)=s.deploy_filename
               AND wf.site_id(+)=pSiteid
        ) LOOP
            IF (INSTR(C.deploy_filename,'deploy_start.min.js')>0) THEN
                l_script_sha256:=getSha256(C.file_content);
            END IF;
            IF (C.build=0) THEN
                l_files.put(C.deploy_filename, C.sha1);
                CONTINUE;
            END IF;

            logDeployment(pWebsiteId, pSiteId, 'Download JAVASCRIPT - '|| C.deploy_filename);
            addDeploy(pFilename=>C.deploy_filename, pFileContent=>C.file_content);
        END LOOP;
        
        /*
        **  1. Build HTML files
        */
        n:=0;
        FOR C IN (
            WITH content AS
            (
                SELECT wa.article_id, w.esm_library, wa.collection_type, '/' || CASE WHEN wa.display_order=1 THEN 'index' ELSE apex_string_util.get_slug(wa.navigation_label) END || '.html' path, 
                        GREATEST(NVL(w.nav_updated_date,w.updated_date),a.updated_date,wa.updated_date) updated_date
                  FROM article a, website_article wa, website w
                 WHERE w.id=pWebsiteId
                   AND wa.website_id=w.id
                   AND a.id=wa.article_id
                UNION ALL 
                SELECT a.id, w.esm_library, null, '/' || apex_string_util.get_slug(wa.navigation_label) || '/' || apex_string_util.get_slug(utl_i18n.unescape_reference(apex_escape.striphtml(regexp_substr(a.body_html,'<h[2-4]>(.+?)<\/h[2-4]>',1,1)))) || '.html' path, 
                        GREATEST(NVL(w.nav_updated_date,w.updated_date),a.updated_date,wa.updated_date) updated_date
                  FROM article a, website_article wa, website w
                 WHERE w.id=pWebsiteId
                   AND wa.website_id=w.id
                   AND a.parent_id=wa.article_id
            )
            SELECT C.article_id, C.esm_library, C.path, C.collection_type, f.sha1, 
                    CASE WHEN f.deployed_date IS NULL OR c.updated_date > f.deployed_date THEN 1 ELSE 0 END build
              FROM content c, website_files f
             WHERE f.website_id(+)=pWebsiteId
               AND f.site_id(+)=pSiteid
               AND f.path(+)=c.path
               AND f.mime_type(+)='html'
        ) LOOP
            IF (C.build=0) THEN
                l_files.put(C.path, C.sha1);
                CONTINUE;
            END IF;
            n:=n+1;
            IF (n=1) THEN
                l_nav:=buildNav(pWebsiteId);
                l_logo:=buildLogo(pWebsiteId);
                l_dropdown:=buildDropdown(pWebsiteId);
                l_common_header:=buildCommonHeader(pWebsiteid);
                l_common_footer:=buildCommonFooter(pWebsiteid);
                l_favicon:=buildFavicon(pWebsiteid);
            END IF;
            buildPageBody(pWebsiteId, C.article_id, l_logo, l_nav, l_dropdown, pEnv, pRestUrl, C.esm_library, l_script_sha256, C.collection_type, l_common_header, l_common_footer, l_favicon, l_clob);
            addDeploy(pFilename=>C.path, pFileContent=>l_clob);
        END LOOP;

        /*
        **  2. Build FONT files
        */
        pck_fonts.getFontFiles(pWebsiteid, pSiteId);  /* Sets website_fontfaces.created_date */
        
        FOR C IN (
            SELECT ff.deploy_filename, ff.src_url, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR ff.created_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM website_fontface ff, website_files wf
             WHERE ff.website_id=pWebsiteid
               AND wf.website_id(+)=pWebsiteid
               AND wf.site_id(+)=pSiteid
               AND wf.path(+)=ff.deploy_filename
               AND wf.mime_type(+)='woff2'
        ) LOOP
            IF (C.build=0) THEN
                l_files.put(C.deploy_filename, C.sha1);
                CONTINUE;
            END IF;

            /* 
            **  DEPLOY FONT FILE FROM GOOGLE
            */
            logDeployment(pWebsiteId, pSiteId, 'Download Google Font - '|| C.deploy_filename);
            l_font_download:=apex_web_service.make_rest_request_b(p_url=>C.src_url, p_http_method=>'GET');
            addDeploy(pFilename=>C.deploy_filename, pFileContent=>l_font_download);
        
        END LOOP;
        
        /*
        **  3. Build CSS files
        */
        pck_css.deploy_core_css;   /* TEMPORARY FIX */
        
        FOR C IN (
            WITH source AS
            (
                SELECT deploy_filename, content_minified, updated_date
                  FROM css
                 UNION ALL
                SELECT '/css/website-styles.min.css', css, css_updated_date
                  FROM website
                 WHERE id=pWebsiteid
            )
            SELECT s.deploy_filename, s.content_minified, s.updated_date, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR s.updated_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM source s, website_files wf
             WHERE wf.website_id(+)=pWebsiteid
               AND wf.site_id(+)=pSiteid
               AND wf.path(+)=s.deploy_filename
               AND wf.mime_type(+)='css'
        ) LOOP
            IF (C.build=0) THEN
                l_files.put(C.deploy_filename, C.sha1);
                CONTINUE;
            END IF;

            logDeployment(pWebsiteId, pSiteId, 'Download CSS - '|| C.deploy_filename);
            addDeploy(pFilename=>C.deploy_filename, pFileContent=>C.content_minified);
        END LOOP;

        /*
        **  4. BUILD _headers INCLUDING CONTENT SECURITY POLICY
        */
        l_script_sha256:='''' || l_script_sha256 || '''';
        FOR C IN (
            WITH source AS
            (
             SELECT '/' || REPLACE(file_name,'.txt') AS deploy_filename, apex_util.blob_to_clob(file_content) file_content, CAST(last_updated_on AS TIMESTAMP WITH TIME ZONE) updated_date
               FROM apex_application_static_files 
              WHERE application_id=101
                AND file_name='_headers.txt'
            )
            SELECT s.deploy_filename, REPLACE(REPLACE(s.file_content,'#RESTURL#',pRestURL),'#SHA256#',l_script_sha256) file_content, s.updated_date, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR s.updated_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM source s, website_files wf
             WHERE wf.website_id(+)=pWebsiteid
               AND wf.site_id(+)=pSiteid
               AND wf.path(+)=s.deploy_filename
        ) LOOP
            IF (C.build=0) THEN
                l_sha1:=LOWER(dbms_crypto.hash(src => C.file_content, typ => dbms_crypto.hash_sh1));
                IF (l_sha1=C.sha1) THEN
                    l_files.put(C.deploy_filename, C.sha1);
                    EXIT;
                END IF;
            END IF;

            logDeployment(pWebsiteId, pSiteId, 'Download CONTENT SECURITY HEADERS - '|| C.deploy_filename);
            addDeploy(pFilename=>C.deploy_filename, pFileContent=>C.file_content);
        END LOOP;

        /*
        **  5. BUILD SITEMAP
        */
        FOR C IN (
            WITH data AS 
            (
                SELECT 'https://' || w.domain_name ||
                        CASE WHEN wa.display_order>1 THEN '/' || apex_string_util.get_slug(wa.navigation_label) END AS path1, 
                        CASE WHEN wa.display_order>1 THEN apex_string_util.get_slug(ac.first_heading) END AS path2, 
                        TO_CHAR(NVL(a.updated_date,wa.updated_date),'yyyy-mm-dd') lastmod
                  FROM website w, website_article wa, article a, article_chars ac
                 WHERE w.id=pWebsiteid
                   AND wa.website_id=w.id
                   AND a.parent_id(+)=wa.article_id
                   AND ac.article_id(+)=a.id
                   AND ac.context(+)='headings'
                 ORDER BY wa.display_order
            ),
            xml AS
            (
                SELECT 
                    XMLElement("urlset", XMLAttributes('http://www.sitemaps.org/schemas/sitemap/0.9' as "xmlns"),
                        XMLAgg(
                            XMLElement("url",
                                XMLElement("loc",path1 || CASE WHEN path2 IS NOT NULL THEN '/' || path2 END),
                                XMLElement("lastmod",lastmod)
                            )
                        )
                    ) as sitemap
                FROM data
            ),
            source AS
            (
                SELECT '/sitemap.xml' AS deploy_filename, sitemap, nav_updated_date updated_date 
                  FROM website, xml
                 WHERE id=pWebsiteid
            )
            SELECT s.deploy_filename, s.sitemap.getClobVal() sitemap, wf.sha1,
                    CASE WHEN wf.deployed_date IS NULL OR s.updated_date>wf.deployed_date THEN 1 ELSE 0 END build
              FROM source s, website_files wf
             WHERE wf.website_id(+)=pWebsiteid
               AND wf.site_id(+)=pSiteid
               AND wf.path(+)=s.deploy_filename
        ) LOOP
            IF (C.build=0) THEN
                l_files.put(C.deploy_filename, C.sha1);
            ELSE
                logDeployment(pWebsiteId, pSiteId, 'Download SITEMAP.XML - '|| C.deploy_filename);
                addDeploy(pFilename=>C.deploy_filename, pFileContent=>C.sitemap);
            END IF;
        END LOOP;

        sendNetlifyDeploy;

        /*
        **  DOGFACE DEPLOYMENT IF USER HAS DOGFACE API KEY AND ESMLIBRARY = 'TEST'
        */
        /*
        SELECT COUNT(*) INTO n FROM dual WHERE EXISTS (SELECT null FROM users WHERE id=pUserid AND dogface_api_key IS NOT NULL);
        n:=0;
        IF (n=1 AND pEsmLibrary='test') THEN
            SELECT json_arrayagg(
                    json_object(KEY 'path' VALUE '/henk.florentmeijer.com/' || file_name, KEY 'hash' VALUE md5)
                returning clob) 
            INTO l_dogface_requested
            FROM website_files
            WHERE website_id=pWebsiteid;

            pck_api.callDogfaceAPI(pUserId=>pUserId, pEndpoint=>'need-for-upload', pMethod=>'POST', pBody=>l_dogface_requested, pBodyBlob=>l_dogface_empty_blob, pData=>l_dogface_required);

            FOR C IN (
                SELECT df.md5, wf.sha1, wf.file_name 
                  FROM website_files wf, 
                        JSON_TABLE(l_dogface_required, '$[*]' 
                        COLUMNS (md5 varchar2 path '$')) df
                 WHERE df.md5=wf.md5
                   AND instr(wf.file_name,'font')=0
            ) LOOP
                --IF (C.file_name<>'_headers') THEN
                    l_dogface_endpoint:=C.md5 || '/henk.florentmeijer.com/' || C.file_name;
                    IF (l_deploy_files(C.sha1).file_content IS NOT NULL) THEN
                        pck_api.callDogfaceAPI(pUserId=>pUserId, pMethod=>'PUT', pEndpoint=>l_dogface_endpoint, pBody=>l_deploy_files(C.sha1).file_content, pBodyBlob=>l_dogface_empty_blob, pData=>l_clob);
                    ELSE
                        pck_api.callDogfaceAPI(pUserId=>pUserId, pMethod=>'PUT', pEndpoint=>l_dogface_endpoint, pBody=>l_dogface_empty_clob, pBodyBlob=>l_deploy_files(C.sha1).file_content_b, pData=>l_clob);
                    END IF;
                --END IF;
            END LOOP;
        END IF;
        */

        EXCEPTION
            WHEN OTHERS THEN
                logDeployment(pWebsiteId, pSiteId, SUBSTR(SQLERRM,1,100),'NOK');
                pck_core.log_error;
    END;

    /*
    **  DELETE OLD DEPLOYMENTS FROM NETLIFY SYSTEM
    **  DELETE UNUSED ASSETS - EXPERIMENTAL
    */
    PROCEDURE runDelete(pUserId IN users.id%type, pWebsiteId IN website.id%type, pSiteId IN website.netlify_site_id%type) IS
        l_clob CLOB;
    BEGIN
        pck_sec.g_session_user_id:=pUserId;
        pck_sec.g_website_id:=pWebsiteId;
        
        pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/'|| pSiteId || '/deploys', pMethod=>'GET', pData=>l_clob);
        FOR C IN (
            SELECT id, published_at, MAX(published_at) OVER() last_published_at 
            FROM
                (
                SELECT id, TO_TIMESTAMP_TZ(published_at, 'YYYY-MM-DD"T"HH24:MI:SS.FXFF3TZR') published_at FROM JSON_TABLE(l_clob, '$[*]' COLUMNS (id, published_at))
                )
        )
        LOOP
            IF (C.published_at<C.last_published_at) THEN
                pck_api.callNetlifyAPI(pUserId=>pUserId, pEndpoint=>'sites/'|| pSiteId || '/deploys/' || C.id, pMethod=>'DELETE', pData=>l_clob);
            END IF;
        END LOOP;
    END;

    /*
    ** Delete Github build directory - have to delete files individually before Github removes directory
    */    
    PROCEDURE deleteDirectory(pWebsiteId IN website.id%type, pUserId IN users.id%type) IS
        l_clob CLOB;
        l_domain_name website.domain_name%type;
        l_github CLOB;
        l_json JSON_OBJECT_T;
    BEGIN
        SELECT domain_name INTO l_domain_name FROM website WHERE id=pWebsiteId;
        pck_api.callGithubAPI(pUserId=>pUserId, pEndpoint=>'contents/'||l_domain_name, pMethod=>'GET', pData=>l_clob);
        FOR C IN (SELECT name, sha, message FROM  JSON_TABLE(l_clob FORMAT JSON, '$[*]' COLUMNS (name, sha, message))) LOOP
            IF (C.message IS NOT NULL) THEN 
                pck_core.log(C.message);
                RAISE_APPLICATION_ERROR(-20050,'Error in deleteDirectory - '|| C.message);
            ELSE
                l_json:=JSON_OBJECT_T.parse('{"message":"Commit by PLSQL"}');
                l_json.put('sha',C.sha);
                l_github:=l_json.to_clob; 
                pck_api.callGithubAPI(pUserId=>pUserId, pEndpoint=>'contents/' || l_domain_name || '/'||C.name, pMethod=>'DELETE', pBody=>l_github, pData=>l_clob);
            END IF;
        END LOOP;
        pck_api.resetGithubCurrentBuild(pUserId);
    END;

    /*
    ** Build and deploy AWS SES infrastructure through Terraform by submitting Github action. 
    ** Contact form sending email address must be stored in Apex Administrator description field!
    */
    PROCEDURE deployInfrastructure(pSenderEmail IN VARCHAR2) IS
        l_clob CLOB;
        l_json_clob CLOB;
        l_json JSON_OBJECT_T;
        l_action JSON_OBJECT_T;
    BEGIN
        -- Apex admin user description must hold valid domain name of sending email address
        FOR C IN (SELECT u.id, u.terraform_token, w.description ses_domain_name
                    FROM apex_workspace_apex_users w, users u 
                   WHERE w.email=u.email
                     AND w.is_admin='Yes'
                   ORDER BY date_created
                   FETCH FIRST ROW ONLY) 
        LOOP
            IF (C.ses_domain_name IS NULL) THEN
                dbms_output.put_line('APEX ADMIN USER MUST HAVE DESCRIPTION SET TO DOMAIN NAME OF SENDING EMAIL ADDRESS');
                EXIT;
            END IF;

            /* trigger the deployment using Githib action */          
            l_json:=new JSON_OBJECT_T;
            l_json.put('event_type', 'trigger_build_infra');

            l_action:=new JSON_OBJECT_T;
            l_action.put('from_email', pSenderEmail||'@'||C.ses_domain_name); --'contact.form@adfreesites.com'
            l_action.put('terraform_token', C.terraform_token);
            l_json.put('client_payload', l_action);

            --l_clob := apex_web_service.make_rest_request(p_url=>'https://api.github.com/repos/'|| C.deploy_repo || '/dispatches' ,p_http_method=>'POST',p_body=>l_json.stringify);

            /* nb; Github API 'dispatches' endpoint does not send a response */
            l_json_clob:=l_json.stringify;
            pck_api.callGithubAPI(pUserId=>C.id, pEndpoint=>'dispatches', pMethod=>'POST', pBody=>l_json_clob, pData=>l_clob);

        END LOOP;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;

/*
**  RUN this after deploying infrastructure for AWS enabling Amazon Simple Email Service
**  Updates the Admin user's "aws gateway url" and "api key" required for SES send mail service
*/
    PROCEDURE setTerraformApikey IS
        l_domain_name website.domain_name%type; 
        l_admin_id users.id%type;
        l_aws_gateway_url VARCHAR2(200);
        l_api_key varchar2(40);
        l_api_key_link VARCHAR2(100);
        l_clob CLOB;
    BEGIN
        SELECT u.id, SUBSTR(w.description,1,INSTR(w.description,'.')-1)
          INTO l_admin_id, l_domain_name
          FROM apex_workspace_apex_users w, users u 
         WHERE w.email=u.email
           AND w.is_admin='Yes'
         ORDER BY date_created
         FETCH FIRST ROW ONLY;

        pck_api.callTerraformAPI(pUserId=>null, pEndpoint=>'api/v2/organizations/Florent/workspaces/Mark_CMS', pMethod=>'GET', pData=>l_clob);

        FOR C IN (SELECT related FROM JSON_TABLE(l_clob, '$.data.relationships.outputs.links' COLUMNS (related))) LOOP
            pck_api.callTerraformAPI(pUserId=>null, pEndpoint=>C.related, pMethod=>'GET', pData=>l_clob);

            /* invoke_url */
            FOR C1 IN (SELECT value FROM JSON_TABLE(l_clob, '$.data[*]' COLUMNS (name PATH '$.attributes.name', value PATH '$.attributes.value')) WHERE name='invoke_url') LOOP
                l_aws_gateway_url:=C1.value || 'send/' || l_domain_name;
            END LOOP;

            /* ses_dkim_tokens */
            dbms_output.put_line('FETCHING DKIM TOKENS');
            FOR C1 IN (SELECT dkim FROM JSON_TABLE(l_clob, '$.data[*]' COLUMNS (name PATH '$.attributes.name', NESTED PATH '$.attributes.value[*]' COLUMNS (dkim VARCHAR2(40) PATH '$'))) WHERE name='ses_dkim_tokens') LOOP
                dbms_output.put_line('ses_dkim_tokens:'||C1.dkim);
            END LOOP;

            /* api_key */
            FOR C1 IN (SELECT api_key_link FROM JSON_TABLE(l_clob, '$.data[*]' COLUMNS (api_key_link PATH '$.links.self', name PATH '$.attributes.name', value PATH '$.attributes.value')) WHERE name='api_key') LOOP
                l_api_key_link:=C1.api_key_link;
            END LOOP;
            pck_api.callTerraformAPI(pUserId=>null, pEndpoint=>l_api_key_link, pMethod=>'GET', pData=>l_clob);
            FOR C1 IN (SELECT value FROM JSON_TABLE(l_clob, '$.data' COLUMNS (value PATH '$.attributes.value'))) LOOP
                l_api_key:=C1.value;
            END LOOP;
        END LOOP;

        IF (l_aws_gateway_url IS NOT NULL AND l_api_key IS NOT NULL) THEN
            UPDATE users 
               SET terraform_aws_gateway_url=l_aws_gateway_url, 
                   terraform_api_key=l_api_key
             WHERE id=l_admin_id;

            dbms_output.put_line('TERRAFORM API_KEY UPDATED SUCESSFULLY');
            dbms_output.put_line('TERRAFORM AWS_GATEWAY_URL UPDATED SUCESSFULLY');
            RETURN;
        END IF;

        IF (l_api_key IS NULL) THEN
            dbms_output.put_line('FAILED TO UPDATE TERRAFORM API_KEY');
        END IF;

        IF (l_aws_gateway_url IS NULL) THEN
            dbms_output.put_line('FAILED TO UPDATE TERRAFORM AWS_GATEWAY_URL');
        END IF;
    END;
       
end;
/