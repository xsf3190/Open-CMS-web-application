CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_CODEPEN" is
    --
    PROCEDURE getCodepen(pArticleId IN article.id%type);
    --
    PROCEDURE uploadCodepen(pId IN VARCHAR2, pZipFile IN BLOB);
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_CODEPEN" is
    /*
    ** Build resources to open requested article in Codepen
    */
    PROCEDURE getCodepen(pArticleId IN article.id%type) IS
        l_website_id website.id%type;
        l_nav_labels LONG;
        l_email users.email%type;
        l_domain_name website.domain_name%type;
        l_contact_form website_article.contact_form%type;
        l_collection_type website_article.collection_type%type;
        l_is_blog BOOLEAN:=FALSE;
        l_is_media BOOLEAN:=FALSE;
        l_thumbnails CLOB;
        l_html CLOB;
        l_footer CLOB;
        l_css CLOB;
        l_javascript CLOB;
        l_fluid_types pck_core.fluid_type_t;
        l_fluid_css LONG;
        l_session_data pck_core.session_data_rt;

        FUNCTION getJSCode(pFilename IN VARCHAR2) RETURN CLOB IS
        BEGIN
            FOR C IN (SELECT apex_util.blob_to_clob(file_content) file_content
                        FROM apex_application_static_files 
                       WHERE application_id=l_session_data.app_id
                         AND file_name=pFilename) 
            LOOP
                RETURN (C.file_content);
            END LOOP;
        END;

    BEGIN
        l_session_data:=pck_core.getSessionData();

        /* Get id of website owning the article because we need to build navigation labels as would appear in deployed site */

        FOR C IN (SELECT website_id FROM website_article WHERE article_id=pArticleId AND user_id=l_session_data.user_id) LOOP
            l_website_id:=C.website_id;
        END LOOP;

        IF (l_website_id IS NULL) THEN
            /* Must be a sub-page article - so get website id from its parent article */
            FOR C IN (SELECT wa.website_id, wa.collection_type
                        FROM website_article wa, article a 
                       WHERE a.id=pArticleId
                         AND wa.article_id=a.parent_id
                         AND author_user_id=l_session_data.user_id ) LOOP
                l_website_id:=C.website_id;
                l_is_blog:=C.collection_type='BLOG';
                l_is_media:=C.collection_type='MEDIA';
            END LOOP;
        END IF;

        FOR C IN (
            SELECT wa.navigation_label, wa.article_id, COUNT(*) OVER () nb_pages
              FROM website w, website_article wa
             WHERE w.id=l_website_id
               AND w.user_id=l_session_data.user_id
               AND wa.website_id=w.id
             ORDER BY wa.display_order) 
        LOOP
            /* Build navigation labels for multi-page websites */
            IF (C.nb_pages>1) THEN
                l_nav_labels:=l_nav_labels || 
                '<a href="#" class="nav-label' || CASE WHEN C.article_id=pArticleId THEN ' selected' END  || '" data-id="' || C.article_id || '">' || C.navigation_label || '</a>';
            END IF;
        END LOOP;

        /* Build page resources */
        IF (l_is_blog OR l_is_media) THEN
            SELECT w.domain_name, a.body_html, 'NO', null
              INTO l_domain_name, l_html, l_contact_form, l_collection_type
              FROM website w, article a
             WHERE w.id=l_website_id
               AND a.id=pArticleId;
        ELSE
            SELECT w.domain_name, w.header || a.body_html, w.footer, wa.contact_form, wa.collection_type
              INTO l_domain_name, l_html, l_footer, l_contact_form, l_collection_type
              FROM website w, website_article wa, article a
             WHERE w.id=l_website_id
               AND wa.website_id=w.id
               AND wa.article_id=a.id
               AND a.id=pArticleId;
        END IF;

        IF (l_nav_labels IS NOT NULL) THEN
            /* Nav labels are sticky by default but blog cards are position:absolute so change to static */
            l_nav_labels:='<nav aria-label="Main Navigation"' || CASE WHEN l_collection_type='BLOG' THEN ' style="position:static"' END || '>' || l_nav_labels || '</nav>';
        END IF;

        /* build html */
        l_html:=REPLACE(l_html,'<h1>&nbsp;</h1>',null);

        IF (INSTR(l_html,'#NAV#')>0) THEN
            l_html:=REPLACE(l_html,'#NAV#',l_nav_labels) || chr(10);
        ELSE
            l_html:=l_nav_labels || chr(10) || l_html || chr(10);
        END IF;
        
        /* Build index of Blog entries */
        IF (l_collection_type='BLOG') THEN
            l_html:=l_html || pck_core.buildBlogList(l_website_id, pArticleId, pIncludeLinks => FALSE);
        ELSIF (l_collection_type='MEDIA') THEN
            l_html:=l_html || pck_core.buildMediaList(l_website_id, pArticleId, pIncludeLinks => FALSE);
        ELSIF (l_collection_type='PRODUCT') THEN
            l_html:=l_html || pck_core.buildProductList(l_website_id, pArticleId);
            l_javascript:=l_javascript || getJSCode('deploy_cart.js');
        ELSIF (l_is_media) THEN
            pck_media.getThumbnails(pArticleId,pDeploy=>TRUE,pHtml=>l_thumbnails);
            l_html:=l_html || 
            '<ol class="gallery-list">' || l_thumbnails || '</ol>
            <div class="gallery-overlay">
                <img src="">
                <span class="counter"></span>
                <button type="button" class="close-fullscreen">&times;</button>
                <button type="button" class="prev">&lt;</button>
                <button type="button" class="next">&gt;</button>
            </div>';
            l_javascript:=l_javascript || getJSCode('deploy_gallery.js');
        END IF;

        IF (l_contact_form='YES') THEN
            SELECT email INTO l_email FROM users WHERE id=l_session_data.user_id;
            l_html:=l_html || pck_core.buildContactForm(l_email,'xx');
            l_javascript:=l_javascript || getJSCode('deploy_contact.js');
        END IF;

        l_html:=l_html || l_footer;

        apex_json.open_object;
        apex_json.write('success', TRUE); 
        apex_json.write('domain_name', l_domain_name);
        apex_json.write('html', l_html);
        apex_json.write('css', pck_core.buildCssRoot(l_website_id, l_session_data.app_id, FALSE));
        apex_json.write('js', l_javascript);
        apex_json.close_object;

        EXCEPTION WHEN OTHERS THEN pck_core.log_error;
    END;

    /*
     **  Update website HTML / CSS from the contents of an exported Codepen zip file
     */        
    PROCEDURE uploadCodepen(pId IN VARCHAR2, pZipFile IN BLOB) IS
        l_website_id website.id%type;
        l_article_id article.id%type;
        l_css CLOB;
        l_header CLOB;
        l_footer CLOB;
        l_body_html CLOB;
        l_zip BLOB:=pZipFile;
        l_files apex_zip.t_files;
        l_session_data pck_core.session_data_rt;
        l_nb_files PLS_INTEGER:=0;

        FUNCTION extractFragment(pClob IN OUT NOCOPY CLOB, pStartChar IN VARCHAR2, pEndChar IN VARCHAR2) RETURN CLOB IS
            pos1 PLS_INTEGER;
            pos2 PLS_INTEGER;
        BEGIN
            pos1:=dbms_lob.instr(pClob,pStartChar,1,1);
            IF (pos1>0) THEN
                pos2:=dbms_lob.instr(pClob,pEndChar,pos1,1);
                IF (pos2>0) THEN
                    pos2:=pos2+LENGTH(pEndChar);
                    RETURN (dbms_lob.substr(pClob, pos2-pos1, pos1));
                END IF;
            END IF;
            RETURN NULL;
        END;
        
        PROCEDURE deleteFragment(pClob IN OUT NOCOPY CLOB, pStartChar IN VARCHAR2, pEndChar IN VARCHAR2) IS
            pos1 PLS_INTEGER;
            pos2 PLS_INTEGER;
        BEGIN
            pos1:=dbms_lob.instr(pClob,pStartChar,1,1);
            IF (pos1>0) THEN
                pos2:=dbms_lob.instr(pClob,pEndChar,pos1,1);
                IF (pos2>0) THEN
                    pos2:=pos2+LENGTH(pEndChar);
                    dbms_lob.fragment_delete(pClob, pos2-pos1, pos1);
                END IF;
            END IF;
        END;

        PROCEDURE replaceFragment(pClob IN OUT NOCOPY CLOB, pStartChar IN VARCHAR2, pEndChar IN VARCHAR2, pReplaceChar IN VARCHAR2) IS
            pos1 PLS_INTEGER;
            pos2 PLS_INTEGER;
        BEGIN
            pos1:=dbms_lob.instr(pClob,pStartChar,1,1);
            IF (pos1>0) THEN
                pos2:=dbms_lob.instr(pClob,pEndChar,pos1,1);
                IF (pos2>0) THEN
                    pos2:=pos2+LENGTH(pEndChar);
                    dbms_lob.fragment_replace(pClob, pos2-pos1, LENGTH(pReplaceChar), pos1, pReplaceChar);
                END IF;
            END IF;
        END;

        PROCEDURE writeJson(pMessage IN VARCHAR2) IS
        BEGIN
            apex_json.open_object;
            apex_json.write('success', TRUE);
            apex_json.write('message', pMessage);
            apex_json.close_object;
        END;

    BEGIN
        l_session_data:=pck_core.getSessionData();

        /* pId contains website_id,article_id */
        l_website_id:=SUBSTR(pId,1,INSTR(pId,',')-1);
        l_article_id:=SUBSTR(pId,INSTR(pId,',')+1);

        l_files:=apex_zip.get_files(p_zipped_blob => l_zip);

        FOR i in 1 .. l_files.COUNT LOOP
            IF (INSTR(l_files(i),'/src/style.css')>0) THEN
                l_css:=apex_util.blob_to_clob(p_blob => apex_zip.get_file_content(p_zipped_blob => l_zip, p_file_name => l_files(i)));
                l_nb_files:=l_nb_files+1;
            ELSIF (INSTR(l_files(i),'/src/index.html')>0) THEN
                l_body_html:=apex_util.blob_to_clob(p_blob => apex_zip.get_file_content(p_zipped_blob=>l_zip, p_file_name=>l_files(i)));
                l_header:=extractFragment(l_body_html,'<header','</header>');
                l_footer:=extractFragment(l_body_html,'<footer','</footer>');
                l_nb_files:=l_nb_files+1;
            END IF;
        END LOOP;

        IF (l_nb_files<2) THEN
            writeJson('** NOT A CODEPEN EXPORT FILE **');
            RETURN;
        END IF;

        /* Extract header and footer elements */

        UPDATE website SET css=l_css, header=l_header, footer=l_footer, updated_date=current_timestamp WHERE id=l_website_id;
        UPDATE article SET body_html=l_body_html, codepen_editor='Y', updated_date=current_timestamp WHERE id=l_article_id;
        
        SELECT css, header, footer INTO l_css, l_header, l_footer FROM website WHERE id=l_website_id FOR UPDATE;
        
        /* remove design tokens */
        deleteFragment(l_css,':root','}');
        deleteFragment(l_css,'<link','>');
        replaceFragment(l_header,'<nav','</nav>','#NAV#');

        SELECT body_html INTO l_body_html FROM article WHERE id=l_article_id FOR UPDATE;
        
        deleteFragment(l_body_html,'<header','</header>');
        deleteFragment(l_body_html,'<footer','</footer>');
        replaceFragment(l_body_html,'<nav','</nav>','#NAV#');

        /* Means no CSS file included in zip - set website.css to NULL*/
        writeJson('Codepen zip uploaded successfully');

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error;
    END;    
end;
/